All Bras by Bra begins here.

A training bra is a kind of bra. Figure of training bra is the file "Items/Clothes/Upper/Bras/trainingbra1.png".

There is 1 training bra. The text-shortcut of training bra is "tbr".

The min size of a training bra is usually 2. The max size of a training bra is usually 4. The support of a training bra is usually 2. A training bra is high cut.

To say ShortDesc of (B - a training bra):
	say "training bra".

To say selfexaminetitle of (B - a training bra):
	say "small cream training bra".

To decide which figure-name is the clothing-image of (B - a training bra):
	decide on figure of training bra.

To say ClothingDesc of (C - a training bra):
	say "A small cream training bra, meant for girls who are still developing breasts.".

To decide which number is the initial outrage of (C - a training bra):
	decide on 0.

To decide which number is the initial cringe of (C - a training bra):
	decide on 2 + (diaper quest * 3).

To decide which object is the unique-upgrade-target of (C - a training bra):
	decide on nothing.

A wonder bra is a kind of bra. Understand "wonderbra" as wonder bra. Figure of wonder bra is the file "Items/Clothes/Upper/Bras/wonderbra1.png".

There is 1 wonder bra. The text-shortcut of wonder bra is "wbr".

The min size of a wonder bra is usually 3. The max size of a wonder bra is usually 6. The support of a wonder bra is usually 3. A wonder bra is low cut.

Definition: a wonder bra is black themed: decide yes.

To say ShortDesc of (B - a wonder bra):
	say "wonderbra".

To say selfexaminetitle of (B - a wonder bra):
	say "padded black wonderbra".

To decide which figure-name is the clothing-image of (B - a wonder bra):
	decide on the figure of wonder bra.

To say ClothingDesc of (C - a wonder bra):
	say "A padded black [printed name of C], which both supports the boobs and pushes them up and together to enhance their cleavage.".

To decide what number is the original price of (C - a wonder bra):
	decide on 2.

To decide which number is the initial outrage of (C - a wonder bra):
	decide on 3.

To decide which number is the adult-influence of (C - a wonder bra):
	decide on 1.

An exercise bra is a kind of bra. A beige exercise bra is a kind of exercise bra. A greyish exercise bra is a kind of exercise bra. A sexy exercise bra is a kind of exercise bra.

Figure of exercise bra is the file "Items/Clothes/Upper/Bras/exercisebra1.png".
Figure of greyish exercise bra is the file "Items/Clothes/Upper/Bras/exercisebra3.png".
Figure of greyish exercise bra full is the file "Items/Clothes/Upper/Bras/exercisebra3.png".
Figure of sexy exercise bra is the file "Items/Clothes/Upper/Bras/exercisebra2.png".

There is 1 beige exercise bra. The text-shortcut of beige exercise bra is "ebr".
There is 1 transformation-rare greyish exercise bra. The text-shortcut of greyish exercise bra is "gbr". There is 1 unique sexy exercise bra. The text-shortcut of sexy exercise bra is "sbr".

The min size of an exercise bra is usually 4. The max size of an exercise bra is usually 10. The support of an exercise bra is usually 4. An exercise bra is high cut.

Definition: an exercise bra is exercise themed: decide yes.
Definition: a greyish exercise bra is grey themed: decide yes.
Definition: a sexy exercise bra is black themed: decide yes.

To decide which figure-name is clothing-image of (C - a exercise bra):
	decide on figure of exercise bra.
To decide which figure-name is clothing-image of (C - a greyish exercise bra):
	decide on figure of greyish exercise bra.
To decide which figure-name is icarus-clothing-image of (C - a greyish exercise bra):
	decide on figure of greyish exercise bra full.
To decide which figure-name is clothing-image of (C - a sexy exercise bra):
	decide on figure of sexy exercise bra.

To say ShortDesc of (B - an exercise bra):
	say "exercise bra".

To say ShortDesc of (B - a sexy exercise bra):
	say "sexy exercise bra".

To say selfexaminetitle of (B - an exercise bra):
	say "beige exercise bra".

To say selfexaminetitle of (B - a greyish exercise bra):
	say "greyish exercise bra".

To say selfexaminetitle of (B - a sexy exercise bra):
	say "blue provocative exercise bra".

To say ClothingDesc of (C - a beige exercise bra):
	say "A secure beige [selfexaminetitle of C], which regulates the bouncing of breasts during exercise and helps reduce the strain on the back.".

To say ClothingDesc of (C - a greyish exercise bra):
	say "A tight greyish [selfexaminetitle of C], which regulates the bouncing of breasts during exercise and helps reduce the strain on the back. Its low cut allows it to expose a lot of cleavage - it would be very eye-catching to watch the wearer's boobs jiggle and bounce at the gym.".

To say ClothingDesc of (C - a sexy exercise bra):
	say "A stretchy blue [selfexaminetitle of C], which regulates the bouncing of breasts during exercise and helps reduce the strain on the back. It's made of several separated strips of fabric, revealing a lot of breast flesh.".

To decide which number is the dexterity-influence of (C - an exercise bra):
	let D be 1;
	increase D by the magic-modifier of C;
	decide on D.

To decide which number is the initial outrage of (C - an exercise bra):
	decide on 0.

To decide which number is the initial outrage of (C - a greyish exercise bra):
	decide on 2.

To decide which number is the initial outrage of (C - a sexy exercise bra):
	decide on 3.

To decide which object is the unique-upgrade-target of (C - a beige exercise bra):
	if pregnancy fetish is 1 and a random number between 1 and 2 is 1, decide on a random off-stage maternity bra;
	if there is an off-stage greyish exercise bra, decide on a random off-stage greyish exercise bra;
	if a random number between 1 and 3 > 1, decide on a random off-stage sexy exercise bra;
	decide on nothing.

To decide which object is the unique-upgrade-target of (C - a greyish exercise bra):
	decide on a random off-stage sexy exercise bra.

Definition: a greyish exercise bra is IcarusScienceAppropriate if it is actually summonable.

A fetish bra is a kind of bra. Figure of fetish bra is the file "Items/Clothes/Upper/Bras/latexbra1.png". A fetish bra is usually normally-nipple-exposing. A fetish bra is usually sheer. A fetish bra is fully covering. A fetish bra is usually latex. A fetish bra is usually transformation-rare.

There is 1 fetish bra. The text-shortcut of fetish bra is "fbr".

Definition: a fetish bra is yellow themed: decide yes.
Definition: a fetish bra is uncovered themed: decide yes.

The min size of a fetish bra is usually 5. The max size of a fetish bra is usually 12. The support of a fetish bra is usually 2.

To say ShortDesc of (B - a fetish bra):
	say "fetish bra".

To say selfexaminetitle of (B - a fetish bra):
	say "lewd latex fetish bra".

To decide which figure-name is clothing-image of (C - a fetish bra):
	decide on figure of fetish bra.

To say ClothingDesc of (C - a fetish bra):
	say "A transparent latex [printed name of C], which clings tightly to the flesh, and has holes for the nipples, leaving them completely exposed.[if lactation fetish is 1][one of][or][or][or][line break]Lactating [milk] could potentially escape the bra, but probably only if you were on your hands and knees.[as decreasingly likely outcomes][end if]";

To decide which number is the original price of (C - a fetish bra):
	decide on 1.

To decide which number is the initial outrage of (C - a fetish bra):
	decide on 9.

A maternity bra is a kind of bra. Figure of maternity bra is the file "Items/Clothes/Upper/Bras/maternitybra1.png". The soak-limit of a maternity bra is usually 15.

There is 1 maternity bra. The text-shortcut of maternity bra is "mbr".

The min size of a maternity bra is usually 7. The max size of a maternity bra is usually 15. The support of a maternity bra is usually 3. A maternity bra is usually fully covering.

A maternity bra is usually milking flappable.

Definition: an maternity bra is pregnancy related: decide yes.

To say ShortDesc of (B - a maternity bra):
	say "maternity bra".

To say selfexaminetitle of (B - a maternity bra):
	say "comfortable tan maternity bra".

To decide which figure-name is clothing-image of (C - a maternity bra):
	decide on figure of maternity bra.

To say ClothingDesc of (C - a maternity bra):
	say "[if pregnancy fetish is 1]A comfortable tan [printed name of C], which is built to support swollen breasts through pregnancy and early motherhood.[otherwise]This bra is huge! It looks like it has the potential to support even the largest of breasts.[end if][if lactation fetish is 1][line break]The cups can easily be unclipped and peeled away for convenient milking.[end if]".

To decide which number is the initial outrage of (C - a maternity bra):
	if the player is presenting as female, decide on 0;
	decide on 2.

A cupless bra is a kind of bra. Figure of cupless bra is the file "Items/Clothes/Upper/Bras/cuplessbra1.png". A cupless bra is usually transformation-rare. [Cupless bras are fully exposing, so they can't be top-displacable.]

There is 1 cupless bra. The text-shortcut of cupless bra is "cub".

Definition: a cupless bra is uncovered themed: [Is it something that should not be concealed?]
	decide yes.

The min size of a cupless bra is 20. The max size of a cupless bra is 20. The support of a cupless bra is 0. A cupless bra is fully exposing.

To say ShortDesc of (B - a cupless bra):
	say "cupless bra".

To compute SelfExamineDesc of (B - a cupless bra):
	say " that hang freely out of your pointless latex cupless bra. ".

To decide which figure-name is clothing-image of (C - a cupless bra):
	decide on figure of cupless bra.

To say ClothingDesc of (C - a cupless bra):
	say "This black bra only has an underbust section, with some frills. It has no cups at all and completely exposes the entirety of [if C is worn]your [BreastDesc].[otherwise]the wearer's breasts.[end if]";

To decide what number is the original price of (C - a cupless bra):
	decide on 1.

To decide which number is the initial outrage of (C - a cupless bra):
	decide on 6.

To set up influence of (C - a cupless bra):
	now C is titfuck-addiction-influencing.

To compute bra strain of (C - a cupless bra):
	do nothing.

To say CupDesc of (C - a cupless bra):
	say "".

Definition: a cupless bra is black themed: decide yes.

To decide which number is the bartering value of (T - a cupless bra) for (M - a gladiator):
	decide on 4.

A ribbon bra is a kind of bra. Figure of ribbon bra is the file "Items/Clothes/Upper/Bras/ribbonbra1.png". A ribbon bra is usually normally-nipple-exposing. A ribbon bra is ridiculously low cut. A ribbon bra is usually optional-top-displacable. The soak-limit of a ribbon bra is usually 9. A ribbon bra is usually transformation-rare.

There is 1 ribbon bra. The text-shortcut of ribbon bra is "rib".

The min size of a ribbon bra is usually 4. The max size of a ribbon bra is usually 13. The support of a ribbon bra is usually 1.

To say ShortDesc of (B - a ribbon bra):
	say "ribbon bra".

To compute SelfExamineDesc of (B - a ribbon bra):
	let X be the largeness of breasts;
	let B be a random bra worn by the player;
	let Y be the size of B;
	if X < 3:
		say " that are inside ";
	otherwise:
		if Y > X + 2:
			say " that hang freely inside the constraints of ";
		otherwise if Y > X and Y < X + 3:
			say " that sit loosely inside the constraints of ";
		otherwise if Y is X:
			say " that sit perfectly inside the constraints of ";
		otherwise if Y < X and Y > X - 2:
			say " that fit tightly inside the constraints of ";
		otherwise if Y < X - 1:
			say " that are overflowing out of the constraints of ";
	say "your red ribbon bra, keeping your nipples exposed. ".

To decide which figure-name is clothing-image of (C - a ribbon bra):
	decide on figure of ribbon bra.

To say ClothingDesc of (C - a ribbon bra):
	say "This bra provides little support, and since the cups are simply two black ribbons across the nipples, reveals a lot of flesh, including the nipples themselves which are framed by a metal ring. [if lactation fetish is 1]However because of its design in this way, it doesn't get soaked by lactating nipples.[otherwise]You're not really sure what the benefit is to wearing this kind of bra.[end if]";

To decide which number is the original price of (C - a ribbon bra):
	decide on 1.

To decide which number is the initial outrage of (C - a ribbon bra):
	decide on 6.

To decide which object is the unique-upgrade-target of (C - a ribbon bra):
	if the number of worn knickers is 0 and the number of worn bra is 0 and the number of worn dress is 0, decide on a random off-stage ribbon dress;
	decide on nothing.

Definition: a ribbon bra is bow themed: decide yes.
Definition: a ribbon bra is red themed: decide yes.

A spike bra is a kind of bra. Figure of spike bra 1 is the file "Items/Clothes/Upper/Bras/spikebra1.png". Figure of spike bra 2 is the file "Items/Clothes/Upper/Bras/spikebra2.png". A spike bra is usually spikey. The min size of a spike bra is usually 2. The max size of a spike bra is usually 15. The support of a spike bra is usually 3. A spike bra is low cut. A spike bra is usually pvc.

A pink spike bra is a kind of spike bra. A pink spike bra is usually cotton. The soak-limit of a pink spike bra is usually 18.

There is 1 unique spike bra. There is 1 unique pink spike bra. The text-shortcut of spike bra is "spb".

To say ShortDesc of (B - a spike bra):
	say "spike bra".

To say selfexaminetitle of (B - a spike bra):
	say "spiked bra".

To decide which figure-name is clothing-image of (C - a spike bra):
	decide on figure of spike bra 2.

To say ClothingDesc of (C - a spike bra):
	say "A bra made out of black straps with spikes facing outwards. It exposes a lot of breast flesh but covers the nipples and looks very dominant and intimidating.".

To decide which figure-name is clothing-image of (C - a pink spike bra):
	decide on figure of spike bra 1.

To say ClothingDesc of (C - a pink spike bra):
	say "A lavender coloured bra with innocent looking flowers above dangerous looking spikes.".

Definition: a pink spike bra is flower themed: decide yes.
Definition: a pink spike bra is pink themed: decide yes.
Definition: a pink spike bra is black themed: decide no.

To decide which object is the unique-upgrade-target of (C - a pink spike bra):
	decide on a random off-stage spike bra.

Definition: a spike bra is black themed: decide yes.
Definition: a spike bra is disintegration-protected: decide yes.
Definition: a spike bra is uncovered themed: decide yes.
Definition: a spike bra is product: decide yes.

To decide which number is the alchemy key of (C - a spike bra):
	decide on 14.

Definition: a spike bra is recipe specific: decide yes.

To decide which text is RecipeDesc of (B - a spike bra):
	decide on "This recipe says '[if egg laying fetish is 1]Medium Sized Egg or [end if][if diaper quest is 1]Grown Up[otherwise]Super Slutty[end if] Bra'".

This is the spike bra specific recipe rule:
	now the Product in row 26 of the Table of Alchemy is 14;
	now the Recipe in row 26 of the Table of Alchemy is 1;
	if egg laying fetish is 1:
		now the Product in row 49 of the Table of Alchemy is 14;
		now the Recipe in row 49 of the Table of Alchemy is 1;
		now the Product in row 24 of the Table of Alchemy is 14;
		now the Recipe in row 24 of the Table of Alchemy is 1;
		now the Product in row 25 of the Table of Alchemy is 14;
		now the Recipe in row 25 of the Table of Alchemy is 1.
The spike bra specific recipe rule is listed in the specific recipe rules.

To compute recipe specific cursing of (T - a spike bra):
	now the size of T is the largeness of breasts + 1;
	if the player is top heavy, now the size of T is the largeness of breasts;
	if the size of T > 15, now the size of T is 15;
	if the size of T < 2, now the size of T is 2;
	now T is bland;
	now the raw-magic-modifier of T is 0;
	if the noun is bra:
		if the noun is fully exposing or the noun is actually nipple exposing:
			now the raw-magic-modifier of T is 1;
		otherwise if diaper quest is 1:
			if the noun is training bra or the noun is sissifying:
				now T is cursed;
				now T is respiration;
				now the raw-magic-modifier of T is -2;
		otherwise:
			now T is cursed;
			now T is temptation;
			now the raw-magic-modifier of T is -2;
	otherwise if the noun is not egg:
		now the raw-magic-modifier of T is -2;
		now T is cursed.

To compute periodic effect of (B - a spike bra):
	if B is blessed and a random number between 1 and (250 - (the magic-modifier of B * 25)) is 1:
		say "You look down at your [printed name of B] and feel more confident about yourself.";
		Dignify 200.

To decide which number is the initial outrage of (C - a spike bra):
	decide on 4.

To decide which number is the initial outrage of (C - a pink spike bra):
	decide on 1.

To decide which number is the bimbo-influence of (C - a spike bra):
	let B be -1;
	if C is cursed, increase B by 1;
	if C is blessed, decrease B by 1;
	decide on B.

To decide which number is the bartering value of (T - a spike bra) for (M - a gladiator):
	decide on 4.

A flimsy bra is a kind of bra. Figure of sheer bra is the file "Items/Clothes/Upper/Bras/sheerbra1.png". Understand "sheer" as flimsy bra.

[The printed name of the bra is sheer bra hence the shortcut. Since sheer is already a keyword the game wouldn't let me create an object called the sheer bra hence why it's object identifier is flimsy bra. A player in-game will never see the word "flimsy".]
There is 1 flimsy bra. The text-shortcut of flimsy bra is "shb".

The min size of a flimsy bra is usually 2. The max size of a flimsy bra is usually 15. The support of a flimsy bra is usually 2. A flimsy bra is transformation-rare. A flimsy bra is sheer. A flimsy bra is erect-nipple-exposing.

To say ShortDesc of (B - a flimsy bra):
	say "sheer bra".

To say selfexaminetitle of (B - a flimsy bra):
	say "sheer nylon bra".

To decide which figure-name is clothing-image of (C - a flimsy bra):
	decide on figure of sheer bra.

To say ClothingDesc of (C - a flimsy bra):
	say "A very sheer flimsy bra made of nylon. It offers an average amount of support.".

To decide which number is the initial outrage of (C - a flimsy bra):
	decide on 3.

To decide which number is the initial cringe of (C - a flimsy bra):
	decide on 4.

To decide what number is the original price of (C - a flimsy bra):
	decide on 2.

Definition: a flimsy bra is sissifying: decide yes.

To decide which object is the unique-upgrade-target of (C - a flimsy bra):
	if diaper quest is 1, decide on nothing;
	if (a random number between 1 and 2 is 1 and there is an off-stage fetish bra) or the number of off-stage ribbon bra is 0, decide on a random off-stage fetish bra;
	decide on a random off-stage ribbon bra.

A pasties is a kind of bra. The support of a pasties is usually 0. A pasties is usually latex. A pasties is usually ridiculously low cut. The min size of a pasties is 20. The max size of a pasties is 20. A pasties is usually fully exposing. A pasties is usually transformation-rare. Understand "stickers", "caps", "nipple" as pasties. The text-shortcut of a pasties is "psts".

To say ShortDesc of (B - a pasties):
	say "nipple pasties".

To set up influence of (C - a pasties):
	now C is titfuck-addiction-influencing.

To decide which number is the initial outrage of (C - a pasties):
	decide on 6.

To compute bra strain of (C - a pasties):
	do nothing.

To say CupDesc of (C - a pasties):
	say "".

To decide which number is the bartering value of (T - a pasties) for (M - a robobellboy):
	decide on 4.

To decide which number is the bartering value of (T - a pasties) for (M - a mechanic):
	decide on 2.

red-pasties is a pasties. Understand "red", "pasties" as red-pasties.

Figure of pasties is the file "Items/Clothes/Upper/Bras/pasties1.jpg".

To decide which figure-name is clothing-image of (C - a pasties):
	decide on figure of pasties.

To say ClothingDesc of (C - a pasties):
	say "These red latex stickers [if item described is worn]expose your [BreastDesc] as much as possible, clinging seamlessly to your nipples[otherwise]would barely do anything to cover the wearer's [BreastDesc], only providing a small amount of 'modesty' to the wearer's nipples[end if][if the lactation rate of the player > 0 and the item described is worn]. It is completely preventing you from lactating at all[end if].".

Definition: a pasties is red themed: decide yes.

[nipple tassels]

tassels is a pasties. tassels is transformation-rare. The text-shortcut of tassels is "tsls".

To say ShortDesc of (B - tassels):
	say "nipple tassels".

Definition: tassels is gem themed: decide yes.
Definition: tassels is red themed: decide no.
Definition: tassels is white themed: decide yes.

Figure of tassels is the file "Items/Clothes/Upper/Bras/tassels1.png".

To decide which figure-name is clothing-image of (C - tassels):
	decide on figure of tassels.

To say ClothingDesc of (C - tassels):
	say "A pair of white nipple caps with [if C is worn]nylon tassels hanging from the center, as if to provide handlebars for your [BreastDesc], which are otherwise completely exposed[otherwise]nylon tassels attached in the center, as if to provide handles for the wearer's breasts, which would otherwise be completely exposed[end if][if the lactation rate of the player > 0 and C is worn]. It is completely preventing you from lactating at all.[otherwise].[end if]".

Definition: tassels is end of transformation chain: decide yes.

To decide which number is the perceived-bimbo-influence of (C - tassels):
	if the largeness of breasts > 3, decide on 1;
	decide on 0.

A chastity bra is a kind of bra. A chastity bra is usually not-top-displacable. Figure of metal bra is the file "Items/Clothes/Upper/Bras/metalbra1.png". A chastity bra is usually metal.

There is 1 chastity bra. The text-shortcut of chastity bra is "cbr".

The min size of a chastity bra is usually 4. The max size of a chastity bra is usually 8. The support of a chastity bra is usually -4.

Definition: a chastity bra is grey themed: decide yes.

To say ShortDesc of (B - a chastity bra):
	say "chastity bra".

To say selfexaminetitle of (B - a chastity bra):
	say "shiny metal chastity bra".

To decide which figure-name is clothing-image of (C - a chastity bra):
	decide on figure of metal bra.

To say ClothingDesc of (C - a chastity bra):
	say "A heavy, shiny silver [printed name of C], which has very strong restraints to prevent excessive breast growth.";

To decide which number is the initial outrage of (C - a chastity bra):
	decide on 3.

To compute bra strain of (B - a chastity bra):
	while the size of B < the largeness of breasts + the leniency of B and the largeness of breasts > 1:
		if the air volume of breasts > 0:
			decrease the air volume of breasts by 1;
		otherwise if the milk volume of breasts > the flesh volume of breasts:
			decrease the milk volume of breasts by 1;
		otherwise if the flesh volume of breasts > 0:
			decrease the flesh volume of breasts by 1;
		otherwise:
			decrease the silicone volume of breasts by 1;
	say "[one of]You can feel your [printed name of B] preventing your [BreastDesc] from growing any further.[or][then purely at random]".

tongue-bra is a bra. tongue-bra is unique. tongue-bra is biological. tongue-bra is ridiculously low cut. The printed name of tongue-bra is "[clothing-title-before]tongue bra[clothing-title-after]". The text-shortcut of tongue-bra is "tgbr". Understand "tongue", "bra" as tongue-bra.
The min size of tongue-bra is 20. The max size of tongue-bra is 20. The support of tongue-bra is 0.

Figure of white tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra1a.png".
Figure of tonguing white tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra1b.png".
Figure of black tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra2a.png".
Figure of tonguing black tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra2b.png".

To decide which figure-name is the clothing-image of (C - tongue-bra):
	if tonguesBlack is true:
		if C is tonguing, decide on figure of tonguing black tongue bra;
		decide on figure of black tongue bra;
	otherwise:
		if C is tonguing, decide on figure of tonguing white tongue bra;
		decide on figure of white tongue bra.

To say ClothingDesc of (C - tongue-bra):
	say "This [tonguesColour] bra sits under your bust, with several large red tongues that rise up and barely cover your nipples and breast flesh. [TongueStatus of C]".

To say ShortDesc of (C - tongue-bra):
	say "tongued bra".
To say MediumDesc of (B - tongue-bra):
	say "[tonguesColour] [selfexaminetitle of B]".

Definition: tongue-bra is demonic: decide yes.
Definition: tongue-bra is tongued: decide yes.
Definition: tongue-bra is transformation-protected: decide yes.
Definition: tongue-bra is white themed if tonguesBlack is false.
Definition: tongue-bra is black themed if tonguesBlack is true.

To decide which number is the initial outrage of (C - tongue-bra):
	if C is tonguing, decide on 17;
	decide on 7.

A trainee bra is a kind of bra. There is 1 trainee bra. A trainee bra is usually unique. A trainee bra has a number called training-progress. The training-progress of a trainee bra is usually 0. A trainee bra can be magnetized. A trainee bra is usually magnetized. The text-shortcut of a trainee bra is usually "teb". The printed name of trainee bra is usually "[TQlink of item described][item style]trainee bra[clothing-title-after]". A trainee bra has a number called train-charge.

Figure of trainee bra is the file "Items/Clothes/Upper/Bras/traineebra1.png".

To decide which figure-name is clothing-image of (C - a trainee bra):
	decide on figure of trainee bra.

To say ClothingDesc of (C - a trainee bra):
	say "This silicone bra is outfitted with a pair of squishy pads, which mould flawlessly to accommodate your [BreastDesc]. Anyone looking at you would have to pay close attention to notice that the pads are making your bust seem a lot more impressive than it really is. Metal accents give the bra a futuristic high-tech appearance, and keep the straps in place with surprising security.".

Definition: a trainee bra is grey themed: decide yes.
Definition: a trainee bra is transformation-protected: decide yes.
Definition: a trainee bra is whore themed: decide yes.
Definition: a trainee bra is removable if it is not magnetized.

A trainee bra is usually latex. The support of a trainee bra is usually -10. The min size of trainee bra is usually 20. The max size of trainee bra is usually 20. A trainee bra is usually top-exclusive.

To say CupDesc of (C - a trainee bra):
	say "".

To decide which number is the perceived-bimbo-influence of (C - a trainee bra):
	decide on 2.[no perceived breast size for now, so this will do.]

trainee bra removability rules is a rulebook. The removability rules of trainee bra is usually trainee bra removability rules.

This is the magnetized trainee bra can't be removed rule:
	if the wearing-target is magnetized:
		if summoning is 0 and autoremove is false, say "The magnetic latches keep the [printed name of wearing-target] all but immobile. You[']ll need to find a way to deactivate it if you want to get it off.";
		rule fails.
The magnetized trainee bra can't be removed rule is listed in the trainee bra removability rules.

Report wearing trainee bra:
	say "The magnetic latches snap closed as you finish putting it on.";
	now the noun is magnetized.

To compute periodic effect of (C - a trainee bra):[remember that training-progress will sometimes be negative]
	increase the train-charge of C by 1;
	if train-charge of C > 40:
		now the train-charge of C is 0;
		if artificial enhancements fetish is 1:
			if the training-progress of C >= 3:
				say "Your [printed name of C] emits [the training-progress of C + 1] short tones, tightening slightly each time. You feel a pinch as the bra emits a [the training-progress of C + 2]th, sustained tone and drops off your chest completely. Looking down, you realise the silicone pads have seamlessly attached themselves to your chest. You have breast implants!";
				BustImplantsUp 5;
				now the training-progress of C is 0;
				destroy C;
				ProgressUp senior robobellboy;
			otherwise:
				say "Your [printed name of C] [if the training-progress of C < 1]tightens and emits a short tone[otherwise]emits [the training-progress of C + 1] short tones, tightening slightly each time[end if]. The flexible lining of the bra begins to vibrate, secreting a warm fluid that it simultaneously massages into your [BreastDesc]. It feels way better than it has any right to, and when it finally stops, you're sure your chest is a lot more sensitive than it used to be.[line break]";
				TitFuckAddictUp 1;
				if lactation fetish is 1, increase the lactation rate of the player by 1;
				arouse 100 + (the sensitivity of breasts * 10);
				increase the training-progress of C by 1;
		otherwise:
			say "Your [printed name of C] [if the training-progress of C < 1]tightens and emits a short tone[otherwise]tightens and emits [the training-progress of C + 1] short tones[end if] as warm fluid begins seeping through the flexible lining of the bra, soaking into your skin. You hear another tone after a while, and look down [if the training-progress of C >= 3]to see the bra dropping off your chest completely![otherwise]to see the silhouette of the bra hasn't changed! Your breasts must be bigger![end if][line break]";
			increase the training-progress of C by 1;
			if the training-progress of C < 1, BustUp 1;
			otherwise BustUp the training-progress of C;
			if lactation fetish is 1, increase the lactation rate of the player by 1;
			if the training-progress of C >= 3:
				now the training-progress of C is 0;
				destroy C;
				ProgressUp senior robobellboy.

acolyte-chestpiece is a cupless bra. The text-shortcut of acolyte-chestpiece is "actp". acolyte-chestpiece is metal. The soak-limit of acolyte-chestpiece is 0. Understand "gold", "golden", "acolyte", "acolyte's", "chestpiece" as acolyte-chestpiece. acolyte-chestpiece is unique. acolyte-chestpiece is top-exclusive. The printed name of acolyte-chestpiece is "[clothing-title-before]acolyte's chestpiece[clothing-title-after]". acolyte-chestpiece has a number called charge. The charge of acolyte-chestpiece is usually 0.

Definition: acolyte-chestpiece is blessable: decide no.
Definition: acolyte-chestpiece is demonic: decide yes.
Definition: acolyte-chestpiece is yellow themed: decide yes.

Figure of acolyte chestpiece is the file "Items/Clothes/Upper/Bras/cuplessbra2.jpg".

To decide which figure-name is clothing-image of (C - acolyte-chestpiece):
	decide on figure of acolyte chestpiece.

To decide which object is the unique-upgrade-target of (C - acolyte-chestpiece):
	decide on nothing.

To say ShortDesc of (B - acolyte-chestpiece):
	say "acolyte's chestpiece".

To compute SelfExamineDesc of (B - a acolyte-chestpiece):
	say " that hang freely out of your pointless gold cupless bra. ".

To say ClothingDesc of (C - acolyte-chestpiece):
	say "This curved set of gem-encrusted gold 'plates' [if C is worn]rides so low that it exposes your[otherwise]would ride so low that it would completely expose the wearer's[end if] nipples.[if the charge of C > 0][line break][end if][if the charge of C > 4]The metal is warm to the touch, and you can tell it has absorbed a great deal of power.[otherwise if the charge of C > 1]The metal is slightly warm to the touch, and you can tell it has absorbed a bit of power.[otherwise]The metal is surprisingly cool to the touch.[end if][line break]You get the feeling that you would sink like a rock if you tried swimming in this.";

To MagicSteal (C - acolyte-chestpiece):
	if the charge of C < 5:
		say "Your [printed name of C] absorbs the magical energy!";
		increase the charge of C by 1;
		if the charge of C is 5:
			say "A surge of warmth passes through the metal, and you get the feeling it has reached maximum power.";
	otherwise:
		increase the magic-power of the player by 1.

This is the worshipper tit slut rule:
	if acolyte-chestpiece is worn, increase the desirability of breasts by 5.
The worshipper tit slut rule is listed in the tit slut eligibility rules.

[any fluid touching the breasts will cause arousal and fatigue gain.]

To decide which number is the titfuck-addiction-influence of (C - acolyte-chestpiece):
	decide on 1 - the magic-modifier of C.

To decide which number is the delicateness-influence of (C - acolyte-chestpiece):
	decide on 2 - the magic-modifier of C.

To decide which number is the strength-influence of (C - acolyte-chestpiece):
	if the charge of C < 5:
		if the magic-modifier of C < 0, decide on the magic-modifier of C;
		decide on 0;
	otherwise:
		if the magic-modifier of C > 0, decide on the magic-modifier of C + 1;
		decide on 1.

All Bras ends here.
