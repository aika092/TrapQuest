All Bras by Bra begins here.

A training bra is a kind of bra. Figure of training bra is the file "Items/Clothes/Upper/Bras/trainingbra1.png".

There is 1 training bra. The text-shortcut of training bra is "tbr".

The min size of a training bra is usually 2. The max size of a training bra is usually 4. The support of a training bra is usually 2. A training bra is high cut.

To say ShortDesc of (B - a training bra):
	say "training bra".

To say selfexaminetitle of (B - a training bra):
	say "small cream training bra".

To decide which figure-name is the clothing-image of (B - a training bra):
	decide on figure of training bra.

To say ClothingDesc of (C - a training bra):
	say "A small cream training bra, meant for girls who are still developing breasts.".

To decide which number is the initial outrage of (C - a training bra):
	decide on 0.

To decide which number is the initial cringe of (C - a training bra):
	decide on 2 + (diaper quest * 3).

To decide which object is the unique-upgrade-target of (C - a training bra):
	decide on nothing.

A wonder bra is a kind of bra. Understand "wonderbra" as wonder bra. Figure of wonder bra is the file "Items/Clothes/Upper/Bras/wonderbra1.png".

There is 1 wonder bra. The text-shortcut of wonder bra is "wbr".

The min size of a wonder bra is usually 3. The max size of a wonder bra is usually 6. The support of a wonder bra is usually 3.

Definition: a wonder bra is black themed: decide yes.

To say ShortDesc of (B - a wonder bra):
	say "wonderbra".

To say selfexaminetitle of (B - a wonder bra):
	say "padded black wonderbra".

To decide which figure-name is the clothing-image of (B - a wonder bra):
	decide on the figure of wonder bra.

To say ClothingDesc of (C - a wonder bra):
	say "A padded black [printed name of C], which both supports the boobs and pushes them up and together to enhance their cleavage.".

To decide which number is the initial outrage of (C - a wonder bra):
	decide on 3.

To decide which number is the adult-influence of (C - a wonder bra):
	decide on 1.

An exercise bra is a kind of bra. A beige exercise bra is a kind of exercise bra. A greyish exercise bra is a kind of exercise bra. A sexy exercise bra is a kind of exercise bra. A tight blue exercise bra is a kind of exercise bra.

Figure of exercise bra is the file "Items/Clothes/Upper/Bras/exercisebra1.png".
Figure of greyish exercise bra is the file "Items/Clothes/Upper/Bras/exercisebra3.png".
Figure of greyish exercise bra full is the file "Items/Clothes/Upper/Bras/exercisebra3.png".
Figure of sexy exercise bra is the file "Items/Clothes/Upper/Bras/exercisebra2.png".
Figure of tight blue exercise bra is the file "Items/Clothes/Upper/Bras/exercisebra5.png".

There is 1 beige exercise bra. The text-shortcut of beige exercise bra is "ebr".
There is 1 transformation-rare greyish exercise bra. The text-shortcut of greyish exercise bra is "gbr".
There is 1 unique sexy exercise bra. The text-shortcut of sexy exercise bra is "sbr".
There is 1 transformation-rare tight blue exercise bra. A tight blue exercise bra is normally-partially-nipple-covering. The text-shortcut of tight blue exercise bra is "tbbr".

The min size of an exercise bra is usually 4. The max size of an exercise bra is usually 10. The support of an exercise bra is usually 4. An exercise bra is high cut.

Definition: an exercise bra is exercise themed: decide yes.
Definition: a greyish exercise bra is grey themed: decide yes.
Definition: a sexy exercise bra is blue themed: decide yes.
Definition: a tight blue exercise bra is blue themed: decide yes.

To decide which figure-name is clothing-image of (C - an exercise bra):
	decide on figure of exercise bra.
To decide which figure-name is clothing-image of (C - a greyish exercise bra):
	decide on figure of greyish exercise bra.
To decide which figure-name is icarus-clothing-image of (C - a greyish exercise bra):
	decide on figure of greyish exercise bra full.
To decide which figure-name is clothing-image of (C - a sexy exercise bra):
	decide on figure of sexy exercise bra.
To decide which figure-name is clothing-image of (C - a tight blue exercise bra):
	decide on figure of tight blue exercise bra.

To say ShortDesc of (B - an exercise bra):
	say "exercise bra".

To say ShortDesc of (B - a sexy exercise bra):
	say "sexy exercise bra".

To say selfexaminetitle of (B - an exercise bra):
	say "beige exercise bra".

To say selfexaminetitle of (B - a greyish exercise bra):
	say "greyish exercise bra".

To say selfexaminetitle of (B - a sexy exercise bra):
	say "blue provocative exercise bra".

To say selfexaminetitle of (B - a tight blue exercise bra):
	say "tight blue exercise bra".

To say ClothingDesc of (C - a beige exercise bra):
	say "A secure beige [selfexaminetitle of C], which regulates the bouncing of breasts during exercise and helps reduce the strain on the back.".

To say ClothingDesc of (C - a greyish exercise bra):
	say "A tight greyish [selfexaminetitle of C], which regulates the bouncing of breasts during exercise and helps reduce the strain on the back. Its low cut allows it to expose a lot of cleavage - it would be very eye-catching to watch the wearer's boobs jiggle and bounce at the gym.".

To say ClothingDesc of (C - a sexy exercise bra):
	say "A stretchy [selfexaminetitle of C], which regulates the bouncing of breasts during exercise and helps reduce the strain on the back. It's made of several separated strips of fabric, revealing a lot of breast flesh.".

To say ClothingDesc of (C - a tight blue exercise bra):
	say "A [selfexaminetitle of C], which regulates the bouncing of breasts during exercise and helps reduce the strain on the back. The fabric is too thin, though...".

To decide which number is the dexterity-influence of (C - an exercise bra):
	let D be 1;
	increase D by the magic-modifier of C;
	decide on D.

To decide which number is the initial outrage of (C - an exercise bra):
	decide on 0.

To decide which number is the initial outrage of (C - a greyish exercise bra):
	decide on 2.

To decide which number is the initial outrage of (C - a sexy exercise bra):
	decide on 3.

To decide which number is the initial outrage of (C - a tight blue exercise bra):
	decide on 4.

To decide which object is the unique-upgrade-target of (C - a beige exercise bra):
	if pregnancy fetish is 1 and a random number between 1 and 2 is 1, decide on a random off-stage maternity bra;
	if there is an off-stage greyish exercise bra, decide on a random off-stage greyish exercise bra;
	if a random number between 1 and 3 > 1, decide on a random off-stage sexy exercise bra;
	decide on nothing.

To decide which object is the unique-upgrade-target of (C - a greyish exercise bra):
	decide on a random off-stage sexy exercise bra.

To decide which object is the unique-upgrade-target of (C - a sexy exercise bra):
	decide on a random off-stage tight blue exercise bra.

Definition: a greyish exercise bra is IcarusScienceAppropriate:
	if it is actually summonable, decide yes;
	decide no.

A fetish bra is a kind of bra. A fetish bra is sheer. A fetish bra is latex. A fetish bra is transformation-rare. Understand "fetish", "bra" as fetish bra.
The min size of a fetish bra is 5. The max size of a fetish bra is 12. The support of a fetish bra is 2.

Definition: a fetish bra is uncovered themed: decide yes.

To say selfexaminetitle of (B - a fetish bra):
	say "lewd latex fetish bra".

To decide which number is the initial outrage of (C - a fetish bra):
	decide on 9.

nipple-hole-bra is a fetish bra. nipple-hole-bra is normally-nipple-exposing. nipple-hole-bra is high cut. Understand "nipple", "hole" as nipple-hole-bra.

To say ShortDesc of (B - nipple-hole-bra):
	say "nipple hole fetish bra".

The text-shortcut of nipple-hole-bra is "fbr".

Definition: nipple-hole-bra is yellow themed: decide yes.

Figure of nipple-hole-bra is the file "Items/Clothes/Upper/Bras/latexbra1.png".

To decide which figure-name is clothing-image of (C - nipple-hole-bra):
	decide on figure of nipple-hole-bra.

To say ClothingDesc of (C - nipple-hole-bra):
	say "A transparent latex fetish bra, which clings tightly to the flesh, and has holes for the nipples, leaving them completely exposed[if C is worn and C is cursed and black-catsuit is worn and black-catsuit is cursed]. The edges of the bra have completely merged with the rest of your catsuit[end if].[if lactation fetish is 1][one of][or][or][or][line break]Lactating [milk] could potentially escape the bra, but probably only if you were on your hands and knees.[as decreasingly likely outcomes][end if]".

window-bra is a fetish bra. Understand "fetish", "window" as window-bra.

To say ShortDesc of (B - window-bra):
	say "fetish window bra".

The text-shortcut of window-bra is "fwbr".

Definition: window-bra is black themed: decide yes.

Figure of window-bra is the file "Items/Clothes/Upper/Bras/latexbra2.png".

To decide which figure-name is clothing-image of (C - window-bra):
	decide on figure of window-bra.

To say ClothingDesc of (C - window-bra):
	say "A black latex fetish window bra, with a patterned sheer window section in the middle, to somewhat expose your cleavage and nipples[if C is worn and C is cursed and black-catsuit is worn and black-catsuit is cursed]. The edges of the bra have completely merged with the rest of your catsuit[end if].".

To decide which object is the unique-upgrade-target of (C - window-bra):
	if nipple-hole-bra is off-stage, decide on nipple-hole-bra;
	decide on nothing.


A maternity bra is a kind of bra. Figure of maternity bra is the file "Items/Clothes/Upper/Bras/maternitybra1.png".

There is 1 maternity bra. The text-shortcut of maternity bra is "mbr".

The min size of a maternity bra is usually 7. The max size of a maternity bra is usually 15. The support of a maternity bra is usually 3. A maternity bra is usually fully covering.

A maternity bra is usually milking flappable.

Definition: an maternity bra is pregnancy themed: decide yes.

To say ShortDesc of (B - a maternity bra):
	say "maternity bra".

To say selfexaminetitle of (B - a maternity bra):
	say "comfortable tan maternity bra".

To decide which figure-name is clothing-image of (C - a maternity bra):
	decide on figure of maternity bra.

To say ClothingDesc of (C - a maternity bra):
	say "[if pregnancy fetish is 1]A comfortable tan [printed name of C], which is built to support swollen breasts through pregnancy and early motherhood.[otherwise]This bra is huge! It looks like it has the potential to support even the largest of breasts.[end if][if lactation fetish is 1][line break]The cups can easily be unclipped and peeled away for convenient milking.[end if]".

To decide which number is the initial outrage of (C - a maternity bra):
	if the player is presenting as female, decide on 0;
	decide on 2.

A cupless bra is a kind of bra. Figure of cupless bra is the file "Items/Clothes/Upper/Bras/cuplessbra1.png". A cupless bra is usually transformation-rare. [Cupless bras are fully exposing, so they can't be top-displacable.]

There is 1 cupless bra. The text-shortcut of cupless bra is "cub".

Definition: a cupless bra is uncovered themed: [Is it something that should not be concealed?]
	decide yes.

The min size of a cupless bra is 20. The max size of a cupless bra is 20. The support of a cupless bra is 0. A cupless bra is fully exposing.

To say ShortDesc of (B - a cupless bra):
	say "cupless bra".

To compute SelfExamineDesc of (B - a cupless bra):
	say " that hang freely out of your pointless latex cupless bra. ".

To decide which figure-name is clothing-image of (C - a cupless bra):
	decide on figure of cupless bra.

To say ClothingDesc of (C - a cupless bra):
	say "This black bra only has an underbust section, with some frills. It has no cups at all and completely exposes the entirety of [if C is worn]your [BreastDesc].[otherwise]the wearer's breasts.[end if]";

To decide which number is the initial outrage of (C - a cupless bra):
	decide on 6.

To set up influence of (C - a cupless bra):
	now C is titfuck-addiction-influencing.

To compute bra strain of (C - a cupless bra):
	do nothing.

To say CupDesc of (C - a cupless bra):
	say "".

Definition: a cupless bra is black themed: decide yes.

To decide which number is the bartering value of (T - a cupless bra) for (M - a gladiator):
	decide on 4.

A ribbon bra is a kind of bra. Figure of ribbon bra is the file "Items/Clothes/Upper/Bras/ribbonbra1.png". A ribbon bra is usually normally-nipple-exposing. A ribbon bra is ridiculously low cut. A ribbon bra is usually optional-top-displacable. A ribbon bra is usually transformation-rare.

There is 1 ribbon bra. The text-shortcut of ribbon bra is "rib".

The min size of a ribbon bra is usually 4. The max size of a ribbon bra is usually 13. The support of a ribbon bra is usually 1.

To say ShortDesc of (B - a ribbon bra):
	say "ribbon bra".

To compute SelfExamineDesc of (B - a ribbon bra):
	let X be the largeness of breasts;
	let B be a random bra worn by the player;
	let Y be the size of B;
	if X < 3:
		say " that are inside ";
	otherwise:
		if Y > X + 2:
			say " that hang freely inside the constraints of ";
		otherwise if Y > X and Y < X + 3:
			say " that sit loosely inside the constraints of ";
		otherwise if Y is X:
			say " that sit perfectly inside the constraints of ";
		otherwise if Y < X and Y > X - 2:
			say " that fit tightly inside the constraints of ";
		otherwise if Y < X - 1:
			say " that are overflowing out of the constraints of ";
	say "your red ribbon bra, keeping your nipples exposed. ".

To decide which figure-name is clothing-image of (C - a ribbon bra):
	decide on figure of ribbon bra.

To say ClothingDesc of (C - a ribbon bra):
	say "This bra provides little support, and since the cups are simply two black ribbons across the nipples, reveals a lot of flesh, including the nipples themselves which are framed by a metal ring. [if lactation fetish is 1]However because of its design in this way, it doesn't get soaked by lactating nipples.[otherwise]You're not really sure what the benefit is to wearing this kind of bra.[end if]";

To decide which number is the initial outrage of (C - a ribbon bra):
	decide on 6.

To decide which object is the unique-upgrade-target of (C - a ribbon bra):
	if the number of worn knickers is 0 and the number of worn bra is 0 and the number of worn dress is 0, decide on a random off-stage ribbon dress;
	decide on nothing.

Definition: a ribbon bra is bow themed: decide yes.
Definition: a ribbon bra is red themed: decide yes.

A spike bra is a kind of bra. Figure of spike bra 1 is the file "Items/Clothes/Upper/Bras/spikebra1.png". Figure of spike bra 2 is the file "Items/Clothes/Upper/Bras/spikebra2.png". A spike bra is usually spikey. The min size of a spike bra is usually 2. The max size of a spike bra is usually 15. The support of a spike bra is usually 3. A spike bra is very low cut. A spike bra is usually pvc.

A pink spike bra is a kind of spike bra. A pink spike bra is usually cotton.

There is 1 unique spike bra. There is 1 unique pink spike bra. The text-shortcut of spike bra is "spb".

To say ShortDesc of (B - a spike bra):
	say "spike bra".

To say selfexaminetitle of (B - a spike bra):
	say "spiked bra".

To decide which figure-name is clothing-image of (C - a spike bra):
	decide on figure of spike bra 2.

To say ClothingDesc of (C - a spike bra):
	say "A bra made out of black straps with spikes facing outwards. It exposes a lot of breast flesh but covers the nipples and looks very dominant and intimidating.".

To decide which figure-name is clothing-image of (C - a pink spike bra):
	decide on figure of spike bra 1.

To say ClothingDesc of (C - a pink spike bra):
	say "A lavender coloured bra with innocent looking flowers above dangerous looking spikes.".

Definition: a pink spike bra is flower themed: decide yes.
Definition: a pink spike bra is pink themed: decide yes.
Definition: a pink spike bra is black themed: decide no.

To decide which object is the unique-upgrade-target of (C - a pink spike bra):
	decide on a random off-stage spike bra.

Definition: a spike bra is black themed: decide yes.
Definition: a spike bra is disintegration-protected: decide yes.
Definition: a spike bra is uncovered themed: decide yes.
Definition: a spike bra is product: decide yes.

To decide which number is the alchemy key of (C - a pink spike bra):
	decide on 14.

Definition: a pink spike bra is recipe specific: decide yes.

To decide which text is RecipeDesc of (B - a pink spike bra):
	decide on "This recipe just says '[if egg laying fetish is 1]Medium Sized Egg or [end if][if diaper quest is 1]Grown Up[otherwise]Super Slutty[end if] Bra'.".

This is the spike bra specific recipe rule:
	now the Product in row 26 of the Table of Alchemy is 14;
	now the Recipe in row 26 of the Table of Alchemy is 1;
	if egg laying fetish is 1:
		now the Product in row 49 of the Table of Alchemy is 14;
		now the Recipe in row 49 of the Table of Alchemy is 1;
		now the Product in row 24 of the Table of Alchemy is 14;
		now the Recipe in row 24 of the Table of Alchemy is 1;
		now the Product in row 25 of the Table of Alchemy is 14;
		now the Recipe in row 25 of the Table of Alchemy is 1.
The spike bra specific recipe rule is listed in the specific recipe rules.

To compute recipe specific cursing of (T - a spike bra):
	now the size of T is the largeness of breasts + 1;
	if the player is top heavy, now the size of T is the largeness of breasts;
	if the size of T > 15, now the size of T is 15;
	if the size of T < 2, now the size of T is 2;
	now T is bland;
	now the raw-magic-modifier of T is 0;
	if the noun is bra:
		if the noun is fully exposing or the noun is actually nipple exposing:
			now the raw-magic-modifier of T is 1;
		otherwise if diaper quest is 1:
			if the noun is training bra or the noun is sissifying:
				now T is cursed;
				now T is respiration;
				now the raw-magic-modifier of T is -2;
		otherwise:
			now T is cursed;
			now T is temptation;
			now the raw-magic-modifier of T is -2;
	otherwise if the noun is not egg:
		now the raw-magic-modifier of T is -2;
		now T is cursed.

To compute periodic effect of (B - a spike bra):
	if B is blessed and a random number between 1 and (250 - (the magic-modifier of B * 25)) is 1:
		say "You look down at your [printed name of B] and feel more confident about yourself.";
		Dignify 200.

To decide which number is the initial outrage of (C - a spike bra):
	decide on 4.

To decide which number is the initial outrage of (C - a pink spike bra):
	decide on 1.

To decide which number is the bimbo-influence of (C - a spike bra):
	let B be -1;
	if C is cursed, increase B by 1;
	if C is blessed, decrease B by 1;
	decide on B.

To decide which number is the bartering value of (T - a spike bra) for (M - a gladiator):
	decide on 4.

A flimsy bra is a kind of bra. Figure of sheer bra is the file "Items/Clothes/Upper/Bras/sheerbra1.png". Understand "sheer" as flimsy bra.

[The printed name of the bra is sheer bra hence the shortcut. Since sheer is already a keyword the game wouldn't let me create an object called the sheer bra hence why its object identifier is flimsy bra. A player in-game will never see the word "flimsy".]
There is 1 flimsy bra. The text-shortcut of flimsy bra is "shb".

The min size of a flimsy bra is usually 2. The max size of a flimsy bra is usually 15. The support of a flimsy bra is usually 2. A flimsy bra is transformation-rare. A flimsy bra is sheer. A flimsy bra is erect-nipple-exposing.

To say ShortDesc of (B - a flimsy bra):
	say "sheer bra".

To say selfexaminetitle of (B - a flimsy bra):
	say "sheer nylon bra".

To decide which figure-name is clothing-image of (C - a flimsy bra):
	decide on figure of sheer bra.

To say ClothingDesc of (C - a flimsy bra):
	say "A very sheer flimsy bra made of nylon. It offers an average amount of support.".

To decide which number is the initial outrage of (C - a flimsy bra):
	decide on 3.

To decide which number is the initial cringe of (C - a flimsy bra):
	decide on 4.

Definition: a flimsy bra is sissifying: decide yes.

To decide which object is the unique-upgrade-target of (C - a flimsy bra):
	if diaper quest is 1, decide on nothing;
	if (a random number between 1 and 2 is 1 and window-bra is off-stage) or the number of off-stage ribbon bra is 0, decide on window-bra;
	decide on a random off-stage ribbon bra.

A pasties is a kind of bra. The support of a pasties is usually 0. A pasties is usually latex. A pasties is usually fully exposing. The min size of a pasties is 20. The max size of a pasties is 20. A pasties is usually transformation-rare. Understand "stickers", "caps", "nipple" as pasties. The text-shortcut of a pasties is "psts". A pasties is not-top-displacable.

Definition: a pasties (called C) is actually nipple exposing rather than actually nipple covering: [Pasties always cover your nipples.]
	decide no.

Definition: a pasties (called C) is nipple exposing rather than at least partially nipple covering:[Pasties always cover your nipples.]
	decide no.

To say ShortDesc of (B - a pasties):
	say "nipple pasties".

To set up influence of (C - a pasties):
	now C is titfuck-addiction-influencing.

To decide which number is the initial outrage of (C - a pasties):
	decide on 6.

To compute bra strain of (C - a pasties):
	do nothing.

To say CupDesc of (C - a pasties):
	say "".

To decide which number is the bartering value of (T - a pasties) for (M - a robobellboy):
	decide on 4.

To decide which number is the bartering value of (T - a pasties) for (M - a mechanic):
	decide on 2.


red-pasties is a pasties. Understand "red", "pasties" as red-pasties.

To compute class set up of (C - red-pasties):
	now C is cursed;
	now the quest of C is vaginal-creampie-quest;
	now vaginal-creampie-quest is persistent;
	now C is vaginal-sex-addiction-influencing.

To say ClassSummonFlav of (C - red-pasties):
	say "A pair of red pasties appears on your nipples!".

Figure of pasties is the file "Items/Clothes/Upper/Bras/pasties1.png".

To decide which figure-name is clothing-image of (C - a pasties):
	decide on figure of pasties.

To say ClothingDesc of (C - a pasties):
	if C is worn and C is cursed and there is a worn cursed catsuit, say "These red latex stickers expose your [BreastDesc] as much as possible, clinging perfectly to your nipples and connecting to the rest of your catsuit with four impossibly thin strands of latex. [run paragraph on]";
	otherwise say "These red latex stickers [if item described is worn]expose your [BreastDesc] as much as possible, clinging seamlessly to your nipples[otherwise]would barely do anything to cover the wearer's [BreastDesc], only providing a small amount of 'modesty' to the wearer's nipples[end if]. [run paragraph on]";
	if the lactation rate of breasts > 0 and C is worn, say "It is completely preventing you from lactating at all.".

Definition: a pasties is red themed: decide yes.

[nipple tassels]

tassels is a pasties. tassels is transformation-rare. The text-shortcut of tassels is "tsls".

To say ShortDesc of (B - tassels):
	say "nipple tassels".

Definition: tassels is gem themed: decide yes.
Definition: tassels is red themed: decide no.
Definition: tassels is white themed: decide yes.

Figure of tassels is the file "Items/Clothes/Upper/Bras/tassels1.png".

To decide which figure-name is clothing-image of (C - tassels):
	decide on figure of tassels.

To say ClothingDesc of (C - tassels):
	if C is worn and C is cursed and there is a worn cursed catsuit, say "A pair of white nipple caps with four strands of thin latex connecting them to the rest of your catsuit, and nylon tassels hanging from the ends of the caps. Your [BreastDesc] are otherwise left completely exposed. [run paragraph on]";
	otherwise say "A pair of white nipple caps with [if C is worn]nylon tassels hanging from the centre, as if to provide handlebars for your [BreastDesc], which are otherwise completely exposed[otherwise]nylon tassels attached in the centre, as if to provide handles for the wearer's breasts, which would otherwise be completely exposed[end if]. [run paragraph on]";
	if the lactation rate of breasts > 0 and C is worn, say "It is completely preventing you from lactating at all.".

Definition: tassels is end of transformation chain: decide yes.

To decide which number is the perceived-bimbo-influence of (C - tassels):
	if the largeness of breasts > 3, decide on 1;
	decide on 0.

A chastity bra is a kind of bra. A chastity bra is usually not-top-displacable. Figure of metal bra is the file "Items/Clothes/Upper/Bras/metalbra1.png". A chastity bra is usually metal.

There is 1 chastity bra. The text-shortcut of chastity bra is "cbr".

The min size of a chastity bra is usually 4. The max size of a chastity bra is usually 8. The support of a chastity bra is usually -4.

Definition: a chastity bra is grey themed: decide yes.

To say ShortDesc of (B - a chastity bra):
	say "chastity bra".

To say selfexaminetitle of (B - a chastity bra):
	say "shiny metal chastity bra".

To decide which figure-name is clothing-image of (C - a chastity bra):
	decide on figure of metal bra.

To say ClothingDesc of (C - a chastity bra):
	say "A heavy, shiny silver [printed name of C], which has very strong restraints to prevent excessive breast growth.";

To decide which number is the initial outrage of (C - a chastity bra):
	decide on 3.

To decide which number is the leniency of (C - a chastity bra):
	decide on 1.

To compute bra strain of (B - a chastity bra):
	while the size of B + the leniency of B < the largeness of breasts and the largeness of breasts > 1:
		if the air volume of breasts > 0:
			decrease the air volume of breasts by 1;
		otherwise if the milk volume of breasts > the flesh volume of breasts:
			decrease the milk volume of breasts by 1;
		otherwise if the flesh volume of breasts > 0:
			decrease the flesh volume of breasts by 1;
		otherwise:
			decrease the silicone volume of breasts by 1;
	say "[one of]You can feel your [printed name of B] preventing your [BreastDesc] from growing any further.[or][then purely at random]".

tongue-bra is a bra. tongue-bra is unique. tongue-bra is biological. tongue-bra is ridiculously low cut. The printed name of tongue-bra is "[clothing-title-before]tongue bra[clothing-title-after]". The text-shortcut of tongue-bra is "tgbr". Understand "tongue", "bra" as tongue-bra.
The min size of tongue-bra is 20. The max size of tongue-bra is 20. The support of tongue-bra is 0.

Figure of white tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra1a.png".
Figure of tonguing white tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra1b.png".
Figure of black tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra2a.png".
Figure of tonguing black tongue bra is the file "Items/Clothes/Upper/Bras/tonguebra2b.png".

To decide which figure-name is the clothing-image of (C - tongue-bra):
	if tonguesBlack is true:
		if C is tonguing, decide on figure of tonguing black tongue bra;
		decide on figure of black tongue bra;
	otherwise:
		if C is tonguing, decide on figure of tonguing white tongue bra;
		decide on figure of white tongue bra.

To say ClothingDesc of (C - tongue-bra):
	say "This [tonguesColour] bra sits under your bust, with several large red tongues that rise up and barely cover your nipples and breast flesh. [TongueStatus of C]".

To say ShortDesc of (C - tongue-bra):
	say "tongued bra".
To say MediumDesc of (B - tongue-bra):
	say "[tonguesColour] [selfexaminetitle of B]".

Definition: tongue-bra is demonic: decide yes.
Definition: tongue-bra is tongued: decide yes.
Definition: tongue-bra is transformation-protected: decide yes.
Definition: tongue-bra is white themed:
	if tonguesBlack is false, decide yes;
	decide no.
Definition: tongue-bra is black themed:
	if tonguesBlack is true, decide yes;
	decide no.

To decide which number is the initial outrage of (C - tongue-bra):
	if C is tonguing, decide on 17;
	decide on 7.

A trainee bra is a kind of bra. There is 1 trainee bra. A trainee bra is usually unique. A trainee bra has a number called training-progress. The training-progress of a trainee bra is usually 0. A trainee bra can be magnetised. A trainee bra is usually magnetised. The text-shortcut of a trainee bra is usually "teb". The printed name of trainee bra is usually "[TQlink of item described][item style]trainee bra[clothing-title-after]". A trainee bra has a number called train-charge.

Figure of trainee bra is the file "Items/Clothes/Upper/Bras/traineebra1.png".

To decide which figure-name is clothing-image of (C - a trainee bra):
	decide on figure of trainee bra.

To say ClothingDesc of (C - a trainee bra):
	say "This silicone bra is outfitted with a pair of squishy pads, which mould flawlessly to accommodate your [BreastDesc]. Anyone looking at you would have to pay close attention to notice that the pads are making your bust seem a lot more impressive than it really is. Metal accents give the bra a futuristic high-tech appearance, and keep the straps in place with surprising security.".

Definition: a trainee bra is grey themed: decide yes.
Definition: a trainee bra is transformation-protected: decide yes.
Definition: a trainee bra is whore themed: decide yes.
Definition: a trainee bra is removable:
	if it is not magnetised, decide yes;
	decide no.

A trainee bra is usually latex. The support of a trainee bra is usually -10. The min size of trainee bra is usually 20. The max size of trainee bra is usually 20. A trainee bra is usually top-exclusive.

To say CupDesc of (C - a trainee bra):
	say "".

To decide which number is the perceived-bimbo-influence of (C - a trainee bra):
	decide on 2.[no perceived breast size for now, so this will do.]

trainee bra removability rules is a rulebook. The removability rules of trainee bra is usually trainee bra removability rules.

This is the magnetised trainee bra can't be removed rule:
	if the wearing-target is magnetised:
		if summoning is 0 and autoremove is false, say "The magnetic latches keep the [printed name of wearing-target] all but immobile. You'll need to find a way to deactivate it if you want to get it off.";
		rule fails.
The magnetised trainee bra can't be removed rule is listed in the trainee bra removability rules.

Report wearing trainee bra:
	say "The magnetic latches snap closed as you finish putting it on.";
	now the noun is magnetised.

To compute periodic effect of (C - a trainee bra):[remember that training-progress will sometimes be negative]
	increase the train-charge of C by 1;
	if train-charge of C > 40:
		now the train-charge of C is 0;
		if artificial enhancements fetish is 1:
			if the training-progress of C >= 3:
				say "Your [printed name of C] emits [the training-progress of C + 1] short tones, tightening slightly each time. You feel a pinch as the bra emits a [the training-progress of C + 2]th, sustained tone and drops off your chest completely. Looking down, you realise the silicone pads have seamlessly attached themselves to your chest. You have breast implants!";
				BustImplantsUp 5;
				now the training-progress of C is 0;
				destroy C;
				ProgressUp senior robobellboy;
			otherwise:
				say "Your [printed name of C] [if the training-progress of C < 1]tightens and emits a short tone[otherwise]emits [the training-progress of C + 1] short tones, tightening slightly each time[end if]. The flexible lining of the bra begins to vibrate, secreting a warm fluid that it simultaneously massages into your [BreastDesc]. It feels way better than it has any right to, and when it finally stops, you're sure your chest is a lot more sensitive than it used to be.";
				increase the raw sensitivity of breasts by 1;
				TitFuckAddictUp 1;
				if lactation fetish is 1, increase the lactation rate of breasts by 1;
				arouse 100 + (the sensitivity of breasts * 10);
				increase the training-progress of C by 1;
		otherwise:
			say "Your [printed name of C] [if the training-progress of C < 1]tightens and emits a short tone[otherwise]tightens and emits [the training-progress of C + 1] short tones[end if] as warm fluid begins seeping through the flexible lining of the bra, soaking into your skin. You hear another tone after a while, and look down [if the training-progress of C >= 3]to see the bra dropping off your chest completely[otherwise]to see the silhouette of the bra hasn't changed! Your breasts must be bigger[end if]!";
			increase the training-progress of C by 1;
			if the training-progress of C < 1, BustUp 1;
			otherwise BustUp the training-progress of C;
			if lactation fetish is 1, increase the lactation rate of breasts by 1;
			if the training-progress of C >= 3:
				now the training-progress of C is 0;
				destroy C;
				ProgressUp senior robobellboy.

acolyte-chestpiece is a cupless bra. The text-shortcut of acolyte-chestpiece is "actp". acolyte-chestpiece is metal. Understand "gold", "golden", "acolyte", "acolyte's", "chestpiece" as acolyte-chestpiece. acolyte-chestpiece is unique. acolyte-chestpiece is top-exclusive. The printed name of acolyte-chestpiece is "[clothing-title-before]acolyte's chestpiece[clothing-title-after]". acolyte-chestpiece has a number called charge.

Definition: acolyte-chestpiece is class-relevant:
	if the class of the player is worshipper, decide yes;
	decide no.

To compute class set up of (C - acolyte-chestpiece):
	now C is cursed.

Definition: acolyte-chestpiece is demonic: decide yes.
Definition: acolyte-chestpiece is yellow themed: decide yes.

Figure of acolyte chestpiece is the file "Items/Clothes/Upper/Bras/cuplessbra2.png".

To decide which figure-name is clothing-image of (C - acolyte-chestpiece):
	decide on figure of acolyte chestpiece.

To decide which object is the unique-upgrade-target of (C - acolyte-chestpiece):
	decide on nothing.

To say ShortDesc of (B - acolyte-chestpiece):
	say "acolyte's chestpiece".

To compute SelfExamineDesc of (B - an acolyte-chestpiece):
	say " that hang freely out of your pointless gold cupless bra. ".

To say ClothingDesc of (C - acolyte-chestpiece):
	say "This curved set of gem-encrusted gold 'plates' [if C is worn]rides so low that it exposes your[otherwise]would ride so low that it would completely expose the wearer's[end if] nipples.[if the charge of C > 0][line break][end if][if the charge of C > 4]The metal is warm to the touch, and you can tell it has absorbed a great deal of power.[otherwise if the charge of C > 1]The metal is slightly warm to the touch, and you can tell it has absorbed a bit of power.[otherwise]The metal is surprisingly cool to the touch.[end if][line break]You get the feeling that you would sink like a rock if you tried swimming in this.";

To MagicSteal (C - acolyte-chestpiece):
	if the charge of C < 5:
		say "Your [printed name of C] absorbs the magical energy!";
		increase the charge of C by 1;
		if the charge of C is 5:
			say "A surge of warmth passes through the metal, and you get the feeling it has reached maximum power.";
	otherwise:
		increase the magic-power of the player by 1.

This is the worshipper tit slut rule:
	if acolyte-chestpiece is worn, increase the desirability of breasts by 5.
The worshipper tit slut rule is listed in the tit slut eligibility rules.

[any fluid touching the breasts will cause arousal and fatigue gain.]

To decide which number is the titfuck-addiction-influence of (C - acolyte-chestpiece):
	decide on 1 - the magic-modifier of C.

To decide which number is the delicateness-influence of (C - acolyte-chestpiece):
	decide on 2 - the magic-modifier of C.

To decide which number is the strength-influence of (C - acolyte-chestpiece):
	if the charge of C < 5:
		if the magic-modifier of C < 0, decide on the magic-modifier of C;
		decide on 0;
	otherwise:
		if the magic-modifier of C > 0, decide on the magic-modifier of C + 1;
		decide on 1.

portal-bra is a bra. portal-bra is unique. portal-bra is metal. The printed name of portal-bra is "[clothing-title-before]portal bra[clothing-title-after]". The text-shortcut of portal-bra is "ptb". Understand "portal", "bra" as portal-bra. The min size of portal-bra is 20. The max size of portal-bra is 20. The support of portal-bra is 0. portal-bra is fully covering. portal-bra is not-top-displacable.
portal-bra has a number called charge.

Definition: portal-bra is displacable: decide no.
Definition: portal-bra is cursable: decide no.
Definition: portal-bra is removable: decide no.
Definition: portal-bra is stealable: decide no.
Definition: portal-bra is grey themed: decide yes.
Definition: portal-bra is transformation-protected: decide yes.
Definition: portal-bra is fluid immune: decide yes.
Definition: portal-bra is breast hiding: decide yes.

To say ClothingDesc of (K - portal-bra):
	say "A gold frame secures a piece of concrete to your chest. You can feel that your breasts are... 'elsewhere'[if K is penetrating breasts]. There is currently someone fucking your breasts![end if].".

To compute SelfExamineDesc of (K - portal-bra):
	say ClothingDesc of K.

To say ShortDesc of (K - portal-bra):
	say "portal bra".

Figure of Portal Bra is the file "Items/Clothes/Upper/Bras/portalbra1.png".

To decide which figure-name is clothing-image of (C - portal-bra):
	decide on figure of Portal Bra.

To compute periodic effect of (C - portal-bra):
	increase the charge of C by 1;
	if the charge of C < 100 and the charge of C > (a random number between 30 and 50) and the player is not in Hotel44 and Hotel44 is not nearby:
		now the charge of C is 100;
		say "[bold type]Suddenly, you feel your breasts being grabbed by two rough hands, and squeezed around a hard [manly-penis]![line break][variable custom style][one of]Oh god, I'm being titfucked right now?![or]That pimp is selling my breasts for sex again![then at random][roman type][line break]";
		now C is penetrating breasts;
	compute school periodic effect of C.

To SilentSquirt (L - a liquid-object) On (C - portal-bra) by (N - a number):
	if N > 0:
		if inside-out is true:
			say "[announced L] disappears through the portal.";
			PuddleUp L by N in Hotel44;
		otherwise:
			if belly is unsoaked, say "[announced L] drips down onto your belly.";
			ContinuedSquirt L on Belly by N;

To compute school periodic effect of (C - portal-bra):
	if C is penetrating breasts:
		cutshow figure of pimp portal cutscene;
		let M be a random ultimate-lesson-actor;
		now M is penetrating breasts;
		dislodge C;
		say "[BigFuckerDesc of M] is [one of]massaging [his of M] [manly-penis] with your breasts[or]sliding [his of M] [manly-penis] in and out of your cleavage[or]roughly groping your breasts and fucking your cleavage[cycling]!";
		stimulate breasts from M;
		if playerRegion is school or the player is in Hotel44 or Hotel44 is nearby or the charge of C > a random number between 102 and 103:
			TimesSubmittedUp M by 1;
			say CleavageClimaxFlav of M;
			BreastsSensitivityUp 1;
			orgasm dislodge M;
			if the semen coating of breasts is 0, say "You feel someone wiping your breasts clean.";
			now the charge of C is 0;
			if pimp is not normally ally, FavourUp pimp;
		otherwise:
			now C is penetrating breasts;
			say StealthPortalSexFlav;
		dislodge M.

To say StealthPortalSexFlav:
	let P be a random reactive person;
	if P is a person, say "[one of][variable custom style]What would [FuckerDesc of P][variable custom style] say if [he of P] knew what was happening to me right now?[roman type][line break][or][or][cycling]".

black-lace-bra is a bra. black-lace-bra is unique. black-lace-bra is silk. The printed name of black-lace-bra is "[clothing-title-before]black lacy bra[clothing-title-after]". The text-shortcut of black-lace-bra is "blcb". Understand "black", "lacy", "bra" as black-lace-bra. The min size of black-lace-bra is 1. The max size of black-lace-bra is 14. The support of black-lace-bra is 2. black-lace-bra is low cut. black-lace-bra is optional-top-displacable.

black-lace-bra can be monified.
Definition: black-lace-bra is whore themed:
	if black-lace-bra is monified, decide yes;
	decide no.

Figure of black lace bra is the file "Items/Clothes/Upper/Bras/stripperbra1.png".
Figure of black lace bra money is the file "Items/Clothes/Upper/Bras/stripperbra2.png".
To decide which figure-name is clothing-image of (C - black-lace-bra):
	if C is monified, decide on figure of black lace bra money;
	decide on figure of black lace bra.

To say ClothingDesc of (K - black-lace-bra):
	say "A sexy black bra with narrow cups that spill out a lot of cleavage[if K is monified]. Several paper bills are sewn into the cups in a manner that makes it look like they have been tucked in[end if].".

To compute SelfExamineDesc of (K - black-lace-bra):
	say ClothingDesc of K.

To say ShortDesc of (K - black-lace-bra):
	say "lacy bra".

To decide which number is the initial outrage of (C - black-lace-bra):
	if C is monified, decide on 8;
	decide on 3.

Definition: black-lace-bra is upgradable:
	if it is not monified, decide yes;
	decide no.
Definition: black-lace-bra is transformation-protected:
	if it is not upgradable, decide yes;
	decide no.
Definition: black-lace-bra is disintegration-protected: decide yes.
Definition: black-lace-bra is loop-safe-upgradable:
	if it is upgradable, decide yes;
	decide no.
Definition: black-lace-bra is transformation-theme-blockable: decide no. [It doesn't care about the theme it shares with other items.]
To decide which object is the unique-upgrade-target of (C - black-lace-bra):
	decide on C.
To transform (C - black-lace-bra):
	say "Paper bills appear on the bra, sewn on and completely unremovable, but looking as though they've just been tucked into the cups!";
	now C is monified.

heart-strap-bra is a bra. heart-strap-bra is transformation-rare. heart-strap-bra is leather. The printed name of heart-strap-bra is "[clothing-title-before]heart strap bra[clothing-title-after]". The text-shortcut of heart-strap-bra is "hsb". Understand "heart", "strap", "bra" as heart-strap-bra. The min size of heart-strap-bra is 1. The max size of heart-strap-bra is 18. The support of heart-strap-bra is 1. heart-strap-bra is very low cut. heart-strap-bra is optional-top-displacable.

To say ClothingDesc of (K - heart-strap-bra):
	say "Pink leather straps in the shape of bra cups expose most of the wearer's breast flesh, but protect the nipples with strategically placed love-hearts.".

To compute SelfExamineDesc of (K - heart-strap-bra):
	say ClothingDesc of K.

To say ShortDesc of (K - heart-strap-bra):
	say "heart strap bra".

Figure of Heart Strap Bra is the file "Items/Clothes/Upper/Bras/heartbra1.png".

To decide which figure-name is clothing-image of (C - heart-strap-bra):
	decide on figure of Heart Strap Bra.

Definition: heart-strap-bra is pink themed: decide yes.
Definition: heart-strap-bra is heart themed: decide yes.

To decide which number is the initial outrage of (C - heart-strap-bra):
	decide on 6.

All Bras ends here.
