Player Title by Miscellaneous Frontend begins here.

To say TitleBimbo:
	if diaper quest is 1:
		say TitleBaby;
	otherwise:
		let T be the class of the player;
		let H be HairColourVagueText;
		say "[NameBimbo] the";
		if the number of worn cursed clothing > 4, say " unlucky";
		if fake-nails is worn and fake-nails is cursed, say " delicate";
		if the bimbo of the player < 4:
			say " [one of]determined[or]level headed[or]sensible[sticky random]";
		otherwise if the bimbo of the player < 7:
			say " [if submissive sex addict is 0][one of]confused[or]uneasy[or]curious[sticky random][otherwise]naturally submissive[end if]";
		otherwise if the bimbo of the player < 10:
			say " [one of]flirty[or]perky[or][if the intelligence of the player < 6]air-headed[otherwise]carefree[end if][or]naughty[sticky random]";
		if the player is incontinent:
			if the player is diapered:
				if a random worn diaper is cursed, say " permanently";
				if the incontinence of the player < 8 or diaper messing < 4, say " diaper bound";
				if the incontinence of the player > 7 and diaper messing >= 4, say "poopypantsed";[Better term?]
			otherwise:
				say " incontinent";
		otherwise if the player is diapered:
			say " diapered";
			if incontinence > 2, say " [if there is worn bed wetting clothing]bedwetting[otherwise]accident-prone[end if]";
		let B be 8;
		if extreme proportions fetish is 1, now B is 13;
		if the largeness of breasts > B:
			if the silicone volume of breasts > 10:
				if the silicone volume of breasts > 18 or fake-nails is worn, say " ultra plastic";
				otherwise say " surgically enhanced";
			otherwise:
				if the bimbo of the player < 10, say " [one of][if the player is sexed female]unfortunately endowed[otherwise]ample bosomed[end if][or]buxom[or]big breasted[sticky random]";
				otherwise say " [one of][if the player is sexed female]impressively endowed[otherwise]extremely top heavy[end if][or]famously big breasted[sticky random]";
				if the thickness of hips > 8, say " and";
		if the flesh volume of arms > 9:
			say " obese";
		otherwise if the flesh volume of arms > 7:
			say " overweight";
		otherwise if the flesh volume of arms > 5:
			say " fat";
		otherwise if the flesh volume of arms > 3:
			say " chubby";
		otherwise if the thickness of hips > 8:
			say " [one of][if the flesh volume of hips > 8]huge assed[otherwise]super curvy[end if][or]voluptuous[sticky random]";
		if there is a worn cursed chastity bond or there is a worn locked chastity bond:
			if T is not virgin warrior, say " [if the player is gendered male and the size of penis > 6]emasculated[otherwise][one of]locked away[or]chastised[sticky random][end if]";
		if the pregnancy of the player is 3:
			say " incubator";
		otherwise if the pregnancy of the player > 0:
			if the pregnancy of the player is 2:
				say " permanently";
			otherwise if the children of the player > 0:
				say " bred";
			say " pregnant";
		otherwise if childValue times 1 > 1:
			say " nurturing";
		otherwise if childValue times 2 > 1:
			say " motherly";
		if saved-flat-intelligence > 19:
			say " [if saved-flat-intelligence > 23]super [end if]genius";
		otherwise if the largeness of hair > 15:
			say " Rapunzel-like";
		otherwise if H is "pink" and the largeness of hair > 9:
			say " long pink haired";
		otherwise if H is "pink":
			say " pink haired";
		otherwise if the largeness of hair > 9:
			say " long haired";
		if the player is possessing a vagina:
			if the vaginalvirgin of the player is 1 and T is not virgin warrior:
				say " virgin";
			otherwise if the vaginalvirgin of the player is 1:
				say " chaste";
			otherwise if the virgin bonus of the player <= -1:
				if the children of the player is 0 and pregnancy fetish is 1 and the pregnancy of the player > 0 and the pregnancy of the player < 3, say " unwed";
				if the bimbo of the player < 9:
					say " shamefully deflowered";
				otherwise if the bimbo of the player < 14:
					say " broken in";
				otherwise:
					say " soulless";
		if the intelligence of the player < 3 and the raw intelligence of the player < 5:
			say " brainless[if the bimbo of the player > 13] bimbo[end if]";
		if the player is a butt slut and the analvirgin of the player is 0:
			if the player-class is not broodmother:
				say " butt slut";
			otherwise:
				say " abdomen slut";
		otherwise if the player is a pussy slut:
			say " craving cunt";
		otherwise if the player is a blowjob slut:
			say " orally fixated";
		otherwise if the player is a tit slut:
			say " fuckpillowed";
		otherwise if there is a worn tongue piercing:
			say " cock [if the oral sex addiction of the player < 6]swallowing[otherwise]hungry[end if]";
		otherwise if the intelligence of the player < 7 and H is "blonde":
			say " dumb blonde";
		otherwise if the bimbo of the player < 13 and the bimbo of the player > 10:
			say " [one of]bimbo[or][if the openness of asshole > 5]anal [end if]slut[or][if the openness of asshole > 5]anal [end if]whore[sticky random]";
		otherwise if the bimbo of the player < 18 and the bimbo of the player >= 13:
			if the player is a nympho:
				say " [one of]nymphomaniac[or][if the openness of asshole > 5]anal [end if]orgasm addict[or][if the openness of asshole > 5]anal [end if]sex addict[sticky random]";
			otherwise if the oral sex addiction of the player > 8:
				say " cock sucker";
			otherwise if the semen taste addiction of the player > 15:
				say " [one of]semen addict[or]cum drinker[sticky random]";
			otherwise:
				say " [one of]bimbo slut[or]dick lover[sticky random]";
		otherwise if the bimbo of the player > 17:
			say "";
		say "[run paragraph on]";
		if the player is flying:
			say " balloon";
		if the player is broken or the bimbo of the player > 17:
			say " [if highest body part sex addiction > the semen addiction of the player / 2][one of]collection of meat-holes[or]walking selection of fuckholes[or]cock sleeve[or]fuck toy[sticky random][otherwise][one of]cum dumpster[or]semen receptacle[or]human vacuum[sticky random][end if]";
		otherwise if the player is queen of spades and the bimbo of the player > 7:
			say " [if the player is presenting as male]jack[otherwise]queen[end if] of spades";
		if the player is a sissy:
			say " sissy[if the player is an adult baby] baby[end if]";
		otherwise if the player is an adult baby:
			if there are worn perceived messed knickers:
				say " messy baby";
			otherwise if the sex addiction of the player > 14 and the diaper addiction of the player > 14:
				say " diaper slut";
			otherwise:
				say " adult baby";
		if T is not "adventurer":
			if armband is worn, say " [armband-print of armband]";
			if T is "catgirl" and the player is gendered male and the bimbo of the player < 13 and the largeness of breasts < 6:
				say " catboy";
			otherwise if the player is a sissy and the player is queen of spades and the bimbo of the player > 7:
				say " [BlackCock] [if highest body part sex addiction > 7]addict[otherwise if the sex addiction of the player > 11]lover[otherwise]enthusiast[end if]";
			otherwise:
				say " [T]";
		otherwise if armband is worn:
			say " [armband-print of armband]";
		otherwise if there is a worn ballet related dress and there is a worn ballet related shoes:
			say " [if the player is presenting as male and the bimbo of the player < 9]ballet dancer[otherwise]ballerina[end if]";
		otherwise if whip-of-domination is worn and the player is feeling dominant:
			say " dominatrix";
		otherwise if newborn-wasp is an alive giant wasp:
			say " wasp [if the number of alive giant wasps < 4]queen[otherwise]broodmother[end if]";
		otherwise if the virgin bonus of the player <= -2:
			say " [one of]fallen[or]disgraced[sticky random] ex-warrior";
		otherwise if there is a worn T-shirt:
			if gang-bang-girl T-shirt is worn, say " gang bang girl";
			if there is a worn gender bender T-shirt, say " gender bender";
		otherwise if the bimbo of the player < 11:
			if the bimbo of the player < 5 or submissive sex addict is 1:
				say " adventurer";
			otherwise if the delicateness of the player > 12 and diaper lover is 0:
				say " delicate flower";
			otherwise if the player is possessing a vagina and the vaginalvirgin of the player is 0 and the analvirgin of the player is 1:
				say " anal virgin";
			otherwise if the size of penis > 7 and transGender is 0:
				say " [one of]shemale[or]dickgirl[sticky random]";
			otherwise:
				if the delicateness of the player > 11 and diaper lover >= 1:
					say " babygirl";
				otherwise if transGender is 0 and (the size of penis > 4 or the player is a flatchested trap):
					if the number of worn clothing > 4, say " cross-dressing";
					say " trap";
				otherwise:
					say " girl[if the player is presenting as male]yboy[end if]";
		if the player is a piss slut and T is not "toilet":
			say " urinal".

To say TitleBaby:
	let T be the class of the player;
	let H be HairColourVagueText;
	say "[NameBimbo] the";
	if the player is incontinent:
		if the player is diapered:
			if a random worn diaper is cursed, say " permanently";
			if the incontinence of the player < 8 or diaper messing < 4, say " diaper bound";
			if the incontinence of the player > 7 and diaper messing >= 4, say "poopypantsed";[Better term?]
		otherwise:
			say " incontinent";
	otherwise if the player is diapered:
		say " diapered";
		if incontinence > 2, say " [if there is worn bed wetting clothing]bedwetting[otherwise]accident-prone[end if]";
	if the number of worn cursed clothing > 4, say " unlucky";
	if the player is a sissy, say " sissy";
	if T is not "adventurer":
		if T is "" and the player is not a sissy, say " adult";
		otherwise say "[if the delicateness of the player > 16] baby[end if] [T]";
	otherwise if there is a worn ballet related dress and there is a worn ballet related shoes:
		say " [if the player is gendered male and the bimbo of the player < 9]ballet dancer[otherwise]ballerina[end if]";
	otherwise:
		if the bimbo of the player < 5 or submissive sex addict is 1:
			say " adventurer";
		otherwise if the delicateness of the player > 16:
			say " baby";
		otherwise if the delicateness of the player > 11:
			say " babygirl";
		otherwise if the size of penis > 4 or the player is a flatchested trap:
			if the number of worn clothing > 4, say " cross-dressing";
			say " trap";
		otherwise:
			say " girl[if the player is gendered male]yboy[end if]".

Player Title ends here.
