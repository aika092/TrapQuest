Demon Tail Plug by Sex Toys begins here.

demon tail plug is a tail plug. demon tail plug is rare. The printed name of demon tail plug is "[clothing-title-before][PlugSize size of item described] demon tail plug[clothing-title-after]". The text-shortcut of demon tail plug is "dtp".

Definition: demon tail plug is class-relevant:
	if the class of the player is succubus, decide yes;
	if the class of the player is princess and the class of the player is bride, decide yes;
	decide no.

Figure of demon tail plug is the file "Items/Accessories/Toys/demontail1.png".

To decide which figure-name is clothing-image of (C - demon tail plug):
	decide on figure of demon tail plug.

To say UniquePlugDesc of (P - demon tail plug):
	say "This [PlugSize size of P] butt plug has a long, red, snake-like tail coming out of the base, ending in a spade tip. [if P is worn and the class of the player is priestess][variable custom style]It makes me feel unclean...[otherwise if P is worn and the class of the player is princess][variable custom style]It makes me feel so vulnerable...[otherwise if P is worn][variable custom style]It makes me feel kind of powerful![end if][roman type][line break]".

To say ShortDesc of (P - demon tail plug):
	say "demon tail plug".
To say MediumDesc of (P - demon tail plug):
	say "demon tail butt plug".

To say ClassSummonFlav of (P - demon tail plug):
	say "You feel your [asshole] suddenly being invaded by a foreign object! Looking around you see a long, snake-like red tail hanging down between your legs.".

To decide which number is the delicateness influence of (P - demon tail plug):
	if the class of the player is priestess or the class of the player is princess, decide on 3;
	decide on 0.

To decide which number is the initial outrage of (P - demon tail plug):
	if the class of the player is succubus, decide on 7;
	decide on 14.

This is the demon tail pussy slut rule:
	if the player is possessing a vagina and there is demon tail plug penetrating asshole, rule succeeds.
The demon tail pussy slut rule is listed in the pussy slut eligibility rules.

To uniquely set up (C - demon tail plug):
	now the size of C is a random number between 3 and 8;
	now C is dominance.

To compute class set up of (C - demon tail plug):
	now the size of C is the openness of asshole;
	if the class of the player is princess:
		now C is expansion;
		now C is cursed;
		if the player is possessing a vagina:
			if vaginal-virginity-quest is appropriate, now the quest of C is vaginal-virginity-quest;
			otherwise now the quest of C is vaginal-creampie-quest;
		otherwise:
			if anal-virginity-quest is appropriate, now the quest of C is anal-virginity-quest;
			otherwise now the quest of C is anal-creampie-quest;
	otherwise:
		now C is dominance;
		now C is bland.

To compute periodic effect of (C - demon tail plug):
	if demon horns is off-stage and demon horns is actually summonable and a random number between (unlucky * 10) and 50 is 10:
		summon demon horns cursed;
		say "[bold type]You feel your hair parting as a pair of [if soulstone is not worn]felt [end if]horns materialise on your head!";
		if soulstone is worn:
			say "Your skin changes colour, to a demonic red tint... and you observe that the horns are not a fake piece of headgear, but real actual horns!!!";
			cutshow Figure of demon horns 3 for demon horns;
		otherwise:
			say "[variable custom style]This feels like a Halloween costume.[roman type][line break]".

Definition: demon tail plug is demonic: decide yes.
Definition: demon tail plug is red themed: decide yes.



nintendolls flag plug is a tail plug. nintendolls flag plug is unique. The printed name of nintendolls flag plug is "[clothing-title-before][PlugSize size of item described] nintendolls flagpole plug[clothing-title-after]". The text-shortcut of nintendolls flag plug is "nfp". Understand "flagpole" as nintendolls flag plug.

Figure of nintendolls flag plug is the file "Items/Accessories/Toys/flag1.png".
Figure of nintendolls flag plug cutscene is the file "Items/Accessories/Toys/flag2.jpg".

To decide which figure-name is clothing-image of (C - nintendolls flag plug):
	decide on figure of nintendolls flag plug.

To say UniquePlugDesc of (P - nintendolls flag plug):
	say "This [PlugSize size of P] butt plug has a short metal pole coming out the end, at the end of which hangs a flag with a logo of a pink ballerina wearing high heels on it.".

To say ShortDesc of (P - nintendolls flag plug):
	say "flagpole plug".
To say MediumDesc of (P - nintendolls flag plug):
	say "flagpole butt plug".

To decide which number is the stealth-influence of (C - nintendolls flag plug):
	if the player is prone, decide on -100;
	decide on -1.

To decide which number is the unworn outrage of (P - nintendolls flag plug):
	if the player is prone, decide on 20;
	decide on 10.

Definition: nintendolls flag plug is white themed: decide yes.


Demon Tail Plug ends here.
