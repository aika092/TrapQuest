Cheerleader Outfit by Dresses begins here.

A cheerleader outfit is a kind of overdress. A cheerleader outfit is unique. The skirt-length of a cheerleader outfit is 4. Understand "cheer", "leader" as cheerleader outfit.

Definition: a cheerleader outfit is class-relevant:
	if the class of the player is cheerleader, decide yes;
	decide no.

To say ClassSummonFlav of (C - a cheerleader outfit):
	say "[bold type]A [ShortDesc of C] appears on you![line break][if the mercyskill of the player is 0]You suddenly feel like you could get away with anything! It's like the consequences of your actions... just aren't as important as they used to be.[end if]".

To compute class set up of (C - a cheerleader outfit):
	if a random number between 4 and max breast size <= the largeness of breasts, now C is audible jiggles;
	now the raw-magic-modifier of C is the total volume of hips / 4.

To compute post transformation effect of (C - a cheerleader outfit):
	if C is blandness and a random number between 4 and max breast size <= the largeness of breasts:
		now C is audible jiggles;
		say "[bold type]You can hear a loud BOING sound every time your [BreastDesc] wobble. [roman type]This must be a [C]!".

To say ShortDesc of (C - a cheerleader outfit):
	say "cheerleader outfit".
To say MediumDesc of (C - a cheerleader outfit):
	say "provocative cheerleader outfit".

To compute SelfExamineDesc of (O - a cheerleader outfit):
	say "You are wearing a [ShortDesc of O]. ".

This is the cheerleaders upskirt fetish rule:
	if there is a worn cheerleader outfit and diaper focus is 0:
		if summoning is 0 and autowear is false, say "You can't get it on past your knees, your [printed name of random worn cheerleader outfit] is somehow magically resisting you wearing this![line break][variable custom style]Oh I get it, because of the stereotypical cheerleader upskirt fetish. People are supposed to catch glimpses of my [if there are worn knickers]undies[otherwise][player-crotch][end if]![roman type][line break]";
		rule fails.
The cheerleaders upskirt fetish rule is listed in the trousers wearability rules.

To set up influence of (C - a cheerleader outfit):
	now C is oral-sex-addiction-influencing.

To decide which number is the initial cringe of (C - a cheerleader outfit):
	decide on 3.

Part 1 - Longsleeved Cheerleader Outfit

longsleeved cheerleader outfit is a cheerleader outfit. longsleeved cheerleader outfit is only arm covering. longsleeved cheerleader outfit is fully covering.

The printed name of longsleeved cheerleader outfit is "[clothing-title-before]longsleeved cheerleader outfit[clothing-title-after]". The text-shortcut of longsleeved cheerleader outfit is "lco".

Figure of longsleeved cheerleader outfit is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader2.png".

To decide which number is the initial outrage of (C - longsleeved cheerleader outfit):
	decide on 3.

To decide which figure-name is clothing-image of (C - longsleeved cheerleader outfit):
	decide on figure of longsleeved cheerleader outfit.

To say ClothingDesc of (C - longsleeved cheerleader outfit):
	say "This red cheerleader outfit is made of a short flowing red skirt, a white long-sleeved top and a red sweater, all subtly sewn together into one piece of clothing.".

To decide which object is the unique-upgrade-target of (C - longsleeved cheerleader outfit):
	decide on vest-top cheerleader outfit.

Definition: longsleeved cheerleader outfit is red themed: decide yes.

Part 2 - Supershort Cheerleader Outfit

supershort cheerleader outfit is a cheerleader outfit. The skirt-length of supershort cheerleader outfit is 1. supershort cheerleader outfit is belly exposing.

The printed name of supershort cheerleader outfit is "[clothing-title-before]supershort cheerleader outfit[clothing-title-after]". The text-shortcut of supershort cheerleader outfit is "scho".

Figure of supershort cheerleader outfit is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader3.png".
Figure of DQ supershort cheerleader outfit is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader5.png".

To decide which number is the initial outrage of (C - supershort cheerleader outfit):
	decide on 10.

To decide which figure-name is clothing-image of (C - supershort cheerleader outfit):
	if diaper quest is 1, decide on figure of dq supershort cheerleader outfit;
	decide on figure of supershort cheerleader outfit.

Definition: supershort cheerleader outfit is blue themed:
	if diaper quest is 0, decide yes;
	decide no.
Definition: supershort cheerleader outfit is white themed:
	if diaper quest is 1, decide yes;
	decide no.

To say ClothingDesc of (C - supershort cheerleader outfit):
	if diaper quest is 1, say "This white cheerleader one-piece dress covers very little skin.";
	otherwise say "This blue cheerleader outfit consists of a vest top and a ridiculously short skirt. They seem intrinsically tied to each other - you find it's impossible to wear or remove one without the other.".

To say TransformReaction of (C - supershort cheerleader outfit):
	if the outrage of C is too humiliating:
		say "[variable custom style]Oh no, change it back! [if asshole is lewdly exposed or vagina is lewdly exposed]This is much too short, my naughty bits are on show! Eek![otherwise]This is much too short, it's not decent![end if][roman type][line break]";
	otherwise if C is almost too much:
		say "[variable custom style]I thought I would hate wearing such a ridiculously short skirt, but it's actually kind of exhilarating...[roman type][line break]";
	otherwise if the humiliation of the player < HUMILIATION-BROKEN:
		say "[variable custom style]The shorter the better! *giggle*[roman type][line break]".

Part 3 - Vest Top Cheerleader Outfit

vest-top cheerleader outfit is a cheerleader outfit.

The printed name of vest-top cheerleader outfit is "[clothing-title-before]vest top cheerleader outfit[clothing-title-after]". The text-shortcut of vest-top cheerleader outfit is "vtco". Understand "vest", "vest top" as vest-top cheerleader outfit.

Figure of vest top cheerleader outfit is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader4.png".

To decide which number is the initial outrage of (C - vest-top cheerleader outfit):
	decide on 4.

To decide which figure-name is clothing-image of (C - vest-top cheerleader outfit):
	decide on figure of vest top cheerleader outfit.

To say ClothingDesc of (C - vest-top cheerleader outfit):
	say "This black and white onepiece cheerleader outfit is very standard and bland; the skirt is quite short as you would expect from a cheerleader outfit but in reality it's a lot less slutty than it could have been. It has the word 'CHEERS' across the chest.".

Definition: vest-top cheerleader outfit is white themed: decide yes.

To decide which object is the unique-upgrade-target of (C - vest-top cheerleader outfit):
	decide on supershort cheerleader outfit.

Part 4 - Latex Cheerleader Outfits

A rubber cheerleader outfit is a kind of cheerleader outfit. Understand "cheerleader", "outfit" as a rubber cheerleader outfit. A rubber cheerleader outfit is low cut.

To decide which number is the initial outrage of (C - a rubber cheerleader outfit):
	decide on 6.

blue-rubber-cheerleader-outfit is a rubber cheerleader outfit. Understand "blue" as blue-rubber-cheerleader-outfit.

Figure of blue rubber cheerleader outfit is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader6.png".

The printed name of blue-rubber-cheerleader-outfit is usually "[clothing-title-before]blue latex cheerleader outfit[clothing-title-after]". The text-shortcut of blue-rubber-cheerleader-outfit is "blco".

To decide which figure-name is clothing-image of (C - blue-rubber-cheerleader-outfit):
	decide on figure of blue rubber cheerleader outfit.

To say ClothingDesc of (C - blue-rubber-cheerleader-outfit):
	say "This low cut blue cheerleader outfit is made of latex! It makes [if C is worn]you[otherwise]the wearer[end if] look like a bit like a fetish model. It has a white star on the chest.".

Definition: blue-rubber-cheerleader-outfit is star themed: decide yes.
Definition: blue-rubber-cheerleader-outfit is blue themed: decide yes.

To decide which object is the unique-upgrade-target of (C - blue-rubber-cheerleader-outfit):
	decide on black-rubber-cheerleader-outfit.

To decide which number is the initial outrage of (C - blue-rubber-cheerleader-outfit):
	decide on 5.

black-rubber-cheerleader-outfit is a rubber cheerleader outfit. Understand "black" as black-rubber-cheerleader-outfit.

Figure of black rubber cheerleader outfit is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader7.png".

The printed name of black-rubber-cheerleader-outfit is usually "[clothing-title-before]black latex cheerleader outfit[clothing-title-after]". The text-shortcut of black-rubber-cheerleader-outfit is "bkco".

To decide which figure-name is clothing-image of (C - black-rubber-cheerleader-outfit):
	decide on figure of black rubber cheerleader outfit.

To say ClothingDesc of (C - black-rubber-cheerleader-outfit):
	say "This low cut black cheerleader outfit is made of latex! It makes [if C is worn]you[otherwise]the wearer[end if] look like a bit like a fetish model. It has a red star on the chest.".

Definition: black-rubber-cheerleader-outfit is star themed: decide yes.
Definition: black-rubber-cheerleader-outfit is black themed: decide yes.

To decide which object is the unique-upgrade-target of (C - black-rubber-cheerleader-outfit):
	decide on black-fetish-cheerleader-outfit.

black-fetish-cheerleader-outfit is a rubber cheerleader outfit. Understand "black", "fetish" as black-fetish-cheerleader-outfit. black-fetish-cheerleader-outfit is high cut.

Figure of black fetish cheerleader outfit is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader8.png".

The printed name of black-fetish-cheerleader-outfit is usually "[clothing-title-before]black fetish cheerleader outfit[clothing-title-after]". The text-shortcut of black-fetish-cheerleader-outfit is "bfco".

To decide which figure-name is clothing-image of (C - black-fetish-cheerleader-outfit):
	decide on figure of black fetish cheerleader outfit

To decide which number is the initial outrage of (C - black-fetish-cheerleader-outfit):
	decide on 8.

To say ClothingDesc of (C - black-fetish-cheerleader-outfit):
	say "This black latex 'cheerleader outfit' is held together by black ribbon which zigzags across [if C is worn]your[otherwise]the wearer's[end if] sides, leaving them mostly bare. It seems more like a BDSM fetish item and less like a real cheerleader's garment...".

Definition: black-fetish-cheerleader-outfit is black themed: decide yes.


denim-overalls is a cheerleader outfit. Understand "overalls" as denim-overalls. denim-overalls is low cut. denim-overalls is denim. The skirt-length of denim-overalls is 0. denim-overalls is crotch-intact. denim-overalls is leg covering. denim-overalls is arm exposing. denim-overalls is normally-partially-nipple-covering.

Figure of denim overalls is the file "Items/Clothes/Upper/Special/Cheerleader/cheerleader9.png".

The printed name of denim-overalls is usually "[clothing-title-before]denim overalls[clothing-title-after]". The text-shortcut of denim-overalls is "dno".

To decide which figure-name is clothing-image of (C - denim-overalls):
	decide on figure of denim overalls.

To decide which number is the initial outrage of (C - denim-overalls):
	decide on 2.

To say ClothingDesc of (C - denim-overalls):
	say "This set of denim overalls has quite the narrow chest section. This means that someone with large breasts would be showing a huge amount of side boob, and it doesn't properly cover the nipples.".

To say ShortDesc of (C - denim-overalls):
	say "overalls".
To say MediumDesc of (C - denim-overalls):
	say "denim overalls".

Definition: denim-overalls is blue themed: decide yes.
Definition: denim-overalls is potentially-top-layer-concealing: decide no. [only partially]
Definition: denim-overalls is potentially-mid-layer-concealing: decide no. [only partially]

This is the can't displace bottom half of overalls first rule:
	if the noun is denim-overalls:
		if the noun is top-placed:
			if auto is 0, say "You would need to displace the top half first.";
			rule fails.
The can't displace bottom half of overalls first rule is listed first in the displacing rules.

Definition: denim-overalls is top-replacable:
	if it is crotch-displaced, decide no;
	if it is usually top-displacable and it is top-displaced and it is worn, decide yes;
	decide no.

To compute periodic effect of (C - denim-overalls):
	if C is crotch-displaced, now C is top-displaced.

Check topReplacing denim-overalls:
	if the noun is crotch-displaced, say "You would need to replace the bottom half first." instead.

This is the denim overalls wearability rule:
	if wearing-target is denim-overalls:
		if summoning is 0:
			repeat with S running through worn shoes:
				if autowear is false, say "You can't your [S] through the tight legs of [NameDesc of denim-overalls]!";
				rule fails.
The denim overalls wearability rule is listed in the overdress wearability rules.

This is the denim overalls removability rule:
	if wearing-target is denim-overalls:
		if summoning is 0:
			repeat with S running through worn shoes:
				if autoremove is false, say "You can't your [S] through the tight legs of [NameDesc of denim-overalls]!";
				rule fails.
The denim overalls removability rule is listed in the overdress removability rules.

Cheerleader Outfit ends here.
