Warrior's Chestpiece by Dresses begins here.

A warrior chestpiece is a kind of overdress. A warrior chestpiece is usually metal. A warrior chestpiece is usually unique. A warrior chestpiece is usually totally-exclusive. A warrior chestpiece is usually unskirted. A warrior chestpiece is usually belly exposing. A warrior chestpiece is usually purity. A warrior chestpiece is usually not-top-displacable. Understand "warrior's" as warrior chestpiece.

Definition: a warrior chestpiece is class-transformation-protected if the class of the player is virgin warrior.

Check wearing warrior chestpiece:
	if the vaginalvirgin of the player is 0, say "The latches refuse to close! It's almost as if the chestpiece is refusing to let you wear it..." instead.

To compute virginity-loss of (H - a warrior chestpiece):
	say "As the [H] falls from your [BreastDesc], you feel [if the virgin bonus of the player < 0]more of [end if]your willpower ripped from you.";
	decrease the virgin bonus of the player by 1;
	now H is in the location of the player.

To compute SelfExamineDesc of (C - a warrior chestpiece):
	say "You are wearing a [ShortDesc of C] over your chest, simultaneously emanating auras of purity and exhibitionism. ".

To say ShortDesc of (C - a warrior chestpiece):
	say "heavy [clothing-material of C] warrior's chestpiece".

To set up influence of (C - a warrior chestpiece):
	set up stat-based influence of C.

A holy warrior chestpiece is a kind of warrior chestpiece.

To uniquely set up (C - a holy warrior chestpiece):
	now C is protection.

An immodest warrior chestpiece is a kind of warrior chestpiece.

[!<TheVirginWarriorButtSlutRule>+

Once an immodest chestpiece is worn, anal sex becomes extremely much more likely.

+!]
This is the virgin warrior butt slut rule:
	if there is a worn immodest warrior chestpiece, increase the desirability of asshole by 20.
The virgin warrior butt slut rule is listed in the butt slut eligibility rules.

Part 1 - Ornate Warrior Chestpiece

An ornate warrior chestpiece is a kind of holy warrior chestpiece. An ornate warrior chestpiece is usually neck covering. An ornate warrior chestpiece is usually ridiculously low cut.

The printed name of ornate warrior chestpiece is usually "[clothing-title-before]ornate warrior's chestpiece[clothing-title-after]". The text-shortcut of ornate warrior chestpiece is "owc".

There is 1 ornate warrior chestpiece.

Figure of ornate warrior chestpiece is the file "Items/Clothes/Upper/Special/Warrior/warrior1.png".

To decide which figure-name is clothing-image of (C - an ornate warrior chestpiece):
	decide on figure of ornate warrior chestpiece.

To say ClothingDesc of (O - an ornate warrior chestpiece):
	say "This is less of a bra and more of a metal chestpiece, by beginning at your shoulders and your sides, travelling around your neck and underneath your bustline and narrowing and ending at your nipples from below, it manages to provide some protection to your upper chest whilst completely exposing essentially the entirety of your breasts. An ornate golden necklace shaped out of what seems to be hearts and religious iconography sits proudly on top of your cleavage, but hides nothing.[line break]It is impossible to wear other necklaces or collars whilst wearing this chestpiece.".

Definition: an ornate warrior chestpiece is heart themed: decide yes.

To decide which number is the initial outrage of (C - an ornate warrior chestpiece):
	decide on 8.

Definition: an ornate warrior chestpiece is grey themed: decide yes.
Definition: an ornate warrior chestpiece is gem themed: decide yes.

Part 2 - Leaf Pattern Warrior Chestpiece

A leaf pattern warrior chestpiece is a kind of holy warrior chestpiece. A leaf pattern warrior chestpiece is usually high cut. A leaf pattern warrior chestpiece is usually belly covering.

The printed name of leaf pattern warrior chestpiece is usually "[clothing-title-before]leaf pattern warrior's chestpiece[clothing-title-after]". The text-shortcut of leaf pattern warrior chestpiece is "lpwc".

There is 1 leaf pattern warrior chestpiece.

Figure of leaf pattern warrior chestpiece is the file "Items/Clothes/Upper/Special/Warrior/warrior3.png".

To decide which figure-name is clothing-image of (C - a leaf pattern warrior chestpiece):
	decide on figure of leaf pattern warrior chestpiece.

To say ClothingDesc of (O - a leaf pattern warrior chestpiece):
	say "This metal piece of 'armour' hangs from your neck, providing protection to your chest and belly. The leafy pattern around the belly makes it look quite provocative. It is very heavy.".

To decide which number is the initial outrage of (C - an leaf pattern warrior chestpiece):
	decide on 5.

To decide which number is the heaviness of (C - a leaf pattern warrior chestpiece):
	decide on 6.

To decide which object is the unique-upgrade-target of (C - a leaf pattern warrior chestpiece):
	decide on a random off-stage ornate warrior chestpiece.

Definition: a leaf pattern warrior chestpiece is grey themed: decide yes.
Definition: a leaf pattern warrior chestpiece is flower themed: decide yes.

Part 3 - Reinforced Warrior Chestpiece

A reinforced warrior chestpiece is a kind of holy warrior chestpiece. A reinforced warrior chestpiece is usually high cut. A reinforced warrior chestpiece is usually belly covering.

The printed name of reinforced warrior chestpiece is usually "[clothing-title-before]reinforced warrior's chestpiece[clothing-title-after]". The text-shortcut of reinforced warrior chestpiece is "rwc".

There is 1 reinforced warrior chestpiece.

Figure of reinforced warrior chestpiece is the file "Items/Clothes/Upper/Special/Warrior/warrior4.png".

To decide which figure-name is clothing-image of (C - a reinforced warrior chestpiece):
	decide on figure of reinforced warrior chestpiece.

To say ClothingDesc of (O - a reinforced warrior chestpiece):
	say "This metal piece of armour wraps around your waist and provides modesty and protection to your chest and belly. It is very heavy.".

To decide which number is the initial outrage of (C - a reinforced warrior chestpiece):
	decide on 2.

To decide which number is the heaviness of (C - a reinforced warrior chestpiece):
	decide on 5.

To decide which object is the unique-upgrade-target of (C - a reinforced warrior chestpiece):
	decide on a random off-stage leaf pattern warrior chestpiece.

Definition: a reinforced warrior chestpiece is grey themed: decide yes.

Part 4 - Plate Warrior Chestpiece

A plate warrior chestpiece is a kind of immodest warrior chestpiece. A plate warrior chestpiece is usually fully exposing.

The printed name of plate warrior chestpiece is usually "[clothing-title-before]plate warrior's chestpiece[clothing-title-after]". The text-shortcut of plate warrior chestpiece is "pwc".

There is 1 plate warrior chestpiece.

Figure of plate warrior chestpiece is the file "Items/Clothes/Upper/Special/Warrior/warrior2.png".

To decide which figure-name is clothing-image of (C - a plate warrior chestpiece):
	decide on figure of plate warrior chestpiece.

To say ClothingDesc of (O - a plate warrior chestpiece):
	say "This is less of a bra and more of a metal chestpiece. Metal plates curve under your bust but leave your nipples fully exposed. The chest-plate is extremely shiny, providing a perfect clear reflection of whatever light hits it. [if O is worn and the humiliation of the player > 37500]You can feel it holding your mind together, preventing you from completely snapping.[otherwise][line break][end if]".

To decide which number is the initial outrage of (C - a plate warrior chestpiece):
	decide on 9.

To decide which object is the unique-upgrade-target of (C - a plate warrior chestpiece):
	decide on a random off-stage hide warrior chestpiece.

Definition: a plate warrior chestpiece is grey themed: decide yes.

Part 5 - Leather Warrior Chestpiece

A hide warrior chestpiece is a kind of immodest warrior chestpiece. A hide warrior chestpiece is usually fully exposing. A hide warrior chestpiece is usually leather.

The printed name of hide warrior chestpiece is usually "[clothing-title-before]leather warrior's chestpiece[clothing-title-after]". The text-shortcut of hide warrior chestpiece is "lwc".

There is 1 hide warrior chestpiece.

Figure of hide warrior chestpiece is the file "Items/Clothes/Upper/Special/Warrior/warrior5.png".

To decide which figure-name is clothing-image of (C - a hide warrior chestpiece):
	decide on figure of hide warrior chestpiece.

To say ClothingDesc of (O - a hide warrior chestpiece):
	say "This leather chestpiece has two large holes made for [if O is worn]your[otherwise]the wearer's[end if] breasts to sit through, leaving them completely exposed. It is directly connected to a leather collar with a valuable looking gem in the centre. [if O is worn and the humiliation of the player > 37500]You can feel it holding your mind together, preventing you from completely snapping.[otherwise][line break][end if]".

To decide which number is the initial outrage of (C - a hide warrior chestpiece):
	decide on 10.

Definition: a hide warrior chestpiece is black themed: decide yes.

To say TransformReaction of (C - a hide warrior chestpiece):
	if the outrage of C is too humiliating:
		say "[variable custom style]No, this is even worse! If being a virgin warrior in this place means wearing such embarrassing things as this, maybe I don't want to be one...[roman type][line break]";
	otherwise if C is almost too much:
		say "[variable custom style]I don't feel proud to be [if breasts is lewdly exposed]showing off my breasts like[otherwise]wearing[end if] this, but it is kind of thrilling...[roman type][line break]";
	otherwise if the humiliation of the player < 40000:
		say "[variable custom style]Wow, even more outrageous... I can't believe how much wearing clothing like this is turning me on![roman type][line break]".

Part 6 - Golden Warrior Priestess Outfit

A golden warrior priestess outfit is a kind of immodest warrior chestpiece. There is 1 golden warrior priestess outfit. A golden warrior priestess outfit is usually metal. A golden warrior priestess outfit is usually super-short. A golden warrior priestess outfit is usually belly exposing. A golden warrior priestess outfit is usually ridiculously low cut. A golden warrior priestess outfit is usually totally-exclusive.

Definition: a golden warrior priestess outfit is yellow themed: [Is it significantly yellow?]
	decide yes.

Figure of golden warrior priestess outfit is the file "Items/Clothes/Upper/Special/Warrior/priestess5.png".

The printed name of golden warrior priestess outfit is usually "[clothing-title-before]golden warrior priestess outfit[clothing-title-after]". The text-shortcut of golden warrior priestess outfit is "gpo".

To decide which figure-name is clothing-image of (C - a golden warrior priestess outfit):
	decide on figure of golden warrior priestess outfit.

To say ClothingDesc of (C - a golden warrior priestess outfit):
	say "This bizarre metal outfit consists of just a few shiny constricting bars. It covers your nipples but not much else.".

To decide which number is the initial outrage of (C - a golden warrior priestess outfit):
	decide on 5.

Definition: a golden warrior priestess outfit is class-transformation-protected if the class of the player is virgin warrior or the class of the player is priestess.

To compute virginity-loss of (H - a golden warrior priestess outfit):
	say "As the [H] shatters into fragments, in a single instant you experience the lives of every single one of your clan's descendants. This scene is probably more extreme than most things in this game, do you want to read it? ";
	if the player is consenting:
		say "You experience the lives of each member of your clan, each one destined to spend her life as nothing more than ";
		if pregnancy fetish is 1:
			say "a broodmare for the monsters of this world: instead of becoming noble priestesses, they all instead chose to devote themselves to carnal pleasures, voluntarily surrendering themselves to any entity they could find. You experience a hundred thousand assaults in a thousand different ways by [if inhuman pregnancy is 2]everything from orcs to goblins to grotesque tentacled horrors[otherwise]men from every corner of the world - big cocks, small cocks, it's all the same after the first ten week-long gangbangs[end if]. Due to their obsessive sexual appetites they tend to be pregnant around the clock [if inhuman pregnancy is 2]and so they slowly fill the world with more and more of these horrible beasts from their own previously sacred wombs, [end if]and when their [if inhuman pregnancy is 2](human) [end if]daughters are old enough, their mothers watch with pride as they initiate themselves into the ranks of nymphomaniac breeding factories. And then eventually their daughters. And then eventually their daughters. For countless generations until the end of the world. ";
		otherwise:
			say "a set of fuckholes for the men of this world: instead of becoming noble priestesses, they all instead chose to devote themselves to carnal pleasures, setting up a sacred 'temple' where the only thing worshipped is cock. You experience a hundred thousand assaults in a thousand different ways. Your sisters are used by men from every corner of the world - big dicks, small dicks, it's all the same after the first ten week-long gangbangs. There are more men than there is time to service them and each of your clan are in use 24 hours a day. Their holy lineage prevents them from ageing, allowing them to continue to serve the hordes of men who come from far and wide to spill their seed inside and over the cursed women. For countless centuries until the end of the world. ";
		say "All of this - every single ounce of cum, every single forced orgasm as huge cocks brutally plow three sore holes, every last spank and nipple tweak and clit flick, many millennia worth of sleep deprivation, [if watersports fetish is 1]every single time an exhausted face is used as a urinal, [end if][if pregnancy fetish is 1]every painful, shameful birthing, [end if]every single degrading word of abuse hurled at any of your clan throughout time[if there is a worn tattoo], or permanently tattooed onto their flesh[end if], and the deep sense of perverse fulfilment that your sisters achieve from all of this happening to them - it all hits your soul at the same instant, like a bullet train forcing its way through the eye of a needle. Your mind, rather unsurprisingly, exits stage left. What is left of your frazzled brain is a shell of a human, with no self-respect and no ability to recognise herself as anything but an object.";
	otherwise:
		say "You lose all self-respect, becoming a sexually broken object to be used.";
	decrease the virgin bonus of the player by 1;
	now the humiliation of the player is 40001;
	if mindbreak protection is 1:
		say "But then you remember that you enabled mindbreak protection in the starting options of this virtual game. You feel something attaching itself to your mind almost like a fishing hook, reeling you back from the brink until you stabilise. You're still going to be a shameless exhibitionist from now on, but at least you'll be one who controls her own destiny, dammit!";
		now the humiliation of the player is 39999;
	only destroy H.

Part 7 - Virgin Magical Girl Outfit

virgin magical girl outfit is an immodest warrior chestpiece. virgin magical girl outfit is knee-length. virgin magical girl outfit is belly exposing. virgin magical girl outfit is low cut. virgin magical girl outfit is totally-exclusive.

Figure of virgin magical girl outfit is the file "Items/Clothes/Upper/Special/Warrior/magicaldress3.png".

The printed name of virgin magical girl outfit is usually "[clothing-title-before]virgin magical girl outfit[clothing-title-after]". The text-shortcut of virgin magical girl outfit is "vmgo".

To decide which figure-name is clothing-image of (C - virgin magical girl outfit):
	decide on figure of virgin magical girl outfit.

To say ClothingDesc of (C - virgin magical girl outfit):
	say "This flimsy floral outfit consists of just a few small patches of fabric stuck to you in strategic locations and a sheer skirt that only covers your backside. Overall it covers your nipples and crotch but not much else. Emeralds adorn the golden neckline and belt sections, emphasising its magical nature.".

To say ShortDesc of (C - virgin magical girl outfit):
	say "magical girl outfit".

To decide which number is the initial outrage of (C - virgin magical girl outfit):
	decide on 6.

Definition: virgin magical girl outfit is class-transformation-protected if the class of the player is virgin warrior or the class of the player is magical girl.

Definition: virgin magical girl outfit is slitted: decide yes.
Definition: virgin magical girl outfit is gem themed: decide yes.
Definition: virgin magical girl outfit is flower themed: decide yes.
Definition: virgin magical girl outfit is white themed: decide yes.

To compute virginity-loss of (H - virgin magical girl outfit):
	say "As the [ShortDesc of H] dissolves into nothingness, [bold type]a burst of dark magical energy shoots out from your body and surges into every tentacle monster in the world, empowering them and growing them to their maximum size. [roman type]You shiver with [joy (the bimbo of the player - 3)] [if there is a tentacle monster penetrating a fuckhole]as the tentacle inside you doubles and triples in size![otherwise if there is a tentacle monster in the location of the player]at the prospect of what's about to happen to you...[otherwise]at the prospect of having to battle your way through this place now that it is filled with very powerful and very very big tentacle monstrosities.[end if]";
	repeat with M running through alive tentacle monsters:
		now M is massive;
		set up M;
	decrease the virgin bonus of the player by 1;
	only destroy H.

Warrior's Chestpiece ends here.
