Onesie by Dresses begins here.

A onesie is a kind of overdress. A onesie is usually transformation-rare. A onesie is usually crotch-intact. A onesie is usually unskirted. A onesie is fully covering. A onesie is usually only arm covering. A onesie is usually leg covering. A onesie is usually not-top-displacable. The soak-limit of a onesie is usually 30. A onesie has a number called charge.

The printed name of onesie is usually "[clothing-title-before]onesie[clothing-title-after]". The text-shortcut of onesie is "one".

To compute SelfExamineDesc of (C - a onesie):
	say "You are wearing a [ShortDesc of C]. ".

To say ShortDesc of (C - a onesie):
	say "soft pink onesie".

To decide which number is the initial outrage of (C - a onesie):
	if diaper quest is 0, decide on the initial cringe of C;
	decide on 0.

To decide which number is the heel-skill-influence of (O - a onesie):
	let X be -2;
	increase X by the magic-modifier of O;
	decide on X.

To uniquely set up (C - a onesie):
	repair C;
	now C is absorption.

To decide what number is the original price of (C - a onesie):
	decide on 20.

Definition: a onesie is sissifying: decide yes.

To set up influence of (C - a onesie):
	now C is diaper-addiction-influencing.

Definition: a onesie is fetish appropriate if diaper lover > 0.
Definition: a onesie is baby themed: decide yes.

[!<TheOnesieBlowjobSlutRule>+

A onesie increases the chance of oral sex.

+!]
This is the onesie blowjob slut rule:
	if there is a worn onesie, increase the desirability of face by 10.
The onesie blowjob slut rule is listed in the blowjob slut eligibility rules.

To compute periodic effect of (L - a onesie):
	if the number of worn diapers is 0 and the player is not in danger:
		increase the charge of L by 1;
		if the charge of L > 10:
			now the charge of L is 0;
			let D be a random eligible diaper;
			if D is diaper:
				if D is actually summonable:
					summon D;
					say "[bold type]You feel a [D] [bold type]appear out of nowhere under your [L][bold type]![roman type][line break]".

To decide which object is the unique-upgrade-target of (C - a onesie):
	decide on nothing.


Part 1 - Velcro Onesie

A velcro onesie is a kind of onesie. There is 1 velcro onesie. A velcro onesie is usually crotch-exposing. The text-shortcut of velcro onesie is "von".

The printed name of velcro onesie is usually "[clothing-title-before]velcro onesie[clothing-title-after]".

Figure of velcro onesie is the file "Items/Clothes/Upper/Special/Onesies/onesie2.png".

To decide which figure-name is clothing-image of (C - a velcro onesie):
	decide on figure of velcro onesie.

To say ClothingDesc of (C - a velcro onesie):
	say "A pink onesie which fully covers your arms and legs. A large open flap at the back [if C is worn]exposes[otherwise]would expose[end if] your [if there is worn top level protection clothing][random worn top level protection clothing][otherwise][fuckholes][end if] to the world. It has a velcro strip which looks like it should be able to close, but in fact the velcro doesn't stick at all. This means it [if C is worn]looks[otherwise]would look[end if] like you've purposefully left it open so everyone can see inside. [if C is worn and the player is a blowjob slut]You can feel the onesie somehow magically encouraging the monsters in this game to use your face for sex.[otherwise][line break][end if]".

To uniquely set up (C - a velcro onesie):
	now C is absorption.

To decide which number is the initial cringe of (C - a velcro onesie):
	decide on 12.

To decide which object is the unique-upgrade-target of (C - a velcro onesie):
	decide on a random off-stage waddle outfit.

Definition: a velcro onesie is pink themed: decide yes.


A frilly onesie is a kind of onesie. There is 1 frilly onesie. A frilly onesie is usually zippable. The printed name of frilly onesie is usually "[clothing-title-before]frilly onesie[clothing-title-after]". The text-shortcut of frilly onesie is "fon".

Figure of frilly onesie is the file "Items/Clothes/Upper/Special/Onesies/onesie3.png".

To decide which figure-name is clothing-image of (C - a frilly onesie):
	decide on figure of frilly onesie.

To say ClothingDesc of (C - a frilly onesie):
	say "A pink onesie which goes halfway up your arms and legs. The onesie has white frills at the neck, arms and leg holes. A childish pattern repeats itself across the fabric. A full crotch length zip allows for easy access for [if the player is a pervert and diaper quest is 0]sex[otherwise]diaper changes[end if]. [if C is worn and the player is a blowjob slut]You can feel the onesie somehow magically encouraging the monsters in this game to use your face for sex.[otherwise][line break][end if]".

To decide which object is the unique-upgrade-target of (C - a frilly onesie):
	decide on a random off-stage velcro onesie.

To decide which number is the initial cringe of (C - a frilly onesie):
	decide on 9.

Definition: a frilly onesie is pink themed: decide yes.

A little dinosaur onesie is a kind of onesie. There is 1 little dinosaur onesie. A little dinosaur onesie is usually leg exposing. A little dinosaur onesie is usually arm exposing. A little dinosaur onesie is manly. The text-shortcut of little dinosaur onesie is "don".

The printed name of little dinosaur onesie is usually "[clothing-title-before]little dinosaur onesie[clothing-title-after]".

Figure of dinosaur onesie is the file "Items/Clothes/Upper/Special/Onesies/onesie1.png".

To decide which figure-name is clothing-image of (C - a little dinosaur onesie):
	decide on figure of dinosaur onesie.

To say ShortDesc of (C - a onesie):
	say "soft white onesie".

To say ClothingDesc of (C - a little dinosaur onesie):
	say "A white onesie with a repeating dinosaur pattern, which includes the words 'little dinosaur' plastered all over it.[if C is crotch-ripped]A huge rip at the bottom of the onesie means that it unfortunately doesn't protect the crotch region.[otherwise if C is worn]A wide crotch makes it feel like you're wearing a second diaper over the first.[end if][if C is worn and the player is a blowjob slut]You can feel the onesie somehow magically encouraging the monsters in this game to use your face for sex.[end if]".

To decide which object is the unique-upgrade-target of (C - a little dinosaur onesie):
	decide on a random frilly onesie.

Definition: a little dinosaur onesie is dinosaur themed: decide yes.
Definition: a little dinosaur onesie is white themed: decide yes.

Onesie ends here.
