Crotchless Panties by Knickers begins here.

a crotchless-panties is a kind of knickers. The armour of crotchless-panties is 1. Understand "pair", "pair of", "crotchless", "panties" as crotchless-panties. The soak-limit of a crotchless-panties is usually 3. a crotchless-panties is usually crotch-exposing. A crotchless-panties is usually transformation-rare. a crotchless-panties has a number called walkCharge.

Definition: a crotchless-panties is same-type if theme-share-target is knickers. [All knickers can end up getting transformed into useless crotchless panties!]

Definition: a crotchless-panties is fetish appropriate if diaper quest is 0.

To compute SelfExamineDesc of (K - a crotchless-panties):
	say "A pair of crotchless panties does nothing to provide any modesty to your [ShortDesc of hips]. ";

To say selfexamineuniquetitle of (K - a crotchless-panties):
	say "pair of crotchless panties".

To uniquely set up (C - a crotchless-panties):
	if a random number between 1 and 3 is 1, now C is protection.

pearl crotchless-panties is a crotchless-panties. The printed name of pearl crotchless-panties is "[clothing-title-before]pair of stimulating crotchless panties[clothing-title-after]". Understand "stimulating" as pearl crotchless-panties. The text-shortcut of pearl crotchless-panties is "stcp".
Definition: pearl crotchless-panties is pink themed: decide yes.
Definition: pearl crotchless-panties is gem themed: decide yes.
buckled crotchless-panties is a crotchless-panties. The printed name of buckled crotchless-panties is "[clothing-title-before]pair of buckled crotchless panties[clothing-title-after]".The text-shortcut of buckled crotchless-panties is "bcp".
Definition: buckled crotchless-panties is black themed: decide yes.
crotch chain is a crotchless-panties. The printed name of crotch chain is "[clothing-title-before]crotch chain[clothing-title-after]".The text-shortcut of crotch chain is "cch".
Definition: crotch chain is red themed: decide yes.
black-and-red lace crotchless-panties is a crotchless-panties. The printed name of black-and-red lace crotchless-panties is "[clothing-title-before]pair of black and red lace crotchless panties[clothing-title-after]".The text-shortcut of black-and-red lace crotchless-panties is "brcp". Understand "black and", "red" as black-and-red lace crotchless-panties.
Definition: black-and-red lace crotchless-panties is red themed: decide yes.
Definition: black-and-red lace crotchless-panties is black themed: decide yes.
sheer-black crotchless-panties is a crotchless-panties. The printed name of sheer-black crotchless-panties is "[clothing-title-before]pair of sheer black crotchless panties[clothing-title-after]".The text-shortcut of sheer-black crotchless-panties is "sbcp". Understand "sheer", "black" as sheer-black crotchless-panties. sheer-black crotchless-panties is sheer.
Definition: sheer-black crotchless-panties is black themed: decide yes.

To decide which figure-name is clothing-image of (C - pearl crotchless-panties):
	decide on figure of crotchless-panties 1.
To decide which figure-name is clothing-image of (C - buckled crotchless-panties):
	decide on figure of crotchless-panties 2.
To decide which figure-name is clothing-image of (C - crotch chain):
	decide on figure of crotchless-panties 3.
To decide which figure-name is clothing-image of (C - black-and-red lace crotchless-panties):
	decide on figure of crotchless-panties 4.
To decide which figure-name is clothing-image of (C - sheer-black crotchless-panties):
	decide on figure of crotchless-panties 5.

To say ClothingDesc of (K - pearl crotchless-panties):
	say "This crotchless g-string has a line of pearls down the middle[if the player is possessing a vagina and K is worn] that stimulate your [vagina] as you walk[end if]. It hides absolutely nothing.".

To say ClothingDesc of (K - buckled crotchless-panties):
	say "This black crotchless g-string has a frill and stylish buckle at the waist. It hides absolutely nothing.".

To say ClothingDesc of (K - crotch chain):
	say "This underwear is literally just a chain attached to a belt. [if the player is possessing a vagina and K is worn]The chain sits inside the entrance to your [vagina] and stimulates you as you walk. [end if]It hides absolutely nothing.".

To say ClothingDesc of (K - black-and-red lace crotchless-panties):
	say "This pair of lace knickers is already sexy, but the crotchless parting makes it downright slutty. It hides absolutely nothing.".

To say ClothingDesc of (K - sheer-black crotchless-panties):
	say "This black underwear is so sheer that it would be slutty even if there wasn't a huge gap at the crotch. It hides absolutely nothing.".

To say selfexamineuniquetitle of (K - crotch chain):
	say "slutty crotch chain".

Figure of crotchless-panties 1 is the file "Items/Clothes/Lower/Underwear/Skimpy/Crotchless/crotchless1.png".
Figure of crotchless-panties 2 is the file "Items/Clothes/Lower/Underwear/Skimpy/Crotchless/crotchless2.png".
Figure of crotchless-panties 3 is the file "Items/Clothes/Lower/Underwear/Skimpy/Crotchless/crotchless3.png".
Figure of crotchless-panties 4 is the file "Items/Clothes/Lower/Underwear/Skimpy/Crotchless/crotchless4.png".
Figure of crotchless-panties 5 is the file "Items/Clothes/Lower/Underwear/Skimpy/Crotchless/crotchless5.png".

To decide which number is the initial outrage of (C - a crotchless-panties):
	if C is walking-stimulating, decide on 15;
	decide on 14.

Definition: a crotchless-panties is end of transformation chain: decide yes. [It's the end of the transformation chain. This will make some things avoid bothering to attempt to transform it.]

To set up influence of (C - a crotchless-panties):
	set up sex-based influence of C.

To say TransformReaction of (C - a crotchless-panties):
	if the outrage of C is too humiliating:
		say "[variable custom style]Come on, what is even the point of these?! They don't help me in any way at all. [if C is cursed]I wish I could[otherwise]I guess I should probably[end if] take them off and wear a normal pair...[roman type][line break]";
	otherwise if C is almost too much:
		say "[variable custom style]What does it say about me that I kind of like the way this looks, even though it's completely useless?[roman type][line break]";
	otherwise if the humiliation of the player < 40000:
		say "[variable custom style]Now THIS is the sort of underwear that a gal like me likes to be seen in! Sweet![roman type][line break]".

Definition: a crotchless-panties is walking-stimulating: decide no.
Definition: crotch chain is walking-stimulating: decide yes.
Definition: pearl crotchless-panties is walking-stimulating: decide yes.

Report going when the player is female and the player is upright and there is worn walking-stimulating crotchless-panties:
	let C be a random worn walking-stimulating crotchless-panties;
	increase the walkCharge of C by 1;
	if the walkCharge of C > 10:
		say "Your [MediumDesc of C] [one of]stimulates your [vagina][or]continues to stimulate you[cycling] as you walk.";
		passively stimulate vagina from C;
		now the walkCharge of C is 0.

cum dump's undergarment is a crotchless-panties. cum dump's undergarment is unique. The armour of cum dump's undergarment is 1. The printed name of cum dump's undergarment is usually "[clothing-title-before]unholy cum dump's undergarment[clothing-title-after]". Figure of unholy panties is the file "Items/Clothes/Lower/Underwear/Skimpy/Crotchless/crotchless6.png". Understand "unholy" as cum dump's undergarment. The text-shortcut of cum dump's undergarment is "uu".

Definition: cum dump's undergarment is uncovered themed: decide yes.
Definition: cum dump's undergarment is butterfly themed: decide yes.
Definition: cum dump's undergarment is white themed: decide yes.

To compute SelfExamineDesc of (K - cum dump's undergarment):
	say "A pair of unholy crotchless panties does nothing to provide any modesty to your [ShortDesc of hips]. ";

To decide which figure-name is clothing-image of (C - cum dump's undergarment):
	decide on figure of unholy panties.

To say ClothingDesc of (K - cum dump's undergarment):
	say "This white pair of crotchless panties has a butterfly pattern above the crotch, giving it an innocent look. But you somehow know that it's much more than that, possessing incredible unholy powers.".

To decide which number is the initial outrage of (C - cum dump's undergarment):
	decide on 14.

Definition: cum dump's undergarment is product if it is fetish appropriate.

To decide which number is the alchemy key of (C - cum dump's undergarment):
	if C is product, decide on 18;
	decide on 0.

Definition: cum dump's undergarment is recipe specific: decide yes.

To decide which text is the RecipeDesc of (B - cum dump's undergarment):
	decide on "The recipe just says 'Cumsoaked or crotchless underwear'.".

This is the cum dump's undergarment specific recipe rule: [We don't check for fetish appropriateness because in DQ this sets up the diaper cover specific recipe]
	now the Product in row 32 of the Table of Alchemy is 18;
	now the Recipe in row 32 of the Table of Alchemy is 1.
The cum dump's undergarment specific recipe rule is listed in the specific recipe rules.

To compute recipe specific cursing of (T - cum dump's undergarment):
	if the noun is knickers:
		if the semen-soak of T > 0 and T is no protection:
			now T is blessed;
		otherwise if the semen-soak of T > 0 or T is no protection:
			now T is bland;
		otherwise:
			now T is cursed;
			now T is dressup;
	otherwise:
		now T is dressup;
		now T is cursed.

To set up influence of (C - cum dump's undergarment):
	now C is semen-addiction-influencing.

Crotchless Panties ends here.
