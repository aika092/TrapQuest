Plug Panties by Knickers begins here.

plug panties is a kind of knickers. Understand "pair of", "plug", "panties" as plug panties. The plug size of plug panties is usually 2. A plug panties is latex.

To compute SelfExamineDesc of (K - a plug panties):
	say "A pair of latex plug panties covers your [ShortDesc of hips]. ";

To decide which number is the initial outrage of (C - a plug panties):
	decide on 1.
To decide which number is the initial armour of (C - a plug panties):
	decide on 15.

To say Plug Size (N - a number) Desc:
	if N is 1, say "";
	if N is 2, say "";
	if N is 3, say "";
	if N is 4, say "";
	if N is 5, say "";
	if N is 6, say "";
	if N is 7, say "";
	if N is 8, say "";
	if N is 9, say "";
	if N is 10, say "".

To say selfexamineuniquetitle of (K - a plug panties):
	say "pair of plug panties".

Definition: a plug panties (called C) is application appropriate:
	if early-bondage-check is true:
		if C is vagina plugging and vagina is an actual target, decide no;
		if C is ass plugging and asshole is an actual target, decide no;
	if C is fetish appropriate and C is actually autowearable, decide yes;
	decide no.

Chapter 1 Vibrating Plug Panties

vibrating plug panties is a kind of plug panties.
There are 2 unique ass plugging vagina plugging vibrating plug panties. The printed name of vibrating plug panties is usually "[clothing-title-before]pair of [clothing-material of item described] plug panties[clothing-title-after]". The printed plural name of vibrating plug panties is usually "[clothing-title-before]pairs of [clothing-material of item described] plug panties[clothing-title-after]". The text-shortcut of vibrating plug panties is "lpp". A vibrating plug panties has a number called charge.

Definition: a vibrating plug panties is black themed: decide yes.

To decide which figure-name is clothing-image of (C - a vibrating plug panties):
	[if the player is not possessing a vagina, decide on figure of male latex plug panties;]
	decide on figure of female latex plug panties.

To say ClothingDesc of (P - a vibrating plug panties):
	say "[if the player is possessing a vagina]These panties have inbuilt anal and vaginal plugs that would prevent anything escaping[otherwise]These panties have an in-built anal plug that would prevent anything escaping[end if]. [if P is cursed and P is sure and P is worn]The ass plug is vibrating! [end if]The latex material looks very tough.".

To compute SelfExamineDesc of (K - a vibrating plug panties):
	say "A pair of latex plug panties covers your [ShortDesc of hips]. ";
	say "The in-built plugs keep your [if the player is possessing a vagina][vagina] and [end if][asshole] open and [if the soreness of asshole > 6 or the soreness of vagina > 6]sore[otherwise]stimulated[end if][if the largeness of belly > 2], and prevent any [semen] from escaping your body[end if]. [if K is cursed and the bimbo of the player < 12][line break][first custom style]Oh god, I can't stop the vibrating! This is bad... [roman type][line break][otherwise if K is cursed and the bimbo of the player > 11][line break][second custom style]It just keeps vibrating in my [asshole]... I'm going insane... [roman type][line break][end if]";

Figure of female latex plug panties is the file "Items/Clothes/Lower/Underwear/Special/Plug/latexplugpanties1.png".
[Figure of male latex plug panties is the file "Items/Clothes/Lower/Underwear/Special/Plug/latexplugpanties2.png".]

To say CurseCurseFlav of (B - a vibrating plug panties):
	if B is in the location of the player, say "The ass plug of the latex plug panties starts vibrating!!".

To say BlessBlandFlav of (B - a vibrating plug panties):
	if B is in the location of the player and B is sure, say "The ass plug of the latex plug panties stops vibrating.".

To compute periodic effect of (P - a vibrating plug panties):
	increase the charge of P by 2;
	if P is cursed, increase the charge of P by 1;
	if P is blessed, decrease the charge of P by 1;
	if the charge of P > 44:
		now the charge of P is 0;
		if the soreness of asshole is 10:
			say "[if P is cursed]The ass plug of your [printed name of P] is vibrating! [end if]Your poor [asshole] spasms. It's so incredibly sensitive!";
			stimulate asshole from P;
		otherwise:
			say "[if P is cursed]The ass plug of your [printed name of P] is vibrating! [end if]Your [asshole] spasms. Wearing [if P is cursed]it is very quickly[otherwise]the [printed name of P] is slowly[end if] making you sore!";
			ruin asshole;
		if the player is possessing a vagina:
			if the soreness of vagina is 10:
				say "Your [vagina] spasms. It's so sensitive, the [printed name of P] are making you go crazy with a mixture lust and frustration!";
				stimulate vagina from P;
			otherwise:
				say "Your [vagina] spasms. Wearing the [printed name of P] is slowly making you sore!";
				ruin vagina.

Chapter 2 Pump Plug Panties

pump plug panties is a kind of plug panties. The printed name of pump plug panties is "[clothing-title-before]pair of [DongSize the plug size of item described] [clothing-material of item described] pump plug panties[clothing-title-after]". The text-shortcut of pump plug panties is "ppp". There is 1 unique ass plugging latex pump plug panties.

Definition: a pump plug panties is black themed: decide yes.
Definition: a pump plug panties is inflation themed: decide yes.

To decide which figure-name is clothing-image of (C - a pump plug panties):
	decide on figure of pump plug panties.

To say ClothingDesc of (P - a pump plug panties):
	say "These panties have an in-built anal [if P is vagina plugging and the player is possessing a vagina]and vaginal [end if]plug that would prevent anything escaping. [if the player is not in a predicament room]There is a hollow tube through the centre of the ass plug with a hole at the base and the tip. [end if]The plug[if P is vagina plugging and the player is possessing a vagina]s are[otherwise] is[end if] [DongSize the plug size of P]. The latex material looks very tough.".
Figure of pump plug panties is the file "Items/Clothes/Lower/Underwear/Special/Plug/pumpplugpanties1.png".

Carry out kneeling when there is a worn pump plug panties:
	if the player is not in a predicament room:
		let P be a random worn pump plug panties;
		if inflation fetish is 1:
			say "The plug of your [printed name of P] blows some [one of][or]more [stopping]air straight up your [asshole]!";
			let B be the largeness of belly;
			AssFill 4 Air;
			if the largeness of belly > B and the largeness of belly > 2, say "You now have a [BellyDesc].";
		otherwise if the plug size of P < 10:
			say "Your [printed name of P] inflates a bit! The plug[if P is vagina plugging and the player is possessing a vagina]s are[otherwise] is[end if] now [DongSize the plug size of P].";
			increase the plug size of P by 1.

Chapter 3 WC plug panties

WC-plug-panties is a plug panties. WC-plug-panties is unique. WC-plug-panties is ass plugging. WC-plug-panties is vagina plugging. The printed name of WC-plug-panties is "[clothing-title-before]pair of [clothing-material of item described] plug panties[clothing-title-after]". The text-shortcut of WC-plug-panties is "plp".

Definition: WC-plug-panties is class-relevant:
	if the class of the player is human toilet, decide yes;
	decide no.

Figure of female WC-plug-panties is the file "Items/Clothes/Lower/Underwear/Special/Plug/WcLatexPant.png".
Figure of male WC-plug-panties is the file "Items/Clothes/Lower/Underwear/Special/Plug/WcLatexPant2.png".

To decide which figure-name is clothing-image of (C - WC-plug-panties):
	if the player is not possessing a vagina, decide on figure of male WC-plug-panties;
	otherwise decide on figure of female WC-plug-panties.

To say ClothingDesc of (P - WC-plug-panties):
	say "[if the player is possessing a vagina]These white latex panties have inbuilt anal and vaginal plugs that would prevent anything escaping[otherwise]These white latex panties have an in-built anal plug that would prevent anything escaping[end if]. The latex material looks very tough.".

To compute SelfExamineDesc of (K - WC-plug-panties):
	say "A pair of white latex plug panties covers your [ShortDesc of hips]. The in-built plugs keep your [if the player is possessing a vagina][vagina] and [end if][asshole] open and [if the soreness of asshole > 6 or the soreness of vagina > 6]sore[otherwise]stimulated[end if][if the semen volume of belly > 0], and prevent any [semen] from escaping your [asshole][end if]. You can sense that they have some magic power which can keep you from getting too sore. But how to activate it?".

To set up influence of (C - WC-plug-panties):
	now C is sex-addiction-influencing.

Definition: a WC-plug-panties is toilet themed: decide yes.
Definition: a WC-plug-panties is white themed: decide yes.

To say WCPantiesVibeFlav:
	say "[variable custom style][one of][if the anal sex addiction of the player < 4]Holy crap, I can't believe how good this feels...[otherwise]Mmm, so good...[end if][or][if the player is a pervert]I've been a good [boy of the player], and this is my reward![otherwise]It's making wetting myself feel amazing...![end if][or][if the player is horny]I'm going to cum soon![otherwise]Mmmm. More...[end if][then at random][roman type][line break]".


anal-beads-plug-panties is a knickers. anal-beads-plug-panties is unique. anal-beads-plug-panties is ass plugging. anal-beads-plug-panties is crotch-assless. anal-beads-plug-panties is sheer. The printed name of anal-beads-plug-panties is usually "[clothing-title-before]pair of anal beads plug panties[clothing-title-after]". The text-shortcut of anal-beads-plug-panties is "abpp". Understand "anal", "beads", "plug", "panties" as anal-beads-plug-panties.
Figure of anal-beads-plug-panties is the file "Items/Clothes/Lower/Underwear/Special/Plug/analbeadsplugpanties1.png".
Figure of anal-beads-plug-panties inserted is the file "Items/Clothes/Lower/Underwear/Special/Plug/analbeadsplugpanties2.png".

To decide which figure-name is clothing-image of (C - anal-beads-plug-panties):
	if C is penetrating asshole, decide on figure of anal-beads-plug-panties inserted;
	decide on figure of anal-beads-plug-panties.

Definition: anal-beads-plug-panties is black themed: decide yes.
Definition: anal-beads-plug-panties is transformation-protected: decide yes.

To say ClothingDesc of (P - anal-beads-plug-panties):
	say "This black sheer backless thong has a set of pink anal beads ending in a small plug instead of a normal rear thong section. It will only stay in place if it's pushed inside your butt.".

To say selfexamineuniquetitle of (K - anal-beads-plug-panties):
	say "pair of anal beads plug panties".

To decide which number is the girth of (K - anal-beads-plug-panties):
	decide on 1.

Definition: anal-beads-plug-panties is displacable: decide yes.

Definition: anal-beads-plug-panties is potentially at least partially asshole covering:
	if anal-beads-plug-panties is penetrating asshole, decide yes;
	decide no.

To say DisplaceFlav of (C - anal-beads-plug-panties):
	say "You reach [if the player is prone]behind you[otherwise]down[end if] and pull your [C] [if C is penetrating asshole]out of your [asshole][otherwise]to the side[end if], freeing [if the bimbo of the player > 6]up access to [end if]your crotch.".

To say ReplaceFlav of (C - anal-beads-plug-panties):
	say "You reach [if the player is prone]behind you[otherwise]down[end if] and push it back into your [asshole].".

Plug Panties ends here.
