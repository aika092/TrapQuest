Princess by Monster begins here.

ex-princess is a monster. ex-princess is intelligent. ex-princess is caged. ex-princess is in School35. ex-princess has a number called torment-count. ex-princess has a number called current-torment. Understand "princess" as ex-princess. Understand "imprisoned" as ex-princess when the item described is caged. Understand "original" as ex-princess when the item described is unconcerned. Understand "enslaved" as ex-princess when the item described is defeated. Understand "button" as ex-princess when diaper quest is 1 and item described is caged.

ex-princess has a number called princess-power.

To say speech style of (M - ex-princess):
	say second custom style.

The text-shortcut of ex-princess is "pri".
Figure of original princess is the file "NPCs/MultiFloor/Princess/princess1.jpg".
Figure of original diapered princess is the file "NPCs/MultiFloor/Princess/princess2.jpg".
Figure of original diapered princess wet masturbating is the file "NPCs/MultiFloor/Princess/princess7a.jpg".
Figure of original diapered princess wet masturbating confident is the file "NPCs/MultiFloor/Princess/princess7b.jpg".
Figure of enslaved princess is the file "NPCs/MultiFloor/Princess/princess3.jpg".
Figure of enslaved diapered princess is the file "NPCs/MultiFloor/Princess/princess4.jpg".
Figure of original princess combat is the file "NPCs/MultiFloor/Princess/princess5.jpg".
Figure of original diapered princess combat is the file "NPCs/MultiFloor/Princess/princess6.jpg".
Figure of temporary caged princess is the file "NPCs/MultiFloor/Princess/cage5.jpg".

Figure of princess final battle piss box is the file "NPCs/MultiFloor/Princess/cutscene-princess-final-battle1.jpg".
Figure of princess final battle pit is the file "NPCs/MultiFloor/Princess/cutscene-princess-final-battle2.jpg".
Figure of princess final battle food is the file "NPCs/MultiFloor/Princess/cutscene-princess-final-battle3.jpg".
Figure of princess final battle inflation is the file "NPCs/MultiFloor/Princess/cutscene-princess-final-battle5.jpg".
Figure of princess final battle harness is the file "NPCs/MultiFloor/Princess/cutscene-princess-final-battle6.jpg".
Figure of princess final battle scan is the file "NPCs/MultiFloor/Princess/cutscene-princess-final-battle8.jpg".
Figure of princess final battle facehugger is the file "NPCs/MultiFloor/Princess/cutscene-princess-final-battle10.jpg".

Figure of princess cutscene 1 is the file "NPCs/MultiFloor/Princess/cutscene-princess-errand1.png".

To decide which figure-name is the monster-image of (M - ex-princess):
	if M is unleashed: [dark princess]
		if M is male, decide on figure of dark princess 2;
		if diaper quest is 0, decide on figure of dark princess 1;
		if M is penetrating face, decide on figure of princess final battle final chance;
		if diaper messing < 7:
			if M is interested, decide on figure of dark princess 2;
			decide on figure of dark princess 1;
		otherwise:
			if M is messy, decide on figure of dark princess 5;
			if M is interested, decide on figure of dark princess 4;
			decide on figure of dark princess 3;
	if M is caged:
		if diaper quest is 1:
			if the current-torment of M > 0, decide on figure of caged pail princess facedown;
			decide on figure of caged pail princess messy; [if diaper messing >= 7, ]
			[otherwise decide on figure of caged pail princess;]
		if watersports fetish is 0, decide on figure of temporary caged princess;
		if the current-torment of M > 0, decide on figure of caged urinal princess piss;
		decide on figure of caged urinal princess;
	if M is guarding:
		if current-final-battle-object is final-battle-imp-piss, decide on Figure of princess final battle piss box;
		if current-final-battle-object is final-battle-pit, decide on Figure of princess final battle pit;
		if current-final-battle-object is final-battle-food, decide on Figure of princess final battle food;
		if current-final-battle-object is final-battle-plug, decide on Figure of princess final battle plug;
		if current-final-battle-object is final-battle-harness, decide on Figure of princess final battle harness;
		if current-final-battle-object is final-battle-final-chance, decide on figure of princess final battle final chance;
		if current-final-battle-object is final-battle-scan and final-battle-scan is not pose-swapped, decide on figure of princess final battle scan;
		if current-final-battle-object is final-battle-sword:
			if the uses of final-battle-sword is 1, decide on figure of princess final battle sword;
			decide on figure of princess final battle sword semen;
		if current-final-battle-object is final-battle-facehugger, decide on Figure of princess final battle facehugger;
	if (M is in School35 and glittery-wand is carried by M and glittery-wand is cursed) or (M is guarding and current-final-battle-object is final-battle-wand-curse):
		if diaper lover > 0, decide on figure of original diapered princess combat;
		decide on figure of original princess combat;
	if diaper lover > 0:
		if M is messy and M is in Dungeon11, decide on Figure of original diapered princess wet masturbating;
		decide on figure of original diapered princess;
	decide on figure of original princess;

To decide which figure-name is the pacified-image of (M - ex-princess):
	if M is diaper-enslaved, decide on figure of enslaved diapered princess;
	decide on figure of enslaved princess.

To say ShortDesc of (M - ex-princess):
	say "princess".

To say MediumDesc of (M - ex-princess):
	say "[if M is caged]imprisoned[otherwise if M is defeated]enslaved[otherwise if M is unleashed]dark[otherwise]original[end if] princess".

To say MonsterDesc of (M - ex-princess):
	if M is caged:
		if diaper quest is 1:
			say "[BigNameDesc of M] is stuck in a small cell behind a solid wall with a single viewing window. The cell is empty except for two pink blocks, which [his of M] ankles are strapped to, keeping [his of M] legs apart in a kneeling position, a foot off the ground. [big he of M][']s wearing nothing except for a thick diaper, a tiara, a pink arm binder and a pink collar. The arm binder is connected to a hole in the ceiling by a tight rope, and [his of M] collar is similarly connected to a hole in the ground by another tight rope. These two ropes serve to allow the [ShortDesc of M][']s captors to move [him of M] into any position of their choice and hold [him of M] there, without needing to go into the room themselves. A clear tube descends in front of the [ShortDesc of M][if the current-torment of M <= 0], with several soiled diapers ready to exit through the flap. However, they are currently being held in place thanks to [NameDesc of M][']s nose and face obstructing the exit, pressed unpleasantly into the worst part of the front most diaper[end if]. Several other used diapers litter the ground, no doubt having at some point descended from the tube, which is likely connected to the diaper pail in the nearby hallway. In summary, it would appear that every diaper used by a member of the school ends up in [NameDesc of M][']s cell, essentially making [his of M] cell into a giant diaper pail.[paragraph break]An intercom system has been set up to allow any speakers to communicate with [NameDesc of M]. You also notice a keyhole to the side of the viewing portal, presumably for use on the rare occasion that [he of M] is fed or [his of M] pail is cleaned out. A big button is also accessible to you, on a stand in front of the viewing portal. [if torment-count of M is 0]What could that do?[otherwise]You know that this forces [him of M] to face plant into the diapers below [him of M], covering [him of M] in more diapers from the tube above, and forcing [him of M] to orgasm.";
			if the current-torment of M > 0, say "[big he of M] is currently being held with [his of M] face to the ground, engulfed in soiled diapers, with a vibrator loudly buzzing away from within [his of M] diaper. You can hear [him of M] moaning with a mixture of arousal and disgust.";
		otherwise:
			say "[BigNameDesc of M] is stuck in a small cell behind a series of strong metal cage bars. The cell is empty except for a toilet in one corner. ";
			if watersports fetish is 1:
				say "[big he of M] is wearing a golden tiara and a pink silk royal dress with the symbol of a [if lady fetish is 0]men's toilets[otherwise]public toilet door[end if] on the chest. [if diaper lover > 0][big he of M] is wearing a thick white diaper under the very short skirt[otherwise]The hem of [his of M] dress is very short, making [his of M] lack of underwear fully visible[end if]. Attached to the front of one vertical metal bar is a large white urinal. On [NameDesc of M][']s side, the piping of the urinal leads through a clear tube to the top of a locked clear plastic box. There is some sort of meal, with a soup-like consistency, contained inside the box. Halfway along the tube, a hollow dildo protrudes, pointing at the [man of M].";
				if the current-torment of M > 0:
					say "The bowl section of the urinal and the top half of the tube are filled with [urine]. A timer on the locked box counts down from [current-torment of M] seconds. It looks like [he of M] has that long to suck the [urine] out of the tube system and transfer it mouthful by mouthful to the toilet. Anything [he of M] doesn't manage to bring [himself of M] to transfer by that time will become part of [his of M] next meal.";
				otherwise if the torment-count of M > 0:
					say "There's currently no [urine] in the tube, but once someone pisses in the urinal and presses the button on top, [he of M][']ll once again be forced to transfer it to [his of M] own toilet with [his of M] mouth, if [he of M] doesn't want it to become a part of [his of M] next meal.";
				otherwise:
					say "It looks like some sort of mechanism has been set up that will force [him of M] to suck up any [urine] that enters the urinal and transfer it to [his of M] own toilet, unless [he of M] wants it to end up part of [his of M] next meal.";
			otherwise: [temporary]
				say "[big he of M] is wearing a golden tiara and a very short pink silk royal dress, clearly revealing [his of M] lack of underwear. Attached to the front of one vertical metal bar is a large white tank of [semen]. On [NameDesc of M][']s side, the piping of the tank leads via a clear tube to the top of a locked clear plastic box. There is some sort of meal, with a soup-like consistency, contained inside the box. Halfway along the tube, a hollow dildo protrudes, pointing at the [man of M].";
				if the current-torment of M > 0:
					say "The top half of the tube is filled with [semen]. A timer on the locked box counts down from [current-torment of M] seconds. It looks like [he of M] has that long to suck the [semen] out of the tube system and transfer it mouthful by mouthful to the toilet. Anything [he of M] doesn't manage to bring [himself of M] to transfer by that time will become part of [his of M] next meal.";
				otherwise if the torment-count of M > 0:
					say "There's currently no [semen] in the tube, but once someone presses the button on top of the tank, [he of M][']ll once again be forced to transfer it to [his of M] own toilet with [his of M] mouth, if [he of M] doesn't want it to become a part of [his of M] next meal.";
				otherwise:
					say "It looks like some sort of mechanism has been set up that will force [him of M] to suck up any [semen] that enters the tube and transfer it to [his of M] own toilet, unless [he of M] wants it to end up part of [his of M] next meal.";
	otherwise if M is defeated:
		if M is diaper-enslaved, say "The [man of M] who used to be a proud powerful [ShortDesc of M] has been reduced to a long haired baby-bottle sucking bimbo, who wanders the school aimlessly with a calm vacant look on [his of M] face.";
		otherwise say "The princess has lost all power and all will to fight back against [his of M] captors. [big he of M] lies on [his of M] back, fully naked except for [his of M] tiara, [his of M] hands magically stuck to [his of M] asscheeks, unable to remove [his of M] knees from behind [his of M] shoulders, making it look like [he of M][']s begging anyone and everyone [he of M] sees to ruin [his of M] asshole. [big his of M] buttcheeks have been magically enhanced, and are now triple the size they first were when you met [him of M].";
	otherwise if M is unleashed: [dark princess]
		say "This is not the princess you first met. Unfortunately, [he of M] is gone forever. Left behind is this corrupted, dark form. [big he of M] has red hair and a purple and black dress in the same style as [his of M] old pink dress, with a parting at the front which draws your eyes to [his of M] crotch. ";
		if M is male:
			say "Here, a huge thick [if lady fetish < 2]futa [end if]cock swings side to side menacingly, dripping precum from the massive balls dangling behind it.";
		otherwise if diaper quest is 0:
			if lady fetish is 2, say "[big his of M] caged clitty is dripping with excitement, creating a little pitter-patter sound as [his of M] sissy juices gradually create a little puddle between [his of M] feet.";
			otherwise say "[big his of M] pussy is dripping wet with excitement, creating a little pitter-patter sound as [his of M] vaginal juices gradually create a little puddle between [his of M] feet.";
		otherwise if diaper messing < 7:
			say "[big he of M] is [if M is interested]diapered, and you can tell from the large yellow stain that it is soaked to capacity with [urine][otherwise]in a fresh plain diaper[end if].";
		otherwise:
			say "[big he of M] is in a giant diaper, [if M is messy]full to the brim with inhuman amounts of stinking shit which has stained the entire garment a dark shade of brown[otherwise if M is interested]and you can tell from the large yellow stain that it is soaked to capacity with [urine][otherwise]plain white but stupidly thick, and as far as you can tell, currently unused[end if].";
	otherwise:
		say "The original princess that rules the dungeons of Bimbacia wears a regal pink silk dress, with a very low neckline and a skirt with a deep slit at the front. [big his of M] outfit seems to be magically fixed to this exact appearance, and no matter how much [he of M] pulls, the skirt of the dress refuses to be pulled any further forward than [his of M] sides, meaning that [his of M] [if diaper lover > 0]heavily diapered crotch[otherwise if lady fetish is 2]caged penis[otherwise]bare pussy[end if] is kept fully visible from the front at all times.".

To say MonsterComment of (M - ex-princess):
	if M is caged, say "[line break][variable custom style][one of][big he of M] looks important. I wonder if [he of M] would reward me if I helped break [him of M] out of here...[or]Poor thing is still trapped here. Are they going to keep [him of M] here like this forever?[stopping][roman type][line break]".

Definition: ex-princess is presenting as male:
	if lady fetish is 2, decide yes;
	decide no.

To set up (M - ex-princess):
	now the monstersetup of M is 1;
	now the raw difficulty of M is 1;
	now the health of M is the maxhealth of M.

To decide which number is the difficulty of (M - ex-princess):
	if M is caged, decide on 1;
	if M is guarding, decide on 10;
	decide on 99.

Definition: ex-princess is human: decide yes.

Definition: ex-princess is able to remove cursed plugs: decide yes.

Definition: ex-princess is butt slut immune: decide yes.

Definition: ex-princess is uniquely unfriendly:
	if it is unleashed, decide yes; [dark princess]
	decide no.

To decide which number is the aggro limit of (M - an ex-princess): [The number at which they turn unfriendly]
	decide on -999999.

Definition: ex-princess is motionless-when-defeated: decide yes.

To check motion of (M - ex-princess):
	if M is unconcerned and M is messy and M is in a labyrinth room:
		let D be the best route from the location of M to Dungeon11;
		if D is a direction, try M going D;
	otherwise:
		check default motion of M.

To check seeking (N - a number) of (M - ex-princess):
	if M is unconcerned and M is messy and M is in Dungeon11:
		if the player is not in Dungeon11, bore M;
	otherwise:
		check default seeking N of M.

Part 1 - Perception

To decide which number is the bimbo tolerance of (M - ex-princess):
	decide on 20.

To decide which number is the bab tolerance of (M - ex-princess):
	decide on 20.

To compute perception of (M - ex-princess):
	say "[BigNameDesc of M] notices you[if the player is sluttily dressed].[otherwise]![end if]"; [The output for clothing humiliation takes place within the 'sluttily dressed' check.]
	if M is unleashed:
		if diaper quest is 1, say "A loud hiss fills the room, as [he of M] audibly fills [his of M] diaper to the brim with highly concentrated [urine]. As the tell-tale yellow stain spreads across the white plastic, an sadistic smile spreads across [his of M] face.";
		say "[speech style of M]'HEHEHEHEHE!'[roman type][line break][big his of M] laugh sounds completely deranged, but there's clearly something still working correctly in [his of M] mind, because [he of M] adopts a combat stance and prepares to pounce.";
	otherwise if the times-met of M is 0:
		say "[big his of M] voice is dry, cracked and very quiet.[line break][speech style of M]'[if the class of the player is princess]Don't tell me... they're replacing me with you?! How cruel. Unless... could it be... my throne sent you here? [otherwise if there is a student in the location of the player]Have you come here to torment me? Or deep down, do you have kind hearts? [otherwise]Have you come here to torment me or are you another innocent? [end if][big please]... if you can... find a way to get me out of here, before it's too late...'[roman type][line break]";
		if prison guard is not in the dungeon, summon prison guard in the dungeon;
	otherwise if M is caged:
		say "[speech style of M]'[big please]... I can't stay sane much longer! You're my only hope...'[roman type][line break]";
	otherwise if M is defeated:
		say "[speech style of M]'Please don't look at my disgusting shame! [big please] don't watch!'[roman type][line break]";
	otherwise if M is messy:
		compute ChangeRequest of M;
	otherwise if the current-errand of M is completed and M is not uniquely unfriendly:
		compute errand completion of M;
	otherwise:
		say "[speech style of M]'Greetings [NameBimbo], saviour of Bimbacia!'[roman type][line break]".

To compute friendly boredom of (M - ex-princess):
	if playerRegion is not school, compute default friendly boredom progress of M.

Part 2 - Misc Flavour

To say IdentifiablePosterReactionFlav of (M - ex-princess):
	say "[BigNameDesc of M] looks at the poster and then presses [his of M] palm into [his of M] face.[line break][speech style of M]'THIS is the person who rescued me from [slut school]? REALLY?'[roman type][line break]".

To say UnidentifiablePosterReactionFlav of (M - ex-princess) with (P - a poster):
	say "[BigNameDesc of M] looks at the banner with a big frown on [his of M] face. [big he of M] doesn't seem to realise it is you.[line break][speech style of M]'Has this world become overrun with harlots and whores like this one? Seriously, do they have no shame?'[roman type][line break]".

Part 3 - Combat

To compute interaction of (M - ex-princess):
	if the player is in danger:
		compute protection of M;
	otherwise if M is unconcerned:
		if there is a tentacle-breeder in the location of the player:
			let T be a random tentacle-breeder in the location of the player;
			say "[speech style of M]'[if inhuman pregnancy >= 2]Poor girl. Let me put you out of your misery.'[roman type][line break][BigNameDesc of M] points [his of M] wand at [NameDesc of T], and suddenly the tentacles and slime begin to thrash around and turn brown. The tentacles restraining the woman quickly rot away, and as she falls to the floor she manages a look of gratitude before vanishing into motes of light[otherwise]This place really needs a spring clean.'[roman type][line break][BigNameDesc of M] points [his of M] wand at [NameDesc of T], which shrinks away inside itself until it has completely gone[end if].";
			destroy T;
			repeat with C running through worn clothing:
				if the quest of C is tentacle-research-quest:
					compute quest completion of tentacle-research-quest on C;
		otherwise if M is messy and M is in Dungeon11:
			say "[BigNameDesc of M] continues to press [his of M] vibrating wand into the front of [his of M] soiled diaper.[line break][speech style of M]'Please... [one of]you stupid curse... just let me cum...'[or]just once...'[or]I really need to cum... and get a change!'[or]I hate this smell!'[cycling][roman type][line break]";
			compute ChangeRequestDecision of M;
		otherwise if the current-errand of M is completed:
			compute errand completion of M.

To compute (M - ex-princess) protecting against (X - a monster):
	if M is in the school:
		if glittery-wand is not carried by M or glittery-wand is not cursed, say "[BigNameDesc of M] [one of]seems to have used the last of [his of M] energy for now, and can't help you fight.[or]is panting heavily.[or]is still too out-of-breath to help.[or]continues panting heavily, trying to recover [his of M] breath.[or]continues panting heavily.[or]seems to be trying to gather enough magical energy for more spells![or]continues panting heavily.[stopping]";
		otherwise say "[BigNameDesc of M] [one of]is being forced to masturbate [himself of M] with the cursed wand![or]continues to fervently press the vibrating wand into [his of M] clit, no matter how much [he of M] tries to stop it with [his of M] other arm.[or]pushes the head of the cursed vibrating wand into [his of M] clit and makes small circles.[or]moans with a mixture of passion and despair as [he of M] brings [himself of M] closer to the bring with the powerful wand.[or]screams out as [he of M] has a powerful orgasm from the vibrating wand on [his of M] clit.[or]can't stop stimulating [his of M] clit, even after coming down from [his of M] orgasmic high![or]shudders as [his of M] oversensitive clit receives further vibrating punishment.[cycling]";
	otherwise if X is facehugger:
		say "[speech style of M]'Ugh[one of], [please] don't tell me this place is infested with these things[or][stopping].'[roman type][line break][BigNameDesc of M] points [his of M] wand at [NameDesc of X], and zaps it with some kind of laser. And a moment later, [NameDesc of X] is no more.";
		destroy X;
	otherwise if M is unconcerned and the player is upright:
		compute M standard fighting against X;
	otherwise if X is not demon lord:
		compute default protecting of M.

To compute (M - ex-princess) standard fighting against (X - a royal guard):
	say "[BigNameDesc of M] raises a hand towards [NameDesc of X].[line break][speech style of M]'Stand down, Sir Knight. I pardon [NameBimbo] of [his of the player] crimes.'[roman type][line break][BigNameDesc of X] [one of][or]frowns with frustration, but [stopping]bows.[line break][speech style of X]'As you wish, my liege.'[roman type][line break][big he of X] turns to you.[line break][speech style of X]'You [one of]have a clean slate, thanks to my forgiving Princess. I suggest you use it wisely, and clean up your act before we next meet[or]continue to have friends in high places, it would seem[stopping].'[roman type][line break]";
	calm X;
	bore X;
	compute mandatory room leaving of X;
	now X is moved.

To compute (M - ex-princess) standard fighting against (X - shopkeeper):
	say "[BigNameDesc of M] raises a hand towards [NameDesc of X].[line break][speech style of M]'[if there is a held store thing]I shall pay for [his of the player] items[otherwise]This one is with me[end if].'[roman type][line break][BigNameDesc of X] bows.[line break][speech style of X]'Of course, your grace.'[roman type][line break]";
	repeat with T running through held store things:
		now the owner of T is nothing;
	send X home.

To compute failed damage of (M - ex-princess):
	say "[BigNameDesc of M] completely ignores your attack, as if [he of M] didn't even feel it!".

To decide which number is the damage modifier of (M - ex-princess):
	decide on (attack-damage * -1).

To compute (M - a monster) stomping (N - ex-princess): [TODO: Different interaction with Xavier]
	if N is in the location of the player and the health of M > 1:
		say "[BigNameDesc of N] looks up at [NameDesc of M] without fear or concern.[line break][speech style of N]'[one of]Who let [him of M] out? How annoying. I guess [he of M][']s too unsafe to leave wandering around like this. A shame, but I guess we should put [him of M] down[or]Weird, I could swear we'd done this already. Oh well, here we go again[stopping].'[roman type][line break][BigNameDesc of N] swirls [his of N] wand around several times and then blasts [NameDesc of M] with the largest beam of blinding white magic you've ever seen. [BigNameDesc of M] is burned and singed all over, and looks like [he of M][']s on the verge of defeat.[line break][speech style of N]'I'll leave the finishing blow for you.'[roman type][line break]";
		satisfy N;
		compute mandatory room leaving of N;
		now N is moved;
		now the health of M is 1.

This is the princess facesitting prevents breathing rule:
	if diaper quest is 0 and ex-princess is female and ex-princess is penetrating face, decide yes;
	decide no.
The princess facesitting prevents breathing rule is listed in the breathing blocking rules.

A breathing consequences rule (this is the consequences for breathing through princess padding rule):
	if diaper quest is 1 and ex-princess is penetrating face and the player is air breathing vulnerable:
		say "You are forced to smell the pungent aroma of [NameDesc of ex-princess][']s [if ex-princess is messy and diaper messing >= 7]stinky[otherwise][urine]-soaked[end if] diaper.";
		let G be wetDiaperFacesitGrossnessLevel;
		if ex-princess is messy, now G is messyDiaperFacesitGrossnessLevel;
		SmellGrossOut G.

To say MouthPenetrationFlav of (M - ex-princess):
	if M is male:
		say DefaultMouthPenetrationFlav of M;
	otherwise:
		say "[BigNameDesc of M] effortlessly rolls you onto your back and engulfs your face with [his of M] [if diaper quest is 1]padded [end if]crotch. You won't be able to breathe except through [his of M] padding until [he of M] is finished with you! (You might want to consider using [bold type]['][link]hold breath[end link]['][roman type])";
		if diaper messing >= 7, say "And then the worst thing imaginable happens. As soon as [NameDesc of M] has made [himself of M] comfortable on your face, [he of M] unleashes hell from [his of M] bowels. A fountain of rancid filth spews from [his of M] pooper, bubbling and gurgling noisily as it quickly spreads to fill every inch of [his of M] diaper.";
		if sex-length of M < 3, now sex-length of M is 2.

To compute facial sex of (M - ex-princess):
	if diaper quest is 1:
		compute diaper facesit sex of M;
	otherwise if M is male:
		compute default facial sex of M;
	otherwise:
		compute facesit sex of M.

To compute diaper facesit sex of (M - ex-princess):
	if the reaction of the player > 0, severeHumiliate;
	TimesSubmittedUp M by 1;
	decrease the sex-length of M by 1;
	if the diaper addiction of the player >= 10:
		let N be the diaper addiction of the player / 5;
		passively stimulate face from M times N;
	if the reaction of the player > 0:
		say "[one of]You lie still and save your energy[or]You don't attempt to escape from your position underneath [his of ex-princess] diapered bottom[or]You try to relax and accept what is happening to you[in random order]. [moderateHumiliateReflect]";
	otherwise:
		say "[one of]You refuse to sit still as you are degraded like this! You struggle, failing to push [him of M] off you as [he of M] bounces [his of M] padded behind on your face. [or]You continue trying to fight [him of M], failing miserably to do any damage as [he of M] continues to grind [his of M] diapered bottom against you. [or]You simply won't give in! You throw your head from side to side, trying to get out. [or]You flail helplessly with your arms and legs! At least it proves to anyone watching that you're not happy about what's happening... [in random order]";
	if there is a worn diaper:
		say "[BigNameDesc of M] [one of]thoroughly rubs and mashes[or]cackles as [he of M] squishes [his of M] hands down on the outside of[or]massages[stopping] your [if there is a worn perceived messed diaper]messy [end if]diaper with [his of M] hands.";
		stimulate vagina from M.

The princess continue sex rules is a rulebook. The continue sex rules of ex-princess is the princess continue sex rules.

This is the princess extra continue sex check rule:
	follow the check normal continue sex stuff rule;
	if the rule succeeded, rule succeeds;
	if current-monster is penetrating face:
		compute punishment of ab-diaper-facesit;
		rule succeeds.
The princess extra continue sex check rule is listed in the princess continue sex rules.


Part 4 - Conversation

Section 1 - Greeting

To consider (T - talk-greet) for (M - ex-princess):
	now the printed name of T is the substituted form of "[FirstGreeting to M]";
	set next numerical response to the substituted form of "[printed name of T]".

To compute basic greeting to (M - ex-princess):
	if M is caged and the current-torment of M > 0:
		say SubmissiveResponse of M;
	otherwise:
		if the times-met of M < 2:
			say FirstResponse of M;
		otherwise:
			say RepeatResponse of M;
		now the boredom of M is 0;
		check guaranteed perception of M.

To say FirstResponse of (M - ex-princess):
	say "[speech style of M]'Who are you?'[roman type][line break]";
	if the times-met of M < 2, now the times-met of M is 2.

To say RepeatResponse of (M - ex-princess):
	say "[speech style of M]'[if M is caged][big please], you've got to help me get out of here!'[otherwise]Oh?'[end if][roman type][line break]".

To say SubmissiveResponse of (M - ex-princess):
	say "You try to talk to [NameDesc of M], but [he of M][']s busy [if diaper quest is 0 and watersports fetish is 1]sucking up the [urine] and transferring it to [his of M] toilet[otherwise if diaper quest is 0]sucking up the [semen] and transferring it to [his of M] toilet[otherwise]occupied by having [his of M] face smushed into used diapers[end if].[line break][speech style of M]'[if diaper quest is 0][one of]*Glug glug glug*...'[or]*Hck hck hck*...'[or]Mmmmmmmph...'[in random order][otherwise]Mmmmgggg!'[end if][roman type][line break]".

Section 2 - Questioning

Definition: ex-princess is fluff-question-appropriate:
	if it is not caged and it is not unleashed and it is undefeated and princess-rewarded is true, decide yes;
	decide no.

talk-princess-question is a talk-object. ex-princess has a number called question-sequence.

To consider (T - talk-princess-question) for (M - a monster):
	if M is ex-princess and M is caged:
		now the printed name of T is the substituted form of "[variable custom style]'[if the question-sequence of M is 0]Who are you[otherwise if the question-sequence of M is 1]Why are you in there[otherwise if the question-sequence of M is 2]What are you going to do if you get out[otherwise if the question-sequence of M is 3]How can I help[otherwise]Did I already ask how I could help you out[end if]?'[roman type][line break]";
		set next numerical response to the substituted form of "[printed name of T]";

To execute (T - talk-princess-question) for (M - a monster):
	if the number of monsters in the location of the player is 1:
		say "[speech style of M]'[if the question-sequence of M is 0]I'm Princess Annie! [big please] help me!'[otherwise if the question-sequence of M is 1]I conjured up this entire pocket dimension and the people inside it... but I was sloppy and got overpowered by my own creation! Now hurry up and get me out of here!'[otherwise if the question-sequence of M is 2]I'm going to get my revenge on that fucking [ShortDesc of headmistress] and [his of headmistress] pawns. [big he of headmistress][']ll wish [he of headmistress][']d never been created...'[otherwise if the question-sequence of M is 3]I'm going to need you to unlock this cell. For that, you'll need a magical skeleton key. My prison guard has one, but [he of prison guard] might need... [']persuading['] to part with it. I happen to know that [he of prison guard] loves digestive biscuits. I'm also going to need something to fight with. Any magical wand will do, but my royal scepter would be our best chance. For that you'll need to first find my tiara. It should present itself to anyone worthy who sits on my throne.'[otherwise][Azathot] above... is this really the best chance I have? I need you to help me escape!'[end if][roman type][line break]";
		increase the question-sequence of M by 1;
	otherwise: [She's not alone so she can't talk sincerely]
		say "You see [him of M] notice you're not alone, and then [he of M] just squeaks and keeps [his of M] mouth shut.".

To say WhereAnswer of (M - ex-princess):
	if playerRegion is dungeon:
		say "[speech style of M]'This is my royal dungeon. It feels so nostalgic...'[roman type][line break]";
	otherwise if playerRegion is hotel:
		let hb be mechanic;
		if demon lord is alive or demon lord is bossdefeated, now hb is demon lord;
		if hb is mechanic:
			say "[speech style of M]'This is Xavier's pleasure hotel. [if hb is alive]If [he of hb] asks you for an amulet, you must never bring it to [him of hb]. There's no telling how powerful [he of hb] would become...'[otherwise]It was unfortunate that [he of hb] had to banished, but it is what [he of hb] deserved.'[end if][roman type][line break]";
		otherwise:
			say "[speech style of M]'This is the demon Xavier's pleasure hotel. [if hb is alive]Was [he of hb] this powerful before I sealed [him of hb]? Maybe I've grown weaker...'[otherwise]I'm not sure how you managed to defeat [him of hb]...'[end if][roman type][line break]";
	otherwise if playerRegion is woods:
		let vb be vine boss;
		say "[speech style of M]'This is the forest of Bimbacia, which the vine lord calls [his of vb] domain. [if the times-submitted of vb > 0]Naturally, that extends to all orifices in the forest as well...'[otherwise if vb is alive]I don't know why I created something so annoying...'[otherwise]Or- it used to. Defeating such a creature is quite a feat!'[end if][roman type][line break]";
	otherwise if playerRegion is school:
		say "[speech style of M]'This is the [slut school]. I can't wait to leave this place.'[roman type][line break]";
	otherwise:
		let mb be vampiress;
		if herald is alive or herald is bossdefeated, now mb is herald;
		if mb is vampiress, say "[speech style of M]'This is Aurora's mansion, although...[he of mb] never fights seriously, so it wouldn't be surprising if someone else defeated [him of mb] and started squatting while [he of mb] was recovering.'[roman type][line break]";
		otherwise say "[speech style of M]'This is Aurora's mansion[if mb is alive], but...something else is trying to claim ownership too. Even I don't know what it is.[otherwise], although I fear that the eldritch entities that have their eyes on this place have something to say about that.'[roman type][line break]".

To say WhoAnswer of (M - ex-princess):
	say "[speech style of M]'I told you before, I'm Princess Annie! I created this dimension.'[roman type][line break]".

To say StoryAnswer of (M - ex-princess):
	if playerRegion is dungeon:
		let db be minotaur;
		if diaper quest is 1, now db is boogeymonster;
		if db is minotaur:
			say "[speech style of M]'I created [NameDesc of db] as a perfect guard for my dungeon. For personal reasons, I gave [him of db] a larger [DickDesc of db] and inhuman stamina, and it made [him of db] impossible to control. Still, if I didn't have my responsibilities as a ruler...'[roman type][line break]";
		otherwise:
			say "[speech style of M]'I created [NameDesc of db] to guard my dungeon, but [he of db] was too scary...I had no choice but to put [him of db] under lock and key.'[roman type][line break]";
	otherwise if playerRegion is hotel:
		let hb be mechanic;
		if demon lord is alive or demon lord is bossdefeated, now hb is demon lord;
		if hb is mechanic:
			say "[speech style of M]'Xavier is a criminal I bound here as part of [his of hb] punishment.'[roman type][line break]";
		otherwise:
			say "[speech style of M]'Xavier is an invader from another dimension, who I defeated and sealed away. [if hb is alive][big he of hb] is even more powerful than [he of hb] was back then...I'll need your help[otherwise]Many of [his of hb] vassals remain in this world[end if].'[roman type][line break]";
	otherwise if playerRegion is woods:
		say "[speech style of M]'When I created this forest, I was going through this phase where I was really into tentacles. Sorry.'[roman type][line break]";
	otherwise if playerRegion is school:
		say "[speech style of M]'I never thought that headmistress would be able to overpower me like that. That bitch...'[roman type][line break]";
	otherwise:
		let mb be vampiress;
		if herald is alive or herald is bossdefeated, now mb is herald;
		if mb is vampiress:
			say "[speech style of M]'This mansion looked a lot cleaner when I created it. This is why you can't trust management to an undead.'[roman type][line break]";
		otherwise:
			say "[speech style of M]'This mansion [first custom style]BELONGS TO THE [caps great ones]. SOON, OTHER PLACES WILL FALL.'[roman type][line break]".

Definition: ex-princess is escape-question-appropriate: decide no. [Princess can have over 9 questions now, we need to drop a couple]
[To say EscapeAnswer of (M - ex-princess):
	say "[speech style of M]'You must defeat the masters of this game. They're beyond my jurisdiction, so I can't really help you.'[roman type][line break]".][TODO: work on]

To say AdviceQuestion of (M - ex-princess):
	let B be a random slutty sister;
	if playerRegion is dungeon:
		if minotaur is alive, now B is minotaur;
		if boogeymonster is alive, now B is boogeymonster;
	if playerRegion is hotel:
		if mechanic is alive, now B is mechanic;
		if demon lord is alive, now B is demon lord;
	if playerRegion is woods:
		if vine boss is alive, now B is vine boss;
	if playerRegion is mansion:
		if vampiress is alive, now B is vampiress;
		if herald is alive, now B is herald;
	if B is a slutty sister, say "[variable custom style]'How do I defeat the Nintendolls?'[roman type][line break]";
	otherwise say "[variable custom style]'How do I defeat the [ShortDesc of B]?'[roman type][line break]".

To say AdviceAnswer of (M - ex-princess):
	if playerRegion is dungeon:
		if minotaur is alive:
			let db be minotaur;
			say "[speech style of M]'[big he of db] may be strong, but [he of db] tires easily. If you exhaust [him of db] first, [he of db] will fall quickly.'[roman type][line break]";
		otherwise if boogeymonster is alive:
			let bm be boogeymonster;
			say "[speech style of M]'You must ignore [his of bm] evil visions and fight [him of bm] head on.'[roman type][line break]";
	otherwise if playerRegion is hotel:
		if demon lord is alive:
			let dl be demon lord;
			say "[speech style of M]'It will be difficult. [big he of dl] is immune to physical weapons, so if you don't have a magic weapon, you will need a blessing from the gods. If you can't manage that, [he of dl] does have another weakness, but... it isn't worth the risk.'[roman type][line break]";[he's also weak to demonic items. And condoms. Which does she mean?]
		otherwise if mechanic is alive:
			let mec be mechanic;
			say "[speech style of M]'If you can separate [him of mec] from [his of mec] wrench, [he of mec] will be greatly weakened. After that, [he of mec] will be just like anyone else.'[roman type][line break]";
	otherwise if playerRegion is woods:
		if vine boss is alive, say "[speech style of M]'You must enter its lair and face it in single combat. If you allow yourself to be violated, you may doom yourself to a horrible fate.'[roman type][line break]";
	otherwise if playerRegion is mansion:
		if herald is alive:
			say "[speech style of M]'I...don't know. I have never seen that thing before.'[roman type][line break]";
		otherwise if vampiress is alive:
			let mb be vampiress;
			say "[speech style of M]'Is [he of mb] really that bad? ...There are two ways to defeat [him of mb]. The safe way is to just not let [him of mb] feed, and [he of mb] will eventually grow weak and starve. The risky way is to take a stake and jam it right up [his of mb] ass. Yeah, [his of mb] weakness is anal in more ways than one.'[roman type][line break]";
	otherwise:
		say "[speech style of M]'Their magic is strong enough to prevent even me entering their room, so you should be prepared to face them alone. Both of them are warriors of exceptional physical and sexual might. It's possible you will defeat them in combat, but to be safe, you should also have a strategy for taking them on in the bedroom.'[roman type][line break]".

To compute teaching of (M - ex-princess):
	if M is unconcerned:
		say "[line break][speech style of M]'Let me show you how to properly wield a magic wand, to get more OOMPH into your zaps. Firstly, stick your bum out more, like this. And the zap itself, it's all in the wrist...'[roman type][line break]";
		teach zapskill;
	otherwise:
		say "[line break][speech style of M]'When the time comes, I will aid you, but until then, I need to conserve all my energy and strength.'[roman type][line break]".

talk-princess-magic is a talk-object.

To consider (T - talk-princess-magic) for (M - a monster):
	if M is ex-princess and M is unconcerned and the magic-fatigue of the player > 0:
		now the printed name of T is the substituted form of "[variable custom style]'Can you help me replenish my magic energy?'[roman type][line break]";
		set next numerical response to the substituted form of "[printed name of T]".

To execute (T - talk-princess-magic) for (M - a monster):
	say "[BigNameDesc of M] smiles.[line break][speech style of M]'Sure.'[roman type][line break][big he of M] clears [his of M] throat and then announces:[line break][speech style of M]'I lick clits!'[roman type][line break]";
	MagicPowerRefresh 99.

princess-rewarded is initially false.
talk-princess-reward is a talk-object.

To consider (T - talk-princess-reward) for (M - a monster):
	if M is ex-princess and M is unconcerned and princess-rewarded is false:
		now the printed name of T is the substituted form of "[variable custom style]'So, is there any reward for rescuing you?'[roman type][line break]";
		set next numerical response to the substituted form of "[printed name of T]".

To execute (T - talk-princess-reward) for (M - a monster):
	now princess-rewarded is true;
	say "[BigNameDesc of M] thinks.[line break][speech style of M]'You do deserve a reward.'[roman type][line break][big he of M] points [his of M] wand towards you. [big he of M] clears [his of M] throat and announces:[line break][speech style of M]'I simp for this stud!'[roman type][line break]";
	MagicPowerUp 2;
	repeat with C running through worn potentially blessable clothing:
		say "Your [C] shines blue and becomes a ";
		increase the raw-magic-modifier of C by 1;
		say "[C]!";
	say "[BigNameDesc of M] looks fatigued.[line break][speech style of M]'Don't ask me to do that again - that took a whole lot of my magic power to perform.'[roman type][line break]";


Section 3 - Drink Requesting

To compute friendly drink of (M - ex-princess):
	if M is interested:
		if M is caged or M is defeated:
			compute unfriendly drink of M;
		otherwise:
			let C be a random can in Solid Rock;
			let LC be the list of off-stage cans;
			sort LC in random order;
			repeat with B running through LC:
				if the fill-type of B <= 7:
					now C is B;
					break;
			if C is can and the number of in-play cans < 5:
				now C is in the location of M;
				DoseFill C;
				say "[speech style of M]'Of course. Take this!'[roman type][line break][BigNameDesc of M] waves [his of M] hand, and a [C] appears before you.";
				compute autotaking C;
			otherwise:
				say "[speech style of M]'I'm sorry, while I can usually conjure up drinks for everyone, right now I need to conserve some of my magical strength.'[roman type][line break]";
				if newbie tips is 1, say "[newbie style]Try drinking some more of the cans that have already spawned first.[roman type][line break]";
	otherwise:
		say "[BigNameDesc of M] acts as if [he of M] didn't even hear you.".

To compute unfriendly drink of (M - ex-princess):
	say "[speech style of M]'Do I look like I'm [if M is unleashed]going to help you[otherwise]in a situation where I can be helping others[end if]?'[roman type][line break]";
	interest M.

Definition: ex-princess is willing to give snacks:
	if it is not caged and it is not unleashed and it is not defeated, decide yes;
	decide no.

Part 5 - Trading

To compute final resolution of (M - ex-princess) taking (T - a thing):
	if T is skeleton key, destroy T;
	otherwise now M is carrying T.

To decide which number is the bartering value of (T - a thing) for (M - ex-princess):
	if M is caged and the number of monsters in the location of M is 1:
		if T is vibe-wand, decide on 99;
		if T is skeleton key and there is a vibe-wand carried by M, decide on 99;
	decide on 0.

To say MonsterOfferAcceptFlav of (M - ex-princess) to (T - a vibe-wand):
	say "[BigNameDesc of M][']s eyes go wide.[line break][speech style of M]'[if T is royal scepter]Yes... Yes! You found it! Huzzah! With this, the fight will be easy[otherwise if there is a vibe-wand carried by T]This is an unnecessary extra, but it might help. I'll take it[otherwise]Hmm, yes, I can use this, I think. With your help, this should serve[end if]. Now [if diaper quest is 1]go drop that in the diaper pail and then [end if]get me out of here. You did bring a key, didn't you?'[roman type][line break]".

To say MonsterOfferAcceptFlav of (M - ex-princess) to (T - skeleton key):
	now the friendly boredom of M is 0;
	say "[BigNameDesc of M] is visibly elated.[line break][speech style of M]'Yes! You found one! This is exactly what I needed! Now, quick, let's go!'[roman type][line break]With the key, the door to [his of M] cell clicks open and [he of M] staggers out. As soon as [he of M] has crossed the threshold of the cell, you watch as pink sparks fizz across [his of M] outfit, transforming it into an elegant (if rather exposing) pink gown[if diaper lover > 0]. However, [his of M] outfit visibly includes a very bulky plain white diaper, fully visible from under the exposing segment of [his of M] skirt. It doesn't look like [he of M] intended to include the diaper in [his of M] transformation, and is equal parts confused and furious[end if].".

To say MonsterOfferRejectFlav of (M - ex-princess) to (T - a thing):
	say "[BigNameDesc of M] shakes [his of M] head.[line break][speech style of M]'[if M is caged and the number of monsters in the location of the player > 1]I don't want that! That's the sort of thing you'd give me if you were trying to help me escape, which would be a very silly thing to do IN FRONT OF PEOPLE, haha! Good joke!'[otherwise if T is skeleton key and M is caged]You'd better get me a magic wand first.'[otherwise if M is caged][big please], they'll just punish me more if they catch me with that. Just bring the things I asked for. A magic wand and a key...'[otherwise]Don't worry, honey, I don't need anything from you any more. You've done more than enough for me.'[end if][roman type][line break]";
	if T is vibe-wand or T is skeleton key, now M is not rejecting T.

To say MonsterTakeFlav of (M - ex-princess) to (T - a vibe-wand):
	if M is caged:
		if diaper quest is 1, say "You quickly [if the player is prone]crawl[otherwise]run[end if] over to the diaper pail in the hallway, and drop [NameDesc of T] into the chute. By the time you return to [NameDesc of M], you can't see it anywhere. It looks like [he of M] has somehow managed to hide it somewhere out of view of the window.";
		otherwise say "You push [NameDesc of T] through the bars, and [NameDesc of M] takes it.";
	otherwise:
		say "[BigNameDesc of M] takes [NameDesc of T].".

To compute resolution of (M - ex-princess) taking (T - a thing):
	do nothing.

To compute resolution of (M - ex-princess) taking (T - skeleton key):
	if M is caged:
		destroy T;
		now M is guarding;
		now the princess-power of M is 14;
		if M is carrying royal scepter, now the princess-power of M is 16;
		now School35 is use-the-floor;
		let LN be the list of alive undefeated staff members;
		sort LN in random order;
		repeat with N running through LN:
			now N is in School33;
			anger N;
			now N is guarding;
			now the scared of N is 0;
			if the number of unleashed staff member in the location of M < 2:
				now N is unleashed;
				try N going north;
				if N is unfriendly, interest N;
				otherwise deinterest N;
		repeat with N running through alive students:
			now N is guarding; [This will stop them joining the fight]
		let STN be the number of staff members in the location of the player;
		if STN > 0:
			say "There's no time to talk to [NameDesc of M] about [his of M] outfit before [his of M] [if STN > 1]captors arrive[otherwise]captor arrives[end if], clearly somehow alerted to [his of M] escape!";
			let N be headmistress;
			if N is not in the location of the player, now N is a random staff member in the location of the player;
			say "[BigNameDesc of N] speaks.[line break][speech style of N]'Really, [NameBimbo]? After all the trust we put in you, and all we've taught you, this is how you repay us? You WILL regret this.'[roman type][line break]";
			if armband is held:
				say "Your [MediumDesc of armband] vanishes!";
				destroy armband; [destroyed rather than in Holding Pen will allow the player back into the school]
			say "[speech style of M]'Not likely.'[roman type][line break]";
			now N is a random unleashed staff member in the location of M;
			compute M enslaving N;
			if STN > 1:
				if royal scepter is carried by M:
					repeat with STU running through staff members:
						if STU is unleashed and (STU is sapphire-teacher or STU is not teacher), compute M enslaving STU;
				otherwise:
					say "[BigNameDesc of M] falls to [his of M] knees with fatigue. [big he of M] looks up at you.[speech style of M]'You can take care of the [if STN > 2]others[otherwise]other one[end if], right?'[roman type][line break]";
					if the player is prone, say "With a final flick of [his of M] wand, [he of M] helps you to stand.";
					now the stance of the player is 0;

Check going while ex-princess is guarding:
	if ex-princess is in the location of the player, say "If you leave [NameDesc of ex-princess] here, [he of ex-princess] might get defeated and captured again. You should see this through to the end." instead.

An all later time based rule (this is the school rescue fight rule):
	if ex-princess is guarding:
		let M be ex-princess;
		let R be the location of M;
		if playerRegion is school:
			let SMN be the number of alive unleashed staff members + the number of alive guarding staff members;
			if SMN > 0:
				if the number of undefeated staff members in the location of M > 0 and (M is not in the location of the player or the player is prone):
					let N be a random unleashed staff member in the location of M;
					if M is in the location of the player:
						say "[speech style of N]'[one of]That's enough. This rebellion is over[or]This is the end[or]Stay down[stopping].'[roman type][line break][BigNameDesc of N] makes a few arcane gestures and ropes of light begin to fly at you and [NameDesc of M].";
						if a random number between 1 and 8 < the princess-power of M:
							let T be the substituted form of "I [one of][if interracial fetish is 1]am a [WhiteSub] whose life is dedicated to serving [BlackCock][otherwise]I am a filthy whore whose life is dedicated to serving [manly-penis][end if][or]am a worthless object, my only purpose is to keep my [cunt] warm and ready[or]spend my weekends guzzling [if watersports fetish is 1]piss[otherwise]cock-snot[end if] in men's public toilets[or]am a disgusting fraud of a Princess who fucks [GrossMates][or]need more cum in my [pussy][if pregnancy fetish is 1]! Fuck a baby into me[end if][in random order]";
							if diaper quest is 1, now T is the substituted form of "I [one of]am dedicated to [if diaper messing >= 3]pooping in Pampers[otherwise]only using diapers[end if] for the rest of my life[or]spend my weekends guzzling [if watersports fetish is 1]piss[otherwise][milk][end if] from my mommy[or]am a worthless baby who can't even make it to the potty in time[or]am a disgusting fraud of a Princess who gets turned on by being treated like a baby[or]need my Mommy[in random order]";
							say "[speech style of M]'[one of]No[or]I can't go back[or]Not yet[stopping]!'[roman type][line break][BigNameDesc of M] raises [his of M] wand and points it at you. You hear [him of M] reciting the incantation to cast a spell.[line break][speech style of M]'[T]!'[roman type][line break]";
							if T matches the text "dedicated":
								say "You feel your body fully heal[if the player is prone], and are raised back to your feet[end if]! But at what cost? You feel significant physical power leaving your body, and you doubt it will return after the fight is done.";
								BodyHeal 10;
								StrengthDown 4; [Deliberately harsh.]
								DexDown 4;
							otherwise if T matches the text "worthless":
								say "You feel your body fully heal[if the player is prone], and are raised back to your feet[end if]! ";
								if the player is at least somewhat continent:
									say "But, oh, you can feel the magic surging down to your loins, making you fully incontinent...";
									BladderIncontinenceUp 10;
									if diaper messing >= 3, RectumIncontinenceUp 10;
								otherwise:
									say "But, oh, bad luck: the magic ricochets off of all of your clothing, surrounding it with negative energy!";
									BodyHeal 10;
									repeat with C running through worn cursable clothing:
										decrease the raw-magic-modifier of C by 1;
							otherwise if T matches the text "guzzling":
								say "You feel your body mostly heal[if the player is prone], and are raised back to your feet[end if]! But the magic ricochets off of all of your clothing!";
								BodyHeal 8;
								repeat with C running through worn transformable clothing:
									potentially transform C;
							otherwise if T matches the text "fraud":
								say "You feel your body significantly heal[if the player is prone], and are raised back to your feet[end if]! But it also feels like there's a dark cloud hanging over you now, like you're going to find yourself getting very unlucky for a while!";
								BodyHeal 7;
								decrease the raw luck of the player by 15;
							otherwise:
								say "You feel your body slightly heal[if the player is prone], and are raised back to your feet[end if]!";
								BodyHeal 5;
							now the stance of the player is 0;
							now the fatigue of the player is 0;
							decrease the princess-power of M by 2;
							if M is carrying royal scepter and the number of undefeated staff members in the location of M > 1 and the princess-power of M >= 10: [should be the first two times]
								say "[speech style of M]'And fuck you.'[roman type][line break]";
								compute M enslaving N;
						otherwise:
							say "Moments later you are both bound in magical rope, and [NameDesc of M][']s wand has been ripped from [his of M] grip. ";
							let PSM be the number of pacified staff members in the location of the player;
							if PSM > 0, say "[BigNameDesc of N] releases [his of N] bound comrade[if PSM > 1]s[end if].";
					if M is not in the location of the player or the player is prone:
						repeat with SM running through monsters in the school:
							now SM is unleashed;
							unless SM is N or SM is M, regionally place SM;
						if diaper focus > 0, now M is diaper-enslaved;
						otherwise now M is sex-enslaved;
						now M is in School34;
						repeat with T running through things held by M:
							if T is clothing, now T is in School15;
							otherwise destroy T;
						compute N dungeon locking;
			if M is guarding:
				now the health of M is 1;
				repeat with SM running through fucked-silly staff members in the location of M:
					compute M enslaving SM;
				if headmistress is in the location of M and headmistress is unleashed and (glittery-wand is not carried by M or glittery-wand is not cursed): [Princess fights headmistress herself]
					compute M enslaving headmistress;
					if headmistress is in the location of the player and headmistress is not unleashed, say MonsterDesc of headmistress;
				let NUSM be the number of undefeated staff members in the location of M;
				if NUSM < a random number between 1 and 2: [A new NPC joins the fray]
					let LSM be the list of alive guarding staff members;
					if the number of entries in LSM > 0:
						sort LSM in random order;
						let SM be entry 1 in LSM;
						now SM is unleashed;
						now SM is in School33;
						interest SM;
						try SM going north;
				if the number of undefeated staff members in the location of M is 0:
					if M is in the location of the player:
						say "[BigNameDesc of M] folds [his of M] hands in triumph.[line break][speech style of M]'It is done. I have dreamed of this moment for such a long time!'[roman type][line break]With a wave of [his of M] hands, everyone else, including [himself of M], is teleported away.";
					now M is in Dungeon11;
					let LN be the list of monsters in the school;
					let NLN be 1;
					repeat with N running through LN:
						repeat with T running through things held by N:
							now T is in School01;
						if N is defeated:
							dislodge N;
							if N is headmistress:
								now N is in Dungeon11;
							otherwise if N is receptionist:
								now N is in School01;
							otherwise if N is nurse:
								now N is in School12;
							otherwise:
								let PHR be a random unbossed placed haunted room;
								let PMR be a random unbossed placed modern room;
								if PHR is a room and the remainder after dividing NLN by 3 is 1:
									now N is in PHR;
								otherwise if PMR is a room and the remainder after dividing NLN by 3 is 2:
									now N is in PMR;
								otherwise:
									now N is in a random unbossed placed labyrinth room;
								increase NLN by 1;
						otherwise:
							destroy N;
					now M is unconcerned;
					now the raw difficulty of M is 99;
					now M is uninterested;
					now the refractory-period of M is the messRefractoryLimit of M + 40;
					now the boredom of M is 0;
					repeat with H running through worn tiara:
						destroy H.

To compute (M - ex-princess) enslaving (N - a monster):
	if diaper quest is 1 or a random number between 0 and diaper lover > 0, now N is diaper-enslaved;
	otherwise now N is sex-enslaved;
	dislodge N;
	now the refractory-period of N is a random number between -100 and 150; [doesn't start off messy]
	if M is in the location of the player:
		let W be a random vibe-wand carried by M;
		if glittery-wand is carried by M, now W is glittery-wand;
		if royal scepter is carried by M, now W is royal scepter;
		say "[if W is not glittery-wand or W is not cursed][one of][BigNameDesc of M] spins [his of M] [ShortDesc of W] and then[or]Once again [NameDesc of M][stopping] blasts[otherwise][BigNameDesc of M] manages to pull the black vibrating wand away from [his of M] crotch for just long enough to hit[end if] [NameDesc of N] with a powerful wave of magical energy. [BigNameDesc of N] screeches in shock as [he of N] has [his of N] outfit transformed, [his of N] clothes becoming [if N is diaper-enslaved]babified[otherwise]ripped[end if] and items of bondage appearing on [his of N] person.";

To compute (M - ex-princess) enslaving (N - headmistress):
	let enslave-success be 1;
	if M is in the location of the player:
		let W be a random vibe-wand carried by M;
		if glittery-wand is carried by M, now W is glittery-wand;
		if royal scepter is carried by M, now W is royal scepter;
		if M is in the location of the player:
			if the health of N > 0 and N is unleashed:
				say "[one of][BigNameDesc of N] notices you and [NameDesc of M] and a look of horror and disbelief crosses [his of N] face.[line break][speech style of N]'No, it can't be! I won't let you!'[roman type][line break][or][stopping]";
				if W is impermanence:
					now enslave-success is 0;
					now the princess-power of M is -1;
					only destroy W;
					now glittery-wand is carried by M;
					now glittery-wand is cursed;
					say "[BigNameDesc of N] throws a magic cloud of shadow towards [NameDesc of M], but [he of M] sees it coming in time and moves to deflect it with [his of M] [ShortDesc of W].[line break][speech style of M]'Too easy!'[roman type][line break][BigNameDesc of M] smiles proudly as [he of M] strikes the shadow with the wand... Or does [he of M]? Just as [he of M] makes [his of M] move, the wand vanishes from [his of M] hand![line break][speech style of M]'Wait WHAT? You gave me a wand of IMPERMANENCE?! YOU IDIOT! NOOOO!'[roman type][line break]That's all [NameDesc of M] has time to say before the shadow wraps itself around the [ShortDesc of W], and a new cursed wand appears in [his of M] hand.[line break][speech style of N]'Here you go, a brand new wand, of my own creation, just for you!'[roman type][line break][BigNameDesc of M] shakes [his of M] hand, trying to drop the black wand, but is unable to. You watch with [horror the sex addiction of the player] as clearly against [his of M] will, [his of M] hand pushes the head of the vibrator towards [his of M] clitoris. There's nothing either of you can do to stop it as [he of M] begins to use the wand to masturbate rather than fight. It looks like you'll have to finish this fight all on your own...";
				otherwise if W is not glittery-wand or (a random number between 1 and 2 is 1 and W is not cursed):
					say "[BigNameDesc of N] tries to throw [one of]a[or]another[stopping] magic cloud of shadow towards [NameDesc of M], but [he of M] [one of]sees it coming in time and defects[or]manages to continue to find the strength to[stopping] it with [his of M] [ShortDesc of W], sending it hurtling ";
					if W is royal scepter:
						say "back to its caster. [BigNameDesc of N] screams as [he of N] is engulfed in the darkness, and when it clears, [he of N] looks rather different.";
					otherwise:
						if the princess-power of M > 9:
							say "harmlessly into a wall.";
							decrease the princess-power of M by 1;
						otherwise:
							say "[one of]narrowly over one shoulder[or]down to the ground between [his of M] knees. Close[or]in a wild random direction that almost hits you in the back[in random order].";
						now enslave-success is 0;
				otherwise:
					now enslave-success is 0;
					now the princess-power of M is -1;
					now glittery-wand is cursed;
					say "[BigNameDesc of N] throws a magic cloud of shadow towards [NameDesc of M], but [he of M] sees it coming in time and deflects it with [his of M] [ShortDesc of W].[line break][speech style of M]'Too easy!'[roman type][line break][BigNameDesc of M] smiles proudly as [he of M] strikes the shadow with the wand. Time seems to slow as [NameDesc of N] grins and says with a glint in [his of N] eye:[line break][speech style of N]'Oh, where did you get that wand from, I wonder?'[line break][speech style of M]'What? NOOOO!'[roman type][line break]That's all [NameDesc of M] has time to say before the shadow wraps itself around the [ShortDesc of W] turning its gems to a jet black. [BigNameDesc of M] shakes [his of M] hand, trying to drop the black wand, but is unable to. You watch with [horror the sex addiction of the player] as clearly against [his of M] will, [his of M] hand pushes the head of the vibrator towards [his of M] clitoris. There's nothing either of you can do to stop it as [he of M] begins to use the wand to masturbate rather than fight. It looks like you'll have to finish this fight all on your own...";
			otherwise:
				say "[BigNameDesc of M] laughs in gleeful triumph as [he of M] blasts [NameDesc of N] with a powerful wave of magical energy. [BigNameDesc of N] screeches in shock as [he of N] has [his of N] outfit transformed";
				now enslave-success is 1;
	if enslave-success is 1:
		if diaper focus > 0, now N is diaper-enslaved;
		otherwise now N is sex-enslaved.

An all later time based rule (this is the asscum annie rule):
	if ex-princess is sex-enslaved and playerRegion is School:
		if the current-torment of ex-princess <= 0:
			now the current-torment of ex-princess is a random number between 25 and 35;
			if ex-princess is in the location of the player:
				say "[BigNameDesc of ex-princess] squeaks as [his of ex-princess] belly fills up with [semen].[line break][speech style of ex-princess]'[one of]I can't hold it!'[or]Not again...'[or]Ergh... so gross...'[or]It's coming out! Don't look!'[then at random][roman type][line break]With a lewd squelch, [semen] starts to spurt out of [his of ex-princess] butthole. ";
				let semen-caught be 0;
				if the player is able to use their hands:
					repeat with B running through carried vessels:
						if semen-caught is 0 and B is not tight topped:
							say "Collect the [semen] in the [ShortDesc of B]?[if B is non-empty] You'll have to dump its current contents of [PotionType of B].[end if]";
							if the player is consenting:
								dump B;
								now the fill-colour of B is creamy;
								now the doses of B is the max-doses of B;
								now semen-caught is 2;
								say "You place the [ShortDesc of B] below [his of ex-princess] butthole and use it to collect all the asscum!";
								now B is anal-origin;
				if semen-caught is 0 and a2m fetish >= 2:
					now autodrink is 1;
					if the player is prone and the player is able to drink:
						say "Do you seal [his of ex-princess] butthole with your lips and drink the asscum?";
						if the player is bimbo consenting, now semen-caught is 1;
					now autodrink is 0;
				if semen-caught is 1:
					say "Opening your [LipDesc] wide, you push them against [NameDesc of ex-princess][']s [asshole], trying to make sure you don't miss a single gulp of the flow of [semen] that's spurting out of [his of ex-princess] backdoor. [if the semen taste addiction of the player < 8]Wincing, you[otherwise]You[end if] swallow down every last drop of [his of ex-princess] magic anal creampie.";
					StomachSemenUp 2;
					moderateHumiliate;
					progress quest of creampie-drinking-quest;
				otherwise if semen-caught is 0:
					say "The [semen] splurts and splatters to the ground[if the player is upright]. If you were on your knees, you might have had a chance to catch it[end if].";
					let L be the location of ex-princess;
					PuddleUp semen by 3 in L;
			otherwise:
				let L be the location of ex-princess;
				PuddleUp semen by 3 in L;
				if ex-princess is in School34 and the player is not in School34 and the player is not in School19, now ex-princess is in School19;
		otherwise:
			decrease the current-torment of ex-princess by time-seconds.

An all later time based rule (this is the fannie facerub rule):
	if ex-princess is diaper-enslaved and playerRegion is School:
		if ex-princess is in the location of the player:
			let D be a random worn dirty diaper;
			if D is diaper:
				say "[BigNameDesc of ex-princess] groans as [he of ex-princess] can't help but rub [his of ex-princess] face into the seat of your diaper.[line break][speech style of ex-princess]'[one of]I can't stop myself... Noooo!'[or]I'm sorry, I'm so sorry!'[or]Eurgh, this is so disgusting!'[or][if the player is in School34]This is your fault, how could you let them defeat us?! I'm gong to be stuck doing this for centuries...'[otherwise][big please] move away from me, don't make me do this!'[end if][or]I can't believe I'm doing this...'[or]Why me? Why me???'[or]Why did I even create this place? Did I secretly... Eurgh... Want this all along?'[or]No. No. It's too disgusting! Make it stop!'[or]Euuuuurgh, yuck yuck yuck...'[or]Ugh, the smell!'[then at random][roman type][line break]";
				ruin vagina;
				compute state check of D;
			if the current-torment of ex-princess <= 0:
				now the current-torment of ex-princess is a random number between 25 and 35;
				say "[speech style of ex-princess]'[one of]Uh-oh[or]Oh god, not again, not so soon[stopping]...'[roman type][line break][BigNameDesc of ex-princess] makes a loud [if diaper messing < 4]hissing[otherwise]BLART[end if] sound from within [his of ex-princess] diaper as [he of ex-princess] soils [himself of ex-princess] again, adding more volume to [his of ex-princess] padding and once again flooding [his of ex-princess] brain with uncontrollable arousal. [big his of ex-princess] mittened hands quickly return to [his of ex-princess] erogenous zones and [he of ex-princess] begins masturbating once again.";
			otherwise:
				say "[BigNameDesc of ex-princess] continues to [one of]rub [his of ex-princess] padded crotch and breasts with [his of ex-princess] mittened hands[or]masturbate as much as [he of ex-princess] can through [his of ex-princess] extra-thick padding[or]pleasure [himself of ex-princess] through [his of ex-princess] thick diaper[or]rub [himself of ex-princess] through [his of ex-princess] soiled nappy[in random order].";
				decrease the current-torment of ex-princess by time-seconds;
				if the current-torment of ex-princess <= 0:
					say "You watch with [horror the sex addiction of the player] as [he of ex-princess] brings [himself of ex-princess] to a shivering, screaming orgasm, leaving [him of ex-princess] panting with exhaustion.";
					increase the torment-count of ex-princess by 1;
					if there is a worn dirty diaper and the player is not in School34:
						say "You feel more dominant!";
						DelicateDown 1;
		otherwise:
			if ex-princess is in School34 and the player is not in School34 and the player is not in School19, now ex-princess is in School19.

To construct unique buttons for (T - ex-princess):
	if ButtonTableFull is 0:
		if T is caged:
			if diaper quest is 1:
				choose a blank row in the Table of Buttons;
				now the ButtonImage entry is Figure of PokeButton;
				now the ButtonCommand entry is "push button";
				now the ButtonColour entry is lightModeFullGreen;
			otherwise if watersports fetish is 1 and the player is not incontinent:
				choose a blank row in the Table of Buttons;
				now the ButtonImage entry is Figure of ToiletButton;
				now the ButtonCommand entry is "use urinal";
				now the ButtonColour entry is lightModeFullGreen;
				if the player is prone, now the ButtonColour entry is lightModeFullYellow; [turn yellow - player needs to stand]
		if T is guarding and (current-final-battle-object is final-battle-wand-curse or current-final-battle-object is final-battle-facehugger):
			choose a blank row in the Table of Buttons;
			now the ButtonImage entry is Figure of TakeAllButton;
			now the ButtonCommand entry is "pull [text-shortcut of ex-princess]";
			now the ButtonColour entry is lightModeFullGreen;
			if the player is immobile, now the ButtonColour entry is lightModeFullRed; [turn red - player immobile]

Check pushing ex-princess when ex-princess is caged:
	allocate 2 seconds;
	say "You push the button. ";
	if the current-torment of ex-princess <= 0:
		now the current-torment of ex-princess is 60;
		if diaper quest is 1, now the current-torment of ex-princess is 999999;
		increase the torment-count of ex-princess by 1;
		say "[BigNameDesc of ex-princess] wails in despair as mechanical winches clunk into motion, forcing [his of ex-princess] arms up and [his of ex-princess] neck and head down, and down, and down until [his of ex-princess] face is pressed into the soiled diapers on the ground. The diapers queued up in the tube roll out, falling down on top of [his of ex-princess] head, half-burying [his of ex-princess] face in gross used nappies.[line break][speech style of ex-princess]'Nooooo! You fucking [cunt]!'[roman type][line break]That's all [he of ex-princess] has time to say before a loud vibrating sound can be heard through the intercom. A frustrated groan escapes [NameDesc of ex-princess][']s lips, which soon turns into a sexual moan[unless the player is broken]. You feel more dominant, and less ashamed of yourself[end if]!";
		DelicateDown 1;
		obsceneDignify;
		repeat with ST running through students in the location of the player:
			FavourUp ST;
	otherwise:
		say "Nothing more happens.";
	do nothing instead.

To compute toilet reaction of (M - ex-princess):
	if M is caged:
		say "[BigNameDesc of M] wails in despair as your [urine] flows towards [his of M] food.[speech style of M]'You fucking [cunt]!'[roman type][line break]That's all [he of M] has time to say before [he of M] has to begin sucking up your [urine] and transferring it, mouthful by mouthful, into [his of M] toilet[unless the player is broken]. You feel more dominant, and less ashamed of yourself[end if]!";
		DelicateDown 1;
		obsceneDignify;
		increase the torment-count of M by 1;
		increase the current-torment of M by 60;
		repeat with ST running through students in the location of the player:
			FavourUp ST;
	otherwise if M is not unleashed:
		say "[BigNameDesc of M] frowns but politely looks the other way.".

An all later time based rule (this is the caged princess torture continues rule):
	if ex-princess is caged and the current-torment of ex-princess > 0:
		let M be ex-princess;
		decrease the current-torment of M by time-seconds;
		if M is in the location of the player:
			if diaper quest is 1:
				say "You watch [NameDesc of M] continue trying to nudge the soiled diapers away from [his of M] face and head [one of]without success[or]in vain[or]but to no avail[at random]. [big his of M] vibrator powerfully and loudly buzzes away from within [his of M] padding.[line break][speech style of M]'[one of]Uhnnnnn[or]Aaaaaah[or]MMMMMMmmmmph[or]Mmmmmmm[at random]...'[roman type][line break]";
				if the remainder after dividing the current-torment of ex-princess by 60 < time-seconds:
					let N be a random threatening monster in the location of the player;
					say "You watch with [horror the bimbo of the player] as [NameDesc of M][']s thighs begin to shake uncontrollably, and moments later [he of M] is screaming into the gross padding in front of [his of M] mouth as [he of M] cums hard. Now out of breath, [he of M] is forced to take in several desperate gasps of pungent diaper air just to stay conscious[if N is monster]. [big he of M] tries to look up towards you and seems to be about to say something, but then looks nervously at [NameDesc of N] and seems to change [his of M] mind about what to say.[line break][speech style of M]'Thank you for making me cum over your fragrant diapers, I hope to smell your diapers again soon[otherwise].[line break][speech style of M]'Please, please, [please], I beg you, help me escape this torment. I'll forever be in your debt[end if]...'[roman type][line break]";
				otherwise if the player is feeling dominant or the player is a nympho:
					say "[one of]You can't help but find the sight rather arousing.[or][or][or][or][or][cycling]";
					arouse 300;
			otherwise:
				say "You watch [NameDesc of M] sucking up [if watersports fetish is 1][urine][otherwise][semen][end if] from the transparent tubing.[line break][speech style of M]'[one of]Hlk Hlk Hlk[or]Hlk Hlk Nnk[or]Gluk gluk gluk[at random]...'[roman type][line break][one of][or][or]With a mouthful of [if watersports fetish is 1][urine][otherwise][semen][end if], [he of M] turns around and spits it into the toilet behind [him of M]. [big he of M] gags in disgust.[line break][speech style of M]'[if a random number between 1 and 2 is 1]Yuck, disgusting[otherwise]Ergh, so gross[end if]!'[roman type][line break][or][or][or][big he of M] spits the mouthful of [if watersports fetish is 1][urine][otherwise][semen][end if] into the toilet. [big he of M] [if a random number between 1 and 2 is 1]half-coughs, half-chokes a few times before gasping for breath[otherwise]moans weakly[end if].[cycling]";
				if the current-torment of ex-princess <= 0:
					let N be a random threatening monster in the location of the player;
					say "Finally the tube is empty, and [NameDesc of M] collapses back to the ground, breathless and exhausted[if N is monster]. [big he of M] looks at you and seems to be about to say something, but then looks nervously at [NameDesc of N] and seems to change [his of M] mind about what to say.[line break][speech style of M]'Thank you for using this obedient toilet slut, I hope you'll come again[otherwise].[line break][speech style of M]'Please, please, [please], I beg you, help me escape this torment. I'll forever be in your debt[end if]...'[roman type][line break]";
				otherwise if the player is feeling dominant or the player is a nympho:
					say "[one of]You can't help but find the sight rather arousing.[or][or][or][or][or][cycling]";
					arouse 300.

An all time based rule (this is the caged princess tortured rule):
	if ex-princess is caged and the current-torment of ex-princess <= 0 and ex-princess is in the location of the player:
		let N be a random threatening friendly monster in the location of ex-princess;
		if N is monster and a random number between 1 and 8 is 1:
			let M be ex-princess;
			say "[if N is student][one of][speech style of N]'Hmm, this looks fun!'[roman type][line break][or][stopping][end if][if watersports fetish is 1 and diaper quest is 0][BigNameDesc of N] waltzes up to the urinal and lets loose a stream of [urine] into the bowl. [BigNameDesc of M] watches on with a mixture of despair and anger.[end if]";
			increase the torment-count of M by 1;
			increase the current-torment of M by 60;
			say "[BigNameDesc of N] pushes the button and you watch with [horror the sex addiction of the player] as ";
			if diaper quest is 1:
				now the current-torment of M is 999999;
				say "mechanical winches clunk into motion, forcing [his of ex-princess] arms up and [his of ex-princess] neck and head down, and down, and down until [his of ex-princess] face is pressed into the soiled diapers on the ground. The diapers queued up in the tube roll out, falling down on top of [his of ex-princess] head, half-burying [his of ex-princess] face in gross used nappies. A loud vibrating sound can be heard through the intercom.[line break][speech style of N]'[one of]Oooh, are you enjoying smelling our nasty used diapers?'[or]That's right bitch, get a good sniff of our diapers while you cum!'[in random order][roman type][line break]A frustrated groan escapes [NameDesc of ex-princess][']s lips, which soon turns into a sexual moan.";
			otherwise:
				now the bladder of N is 0;
				say "the [if watersports fetish is 1][urine][otherwise][semen][end if] begins to flow down the tube.[line break][speech style of N]'[one of]Get to work, [if watersports fetish is 1]toilet [boy of M][otherwise]cum-bucket[end if][or]Time's ticking, [cunt][or]Grub's up, bitch[then at random]!'[roman type][line break][BigNameDesc of M] [one of]sobs quietly to [himself of M][or]mutters expletives under [his of M] breath[or]groans weakly[in random order] as [he of M] gets to work.".


[We can make this resolve to 'yes' and also output some text about what they get up to instead of taking their action.]
Definition: ex-princess (called M) is distracted:
	if M is unconcerned and headmistress is in the location of M and M is in the location of the player and the teaseTimer of headmistress <= 0:
		let E be a random vibe-wand held by M;
		if E is nothing, now E is royal scepter;
		say "[speech style of M]'[one of]I'm going to pay you back for every single time you forced me to cum, bitch[or]Found you again[stopping]!'[roman type][line break]Suddenly, [NameDesc of M] steps behind [NameDesc of headmistress] and pushes the head of [his of M] [ShortDesc of E] up against [NameDesc of headmistress][']s snatch.[line break][speech style of headmistress]'Oh Lo!'[roman type][line break][BigNameDesc of headmistress] groans, trying to keep [his of headmistress] tongue out of [his of headmistress] mouth as the powerful magical artefact between [his of headmistress] legs begins to vibrate, sending incomparably powerful vibrations directly into [his of headmistress] delicate sex.[line break][speech style of M]'[one of]Cum for me, slave[or]Come on slut, I know you've got another one in you[stopping].'[roman type][line break][BigNameDesc of M] teases [him of headmistress].[paragraph break][speech style of headmistress]'GGGGAAAAH!'[roman type][line break][BigNameDesc of headmistress] yells as [he of headmistress] cums, [his of headmistress] knees buckling as the sensations overwhelm [him of headmistress]. [big he of headmistress] ends up face down on the floor, shuddering in pleasure, still holding [his of headmistress] tongue out of [his of headmistress] mouth to avoid being shocked, which means that [he of headmistress][']s effectively licking the dirty floor.[line break][speech style of M]'[one of]Pathetic[or]And remember, if you lose count of how many times I've done this, we start again from zero[in random order].'[roman type][line break]You can't help but find the sight of this very arousing.";
		Arouse 2000;
		now the teaseTimer of headmistress is 50;
		decide yes;
	if M is unconcerned and golden-phallus is held by M:
		if golden-phallus is carried by M:
			if M is not in the location of the player: [she puts it in her butt]
				now golden-phallus is worn by M;
				now golden-phallus is expansion;
				now golden-phallus is cursed;
		otherwise: [it's worn]
			if shopkeeper is undefeated and shopkeeper is awake and M is in the location of shopkeeper and M is in the location of the player:
				say "[BigNameDesc of M] suddenly drops to [his of M] knees in front of [NameDesc of shopkeeper].[line break][speech style of M]'You bastard... You win. I've rescinded the new tax law. Now PLEASE let me remove this infernal thing!'[roman type][line break][BigNameDesc of shopkeeper] smirks.[line break][speech style of shopkeeper]'My my my, I'm sure I don't have any clue what you're talking about. I'm so glad to hear that you've seen reason though. And also, for completely no reason, I'm just going to say the words [']GOLDEN BITCH!['] out loud.'[roman type][line break][BigNameDesc of M] makes a panicked whining sound, and then squats and pulls [his of M] [if diaper lover > 0]diaper[otherwise]panties[end if] to one side. The [golden-phallus] you had brought to [him of M] comes sliding out of [his of M] asshole, except now it's three or four times as thick - you can't believe that thing was inside [him of M] - it's almost as thick as your leg!!![line break][speech style of shopkeeper]'Oh you put it in your asshole? You poor thing.'[roman type][line break][BigNameDesc of shopkeeper] remarks, but [he of shopkeeper] sounds more amused than remorseful. [big he of shopkeeper] bends down and picks [NameDesc of golden-phallus] up.[line break][speech style of shopkeeper]'I'm sorry, this must have been a faulty model. I'll be sure to return it to the sender, and inform them that your experience was less than satisfactory. And that they no longer have to concern themselves with import duty.'[roman type][line break][BigNameDesc of shopkeeper] says mockingly.[paragraph break]And then [NameDesc of shopkeeper] sighs, and offers the squatting, panting [NameDesc of M] [his of shopkeeper] hand.[line break][speech style of shopkeeper]'No hard feelings?'[roman type][line break][BigNameDesc of M] reluctantly accepts [his of shopkeeper] hand, and allows [him of shopkeeper] to pull [him of M] to [his of M] feet.[line break][speech style of M]'Ask me again when my asshole has closed back up properly.'[roman type][line break][BigNameDesc of M] grumbles.";
				cutshow Figure of princess cutscene 1 for M;
				now golden-phallus is carried by shopkeeper;
				now golden-phallus is bland;
				now golden-phallus is sure;
				now golden-phallus is identified;
				decide yes;
			otherwise if the refractory-period of M < 0:
				now the refractory-period of M is a random number between 18 and 36;
				if M is in the location of the player, say "[BigNameDesc of M] [one of]squeals, winces, and grasps at [his of M] crotch[one of] with a shocked look on [his of M] face.[line break][speech style of M]'Oh my god! I mean... It's nothing...'[roman type][line break][or]. [stopping][big he of M] is walking with a very visible waddle.[or]staggers, grasps [his of M] crotch, and [one of]grunts[or]groans[or]moans[at random]. What is going on down there?![cycling]";
				decide yes;
	decide no.

Definition: ex-princess is messy:
	if diaper quest is 1 or diaper messing >= 7:
		if it is unconcerned and the refractory-period of it <= (the messRefractoryLimit of it), decide yes;
		if it is unleashed and (it is grabbing the player or it is penetrating face or (it is uninterested and the boredom of it > 0)), decide yes;
		if it is diaper-enslaved and the current-torment of it <= 0, decide yes;
	decide no.
Definition: ex-princess (called M) is just messing:
	if M is unconcerned and M is messy and the refractory-period of M + 4 > the messRefractoryLimit of M, decide yes;
	decide no.

To compute boredom reset of (M - ex-princess):
	if M is unleashed and M is in the location of the player, say "[BigNameDesc of M][']s diaper magically cleans itself.";
	now the boredom of M is 0.

To compute mess moment of (M - ex-princess):
	if M is in the location of the player:
		compute default mess moment of M;
	otherwise if M is unconcerned:
		bore M;
		now M is in Dungeon11.

To say SuddenMessFlav of (M - ex-princess):
	say "[speech style of M]'Oh crap, here we go again...'[roman type][line break][BigNameDesc of M] grimaces and quickly adopts a squatting stance. Then you hear the loud sound of [if diaper messing >= 7][his of M] butthole rasping and squelching[otherwise]liquid hissing[end if] as [he of M] fills [his of M] huge diaper.";
	if M is unconcerned:
		if M is interested:
			if M is in Dungeon11:
				say "[BigNameDesc of M] sits on [his of M] throne.[line break][speech style of M]'Thanks to this stupid curse, this diaper never comes off. There's only one way to make it clean itself...'[roman type][line break][BigNameDesc of M] presses [his of M] wand into the front of [his of M] padding.[line break][speech style of M]'And that's for me to have an orgasm...'[roman type][line break]The magic wand begins vibrating, and while [he of M] begins by trying to do [his of M] best to hide it, [NameDesc of M][']s face soon becomes one of poorly concealed pleasure.";
				cutshow Figure of original diapered princess wet masturbating confident for M;
			compute ChangeRequest of M;
		otherwise:
			check perception of M.

To compute ChangeRequest of (M - ex-princess):
	if the player is not in danger and the player is not immobile:
		say "[BigNameDesc of M] looks at you bashfully.[line break][speech style of M]'[if M is in Dungeon11]This blasted curse is fighting against me... stopping me from being able to make myself cum... [end if]I'm sorry to ask this of you[one of], [if M is not in Dungeon11]but... it seems that I've been cursed to only get a change when someone else makes me... makes me cum... by letting me grind on their face. No matter what I try, I haven't managed to reverse the curse yet. Would you... would you let me... I'm really sorry to ask this of you, [end if]but... would you let me use your face[or] again, but is there any chance you'd be willing to let me use your face so that I can get a change[stopping]?'[roman type][line break]";
		compute ChangeRequestDecision of M.

To compute ChangeRequestDecision of (M - ex-princess):
	say "Let [NameDesc of M] grind [his of M] [if diaper messing >= 7]messy[otherwise]piss-soaked[end if] diaper on your face until [he of M] cums?";
	if the player is consenting:
		if the player is upright:
			say "[bold type]You get on your knees.[roman type][line break]";
			now the stance of the player is 1;
		let G be wetDiaperFacesitGrossnessLevel;
		if diaper messing >= 7, now G is messyDiaperFacesitGrossnessLevel;
		say "[BigNameDesc of M] smiles with shy appreciation as [he of M] mounts your nose with the front of [his of M] diaper. ";
		if the player is air breathing vulnerable:
			say "The strong smell hits your nostrils as [he of M] begins to grind away.";
			SmellGrossOut G;
			say "It's not a short process for [him of M] to build all the way to climax, and so by the time [he of M] finally does reach [his of M] peak, your nostrils and airways have been fully drenched in [his of M] shameful scent.";
		SlowGrossOut G;
		now the refractory-period of M is a random number between 0 and 200;
	otherwise if M is not in Dungeon11:
		say "You wrinkle your nose and step away from [him of M]. [big he of M] looks crestfallen and frustrated.[line break][speech style of M]'...Fine. I won't force you. But don't expect me to be able to help you while I'm stuck like this.'[roman type][line break][big he of M] waddles away from you, towards the Throne Room.";
		bore M;
		now M is in Dungeon11;
	otherwise:
		say "You make no move to help [him of M], [one of]leaving[or]continuing to leave[stopping] [him of M] with [his of M] frustration.".


To compute diaper mess reaction of (M - ex-princess):
	if M is unconcerned, say "[BigNameDesc of M] looks at you with a caring smile.".

To compute MasturbationReaction of (M - ex-princess):
	if M is unconcerned, say "[BigNameDesc of M] looks at you with a mildly interested smile.".

Princess ends here.
