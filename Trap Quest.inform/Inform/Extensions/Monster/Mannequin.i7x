Mannequin by Monster begins here.

A mannequin is a kind of monster.  A mannequin is usually woods dwelling.  A mannequin is usually willing to do anal.  A mannequin is usually willing to do oral. A mannequin is poison immune.

Figure of topless mannequin is the file "mannequin1.png".
Figure of stylish mannequin is the file "mannequin2.png".
Figure of swimmer Mannequin is the file "mannequin3.png".
Figure of flirty mannequin is the file "mannequin4.png".
Figure of lycra-clad mannequin is the file "mannequin5.png".
Figure of latex-clad Mannequin is the file "mannequin6.png".
Figure of lingerie-clad mannequin is the file "mannequin7.png".
Figure of skater mannequin is the file "mannequin8.png".
Figure of goth Mannequin is the file "mannequin9.png".

The description of mannequin is usually "[MannequinDesc]".  The text-shortcut of mannequin is "man".

A stylish mannequin is a kind of mannequin.  There is 1 stylish mannequin.
The printed name of stylish mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]stylish mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of stylish mannequin is "stm".
A topless mannequin is a kind of mannequin.  There is 1 topless mannequin.
The printed name of topless mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]topless mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of topless mannequin is "tom".
A flirty mannequin is a kind of mannequin.  There is 1 flirty mannequin.
The printed name of flirty mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]flirty mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of flirty mannequin is "fli".
A lycra-clad mannequin is a kind of mannequin.  There is 1 lycra-clad mannequin.
The printed name of lycra-clad mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]lycra clad mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of lycra-clad mannequin is "lyc".  Understand "lycra", "clad" as lycra-clad mannequin.
A swimmer mannequin is a kind of mannequin.  There is 1 swimmer mannequin.
The printed name of swimmer mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]swimmer mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of swimmer mannequin is "swi".
A latex-clad mannequin is a kind of mannequin.  There is 1 latex-clad mannequin.
The printed name of latex-clad mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]latex clad mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of latex-clad mannequin is "lat".  Understand "latex", "clad" as latex-clad mannequin.
A lingerie-clad mannequin is a kind of mannequin.  There is 1 lingerie-clad mannequin.
The printed name of lingerie-clad mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]lingerie clad mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of lingerie-clad mannequin is "lin".  Understand "lingerie", "clad" as lingerie-clad mannequin.
An skater mannequin is a kind of mannequin.  There is 1 skater mannequin.
The printed name of skater mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]skater mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of skater mannequin is "ska".
A goth mannequin is a kind of mannequin. There is 1 goth mannequin.
The printed name of goth mannequin is "[if item described is in the location of the player][TQlink of item described][end if][input-style]goth mannequin[shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". A goth mannequin is usually mansion dwelling. A goth mannequin is usually not woods dwelling. The text-shortcut of goth mannequin is "got".

To say MannequinDesc:
	if images visible is 1:
		if item described is goth mannequin:
			display the Figure of goth mannequin;
		otherwise if item described is stylish mannequin:
			display the Figure of stylish mannequin;
		otherwise if item described is topless mannequin:
			display the Figure of topless mannequin;
		otherwise if item described is flirty mannequin:
			display the Figure of flirty mannequin;
		otherwise if item described is lycra-clad mannequin:
			display the Figure of lycra-clad mannequin;
		otherwise if item described is swimmer mannequin:
			display the Figure of swimmer mannequin;
		otherwise if item described is latex-clad mannequin:
			display the Figure of latex-clad mannequin;
		otherwise if item described is lingerie-clad mannequin:
			display the Figure of lingerie-clad mannequin;
		otherwise:
			display the Figure of skater mannequin;
	if item described is goth mannequin or item described is topless mannequin or item described is stylish mannequin or item described is flirty mannequin, say "[if item described is goth mannequin]An unsettling female mannequin complete with garish makeup and a blank stare. Her solid black hair frames a stark pale face which emphasises her scarlet red lips.  Everything she is wearing is black: her criminally short punk dress, her knee high boots and her gloves[otherwise if item described is topless mannequin]A topless female mannequin with a tanned skin tone, wearing tight denim jeans. She has blonde hair drawn back into a silky ponytail and a pink scrunchie around each wrist. Her breasts are perfectly spherical and unmoving[otherwise if item described is stylish mannequin]An unsettling female mannequin complete with striking makeup and a blank stare. 'She' has armpit-length black ponytailed hair, and a short pink dress underneath a trendy belt. It is low-cut and very thin, so her featureless breasts and smooth crotch are on full display for your prying eyes. She has long white fingernails, which match the latex handbag hanging over one of her shoulders[otherwise]An unsettling female mannequin, with a warm tanned skin tone and an almost smirking facial expression complemented by excessive amounts of eye shadow and eyeliner.  Wavy black hair flows down her front and back to her waist, giving her a very provocative look.  This is emphasised by the sheer red negligee she is wearing.  It is practically see-through when the light touches it in the right way[end if].  "; [split into two because say clause was too long]
	otherwise say "[if item described is lycra-clad mannequin]An unsettling female mannequin, with a warm tanned skin tone and a politely smiling facial expression, painted in 'night on the town' make up.  Reddish-brown hair rests on her shoulders, kept mostly tidy thanks to a ponytail.  Her grey playsuit boasts a very low V neck that almost exposes her belly button, and is ever so slightly sheer, giving you glimpses of what is underneath.  Or rather, what would be underneath if she wasn't a doll[otherwise if item described is swimmer mannequin]An unsettling female mannequin complete with striking makeup and a blank stare. 'She' has armpit-length black ponytailed hair and is modelling a tight blue one-piece swimsuit[otherwise if item described is latex-clad mannequin]An unsettling female mannequin complete with striking makeup and a blank stare. 'She' has armpit-length black ponytailed hair, and is modelling a provocative pink latex dress.  The V neck is so low that her belly button is left bare.  A pair of grey latex knee-length boots adorn her feet[otherwise if item described is lingerie-clad mannequin]An unsettling female mannequin, with a warm tanned skin tone and an almost smirking facial expression complemented by make up that really make her eyes pop.  Her 'outfit' consists of only the most skimpy, lacy black and burgundy lingerie: a full set of strapless bra, thong, stockings and suspenders[otherwise]An unsettling female mannequin complete with striking makeup and blankly smiling stare.  A pink punk-styled top sits above a tiny white skater mini-skirt, which barely hides whatever underwear she might be wearing.  Fishnet tights sit under multicoloured striped knee-length socks and knee-pads.  Fingerless gloves, elbow-pads and black & white trainers finish off the skater look[end if].  ";
	say "A small handbag hangs delicately over one shoulder.  She would look just like a real human if it wasn't for her doll-like joints and completely static facial expression.";
	if diaper quest is 0, say "[if the bimbo of the player <= 4][first custom style][one of]Wow, she's really creepy.[or]She doesn't look tough, but she's not human so I should keep an eye out.[or]I wonder if she's got a pussy.[or]This reminds me of this one time me and my friend took a department store mannequin home.[or]Even the clothing models look like total harlots.[or]Hmm, I doubt she can feel pain, but she probably has some exploitable weak points.[or]So that's how I'm expected to dress. Disgusting.[in random order][otherwise if the bimbo of the player <= 8][variable custom style][one of]She's actually kind of attractive for a walking piece of plastic.[or]What kind pervert dressed her up so sexy?[or]Is that how I'll look by the end of this? No... I've got to resist![in random order][otherwise if the bimbo of the player <= 12][second custom style][one of]Her make up looks really good.[or]Is that a wig or is that real hair?  She has good fashion sense.[or]Those clothes don't look too bad...[in random order][otherwise if bimbo of the player <= 16][second custom style][one of]I should have her do my make up![or]Maybe she can give me some fashion pointers, teehee![or]People stare at her all day long, just like they will with me!*giggle*[or]I want, no, I *NEED* to look like her![in random order][otherwise if bimbo of the player <= 20][second custom style][one of]I wish *MY* head could just be empty plastic![or]Maybe I should be a mannequin when I get home![or]She[']s so lucky, people make all her choices for her every day![in random order][end if][roman type][line break]".


To say MannequinHairColour of (M - a mannequin):
	say "[if M is stylish mannequin]silvery blonde[otherwise if M is topless mannequin]strawberry blonde[otherwise if M is flirty mannequin]cherry blonde[otherwise if M is lycra-clad mannequin]lustrous black[otherwise if M is swimmer mannequin]waxy black[otherwise]platinum blonde[end if]";

To set up (M - a mannequin):
	reset M;
	now the monstersetup of M is 1;
	now the difficulty of M is 5;
	now the health of M is the maxhealth of M.

This is the spawn initial mannequin rule:
	if the number of alive mannequins is 0:
		let M be a random mannequin;
		summon M in the woods.
The spawn initial mannequin rule is listed in the setting up woods monsters rules.

To DifficultyUp (M - a mannequin) by (X - a number):
	while X > 0:
		increase the difficulty of M by 1;
		decrease X by 1;
	if diaper quest is 0 and the difficulty of M > 10 and the number of sex toys retained by M is 0:
		let P be a random off-stage dong;[If she doesn't have a dildo and her difficulty is high enough, she gets a dildo from off-stage]
		if a random number between 5 and the difficulty of M > the size of P * 2, now M is retaining P.

[It would be annoying to manage which dildo she's using, so we have her discard any extra dildos when we make this check]
To decide which number is the girth of (M - a mannequin):
	let C be the number of sex toys retained by M;
	if C > 0 :
		let P be a random sex toy retained by M;
		repeat with T running through sex toys retained by M:
			unless T is P:
				now M is not retaining T;
		decide on the size of P;[If the mannequin has a plug or a dildo, she can use it on you]
	decide on 2.

Part 1 - Misc Flavour

To say SummoningFlav of (M - a mannequin):
	if M is in the location of the player, say "A quiet moan can be heard as several gears, pieces of clothing, and what looks like ceramic fall through the stone opening between the statue's legs. The gears whir rapidly as the pieces begin to put themselves together, forming a [printed name of M].";[TODO: goth mannequin eventually]
	otherwise say "You hear the sound of mechanical joints whirring.".

To say SummoningFlav of (M - a goth mannequin):
	if M is in the location of the player, say "A deafening whir passes through the room as every mannequin turns toward the mummy at once, and the fabric slowly begins to unravel. A porcelain face comes into view as the linens fall away, followed by jointed arms, legs and stylish black clothing. The whir peters out as the [printed name of M] takes its first few steps into the room, and the mummy's wrappings slowly reform behind it.";[TODO: goth mannequin eventually]
	otherwise say "You hear the sound of mechanical joints whirring.".

Part 2 - Perception

To compute perception of (M - a mannequin):
	if permanent makeup is 0 or the sex addiction of the player < 18:
		if the latex-transformation of the player < 3:
			anger M;
			say "The [M] seems to notice you!  Uh-oh...";
		otherwise:
			bore M;
	otherwise if the player is a bit horny:
		if a random number between 5 and 25 < the anal sex addiction of the player and the player is not immobile and the player is upright and the player is not unable to orgasm so soon and the player is not flying:
			say "You look at the [M] and feel a burning desire to have an orgasm milked out of you[if the latex-transformation of the player > 3], even though you know it won't work[end if].  In this moment of weakness, you drop to your knees and look at the [M] expectantly.";
			try kneeling;
			if the player is not ass protected, now presented-orifice is asshole;
			if the player is prone or the bimbo of the player > 14:
				say "The [M] seems to notice you!  You giggle in nervous delight.";
				anger M;
		otherwise if the bimbo of the player > 14:
			say "The [M] seems to notice you!  You giggle in nervous delight.";
			anger M;
	otherwise if M is objectifying the player:
		say "The [M] seems to notice you!  You giggle in nervous delight.";
		anger M.

To compute DQ perception of (M - a mannequin):
	if the make-up of face < 3 or the sex addiction of the player < 18:
		anger M;
		say "The [M] seems to notice you!  Uh-oh...";
	otherwise if M is objectifying the player:
		anger M;
		say "The [M] seems to notice you!  You giggle in nervous delight.".


Part 3 - Combat

Section 1 - Attack

Definition: a mannequin (called M) is eager to slap:
	if a random number between 1 and 3 is 1, decide yes;
 	decide no.

To compute flying player taunting of (M - a mannequin):
	if a random number from 1 to 6 is 1, say "The [M] is [one of][or]still [stopping]watching you, waiting.".

To decide which number is the rounds of sex left of (M - a mannequin):
	if the latex-transformation of the player > 2, decide on 0;
	decide on 10 - ((a random number between 1 and 3) + arousal of the player / 1000 + the sex-length of M).

To compute unique climax of (M - a mannequin) of (F - asshole):
	FavourUp M;
	if the latex-transformation of the player > 2:
		say "The [M] looks confused, removes her hand from your [asshole] and leaves you alone.";
		bore M for 600 seconds;
	otherwise:
		if the player is unable to orgasm so soon and the anal sex addiction of the player > 2, say "Even though you came recently, you feel that familiar pressure building up inside you.";
		anally orgasm shamefully;
		if image cutscenes is 1, display figure of mannequin cutscene 4;
		bore M;
		let P be a random sex toy retained by M;
		if P is sex toy:
			if a random number between the difficulty of M and 5 > the size of P * 2:
				say "The [M] [if P is anal beads]pushes all [the notches of P] into your asshole before removing her hands and apparently losing interest[otherwise]removes her hands from the [printed name of P], and seems to lose interest[end if].";
				now M is not retaining P;
				summon P;
				if P is anal beads, now the notch-taken of P is the notches of P;
				[DifficultyDown M by 3; We'll see if this is needed]
			otherwise:[if she doesn't leave it inside you, she saves it for next time.]
				say "The [M] pulls the [printed name of P] out of your asshole and seems to lose interest.";
		otherwise:
			say "The [M] removes her fingers from your [asshole], and seems to lose interest.";

To compute anal sex of (M - a mannequin):
	[What she's doing to your ass]
	if there is a sex toy retained by M:
		let P be a random sex toy retained by M;
		if P is anal beads:
			say "The [M] [one of]pushes the beads in and out of your [asshole] one at a time, stretching you over and over[or]pushes each bead into your [asshole] and slowly pulls them out one by one, forcing you to experience the stretching of your sphincter over and over[or]holds one bead just inside your [asshole], keeping you as stretched and full as possible[at random][run paragraph on]";
		otherwise:
			say "[one of]The [M] keeps fucking you with the [printed name of P][or]The [M] continues pumping the [printed name of P] in and out of your [asshole][or]The [M] fucks you with the [printed name of P][or]The [M] keeps pushing the [printed name of P] in and out of your hole[at random][if the player is male], [one of]angling it against your prostate[or]keeping the tip firmly in contact with your prostate[or]repeatedly prodding your 'g-spot'[or]relentlessly pounding your prostate[at random][end if][run paragraph on]";
	otherwise:
		say "[if the player is male]The [M] [one of]keeps expertly stroking your prostate[or]keeps skilfully prodding your prostate[or]firmly rubs your prostate[or]relentlessly prodding your prostate with expert movements of her fingers[at random][otherwise]The [M] keeps [one of]expertly pressing on your g-spot through your [asshole][or]pressing that same spot inside your hole, always with that perfectly maddening amount of force[or]rubbing your g-spot through your [asshole], endlessly repeating her perfect movements[at random][end if][run paragraph on]";
	[How she's interacting with your crotch.]
	if there is a worn chastity cage:
		say ", as if milking an orgasm out of you!";
	otherwise if the player is female:
		say " as she [if there is a worn pussy protection clothing]rubs your clit[otherwise][one of]strokes[or]teases[at random] your clit[end if], as if milking an orgasm out of you!";
	otherwise if the size of penis > 0:
		say " as she [unless there is a worn pussy protection clothing and the size of penis > 2][one of]pumps[or]strokes[or]jerks off[at random] your [ShortDesc of penis][otherwise]massages your [ShortDesc of penis][end if], [if the size of penis < 3]as if it was a clitoris![otherwise]as if milking you![end if]";
	otherwise:
		say ", as if milking an orgasm out of you!";
	increase the sex-length of M by 1.

To compute the default taunting of (M - a mannequin):
	say "[one of]The [M] stands in front of you, motionless.[or][or][cycling]".

Definition: a mannequin (called M) is requiring facial penetration:
	decide no.

To compute (M - a goth mannequin) entering mouth:
	let S be a random off-stage pink scrunchie;
	if the make-up of face is 3 and permanent makeup is 0:
		say "The [M] [if presented-orifice is face]seems to appreciate you holding your mouth open and your head still and [end if]pulls out a brush you've never seen before.  As she brushes it across your face, nothing observable seems to change but your current makeup feels more... durable.";
		now permanent makeup is 1;
		satisfy M;
	otherwise if the lips of face < 2 + artificial enhancements fetish:
		say "The [M] puts both hands on your cheeks, pushing until your lips pucker out. She pulls out a flesh-coloured tube of lipstick and applies several coats to your puckered lips. The feeling of having your lips pushed out lingers after she releases you...";
		if image cutscenes is 1, display figure of mannequin cutscene 1;
		LipsUp 1;
		satisfy M;
	otherwise if the make-up of face > a random number between 0 and 2 and S is actually summonable and the largeness of hair > 4:
		if the player is ponytailed, say "The [M] pulls out an identical pink scrunchie and fashions your hair into a [if the bimbo of the player > 8][second custom style]sexy [roman type][end if]pair of pigtails.[if the player is male][one of][line break][variable custom style]There's no mistaking it now, no men wear pigtails.[roman type][line break][or][stopping][end if]";
		otherwise say "The [M] pulls out a pink scrunchie and fashions your hair into a [if the bimbo of the player < 7]tidy[otherwise][second custom style]cute[roman type][end if] ponytail.[if the player is male][one of][line break][variable custom style]This is definitely girly hair now.[roman type][line break][or][stopping][end if]";
		summon S cursed;
		satisfy M;
	otherwise if the make-up of face > 1 and the largeness of hair > 4 and a random number between 1 and 5 > 2 and artificial enhancements fetish is 1 and the fake largeness of hair < 10:
		FakeHairUp 4;
		say "The [M] pulls out some fake hair extensions in just your colour, and at rapid speed skilfully attaches them to your existing hair.  You now have [ShortDesc of hair]!";
		satisfy M;
	otherwise if asshole is an actual target:
		follow the monster asshole insertion rules; [That's right, we decide that the mannequin goes after the asshole instead.]
	otherwise:
		compute SelectionFailure of M.

To compute (M - a mannequin) entering mouth:
	let H be a random off-stage rubber top hat;
	let S be a random off-stage pink scrunchie;
	let F be a random off-stage fake nails;
	if H is actually summonable and artificial enhancements fetish is 1:
		say "The [M] pulls out a tiny pink top hat and fixes it onto your head!  You immediately feel a bit more bimbo-like...";
		summon H cursed;
		satisfy M;
	otherwise if the make-up of face is 3 and permanent makeup is 0:
		say "The [M] [if presented-orifice is face]seems to appreciate you holding your mouth open and your head still and [end if]pulls out a brush you've never seen before.  As she brushes it across your face, nothing observable seems to change but your current makeup feels more... durable.";
		now permanent makeup is 1;
		satisfy M;
	otherwise if the make-up of face > a random number between 0 and 2 and S is actually summonable and the largeness of hair > 4:
		if the player is ponytailed, say "The [M] pulls out an identical pink scrunchie and fashions your hair into a [if the bimbo of the player > 8][second custom style]sexy [roman type][end if]pair of pigtails.[if the player is male][one of][line break][variable custom style]There's no mistaking it now, no men wear pigtails.[roman type][line break][or][stopping][end if]";
		otherwise say "The [M] pulls out a pink scrunchie and fashions your hair into a [if the bimbo of the player < 7]tidy[otherwise][second custom style]cute[roman type][end if] ponytail.[if the player is male][one of][line break][variable custom style]This is definitely girly hair now.[roman type][line break][or][stopping][end if]";
		summon S cursed;
		satisfy M;		
	otherwise if the make-up of face > 1 and the largeness of hair > 4 and a random number between 1 and 5 > 2 and artificial enhancements fetish is 1 and the fake largeness of hair < 10:
		FakeHairUp 2;
		say "The [M] pulls out some fake hair extensions in just your colour, and at rapid speed skilfully attaches them to your existing hair.  You now have [ShortDesc of hair]!";
		satisfy M;
	otherwise if the make-up of face < 3:
		say "The [M] [if presented-orifice is face]seems to appreciate you holding your mouth open and your head still and [end if]pulls out a makeup kit, and starts wildly applying it to your face!";
		FaceUp 1;
		if image cutscenes is 1, display figure of mannequin cutscene 2;
		satisfy M;
	otherwise if artificial enhancements fetish is 1 and F is actually summonable:
		say "The [M] pulls out a set of long and slutty pink fake nails.  Before you can react to what is happening, all ten have been glued on top of your real nails.  Wow that was fast!  You are immediately filled with a horrible sense of dread; almost all you can think about is [if the player is horny]how horny you are and [end if]how awful, and painful, it would be if any of your nails were to break.  The invasive thought is so powerful that you also feel significantly weaker and slower as your mind makes you think twice about every action you take.";
		summon F cursed;
		satisfy M;
	otherwise if asshole is an actual target:
		follow the monster asshole insertion rules; [That's right, we decide that the mannequin goes after the asshole instead.]
	otherwise:
		compute SelectionFailure of M.
			
To compute (M - a mannequin) entering asshole:
	let P be a random sex toy retained by M;
	if P is sex toy:
		say "The [M] pushes a [printed name of P] into your [asshole], [if size of penis is 0]angling it toward herself as she begins pumping it in and out of your hole[otherwise]angling it against your prostate with pinpoint accuracy as she begins pumping it in and out of your hole[end if].";
	otherwise:
		say "The [M] inserts two fingers into your [asshole], and starts stroking in a come hither motion.";
	if the player is female: 
		say "[unless there is a worn chastity cage or the player is pussy protected]With her other hand she starts expertly pinching and rubbing your clitoris.  [end if]She's trying to force an orgasm out of you!";
	otherwise if the size of penis > 0:
		say "She [if there is a worn chastity cage]grasps your testicles with her other hand, delicately massaging them as her faux-knuckles bump against your cage.  [otherwise if the size of penis > 3]grabs your [ShortDesc of penis] with her other hand and skilfully begins stroking away.  [otherwise]delicately teasing your [ShortDesc of penis] with her fingertips.  [end if]She's trying to force an orgasm out of you!";
	otherwise:
		say "She holds you still with her other hand.  It's like she's trying to force an anal orgasm out of you!";
	if image cutscenes is 1, display figure of mannequin cutscene 3;
	now M is penetrating asshole;
	now the sex-length of M is 0.

This is the mannequin forces orgasm through diaper rule:
	if there is a worn total protection diaper and the player is diaper focused and the player is a bit horny and the player is not grossed out:
		let M be current-monster;
		let D be a random worn diaper;
		while the coverer of D is clothing:
			now D is the coverer of D;
		say "The [M] seems to detect that you are very aroused.  Staring directly into your eyes with her blank expression, she presses a hand to the front of your [D] and pushes firmly.  Her hand begins to vibrate, and within seconds has reached an intensely strong force[if there is a worn chastity cage]!  The pulses flow straight through your [random worn chastity cage] and into your [genitals][end if]!  [variable custom style]Unfff.... it's too strong!  I'm gonna...[roman type]Your eyes roll into the back of your head and you start shaking.";
		orgasm;
		SexAddictUp 1;
		say "The [M] cocks her creepy head to one side, silently pulls her hand away and then leaves you alone to recover[if the player is male and the bimbo of the player < 6]I can't believe I just came into my diaper...[end if].[roman type][line break]";
		bore M;
		FavourUp M;
		rule succeeds;
	otherwise if there is a worn diaper and diaper lover >= 3 and the stomach-food of the player > 3 + incontinence and the player is not feeling full and face is not actually occupied and the player is not incontinent and the make-up of face > 0: [Will only occur if there is enough food to ensure that the player will go from NOT feeling full to feeling full.]
		let M be current-monster;
		say "You see the [M] moving something towards your face and expect her to apply a new layer of make up.  But instead, she pushes a small white pill into your mouth and down your throat!  You are forced to swallow it before you can react!  You suddenly hear your stomach churning loudly, as it speeds up your respiratory processes dramatically, quickly digesting all the food in your stomach!";
		increase rectum by the stomach-food of the player;
		now the stomach-food of the player is 1;
		say "The [M] cocks her creepy head to one side, silently turns away and then leaves you alone to contemplate what might be coming very soon...";
		bore M;
		FavourUp M;
		rule succeeds.
The diaper punishment rule of a mannequin is usually the mannequin forces orgasm through diaper rule.	

Section 2 - DQ

The mannequin diaper quest rules is a rulebook.  The diaper quest rules of a mannequin is usually the mannequin diaper quest rules.

The mannequin forces orgasm through diaper rule is listed first in the mannequin diaper quest rules.

This is the mannequin donates clothing rule:
	let S be a random off-stage woven stockings;
	if S is actually summonable and the number of worn shoes is 0:
		summon S cursed;
		say "The [current-monster] produces a [printed name of S] and pushes them onto your feet!  Uh-oh!  ";
		satisfy current-monster;
		rule succeeds;
	let B be a random off-stage flimsy bra;
	if B is actually summonable and the number of worn dress is 0:
		summon B cursed;
		say "The [current-monster] produces a [printed name of B] and pulls it over your breasts!  Uh-oh!  ";
		satisfy current-monster;
		rule succeeds;
	let F be a random off-stage floral purple sissy dress;
	if F is actually summonable:
		summon F cursed;
		say "The [current-monster] produces a [F] and pulls it over you!  Uh-oh!  ";
		satisfy current-monster;
		rule succeeds;
	let D be a random eligible diaper;
	if D is actually summonable:
		summon D cursed;
		say "The [current-monster] produces a [D] and pulls it into place!  Uh-oh!  ";
		satisfy current-monster;
		rule succeeds;
	let M be a random off-stage babydoll microskirt;
	if M is actually summonable:
		summon M cursed;
		say "The [current-monster] produces a [M] and pulls it up your legs onto your waist!  Uh-oh!  ";
		satisfy current-monster;
		rule succeeds.
The mannequin donates clothing rule is listed last in the mannequin diaper quest rules.

This is the mannequin applies make-up in diaper quest rule:
	let M be current-monster;
	if the make-up of face < 3:
		say "The [M] pulls out a make up kit, and starts wildly applying it to your face!  You soon look like a dolled up [if the bimbo of the player < 10]whore[otherwise]clown[end if] who clearly doesn't know how to make herself look respectful!";
		now the make-up of face is 3;
		if image cutscenes is 1, display figure of mannequin cutscene 2;
		satisfy M;
		rule succeeds.
The mannequin applies make-up in diaper quest rule is listed last in the mannequin diaper quest rules.

The selection frustrated rule is listed last in the mannequin diaper quest rules.

Section 3 - Damage

To compute damage of (M - a mannequin):
	if the health of M > 0:
		if M is uninterested:
			say "The [noun] seems to notice you.  Uh-oh...";
			now M is interested;
			anger M;
		otherwise:
			say DamageReaction (the health of M) of M;
	otherwise:
		compute death of M.

To say DamageReaction (N - a number) of (M - a mannequin):
	if N > (the maxhealth of M / 4) * 3:
		say "The [noun] doesn't even flinch!";
	otherwise if N > (the maxhealth of M / 4) * 2:
		say "A rattle passes through the [noun]'s joints as it takes the hit!";
	otherwise if N > (the maxhealth of M / 4):
		say "The [noun] rattles ominously, taking the hit!";
	otherwise:
		say "The [noun] looks like its joints are starting to fail!".

To compute unique death of (M - a mannequin):
	say "The [noun] falls and breaks into pieces.";
	let J be a random off-stage make up kit;
	if a random number between 1 and 5 > 3 and J is make up kit:
		say "You notice that her [J] appears to have remained intact.";
		now J is in the location of the player;
		compute autotaking J.


Mannequin ends here.
