Mannequin by Monster begins here.

A mannequin is a kind of monster. The poison-status of a mannequin is -1.

Definition: a mannequin is woods dwelling: decide yes.

To say ShortDesc of (M - mannequin):
	say "mannequin".

Figure of topless mannequin is the file "NPCs/Forest/Mannequin/mannequin1.png".
Figure of stylish mannequin is the file "NPCs/Forest/Mannequin/mannequin2.png".
Figure of swimmer mannequin is the file "NPCs/Forest/Mannequin/mannequin3.png".
Figure of flirty mannequin is the file "NPCs/Forest/Mannequin/mannequin4.png".
Figure of lycra-clad mannequin is the file "NPCs/Forest/Mannequin/mannequin5.png".
Figure of latex-clad mannequin is the file "NPCs/Forest/Mannequin/mannequin6.png".
Figure of lingerie-clad mannequin is the file "NPCs/Forest/Mannequin/mannequin7.png".
Figure of skater mannequin is the file "NPCs/Forest/Mannequin/mannequin8.png".
Figure of goth mannequin is the file "NPCs/Forest/Mannequin/mannequin9.png".

Figure of Mannequin Cutscene 1 is the file "NPCs/Forest/Mannequin/cutscene-mannequin-event1.png".
Figure of Mannequin Cutscene 2 is the file "NPCs/Forest/Mannequin/cutscene-mannequin-event2.png".
Figure of Mannequin Cutscene 3 is the file "NPCs/Forest/Mannequin/cutscene-mannequin-force1.png".
Figure of Mannequin Cutscene 4 is the file "NPCs/Forest/Mannequin/cutscene-mannequin-force2.png".
Figure of Mannequin Cutscene 5 is the file "NPCs/Forest/Mannequin/cutscene-mannequin-event3.png".

The text-shortcut of mannequin is "man".

stylish mannequin is a mannequin. The text-shortcut of stylish mannequin is "stm".
To decide which figure-name is the monster-image of (M - stylish mannequin):
	decide on figure of stylish mannequin.
To say MediumDesc of (M - stylish mannequin):
	say "stylish mannequin".

topless mannequin is a mannequin. The text-shortcut of topless mannequin is "tom".
To decide which figure-name is the monster-image of (M - topless mannequin):
	decide on figure of topless mannequin.
To say MediumDesc of (M - topless mannequin):
	say "topless mannequin".

flirty mannequin is a mannequin. The text-shortcut of flirty mannequin is "fli".
To decide which figure-name is the monster-image of (M - flirty mannequin):
	decide on figure of flirty mannequin.
To say MediumDesc of (M - flirty mannequin):
	say "flirty mannequin".

lycra-clad mannequin is a mannequin. The text-shortcut of lycra-clad mannequin is "lyc". Understand "lycra", "clad" as lycra-clad mannequin.
To decide which figure-name is the monster-image of (M - lycra-clad mannequin):
	decide on figure of lycra-clad mannequin.
To say MediumDesc of (M - lycra-clad mannequin):
	say "lycra-clad mannequin".

swimmer mannequin is a mannequin. The text-shortcut of swimmer mannequin is "swi".
To decide which figure-name is the monster-image of (M - swimmer mannequin):
	decide on figure of swimmer mannequin.
To say MediumDesc of (M - swimmer mannequin):
	say "swimmer mannequin".

latex-clad mannequin is a mannequin. The text-shortcut of latex-clad mannequin is "lat". Understand "latex", "clad" as latex-clad mannequin.
To decide which figure-name is the monster-image of (M - latex-clad mannequin):
	decide on figure of latex-clad mannequin.
To say MediumDesc of (M - latex-clad mannequin):
	say "latex-clad mannequin".

lingerie-clad mannequin is a mannequin. The text-shortcut of lingerie-clad mannequin is "lin". Understand "lingerie", "clad" as lingerie-clad mannequin.
To decide which figure-name is the monster-image of (M - lingerie-clad mannequin):
	decide on figure of lingerie-clad mannequin.
To say MediumDesc of (M - lingerie-clad mannequin):
	say "lingerie-clad mannequin".

skater mannequin is a mannequin. The text-shortcut of skater mannequin is "ska".
To decide which figure-name is the monster-image of (M - skater mannequin):
	decide on figure of skater mannequin.
To say MediumDesc of (M - skater mannequin):
	say "skater mannequin".

goth mannequin is a mannequin. The text-shortcut of goth mannequin is "got".
To decide which figure-name is the monster-image of (M - goth mannequin):
	decide on figure of goth mannequin.
To say MediumDesc of (M - goth mannequin):
	say "goth mannequin".
Definition: goth mannequin is woods dwelling:
	if doomed > 2, decide yes;
	decide no.
Definition: goth mannequin is mansion dwelling:
	if doomed > 0, decide yes;
	decide no.
Definition: goth mannequin is woods prioritised: [it'll have a decent chance of spawning even if there is another mannequin]
	if it is woods dwelling, decide yes;
	decide no.

To say mannequin-model of (M - mannequin):
	if lady fetish is 2, say "male";
	otherwise say "female".

To say MonsterDesc of (M - a mannequin):[topless mannequin]
	say "A topless [mannequin-model of M] mannequin with a tanned skin tone, wearing tight denim jeans. [big he of M] has blonde hair drawn back into a silky ponytail and a pink scrunchie around each wrist. [if lady fetish is 2]A[otherwise][big his of M] breasts are perfectly spherical and unmoving, and a[end if] small handbag hangs delicately over one shoulder.".

To decide if (M - a mannequin) is theme-matched:[topless mannequin]
	if breasts is exposed, decide yes;
	decide no.

To say MonsterDesc of (M - goth mannequin):
	say "An unsettling [mannequin-model of M] mannequin complete with garish makeup and a blank stare. [big his of M] solid black hair frames a stark pale face which emphasises [his of M] scarlet red lips. Everything [he of M] is wearing is black: [his of M] criminally short punk dress, [his of M] knee high boots and [his of M] gloves, and even the handbag hanging over [his of M] shoulder.".

To decide if (M - goth mannequin) is theme-matched:
	if there is a worn black themed clothing, decide yes;
	if there is a worn cursed clothing, decide yes;
	decide no.

To say MonsterDesc of (M - stylish mannequin):
	say "An unsettling [mannequin-model of M] mannequin complete with striking makeup and a blank stare. '[big he of M]' has armpit-length black ponytailed hair, and a short pink dress underneath a trendy belt. It is low-cut and very thin, so [his of M] featureless [ChestDesc of M] and smooth crotch are on full display for your prying eyes. [big he of M] has long white fingernails, which match the latex handbag hanging delicately from one shoulder.".

To decide if (M - stylish mannequin) is theme-matched:
	if there is a worn pink themed clothing, decide yes;
	decide no.

To say MonsterDesc of (M - flirty mannequin):
	say "An unsettling [mannequin-model of M] mannequin, with a warm tanned skin tone and an almost smirking facial expression complemented by excessive amounts of eye shadow and eyeliner. Wavy black hair flows down [his of M] front and back to [his of M] waist, giving [him of M] a very provocative look. This is emphasised by the sheer red negligee [he of M] is wearing. It is practically see-through when the light touches it in the right way.".

To decide if (M - flirty mannequin) is theme-matched:
	if there is a worn red themed clothing, decide yes;
	decide no.

To say MonsterDesc of (M - lycra-clad mannequin):
	say "An unsettling [mannequin-model of M] mannequin, with a warm tanned skin tone and a politely smiling facial expression, painted in 'night on the town' make up. Reddish-brown hair rests on [his of M] shoulders, kept mostly tidy thanks to a ponytail. [big his of M] grey playsuit boasts a very low V neck that almost exposes [his of M] belly button, and is ever so slightly sheer, giving you glimpses of what is underneath. Or rather, what would be underneath if [he of M] wasn't a doll".

To decide if (M - lycra-clad mannequin) is theme-matched:
	if there is a worn grey themed clothing, decide yes;
	if there is a worn lycra clothing, decide yes;
	decide no.

To say MonsterDesc of (M - latex-clad mannequin):
	say "An unsettling [mannequin-model of M] mannequin complete with striking makeup and a blank stare. '[big he of M]' has armpit-length black ponytailed hair, and is modelling a provocative pink latex dress. The V neck is so low that [his of M] belly button is left bare. A pair of grey latex knee-length boots adorn [his of M] feet, and a small hangbag hangs delicately from one shoulder.".

To decide if (M - latex-clad mannequin) is theme-matched:
	if there is a worn pink themed clothing, decide yes;
	if there is a worn latex clothing, decide yes;
	decide no.

To say MonsterDesc of (M - swimmer mannequin):
	say "An unsettling [mannequin-model of M] mannequin complete with striking makeup and a blank stare. '[big he of M]' has armpit-length black ponytailed hair and is modelling a tight blue one-piece swimsuit. A small handbag hangs from one of [his of M] shoulders.".

To decide if (M - swimmer mannequin) is theme-matched:
	if there is a worn swimming themed clothing, decide yes;
	decide no.

To say MonsterDesc of (M - lingerie-clad mannequin):
	say "An unsettling [mannequin-model of M] mannequin, with a warm tanned skin tone and an almost smirking facial expression complemented by make up that really make [his of M] eyes pop. [big his of M] 'outfit' consists of only the most skimpy, lacy black and burgundy lingerie: a full set of strapless bra, thong, stockings and suspenders. A small handbag is draped over one of [his of M] shoulders.".

To decide if (M - lingerie-clad mannequin) is theme-matched:
	repeat with C running through currently uncovered clothing:
		if C is bra or C is knickers or C is black themed stockings or C is black themed suspenders or C is black themed dress, decide yes;
	decide no.

To say MonsterDesc of (M - skater mannequin):
	say "An unsettling [mannequin-model of M] mannequin complete with striking makeup and blankly smiling stare. A pink punk-styled top sits above a tiny white skater mini-skirt, which barely hides whatever underwear [he of M] might be wearing. Fishnet tights sit under multicoloured striped knee-length socks and knee-pads. Fingerless gloves, elbow-pads and black & white trainers finish off the skater look.".

To decide if (M - skater mannequin) is theme-matched:
	if there is a worn red themed clothing, decide yes;
	if there is a worn rollerskates, decide yes;
	decide no.

To say MonsterComment of (M - a mannequin):[TODO: fix this]
	if M is theme-matched:
		if the bimbo of the player <= 6:
			if the player is gendered male:
				say "[line break][first custom style]Isn't she wearing the same thing as me? This is so embarrassing, dude...[roman type][line break]";
			otherwise:
				say "[line break][first custom style]Ugh. I can't believe that freaky thing is actually dressed like I am.'[roman type][line break]";
		otherwise if the bimbo of the player <= 12:
			say "[line break][variable custom style]Our outfits are kind of similar, aren't they...[roman type][line break]";
		otherwise:
			say "[line break][second custom style]We totally match![roman type][line break]";
	otherwise if diaper quest is 0:
		say "[if the bimbo of the player <= 4][line break][first custom style][one of]Wow, [he of M]'s really creepy.[or][big he of M] doesn't look tough, but [he of M]'s not human so I should keep an eye out.[or]I wonder if [he of M]'s got a pussy.[or]This reminds me of this one time me and my friend took a department store mannequin home.[or]Even the clothing models look like total harlots.[or]Hmm, I doubt [he of M] can feel pain, but [he of M] probably has some exploitable weak points.[or]So that's how I'm expected to dress. Disgusting.[in random order][otherwise if the bimbo of the player <= 8][line break][variable custom style][one of][big he of M]'s actually kind of attractive for a walking piece of plastic.[or]What kind pervert dressed [him of M] up so sexy?[or]Is that how I'll look by the end of this? No... I've got to resist![in random order][otherwise if the bimbo of the player <= 12][line break][second custom style][one of][big his of M] make up looks really good.[or]Is that a wig or is that real hair? [big he of M] has good fashion sense.[or]Those clothes don't look too bad...[in random order][otherwise if bimbo of the player <= 16][second custom style][one of]I should have [him of M] do my make up![or]Maybe [he of M] can give me some fashion pointers, tee-hee![or]People stare at [him of M] all day long, just like they will with me!*giggle*[or]I want, no, I *NEED* to look like [him of M]![in random order][otherwise if bimbo of the player <= 20][second custom style][one of]I wish *MY* head could just be empty plastic![or]Maybe I should be a mannequin when I get home![or][big he of M][']s so lucky, people make all [his of M] choices for [him of M] every day![in random order][end if][roman type][line break]".

To decide which object is the banish-item of (M - a mannequin):
	decide on a random off-stage make up kit.
To decide which object is the banish-item of (M - lingerie-clad mannequin):
	if diaper quest is 0:
		decide on anal-beads-plug-panties;
	otherwise if black briefs is off-stage:
		decide on black briefs;
	otherwise:
		decide on nothing.
To decide which object is the banish-item of (M - skater mannequin):
	decide on white-rollerskates.

To set up (M - a mannequin):
	reset M;
	now the monstersetup of M is 1;
	let T be the banish-item of M;
	if T is a thing:
		let MKexists be false; [check if there are any make up kits already in its banishItems]
		repeat with X running through the banishItems of M:
			if X is make up kit, now MKexists is true;
		if MKexists is false, add T to the banishItems of M;
	now the raw difficulty of M is the starting difficulty of M;
	now the health of M is the maxhealth of M.

To decide which number is the starting difficulty of (M - mannequin):
	decide on 6.

To decide which number is the starting difficulty of (M - goth mannequin):
	decide on 9.

[It would be annoying to manage which dildo she's using, so we have her discard any extra dildos when we make this check]
To decide which number is the girth of (M - a mannequin):
	let C be the number of sex toys carried by M;
	if C > 0:
		let P be a random sex toy carried by M;
		repeat with T running through sex toys carried by M:
			unless T is P, only destroy T;
		decide on the size of P;[If the mannequin has a plug or a dildo, she can use it on you]
	decide on 2.

Definition: a mannequin is willing to do anal: decide yes.

Definition: a mannequin is willing to do oral: decide yes.

Part 1 - Misc Flavour

To say SummoningFlav of (M - a mannequin):
	if M is in the location of the player, say "A quiet moan can be heard as several gears, pieces of clothing, and what looks like ceramic fall through the stone opening between the statue's legs. The gears whirr rapidly as the pieces begin to put themselves together, forming a [printed name of M].";
	otherwise say "You hear the sound of mechanical joints whirring.".

To say SummoningFlav of (M - a goth mannequin):
	if M is in the location of the player, say "A deafening whirr passes through the room as every mannequin turns toward the mummy at once, and the fabric slowly begins to unravel. A porcelain face comes into view as the linens fall away, followed by jointed arms, legs and stylish black clothing. The whirr peters out as the [printed name of M] takes [his of M] first few steps into the room, and the mummy's wrappings slowly reform behind it.";
	otherwise say "You hear the sound of mechanical joints whirring.".

To say MuteGreetResponse of (M - a mannequin):
	say "The mannequin doesn't reply. It would appear to be magically animated rather than sentient.".

To say MuteQuestion of (M - a mannequin):
	say SexDollQuestion of M.

Part 2 - Perception

To compute perception of (M - a mannequin):
	if permanent makeup is 0 or the anal sex addiction of the player < 9:
		if the latex-transformation of the player < 3:
			anger M;
			say "[BigNameDesc of M] seems to notice you! Uh-oh...";
		otherwise:
			bore M;
	otherwise if the player is a bit horny:
		if a random number between 5 and 15 < the anal sex addiction of the player and the player is not immobile and the player is upright and the player is not unable to orgasm so soon and the player is not flying:
			say "You look at [NameDesc of M] and feel a burning desire to have an orgasm milked out of you[if the latex-transformation of the player > 3], even though you know it won't work[end if]. In this moment of weakness, you drop to your knees and look at [NameDesc of M] expectantly.";
			try kneeling;
			if the player is not ass protected, now presented-orifice is asshole;
			if the player is prone or the bimbo of the player > 14:
				say "[BigNameDesc of M] seems to notice you! You giggle in nervous delight.";
				anger M;
		otherwise if the bimbo of the player > 14:
			say "[BigNameDesc of M] seems to notice you! You giggle in nervous delight.";
			anger M;
	otherwise if M is objectifying the player:
		say "[BigNameDesc of M] seems to notice you! You giggle in nervous delight.";
		anger M.

To compute DQ perception of (M - a mannequin):
	say "[BigNameDesc of M] seems to notice you! Uh-oh...";
	anger M.

Part 3 - Combat

Section 1 - Attack

Definition: a mannequin (called M) is eager to slap:
	if a random number between 1 and 3 is 1, decide yes;
	decide no.

To compute flying player taunting of (M - a mannequin):
	if a random number from 1 to 6 is 1, say "[BigNameDesc of M] is [one of][or]still [stopping]watching you, waiting.".

This is the mannequin unique punishment rule:
	if (short skirts permanent fetish is 0 or short skirts permanent fetish > 3) and current-monster is not topless mannequin and current-monster is not lycra-clad mannequin and current-monster is not swimmer mannequin and current-monster is not lingerie-clad mannequin and the player is getting unlucky:
		say "[BigNameDesc of current-monster] strikes a confident pose, and [his of current-monster] eyes stare deep down into your soul. Instantly, you realise that [second custom style]short skirts like the one [he of current-monster] is wearing are the best![roman type][line break]You'll feel awful if you ever wear a skirt longer than that sort of nice and short upper-thigh-length style. [GotUnluckyFlav]";
		now short skirts permanent fetish is 3;
		rule succeeds;
	otherwise if the player is allowed-panties and current-monster is lingerie-clad mannequin and (skimpy underwear fetish is -1 or skimpy underwear fetish > 3):
		say "[BigNameDesc of current-monster] strikes a confident pose, and [his of current-monster] eyes stare deep down into your soul. Instantly, you realise that [second custom style]skimpy panties like the one [he of current-monster] is wearing are the best![roman type][line break]You'll feel awful if you ever wear panties or briefs. It's hotpants, thongs and g-strings for you from now on!";
		now skimpy underwear fetish is 3;
		rule succeeds.
The unique punishment rule of a mannequin is usually the mannequin unique punishment rule.

To decide which number is the rounds of sex left of (M - a mannequin):
	if the latex-transformation of the player > 2, decide on 0;
	decide on 10 - ((a random number between 1 and 3) + arousal of the player / 1000 + the sex-length of M).

To compute unique climax of (M - a mannequin) in (F - asshole):
	FavourUp M;
	if the latex-transformation of the player > 2:
		say "[BigNameDesc of M] looks confused, removes [his of M] hand from your [asshole] and leaves you alone.";
		bore M for 600 seconds;
	otherwise:
		if the player is unable to orgasm so soon and the anal sex addiction of the player > 2, say "Even though you came recently, you feel that familiar pressure building up inside you.";
		anally orgasm shamefully;
		cutshow figure of mannequin cutscene 4 for M;
		bore M;
		let P be a random sex toy carried by M;
		if P is sex toy:
			if a random number between the difficulty of M and 15 > the size of P * 2:
				say "[BigNameDesc of M] [if P is anal beads]pushes all [the notches of P] beads into your asshole before removing [his of M] hands and apparently losing interest[otherwise]removes [his of M] hands from the [printed name of P], and seems to lose interest[end if].";
				only summon P;
				if P is anal beads, now the notch-taken of P is the notches of P;
			otherwise:[if she doesn't leave it inside you, she saves it for next time.]
				say "[BigNameDesc of M] pulls the [printed name of P] out of your asshole and seems to lose interest.";
		otherwise:
			say "[BigNameDesc of M] removes [his of M] fingers from your [asshole], and seems to lose interest.";

To compute anal sex of (M - a mannequin):
	[What she's doing to your ass]
	if there is a sex toy carried by M:
		let P be a random sex toy carried by M;
		if P is anal beads:
			say "[BigNameDesc of M] [one of]pushes the beads in and out of your [asshole] one at a time, stretching you over and over[or]pushes each bead into your [asshole] and slowly pulls them out one by one, forcing you to experience the stretching of your sphincter over and over[or]holds one bead just inside your [asshole], keeping you as stretched and full as possible[at random][run paragraph on]";
		otherwise:
			say "[one of][BigNameDesc of M] keeps fucking you with the [printed name of P][or][BigNameDesc of M] continues pumping the [printed name of P] in and out of your [asshole][or][BigNameDesc of M] fucks you with the [printed name of P][or][BigNameDesc of M] keeps pushing the [printed name of P] in and out of your hole[at random][if the player is sexed male], [one of]angling it against your prostate[or]keeping the tip firmly in contact with your prostate[or]repeatedly prodding your 'G-spot'[or]relentlessly pounding your prostate[at random][end if][run paragraph on]";
	otherwise:
		say "[if the player is sexed male][BigNameDesc of M] [one of]keeps expertly stroking your prostate[or]keeps skilfully prodding your prostate[or]firmly rubs your prostate[or]relentlessly prodding your prostate with expert movements of [his of M] fingers[at random][otherwise][BigNameDesc of M] keeps [one of]expertly pressing on your G-spot through your [asshole][or]pressing that same spot inside your hole, always with that perfectly maddening amount of force[or]rubbing your G-spot through your [asshole], endlessly repeating [his of M] perfect movements[at random][end if][run paragraph on]";
	[How she's interacting with your crotch.]
	if chastity-belt is worn or (there is a worn chastity cage and the player is not possessing a vagina):
		say ", as if milking an orgasm out of you!";
	otherwise if the player is possessing a penis:
		say " as [he of M] [unless there is pussy covering clothing and the size of penis > 2][one of]pumps[or]strokes[or]jerks off[at random] your [ShortDesc of penis][otherwise]massages your [ShortDesc of penis][end if], [if the size of penis < 3]as if it was a clitoris![otherwise]as if milking you![end if]";
	otherwise if the player is possessing a vagina:
		say " as [he of M] [if there is pussy covering clothing]rubs your clit[otherwise][one of]strokes[or]teases[at random] your clit[end if], as if milking an orgasm out of you!";
	otherwise: [failsafe]
		say ", as if milking an orgasm out of you!";
	increase the sex-length of M by 1.

To compute the default taunting of (M - a mannequin):
	say "[one of][BigNameDesc of M] stands in front of you, motionless.[or][or][cycling]".

Definition: a mannequin is requiring facial penetration: decide no.

To compute (M - a goth mannequin) entering mouth:
	let S be a random off-stage pink scrunchie;
	if the make-up of face is 3 and permanent makeup is 0:
		say "[BigNameDesc of M] [if presented-orifice is face]seems to appreciate you holding your mouth open and your head still and [end if]pulls out a brush you've never seen before. As [he of M] brushes it across your face, nothing observable seems to change but your current makeup feels more... durable.";
		cutshow figure of mannequin cutscene 1 for M;
		now permanent makeup is 1;
		satisfy M;
	otherwise if the lips of face < 2 + artificial enhancements fetish:
		say "[BigNameDesc of M] puts both hands on your cheeks, pushing until your lips pucker out. [big he of M] pulls out a flesh-coloured tube of lipstick and applies several coats to your puckered lips. The feeling of having your lips pushed out lingers after [he of M] releases you...";
		cutshow figure of mannequin cutscene 1 for M;
		LipsUp 1;
		satisfy M;
	otherwise if the make-up of face > a random number between 0 and 2 and S is actually summonable and the largeness of hair > 4:
		if the player is ponytailed, say "[BigNameDesc of M] pulls out an identical pink scrunchie and fashions your hair into a [if the bimbo of the player > 8][line break][second custom style]sexy [roman type][line break][end if]pair of pigtails.[if the player is gendered male][one of][line break][variable custom style]There's no mistaking it now, no men wear pigtails.[roman type][line break][or][stopping][end if]";
		otherwise say "[BigNameDesc of M] pulls out a pink scrunchie and fashions your hair into a [if the bimbo of the player < 7]tidy[otherwise][line break][second custom style]cute[roman type][line break][end if] ponytail.[if the player is gendered male][one of][line break][variable custom style]This is definitely girly hair now.[roman type][line break][or][stopping][end if]";
		summon S;
		satisfy M;
	otherwise if the make-up of face > 1 and the largeness of hair > 4 and a random number between 1 and 5 > 2 and artificial enhancements fetish is 1 and the fake largeness of hair < 10:
		FakeHairUp 4;
		say "[BigNameDesc of M] pulls out some fake hair extensions in just your colour, and at rapid speed skilfully attaches them to your existing hair. You now have [ShortDesc of hair]!";
		satisfy M;
	otherwise if asshole is an actual target:
		follow the monster asshole insertion rules; [That's right, we decide that the mannequin goes after the asshole instead.]
	otherwise:
		compute SelectionFailure of M.

To compute (M - a mannequin) entering mouth:
	let S be a random off-stage pink scrunchie;
	let CC be a random off-stage fetish appropriate plastic-cage;
	if (the class of the player is silicone queen or the number of worn headgear is 0) and condom pigtails is off-stage and total pinned condoms > 0:
		let H be a random worn headgear;
		repeat with C running through worn headgear:
			only destroy C;
		say "[BigNameDesc of M] [if H is headgear]removes your [ShortDesc of H] and [end if]fixes your hair into pigtails! It's only after [he of M][']s finished that you realise your new pigtail ties are actually used condoms...";
		summon condom pigtails cursed;
		satisfy M;
	otherwise if rubber top hat is off-stage and rubber top hat is actually summonable and artificial enhancements fetish is 1:
		say "[BigNameDesc of M] pulls out a tiny pink top hat and fixes it onto your head! You immediately feel a bit more bimbo-like...";
		summon rubber top hat cursed;
		satisfy M;
	otherwise if the make-up of face is 3 and permanent makeup is 0:
		say "[BigNameDesc of M] [if presented-orifice is face]seems to appreciate you holding your mouth open and your head still and [end if]pulls out a brush you've never seen before. As [he of M] brushes it across your face, nothing observable seems to change but your current makeup feels more... durable.";
		cutshow figure of mannequin cutscene 1 for M;
		now permanent makeup is 1;
		satisfy M;
	otherwise if the make-up of face > a random number between 0 and 2 and S is actually summonable and the largeness of hair > 4:
		if the player is ponytailed, say "[BigNameDesc of M] pulls out an identical pink scrunchie and fashions your hair into a [if the bimbo of the player > 8][line break][second custom style]sexy [roman type][line break][end if]pair of pigtails.[if the player is gendered male][one of][line break][variable custom style]There's no mistaking it now, no men wear pigtails.[roman type][line break][or][stopping][end if]";
		otherwise say "[BigNameDesc of M] pulls out a pink scrunchie and fashions your hair into a [if the bimbo of the player < 7]tidy[otherwise][line break][second custom style]cute[roman type][line break][end if] ponytail.[if the player is gendered male][one of][line break][variable custom style]This is definitely girly hair now.[roman type][line break][or][stopping][end if]";
		summon S;
		satisfy M;
	otherwise if the make-up of face > 1 and the largeness of hair > 4 and a random number between 1 and 5 > 2 and artificial enhancements fetish is 1 and the fake largeness of hair < 10:
		FakeHairUp 2;
		say "[BigNameDesc of M] pulls out some fake hair extensions in just your colour, and at rapid speed skilfully attaches them to your existing hair. You now have [ShortDesc of hair]!";
		satisfy M;
	otherwise if the make-up of face < 3:
		say "[BigNameDesc of M] [if presented-orifice is face]seems to appreciate you holding your mouth open and your head still and [end if]pulls out a makeup kit, and starts wildly applying it to your face!";
		FaceUp 1;
		if the make-up of face > 0, cutshow figure of mannequin cutscene 2 for M;
		otherwise cutshow figure of mannequin cutscene 5 for M;
		satisfy M;
	otherwise if artificial enhancements fetish is 1 and fake-nails is actually summonable:
		say "[BigNameDesc of M] pulls out a set of long and slutty pink fake nails. Before you can react to what is happening, all ten have been glued on top of your real nails. Wow that was fast! You are immediately filled with a horrible sense of dread; almost all you can think about is [if the player is horny]how horny you are and [end if]how awful, and painful, it would be if any of your nails were to break. The invasive thought is so powerful that you also feel significantly weaker and slower as your mind makes you think twice about every action you take.";
		summon fake-nails cursed;
		satisfy M;
	otherwise if the player is possessing a penis and CC is an actually summonable clothing and the number of pussy covering actually unavoidable clothing is 0:
		say "[BigNameDesc of M] parts your quivering thighs and squishes your [sissy-penis] into a bright pink chastity cage. [if the player is horny]You feel the cage keeping your [sissy-penis] soft, no matter how horny you are![end if][roman type][line break]";
		summon CC uncursed;
		compute M keylocking CC;
		say FullExamineDesc of CC;
		satisfy M;
	otherwise if asshole is an actual target:
		follow the monster asshole insertion rules; [That's right, we decide that the mannequin goes after the asshole instead.]
	otherwise:
		compute SelectionFailure of M.

To set up sex length of (M - a mannequin) in (F - asshole):
	set up sex length 0 of M in F.

To compute (M - a mannequin) entering anally:
	let P be a random sex toy carried by M;
	if P is nothing and the difficulty of M - the starting difficulty of M >= 2:[After a certain difficulty level, she starts using dildos.]
		now P is a random off-stage plentiful dong;
		if a random number between 5 and the difficulty of M > the size of P, now M is carrying P;
	if M is lingerie-clad mannequin and anal-beads-plug-panties is off-stage and anal-beads-plug-panties is actually summonable:
		summon anal-beads-plug-panties cursed with quest;
		displace anal-beads-plug-panties;
		say "[BigNameDesc of M] forces you to wear a [MediumDesc of anal-beads-plug-panties]! And it looks like it's cursed! It looks like it's up to you - do you [bold type]replace[roman type] it and put the plug section inside to partially preserve your modesty?";
		satisfy M;
	otherwise:
		if P is sex toy:
			say "[BigNameDesc of M] pushes a [printed name of P] into your [asshole], [if the player is not possessing a penis]angling it toward [himself of M] as [he of M] begins pumping it in and out of your hole[otherwise]angling it against your prostate with pinpoint accuracy as [he of M] begins pumping it in and out of your hole[end if]. [run paragraph on]";
		otherwise:
			say "[BigNameDesc of M] inserts two fingers into your [asshole], and starts stroking in a come hither motion. [run paragraph on]";
		if there is a worn chastity-belt:
			say "[big he of M] holds you still with [his of M] other hand. It's like [he of M]'s trying to force an anal orgasm out of you!";
		otherwise if the player is possessing a penis:
			say "[big he of M] [if there is a worn chastity cage and the player is possessing a scrotum]grasps your testicles with [his of M] other hand, delicately massaging them as [his of M] faux-knuckles bump against your cage. [otherwise if there is a worn chastity cage]rubs your cage with [his of M] other hand. [otherwise if the size of penis > 3]grabs your [ShortDesc of penis] with [his of M] other hand and skilfully begins stroking away. [otherwise]delicately teasing your [ShortDesc of penis] with [his of M] fingertips. [end if][big he of M]'s trying to force an orgasm out of you!";
		otherwise if the player is possessing a vagina:
			say "[unless the player is pussy protected]With [his of M] other hand [he of M] starts expertly pinching and rubbing your clitoris. [end if][big he of M]'s trying to force an orgasm out of you!";
		otherwise:
			say "[big he of M] holds you still with [his of M] other hand. It's like [he of M]'s trying to force an anal orgasm out of you!";
		cutshow figure of mannequin cutscene 3 for M;
		now M is penetrating asshole;
		now the sex-length of M is 0.

This is the mannequin forces orgasm through diaper rule:
	if there is a worn total protection diaper and the player is diaper focused and the player is able to orgasm:
		let M be current-monster;
		let D be a random worn diaper;
		while the coverer of D is clothing:
			now D is the coverer of D;
		say "[BigNameDesc of M] seems to detect that you are very aroused. Staring directly into your eyes with [his of M] blank expression, [he of M] presses a hand to the front of your [D] and pushes firmly. [big his of M] hand begins to vibrate, and within seconds has reached an intensely strong force[if there is a worn chastity bond]! The pulses flow straight through your [random worn chastity bond] and into your [genitals][end if]![line break][variable custom style]Unfff... it's too strong! I'm gonna...[roman type][line break]Your eyes roll into the back of your head and you start shaking.";
		orgasm;
		say "[BigNameDesc of M] cocks [his of M] creepy head to one side, silently pulls [his of M] hand away and then leaves you alone to recover[if the player is male and the bimbo of the player < 6]I can't believe I just came into my diaper...[end if].[roman type][line break]";
		bore M;
		FavourUp M;
		rule succeeds;
	otherwise if (there is a worn diaper or diaper quest is 1) and diaper messing >= 3 and the stomach-food of the player > 3 + the rectum-incontinence of the player and the player is not feeling full and face is not actually occupied and the player is not incontinent and the make-up of face > 0: [Will only occur if there is enough food to ensure that the player will go from NOT feeling full to feeling full.]
		let M be current-monster;
		say "You see [NameDesc of M] moving something towards your face and expect [his of M] to apply a new layer of make up. But instead, [he of M] pushes a small white pill into your mouth and down your throat! You are forced to swallow it before you can react! You suddenly hear your stomach churning loudly, as it speeds up your respiratory processes dramatically, quickly digesting all the food in your stomach!";
		increase rectum by the stomach-food of the player;
		now the stomach-food of the player is 1;
		say "[BigNameDesc of M] cocks [his of M] creepy head to one side, silently turns away and then leaves you alone to contemplate what might be coming very soon...";
		bore M;
		FavourUp M;
		rule succeeds.
The diaper punishment rule of a mannequin is usually the mannequin forces orgasm through diaper rule.

Section 2 - DQ

The mannequin diaper quest rules is a rulebook. The diaper quest rules of a mannequin is usually the mannequin diaper quest rules.

The mannequin forces orgasm through diaper rule is listed first in the mannequin diaper quest rules.

This is the mannequin donates clothing rule:
	if current-monster is able to donate babywear:
		compute babywear donation of current-monster;
		rule succeeds.
The mannequin donates clothing rule is listed last in the mannequin diaper quest rules.

This is the mannequin applies make-up in diaper quest rule:
	let M be current-monster;
	if the make-up of face < 3:
		say "[BigNameDesc of M] pulls out a make up kit, and starts wildly applying it to your face! You soon look like a dolled up [if the bimbo of the player < 10]whore[otherwise]clown[end if] who clearly doesn't know how to make [himself of the player] look respectful!";
		FaceUp 3;
		cutshow figure of mannequin cutscene 2 for M;
		satisfy M;
		rule succeeds.
The mannequin applies make-up in diaper quest rule is listed last in the mannequin diaper quest rules.

The selection frustrated rule is listed last in the mannequin diaper quest rules.

Section 3 - Damage

To say CombatProvokedReaction of (M - a mannequin):
	say "[BigNameDesc of M] seems to notice you. Uh-oh...".

To say DamageReactHealthy of (M - a mannequin):
	say "[BigNameDesc of M] doesn't even flinch!".

To say DamageReactDamaged of (M - a mannequin):
	say "A rattle passes through [NameDesc of M][']s joints as [he of M] takes the hit!".

To say DamageReactTired of (M - a mannequin):
	say "[BigNameDesc of M] rattles ominously, taking the hit!".

To say DamageReactWeak of (M - a mannequin):
	say "[BigNameDesc of M] looks like [his of M] joints are starting to fail!".

To say BanishFleeFlav of (M - a mannequin):
	say "[BigNameDesc of M] falls and breaks into pieces.".

To compute damaging attack of (M - a mannequin):
	let C be nothing;
	if (M is goth mannequin or difficulty of M > the starting difficulty of M) and a random number between 1 and 6 is 1:
		compute stunning attack of M;
	otherwise:
		compute striking attack of M.

To compute stunning attack of (M - a monster):
	let T be 0;
	if M is theme-matched, now T is 1;
	say "[BigNameDesc of M] emits a clicking noise as [he of M] strikes a pose with one hand on [his of M] hip. [if T is 1 and M is topless mannequin][big he of M] isn't alive, but you wouldn't know that from the casual elegance of [his of M] pose. You begin to feel intimidated[otherwise if T is 1]The casual elegance of [his of M] pose emphasises how much better [his of M] outfit looks compared to yours, and you feel a wave of embarrassment[otherwise]You find yourself feeling intimidated by the casual elegance of [his of M] movements[end if] as [he of M] struts straight for you.";
	if M is goth mannequin:[goth forces you to kneel. Can only be resisted with int]
		say "The closer [he of M] gets, the more inadequate you feel, and when [he of M] stops in front of you and makes a simple downward gesture of [his of M] porcelain fingers, your will is pushed to its breaking point.";
		if T is 1 or the intelligence roll of M >= the intelligence of the player:
			say "You aren't able to hold it together, and you drop to your knees as [he of M] returns to [his of M] normal posture.";
			now the stance of the player is 1;
			moderateHumiliate;
		otherwise:
			say "Somehow, you manage to hold it together, stumbling away from [him of M] as [he of M] returns to [his of M] normal posture.";
	otherwise:[mannequins try to stun you. Can be dodged or resisted with int]
		if the accuracy roll of M >= the dexterity of the player:
			say "The closer [he of M] gets, the more inadequate you feel, so you aren't prepared to resist when [he of M] suddenly reaches out and taps you on the forehead.";
			if T is 1 or the intelligence roll of M >= the intelligence of the player:
				say "You feel a strange tingling feeling in your joints as you suddenly find yourself imitating [his of M] perfect pose. It's exhausting, and worse, your body seems to want to hold this posture for a while!";
				moderateHumiliate;
				FatigueUp 20;
				now another-turn-flavour is the substituted form of "You're still stuck imitating [FuckerDesc of M]'s pose!";
				now another-turn is 1; [Selkie: Any chance we could have this effect worsen so in the end the player has a good chance of automatically adopting the pose again when feeling under threat?
				Aika: Perhaps under threat from the same mannequin, sure]
			otherwise:
				say "You feel a strange tingling feeling in your joints as you are suddenly driven to imitate [his of M] pose, and you exhaust yourself a little trying to shake it off.";
				FatigueUp 10;
		otherwise:
			say "You back away before [he of M] can get any closer.";
			slightHumiliate.

Mannequin ends here.
