Tentacle Monster by Monster begins here.

Tentacle monster is a kind of monster. A tentacle monster is neuter. A tentacle monster can be player-brood. A tentacle monster is usually not player-brood.[This will be used to flag the TM as not one of your children]

A tentacle monster can be newborn, large, massive (this is the tentacle-size property). Understand the tentacle-size property as describing a tentacle monster. A tentacle monster has a number called evolved.

Definition: a tentacle monster is willing to do vaginal: decide yes.

Definition: a tentacle monster is willing to do anal: decide yes.

Definition: a tentacle monster is willing to do oral: decide yes.

Definition: a tentacle monster is willing to do titfucks: decide yes.

Definition: a tentacle monster is virginity taking: decide yes.

Definition: a tentacle monster is father material: decide yes.

Definition: a tentacle monster is summoningRelevant if tentacle fetish is 0 and it is undefeated and it is regional. [Doesn't count towards the number of monsters in the region for the purposes of summoning portals.]

Report examining tentacle monster:
	progress quest of tentacle-research-quest.

Figure of newborn tentacle monster is the file "NPCs/Bosses/TentacleMonster2.png".
Figure of large tentacle monster is the file "NPCs/Bosses/boogeymonster1.png".
Figure of massive tentacle monster is the file "NPCs/Bosses/TentacleMonster3.png".
Figure of friendly newborn tentacle monster is the file "NPCs/Bosses/TentacleMonster4.png".
Figure of friendly large tentacle monster is the file "NPCs/Bosses/TentacleMonster5.png".
Figure of friendly massive tentacle monster is the file "NPCs/Bosses/TentacleMonster6.png".

Figure of Tentacle Cutscene 1 is the file "Special/Cutscene/cutscene-tentacle-birth1.png".
Figure of Tentacle Cutscene 2 is the file "Special/Cutscene/cutscene-tentacle-belly1.jpg".
Figure of Tentacle Cutscene 3 is the file "Special/Cutscene/cutscene-tentacle-grab1.png".
Figure of Tentacle Cutscene 4 is the file "Special/Cutscene/cutscene-tentacle-grab2.png".
Figure of Tentacle Cutscene 5 is the file "Special/Cutscene/cutscene-tentacle-climax1.jpg".
Figure of Tentacle Cutscene 6 is the file "Special/Cutscene/cutscene-tentacle-climax2.png".
Figure of Tentacle Cutscene 7 is the file "Special/Cutscene/cutscene-tentacle-grab3.png".

To decide which figure-name is the monster-image of (M - a tentacle monster):
	if M is newborn, decide on figure of newborn tentacle monster;
	if M is large, decide on figure of large tentacle monster;
	decide on figure of massive tentacle monster.

To decide which figure-name is the friendly-monster-image of (M - a tentacle monster):
	if M is newborn, decide on figure of friendly newborn tentacle monster;
	if M is large, decide on figure of friendly large tentacle monster;
	decide on figure of friendly massive tentacle monster.

To decide which figure-name is the sex-profile of (M - a tentacle monster):
	if diaper quest is 0 and M is penetrating a body part:
		if M is massive, decide on figure of tentacle cutscene 4;
		if the reaction of the player is 0, decide on figure of tentacle cutscene 7;
		decide on figure of tentacle cutscene 3;
	decide on figure of Missing NPC.

The text-shortcut of tentacle monster is "tnm". Understand "tentacle", "monster", "tentacle monster" as tentacle monster.

A smart-tentacle-monster is a kind of tentacle monster.

cunning-tentacle-monster is a smart-tentacle-monster.
To say ShortDesc of (M - cunning-tentacle-monster):
	say "cunning tentacle monster".
Understand "cunning" as cunning-tentacle-monster.

intelligent-tentacle-monster is a smart-tentacle-monster.
To say ShortDesc of (M - intelligent-tentacle-monster):
	say "intelligent tentacle monster".
Understand "intelligent" as intelligent-tentacle-monster.

clever-tentacle-monster is a smart-tentacle-monster.
To say ShortDesc of (M - clever-tentacle-monster):
	say "clever tentacle monster".
Understand "clever" as clever-tentacle-monster.

mischievous-tentacle-monster is a smart-tentacle-monster.
To say ShortDesc of (M - mischievous-tentacle-monster):
	say "mischievous tentacle monster".
Understand "mischievous" as mischievous-tentacle-monster.

devious-tentacle-monster is a smart-tentacle-monster.
To say ShortDesc of (M - devious-tentacle-monster):
	say "devious tentacle monster".
Understand "devious" as devious-tentacle-monster.

A durable-tentacle-monster is a kind of tentacle monster.

armoured-tentacle-monster is a durable-tentacle-monster.
To say ShortDesc of (M - armoured-tentacle-monster):
	say "armoured tentacle monster".
Understand "armoured" as armoured-tentacle-monster.

tough-tentacle-monster is a durable-tentacle-monster.
To say ShortDesc of (M - tough-tentacle-monster):
	say "tough tentacle monster".
Understand "tough" as tough-tentacle-monster.

sturdy-tentacle-monster is a durable-tentacle-monster.
To say ShortDesc of (M - sturdy-tentacle-monster):
	say "sturdy tentacle monster".
Understand "sturdy" as sturdy-tentacle-monster.

stout-tentacle-monster is a durable-tentacle-monster.
To say ShortDesc of (M - stout-tentacle-monster):
	say "stout tentacle monster".
Understand "stout" as stout-tentacle-monster.

resilient-tentacle-monster is a durable-tentacle-monster.
To say ShortDesc of (M - resilient-tentacle-monster):
	say "resilient tentacle monster".
Understand "resilient" as resilient-tentacle-monster.

A wet-tentacle-monster is a kind of tentacle monster.

[Definition: a wet-tentacle-monster is musky: decide yes.]

slimy-tentacle-monster is a wet-tentacle-monster.
To say ShortDesc of (M - slimy-tentacle-monster):
	say "slimy tentacle monster".
Understand "slimy" as slimy-tentacle-monster.

slippery-tentacle-monster is a wet-tentacle-monster.
To say ShortDesc of (M - slippery-tentacle-monster):
	say "slippery tentacle monster".
Understand "slippery" as slippery-tentacle-monster.

sticky-tentacle-monster is a wet-tentacle-monster.
To say ShortDesc of (M - sticky-tentacle-monster):
	say "sticky tentacle monster".
Understand "sticky" as sticky-tentacle-monster.

slick-tentacle-monster is a wet-tentacle-monster.
To say ShortDesc of (M - slick-tentacle-monster):
	say "slick tentacle monster".
Understand "slick" as slick-tentacle-monster.

A dull-tentacle-monster is a kind of tentacle monster.

wriggling-tentacle-monster is a dull-tentacle-monster.
To say ShortDesc of (M - wriggling-tentacle-monster):
	say "wriggling tentacle monster".
Understand "wriggling" as wriggling-tentacle-monster.

creeping-tentacle-monster is a dull-tentacle-monster.
To say ShortDesc of (M - creeping-tentacle-monster):
	say "creeping tentacle monster".
Understand "creeping" as creeping-tentacle-monster.

prowling-tentacle-monster is a dull-tentacle-monster.
To say ShortDesc of (M - prowling-tentacle-monster):
	say "prowling tentacle monster".
Understand "prowling" as prowling-tentacle-monster.

skulking-tentacle-monster is a dull-tentacle-monster.
To say ShortDesc of (M - skulking-tentacle-monster):
	say "skulking tentacle monster".
Understand "skulking" as skulking-tentacle-monster.

hunting-tentacle-monster is a dull-tentacle-monster.
To say ShortDesc of (M - hunting-tentacle-monster):
	say "hunting tentacle monster".
Understand "hunting" as hunting-tentacle-monster.

slinking-tentacle-monster is a dull-tentacle-monster.
To say ShortDesc of (M - slinking-tentacle-monster):
	say "slinking tentacle monster".
Understand "slinking" as slinking-tentacle-monster.

tentacleShortcutCount is initially 1.
To TentacleShortcutAssign (C - a tentacle monster):
	if the text-shortcut of C is "tnm":
		now the text-shortcut of C is substituted form of "tnm[tentacleShortcutCount]";
		increase tentacleShortcutCount by 1.

To say MediumDesc of (M - a tentacle monster):
	say "[tentacle-size of M][if the evolved of M is 3] evolved[end if] [ShortDesc of M]".

To say MonsterDesc of (M - a tentacle monster):
	if M is newborn:
		say NewTentacleDesc;
	otherwise if M is large:
		say LargeTentacleDesc;
	otherwise:
		say MassiveTentacleDesc.

To say TentacleColour of (M - a tentacle monster):
	say "[if the evolved of M is 3]pure white[otherwise if M is not player-brood]grey[otherwise]dark red[end if]".

To decide which number is the girth of (M - a tentacle monster):
	if M is massive, decide on 9;
	if M is large, decide on 6;
	decide on 4.

To decide which number is the base semen load of (M - a tentacle monster):
	let C be 2;
	if M is large, increase C by 2;
	if M is massive, increase C by 4;
	if M is wet-tentacle-monster, increase C by 2;
	decide on C.

A wet-tentacle-monster has a number called dripTimer. The dripTimer of a wet-tentacle-monster is usually 0.

To compute unique periodic effect of (M - a wet-tentacle-monster):
	if the class of the player is maid or a random number between 1 and 5 > 3, increase the dripTimer of M by 1;
	if a random number between 30 and 100 <= the dripTimer of M:
		now the dripTimer of M is 0;
		if M is in the location of the player:
			say "[BigNameDesc of M] leaves a trail of [semen] behind [his of M] body.";
		SemenPuddleUp 1 in (the location of M).

To set up (M - a tentacle monster):
	reset M;
	now the monstersetup of M is 1;
	let D be 5;
	if M is large:
		increase D by 3;
	otherwise if M is massive:
		increase D by 3;
	now the raw difficulty of M is D;
	TentacleShortcutAssign M;
	now M is not player-brood;
	now the health of M is the maxhealth of M.

Definition: a tentacle monster is blocker if it is not newborn or the player is prone.

To compute birth set up of (M - a tentacle monster):
	now M is newborn;
	set up M;
	now the evolved of M is 0;
	regionally place M;
	now M is player-brood.

Definition: a tentacle monster (called M) is willing to shag:
	if M is awake and the scared of M is 0 and the boredom of M < 120 and M is threatening, decide yes;
	decide no.

To decide which number is the rounds of sex left of (M - a tentacle monster):
	if the class of the player is schoolgirl and the class of the player is magical girl and there is a heart wand in the location of the player and the player is able to orgasm and the player is not unable to orgasm so soon and M is penetrating a fuckhole, decide on 2; [If the player will win the fight when they orgasm, the sex never ends.]
	decide on the sex-length of M.

To say PregGrowth of (M - a tentacle monster):
	say "[one of]the tentacle monster's spawn[or]the tentacle monster's [child][or]your own tentacle spawn's further brood[as decreasingly likely outcomes]".

To compute labour to (M - a tentacle monster):
	compute tentacle birth. [Possibly this can be changed so tentacle monster daddy is different from lake monster, vine, and belt daddy.]

To compute fatherhood to (M - a tentacle monster):
	distract M.

Definition: a tentacle monster is butt slut immune: decide yes.

To say SummoningFlav of (M - a tentacle monster):
	let S be a random tentacle-breeder in the location of the player;
	distract M;
	if inhuman pregnancy is 2, say "[bold type]You [if S is player-breeder]watch as your previous self gives birth to a red mass of tentacles with a loud squelch. The newborn tentacle monster looks around, blinking with a single giant eye as it takes in its new surroundings[otherwise if S is tentacle-breeder]watch as the girl stuck to the wall gives birth to a red mass of tentacles with a loud squelch. The newborn tentacle monster looks around, blinking with a single giant eye as it takes in its new surroundings[otherwise]hear a loud squelch and mindless moaning coming from nearby[end if]![roman type][line break]";
	otherwise say "[bold type]You [if S is tentacle-breeder]watch as the hole in the flesh in the wall widens, and a ball of grey flesh drops out to the ground. The newly spawned tentacle monster looks at you with its single eye before scuttling off into the distance[otherwise]hear a loud squelch coming from nearby[end if]![roman type][line break]";
	compute mandatory room leaving of M.

To compute (M - demon lord) stomping (N - a tentacle monster):
	if M is in the location of the player, say "With a flick of [his of M] finger, [NameDesc of N] bursts into flames and quickly disappears in a puff of smoke.";
	destroy N;

Part 0 - Motion

To compute monstermotion of (M - a tentacle monster): [Tentacle monsters shouldn't really be in the School unless they're in the School Dungeon with the player]
	if M is in the School, now M is in Hotel01;
	compute room leaving of M.

Part 1 - Perception

Definition: a tentacle monster is uniquely unfriendly if playerRegion is School. [The player looking ultra-slutty doesn't override the fact that the tentacle monster defends them when pregnant. However the tentacle monster in the school dungeon must always be an enemy.]

To compute perception of (M - a tentacle monster):
	say "[BigNameDesc of M] notices you! [big he of M] stares at your belly with [his of M] single large eye.";
	let R be a random number between 1 and 5;
	if the largeness of belly > 2:
		if M is smart-tentacle-monster, now R is 1;
		if M is dull-tentacle-monster, now R is 5;
	otherwise:
		if the pregnancy of the player > 0 and M is smart-tentacle-monster, now R is 5;
		otherwise now R is 1;
	unless R is 1:
		say "[big he of M] doesn't seem to be aggressive.";
		calm M;
	otherwise:
		say "[big he of M] rears up with [his of M] tentacles[if M is newborn], coming right for you[otherwise if inhuman pregnancy is 2], looking like [he of M] wants to breed[otherwise], clearly selecting you as [his of M] next target[end if]!";
		anger M.

To compute (M - a monster) stomping (N - a tentacle monster):
	if M is in the location of the player, say "[BigNameDesc of M] roars at the [N], which takes one wide eyed look at [him of M] and then runs away.";
	regionally place N;
	bore N.

Part 2 - Combat

Section 1 - Protect and Attack

To compute (M - a tentacle monster) protecting against (X - a monster):
	if the player is monster stuck and X is not tentacle monster:
		do nothing;
	otherwise if X is tentacle monster and M is not durable-tentacle-monster:
		say "[BigNameDesc of M] seems to lose its friendly demeanour and becomes aggressive!";
		anger M;
	otherwise:
		if M is massive:
			compute X receiving 3 damage from M;
		otherwise if M is large:
			compute X receiving 2 damage from M;
		otherwise:
			compute X receiving 1 damage from M.

To say AllyDamageFlav of (M - a tentacle monster) on (X - a monster):
	say "[BigNameDesc of M] [one of]charges in to protect you and your pregnancy. It [or][stopping][if M is newborn]weakly slaps[otherwise if M is large]heavily strikes[otherwise]brutally slams[end if] the [X] with its tentacles!";

To compute the flying player taunting of (M - a tentacle monster):
	say "[one of][BigNameDesc of M] clearly refuses to move from your location until you stop floating.[or][stopping]".

The latex punishment rule of a tentacle monster is usually the no latex punishment rule.

The tentacle monster continue sex rules is a rulebook. The continue sex rules of tentacle monster is usually the tentacle monster continue sex rules.

The default check for sex rule is listed first in the tentacle monster continue sex rules.
The default finish sex rule is listed last in the tentacle monster continue sex rules.

This is the tentacle monster continues sex rule:[TODO: update for evolved tentacle monsters]
	let M be current-monster;
	if the number of orifices penetrated by M is 1, say "[BigNameDesc of M][']s tentacle [if M is massive]plows[otherwise]thrusts[end if] into your [variable random orifice penetrated by M] ";
	otherwise say "[BigNameDesc of M][']s tentacles [if M is massive]plow[otherwise]thrust[end if] into you ";
	if M is newborn, say "steadily.";
	otherwise say "powerfully.";
	decrease the sex-length of M by 1;
	if M is penetrating asshole, ruin asshole;
	if M is penetrating vagina, ruin vagina;
	if the reaction of the player > 0 and M is penetrating a fuckhole:
		if M is not massive, say "Your lack of resistance against the monster is preventing you from getting more sore, [if M is player-brood]but it is extremely humiliating to submit to being bred by your own hellish tentacle spawn[otherwise]but you can feel your thoughts clouding as you are bred by the freakish tentacle spawn[end if].";
	if M is penetrating breasts, compute titfuck of M;
	let spare-tentacle be 0;
	let NB be the number of body parts penetrated by M;
	if the number of body parts penetrated by M < 2, now spare-tentacle is 1;
	if M is large and the number of body parts penetrated by M < 3, now spare-tentacle is 1;
	if M is massive and the number of body parts penetrated by M < 4, now spare-tentacle is 1;
	if the number of body parts penetrated by M is 0, now spare-tentacle is 0; [Magical schoolgirl just managed to free herself.]
	if spare-tentacle is 1 and (a random number between 1 and 4 > 1 or the class of the player is schoolgirl or the class of the player is magical girl or M is dull-tentacle-monster):
		choose a sex method;
		if targeted-body-part is face:
			say "[BigNameDesc of M] decides to escalate things and before you can react it has forced another of its warm, thick appendages into your open mouth! It thrusts away at the same pace as the other[if the number of orifices penetrated by M is 2]s[end if]. [if M is massive]The tentacle is so large that your jaw is forced into a painfully open position.[otherwise]The tentacle keeps hitting the back of your throat, causing you to make loud gagging noises.[end if]";
			now M is penetrating face;
		otherwise if targeted-body-part is asshole:
			if the player is not ass protected:
				say "You feel the tip of another tentacle push against your anal sphincter, and before you can [if the bimbo of the player > 13]express your enthusiasm[otherwise if M is penetrating face]scream [line break][variable custom style]'MMMMFF'[roman type][line break][otherwise]yell in shock[end if] [NameDesc of M] has buried its appendage deep inside your [asshole]. It joins the fucking pace of the other[if the number of orifices penetrated by M is 2]s[end if].";
				if M is player-brood:
					say "[if M is massive]The tentacle is so large that you can feel your [asshole] being permanently gaped.[otherwise if M is penetrating face and M is penetrating vagina]You're being triple penetrated by your own tentacle offspring![otherwise if M is penetrating vagina]You're being double penetrated by your own tentacle offspring![otherwise]You're being spit-roasted by your own tentacled offspring![end if]";
				now M is penetrating asshole;
				ruin asshole;
			otherwise:
				let C be a random worn top level ass protection clothing;
				if M is newborn:
					if C is displacable:
						say "You feel a tentacle pull your [ShortDesc of C] out of the way!";
						displace C;
					otherwise:
						say "You feel a tentacle try to pull your [ShortDesc of C] out of the way, but it [one of][or]still [stopping]doesn't have the strength!";
				otherwise:
					say "You feel a tentacle rip your [ShortDesc of C] away!";
					destroy C;
		otherwise if targeted-body-part is vagina:
			if the player is not pussy protected:
				say "Another tentacle purposefully glides straight into your [vagina][if the openness of vagina < 4], stretching it painfully as it forces its way in[end if]. It joins the fucking pace of the other[if the number of orifices penetrated by M is 2]s[end if]. ";
				if M is player-brood:
					say "[if M is massive]The tentacle is so large that you can feel your [vagina] being permanently widened.[otherwise if M is penetrating face and M is penetrating asshole]You're being triple penetrated by your own tentacle offspring![otherwise if M is penetrating asshole]You're being double penetrated by your own tentacle offspring![otherwise]You're being spit-roasted by your own tentacled offspring![end if]";
				now M is penetrating vagina;
				ruin vagina;
			otherwise:
				let C be a random worn top level protection clothing;
				if M is newborn:
					if C is displacable:
						say "You feel a tentacle pull your [ShortDesc of C] out of the way!";
						displace C;
					otherwise:
						say "You feel a tentacle try to pull your [ShortDesc of C] out of the way, but it [one of][or]still [stopping]doesn't have the strength!";
				otherwise:
					say "You feel a tentacle rip your [ShortDesc of C] away!";
					destroy C;
		otherwise if targeted-body-part is breasts:
			say "Another tentacle purposefully glides straight [if there is worn breast covering clothing]up underneath your [random worn breast covering clothing] and [end if]between your breasts!";
			now M is penetrating breasts;
	rule succeeds.
The tentacle monster continues sex rule is listed last in the tentacle monster continue sex rules.

To set up sex length of (M - a tentacle monster):
	now the sex-length of M is a random number between 3 and 4;
	if M is large, increase the sex-length of M by a random number between 0 and 1;
	if M is massive, increase the sex-length of M by a random number between 0 and 1.

The tentacle monster end of sex rules is a rulebook. The end-of-sex-rules of tentacle monster is usually the tentacle monster end of sex rules.

This is the tentacle monster climaxes rule:
	let M be current-monster;
	TimesSubmittedUp M by 1;
	if M is wrapped:
		say "[BigNameDesc of M] climaxes. You can tell because the condoms covering its tentacles [if M is newborn]bulge as they capture their loads. One of them bursts open, causing a bit of [semen] to splatter onto your body and the ground[otherwise if M is large]bulge ridiculously as they struggle to contain their loads. A couple of them burst open, causing a small amount of semen to splatter onto your body, clothes, and the ground[otherwise]bulge obscenely as they capture their loads. Several of them burst open, causing a generous amount of [semen] to cascade onto your body, clothes, and the ground[end if].";
		let cum-amount be the semen load of M / 2;
		SemenPuddleUp cum-amount;
		if bukkake fetish is 1, CumFaceUp cum-amount * 5;
	otherwise:
		say "[BigNameDesc of M] climaxes. You can tell because its tentacles pulse and spasm, and you can see several of its other tentacles spurting [if M is newborn]small amounts[otherwise if M is large]generous amounts[otherwise]litres if not gallons[end if] of [semen] onto your body, clothes and the ground.";
		let cum-amount be the semen load of M;
		SemenPuddleUp cum-amount;
		if bukkake fetish is 1, CumFaceUp cum-amount * 5;
	orgasm M; [needs to go above the below functions because this function cares about where is being penetrated]
	if M is penetrating breasts:
		increase the raw sensitivity of breasts by 1;
		now M is not penetrating breasts;
	if M is penetrating face:
		compute facial climax of M;
		BlowCount;
	if M is penetrating asshole:
		compute anal climax of M;
		AnalCount;
	if M is penetrating vagina:
		compute vaginal climax of M;
		FuckCount;
	if the magic-power of the player > 0:
		say "You feel some magic draining from your body...[line break]";
		compute MagicDrain of M;
		if the class of the player is "magical schoolgirl":
			say "As the magic drains away, you feel your brain [one of]sparkling with brilliance[or]gaining even more smarts[stopping]!";
			IntUp 2;
	progress quest of tentacle-quest;
	if M is wrapped:
		let X be the semen load of M;
		while X > 0:
			decrease X by 1;
			compute condom filling of M;
		now M is unwrapped;
	if M is not massive:
		DifficultyUp M by 3;
	say "[BigNameDesc of M] [one of]seems to stop[or]once again stops[stopping] moving as its tentacles rest peacefully on the ground and its single eye closes.[unless M is massive][line break][one of]It appears to have entered some kind of dormant state. You watch with silent awe as it[or]The monster[stopping] smoothly but rapidly grows in size until its main body is [end if][if M is newborn]as big as yours, and its many tentacles have thickened to the width of a small bottle.[otherwise if M is large]as big as a car, and its multitude of tentacles have thickened to the width of your forearm.[otherwise][one of]Much to your relief it doesn't seem to grow any larger.[or][stopping][end if]";
	if magical-maid-outfit is worn and (M is not massive or (inhuman pregnancy is 2 and the player is female)):
		if M is large, now M is massive;
		if M is newborn, now M is large;
		say "[bold type]Just as you are about to begin to crawl away, you feel some magic drain from your [magical-maid-outfit], and it wakes up again![line break][variable custom style][one of]What?![or]I can't... I'll break...[stopping][roman type][line break]";
		MagicPowerDown 1;
		if inhuman pregnancy is 2 and the player is female:
			say "With renewed vigour it leaps into action once again, and within seconds is filling all of your holes and molesting you anew. Before you know it, a second barrage of [semen] is coating you and filling you to the brim. More magic ebbs and flows from [NameDesc of magical-maid-outfit] into [NameDesc of M], enabling [him of M] to recommence fucking you over and over and over again, pumping you full of its potent [semen] until eventually you white out.";
			now delayed fainting is 1;
			now the fainting reason of the player is 18;
			bore M;
			if M is not massive:
				DifficultyUp M by 3;
				now M is massive;
		otherwise:
			dislodge M;
	otherwise:
		say "[if the bimbo of the player < 11 and M is large and the openness of asshole < 9 and the openness of vagina < 9][line break][first custom style]'Holy shit. I am getting the fuck out of here while this thing is sleeping. I could never take that...'[roman type][line break][end if]";
		if M is large:
			now M is massive;
			if heart hairpin is off-stage:
				if the class of the player is schoolgirl and the player is ponytailed:
					let S be a random worn scrunchie;
					say "Your [S] explodes in a shower of cartoon lovehearts!";
					destroy S;
				if heart hairpin is actually summonable:
					say "[bold type]Suddenly, you feel a small weight in your hair. A hair clip in the shape of a heart has appeared! You feel a wave of shame come over you... You doubt you'll be very good at this job, judging by how things have been going so far.[roman type][line break]";
					summon heart hairpin cursed;
					if the player is male and (fast tg is 3 or (the size of penis <= min penis size and tg fetish > 0)):
						say "Your whole body suddenly goes numb, then is filled with an almost electric tingle. You feel terrible wrenching from your insides that you're sure should hurt, but you just don't seem to be able to feel much of anything right now. The tingling comes to a focus in your crotch, filling you with a sense of terrible foreboding. [if the size of penis > 0]As feeling comes back to you, you reach down and can immediately tell you're missing something kind of notable: your [player-penis]![otherwise]As feeling comes back to you, you reach down with a sense of foreboding.[end if] It seems whatever magic made that hair clip appear has decided you'd be better off as a girl...";
						SexChange the player;
		if M is newborn:
			now M is large;
		now the sleep of M is 250;
		if the class of the player is schoolgirl, decrease the sleep of M by 100;
		if the evolved of M is 3 or M is in School34, decrease the sleep of M by 100;
		if M is dull-tentacle-monster, decrease the sleep of M by 25;
		deinterest M;
	rule succeeds.
The tentacle monster climaxes rule is listed in the tentacle monster end of sex rules.

To say CondomPinFlav of (M - a tentacle monster) on (C - a clothing):
	if M is penetrating a body part:
		say "Completely of its own accord, the used condom suddenly zips from [NameDesc of M] and flies through the air until it hits your [ShortDesc of C].";
	otherwise:
		say "Another condom flies through the air and attaches to your [ShortDesc of C].".

To say CondomPinReactionFlav of (M - a tentacle monster) on (C - a clothing):
	do nothing.

To decide if (M - a tentacle monster) is losing wrapper in (F - a fuckhole):
	if M is penetrating vagina and F is asshole, decide no; [Otherwise the vaginal condom automatically fails afterwards because the asshole one failed which doesn't make much sense]
	if M is massive and a random number between 1 and 4 > 1, decide yes;
	if M is large and a random number between 1 and 4 > 2, decide yes;
	if M is newborn and a random number between 1 and 3 is 1, decide yes;
	decide no.

To decide if (M - a tentacle monster) is willing to creampie (F - a fuckhole):
	if the reaction of the player is 0 and the class of the player is not schoolgirl:
		if M is massive and a random number between 1 and 6 is 1, decide no;[1 in 6]
		if M is large and a random number between 1 and 3 is 1, decide no;[1 in 3]
		if M is newborn and a random number between 1 and 3 > 1, decide no;[2 out of 3]
	decide yes.

To compute vaginal climax of (M - a tentacle monster):
	if egg laying fetish is 1:
		say "A warm, hard lumpy feeling lets you know you're feeling the tentacle in your [vagina] filling you up with several alien eggs[if M is wrapped], carrying the tattered remnants of the condom with it as it pulls out[otherwise] before pulling out[end if].";
		if M is wrapped, now M is unwrapped;
		if M is newborn:
			WombFill the semen load of M small eggs;
		otherwise if M is large:
			WombFill the semen load of M medium eggs;
		otherwise:
			WombFill (the semen load of M / 2) - 1 large eggs;
	otherwise if M is wrapped:
		if M is losing wrapper in vagina:
			say "The tentacle in your [vagina] pumps its potent load into the condom, which holds for an instant before bursting, flooding the deepest recesses of your womb with [semen].";
			now M is unwrapped;
			PussyFill the semen load of M;
			now M is wrapped;
			say CreampieReactionFlav to M in vagina;
		otherwise:
			say "The tentacle in your [vagina] pumps its potent load into the condom.";
			PussyFill the semen load of M;
	otherwise:
		if M is willing to creampie vagina:
			say "The tentacle in your [vagina] pumps its potent load into the deepest recesses of your womb.";
			PussyFill the semen load of M;
			say CreampieReactionFlav to M in vagina;
		otherwise:
			say "Your struggling causes the tentacle in your [vagina] to slip out at the last moment, spraying your belly with [semen].";
			CumBellyUp the semen load of M;
	if the largeness of belly < 10:[We haven't shown an image already]
		if M is penetrating face, cutshow figure of tentacle cutscene 6 for M;
		otherwise cutshow figure of tentacle cutscene 5 for M;
	now M is not penetrating vagina.

To say CreampieReactionFlav to (M - a tentacle monster) in (F - vagina):
	if M is player-brood:
		say "[one of][line break][if the bimbo of the player < 8][line break][first custom style]Oh no, please please please don't let me get pregnant with another one of these horrible creatures![otherwise][line break][variable custom style]It looks like this is the circle of life as I'm going to know it from now on...[end if][roman type][line break][or][stopping]";
	otherwise:
		say "[one of][variable custom style]Is it trying to turn me into a monster incubator?![roman type][line break][or][stopping]";

To compute anal climax of (M - a tentacle monster):
	if egg laying fetish is 1:
		say "A warm, hard lumpy feeling lets you know you're feeling the tentacle in your [asshole] filling you up with several alien eggs[if M is wrapped], carrying the tattered remnants of the condom with it as it pulls out[otherwise] before pulling out[end if].";
		if M is wrapped, now M is unwrapped;
		if M is newborn:
			AssFill the semen load of M small eggs;
		otherwise if M is large:
			AssFill the semen load of M medium eggs;
		otherwise:
			AssFill (the semen load of M / 2) - 1 large eggs;
	otherwise:
		if M is wrapped:
			if M is losing wrapper in asshole:
				say "The tentacle in your [asshole] breaks through the condom, filling you up with its load before pulling out.";
				now M is unwrapped;
				AssFill the semen load of M;
				now M is wrapped;[we unwrap at the end]
			otherwise:
				say "The tentacle in your [asshole] thoroughly fills the condom before pulling out.";
				AssFill the semen load of M;
		otherwise:
			if M is willing to creampie vagina:
				say "You feel the tentacle in your [asshole] filling you up with its load before pulling out.";
				AssFill the semen load of M;
			otherwise:
				say "Your struggling causes the tentacle in your [asshole] to slip out at the last moment, spraying your ass and thighs with [semen].";
				CumThighsUp the semen load of M;
	now M is not penetrating asshole;
	if the largeness of belly >= 10:
		cutshow figure of tentacle cutscene 2 for M;
	otherwise if M is not penetrating vagina:
		if M is penetrating face, cutshow figure of tentacle cutscene 6 for M;
		otherwise cutshow figure of tentacle cutscene 5 for M;

To compute facial climax of (M - a tentacle monster):
	if M is wrapped:
		if M is losing wrapper in face:
			say "The condom in your mouth bursts, forcing you to gulp salty [semen].";
			StomachSemenUp the semen load of M;
		otherwise:
			say "The condom in your mouth bulges[if M is massive] obscenely[end if] with [NameDesc of M][']s load, but manages to resist the strain. ";
			compute condom filling of M;
	otherwise:
		if M is willing to bukkake:
			say "Your struggling causes the tentacle in your mouth to pop out at the last moment, spraying your face with its [semen].";
			CumFaceUp the semen load of M;
		otherwise:
			say "The tentacle in your mouth forces you to gulp down the salty [semen].";
			StomachSemenUp the semen load of M;
	now M is not penetrating face.

To decide if (M - a tentacle monster) is losing wrapper in (F - face):
	if the number of body parts penetrated by M > 1, decide no; [since we don't track condoms separately, let's let the face condom always succeed if the tentacle monster is wearing several condoms (face goes first)]
	if M is massive and a random number between 1 and 3 > 1, decide yes;[2/3]
	if M is large and a random number between 1 and 3 is 1, decide yes;[1/3]
	decide no.[newborns never break the condom]

Definition: A tentacle monster (called M) is willing to bukkake:
	if the reaction of the player is 0:
		if M is massive and a random number between 1 and 6 is 1, decide yes;[1 in 6]
		if M is large and a random number between 1 and 3 is 1, decide yes;[1 in 3]
		if M is newborn and a random number between 1 and 3 > 1, decide yes;[2 out of 3]
	decide no.


To say StrikingSuccessFlav of (M - a tentacle monster) on (B - a body part):
	say "[BigNameDesc of M] bashes you [TargetName of B] with its tentacles, causing you huge amounts of pain!";
	if the class of the player is maid and the magic-power of the player > 0, BodyRuin 1;
	if M is massive, BodyRuin 1.

To say StrikingFailureFlav of (M - a tentacle monster) on (B - a body part):
	say "You manage to narrowly dodge [NameDesc of M][']s tentacle as it comes flying towards your [printed name of B].".

This is the tentacle monster convinced rule:
	if presented-orifice is a potential target:
		say PresentAcceptanceFlav of current-monster;
		now the chosen-orifice of current-monster is presented-orifice;
		rule succeeds;
	otherwise if presented-orifice is not nothing:
		say PresentRejectionFlav of current-monster.
The monster convinced rule of a tentacle monster is usually the tentacle monster convinced rule.

To compute the orifice choosing of (M - a tentacle monster):
	now the chosen-orifice of current-monster is a random actual target body part.

To say PullAttempt of (M - a tentacle monster) at (C - a clothing):
	say "[BigNameDesc of M] stabs at your [printed name of C] with a tentacle!".

To compute (M - a tentacle monster) ripping (C - a clothing):
	say "It pushes straight through your [printed name of C], ripping it!";
	now C is crotch-ripped.

To compute (M - a tentacle monster) destroying (C - a clothing):
	say "It pushes straight through your [printed name of C], destroying it!";
	destroy C.

To compute (M - a tentacle monster) entering mouth:
	say "[BigNameDesc of M] [if presented-orifice is face]gently but firmly sends a single tentacle into your waiting mouth.[otherwise]grips your wrists with two of its tentacles to hold you in place, then forces a third into your mouth.[end if]";
	now M is penetrating face;
	set up sex length of M.

To say AssholePenetrationFlav of (M - a tentacle monster):
	say "Without hesitation [NameDesc of M] buries a [DickDesc of M] deep inside your [asshole].[if M is massive][line break]The tentacle is so large that you can feel your [asshole] being permanently gaped.[end if]".

To say VaginaPenetrationFlav of (M - a tentacle monster):
	say "Without hesitation [NameDesc of M] forces a [DickDesc of M] deep inside your [vagina].[if M is massive][line break]The tentacle is so large that you can feel your [vagina] being permanently gaped.[end if]".

To say SelectionFrustrated of (M - a tentacle monster):
	say "[BigNameDesc of M][']s one eye stares at you with visible frustration but then seems to decide to leave you alone.";
	bore M.

To say BreastsPenetrationFlav of (M - a tentacle monster):
	say "[BigNameDesc of M] coils a [DickDesc of M] around your [ShortDesc of breasts], squeezing them together as it forces its way between their curves and begins to thrust.".

To compute condom request choice of (M - a tentacle monster):
	if M is unwrapped:
		say "Looks like [NameDesc of M] is probably going for your [chosen-orifice of M]. Do you want to try making [him of M] wear a condom?";
		if the player is consenting:
			if M is convinced to wear a condom:
				say CondomForceSuccessFlav of M;
				now M is wrapped;
			otherwise:
				say CondomForceFailFlav of M.

To say CondomForceSuccessFlav of (M - a tentacle monster):
	say "You grab hold of one of [NameDesc of M][']s [DickDesc of M]s and quickly unroll a condom down its length. To your surprise, several more condoms fly out of your pack thanks and with clear magically influenced purpose, apply themselves to each of [NameDesc of M][']s undulating limbs.";

To say CondomForceFailFlav of (M - a tentacle monster):
	say "You grab hold of a [DickDesc of M] and try to unroll a condom down its length. [BigNameDesc of M] quickly smacks your hand away. Looks like [he of M][']s going in bare...";

To say TitfuckResistFlav of (M - a tentacle monster):
	let C be a random worn top level covering clothing;
	say "[one of]You do everything you can to push [him of M] away, but [if C is clothing]your [ShortDesc of C] keeps [him of M] firmly sandwiched between them![otherwise]you simply don't have the leverage from your position on your knees![end if][or]You struggle but you can't get away![or]You resist, but you simply can't get away![in random order]".

To say TitfuckForceFlav of (M - a tentacle monster):
	say "[one of][BigNameDesc of M] keeps your [ShortDesc of breasts] pushed together as the [DickDesc of M] thrusts between them.[or][BigNameDesc of M] continues to powerfully thrust between your [ShortDesc of breasts]![or]Your [ShortDesc of breasts] jiggle[if the largeness of breasts > 10] obscenely[end if] as the [DickDesc of M] thrusts between them![or]The [DickDesc of M] holds your [ShortDesc of breasts] together as it roughly thrusts between them.[or][BigNameDesc of M][']s [DickDesc of M] gently bumps your chin as it enthusiastically thrusts between your [ShortDesc of breasts].[in random order]".

To say TitfuckReceiveFlav of (M - a tentacle monster):
	say TitfuckForceFlav of M.

To say DickDesc of (M - a tentacle monster):
	say "[if M is large]thick [otherwise if M is massive]huge [end if]tentacle".

Section 2 - Damage

To compute damage reaction of (M - a tentacle monster):
	if M is awake:
		if M is uninterested:
			say "[BigNameDesc of M] turns to fight!";
		otherwise:
			if M is friendly:
				say "[BigNameDesc of M][']s single eye widens with surprise, but [he of M] raises [his of M] appendages and takes an offensive stance!";
				if a random number between 1 and 3 is 1, increase the sex-length of M by 1;
	otherwise:
		now the sleep of M is 0;
		say "[big he of M] wakes up, and looks around for the cause of this rude awakening! It looks like you might be in trouble...".

To say BanishFleeFlav of (M - a tentacle monster):
	say "[BigNameDesc of M] [if the evolved of M is 3]has lost the last of [his of M] magical energy. [big his of M] white colour fades to black. [big he of M] [end if][if the class of the player is magical girl]tries to grip into the ground with [his of M] tentacles as a portal opens up to the demon realm behind [him of M], but it's not use. [BigNameDesc of M] is sucked back into the demon realm, banished thanks to your magic[otherwise]makes a weird gurgling hissing squeak noise and scuttles away as fast as [his of M] tentacles can carry [him of M][end if]! [one of][bold type]You won't be seeing [him of M] again.[roman type][line break][or]Good riddance.[or]Another monster driven from the land. Good job![or]Good riddance.[stopping]".

Definition: a tentacle monster is auto-banish-loot-dropping if it is massive. [Will it automatically drop jewellery when it is banished in this manner?]

To compute unique banishment of (M - a tentacle monster):
	say "[bold type]You feel [if M is not newborn]huge amounts of [end if]magic power surging through your veins![roman type][line break][one of][variable custom style]Wow! This is what happens when I defeat tentacle monsters?[roman type][line break][or][stopping]";
	if M is newborn:
		MagicPowerUp 1;
	otherwise if M is large:
		MagicPowerUp 2;
	otherwise if M is massive:
		MagicPowerUp 3;
	if the magic-power of the player > 2:
		if the class of the player is virgin warrior and virgin magical girl outfit is off-stage and the number of worn golden warrior priestess outfit is 0:
			repeat with O running through worn breast covering clothing:
				say "Your [O] vanishes!";
				destroy O;
			repeat with O running through worn dresses:
				say "Your [O] vanishes!";
				destroy O;
			repeat with O running through worn skirts:
				say "Your [O] vanishes!";
				destroy O;
			summon virgin magical girl outfit;
			say "[bold type]Thin white fabric appears on your body, covering your chest and waist![roman type][line break]";
			now warrior-summoned is 2;
		otherwise if heart hairpin is off-stage:
			if the class of the player is schoolgirl and the player is ponytailed:
				let S be a random worn scrunchie;
				say "Your [S] explodes in a shower of cartoon lovehearts!";
				destroy S;
			if heart hairpin is actually summonable:
				say "[bold type]Suddenly, you feel a small weight in your hair. A hair clip in the shape of a heart has appeared! You feel a sudden rush, which almost takes you to your knees.[roman type][line break]";
				summon heart hairpin cursed;
				if the player is male and (fast tg is 3 or (the size of penis <= min penis size and tg fetish > 0)):
					say "Your whole body suddenly goes numb, then is filled with an almost electric tingle. You feel terrible wrenching from your insides that you're sure should hurt, but you just don't seem to be able to feel much of anything right now. The tingling comes to a focus in your crotch, filling you with a sense of terrible foreboding. [if the player is possessing a penis]As feeling comes back to you, you reach down and can immediately tell you're missing something kind of notable: your [player-penis]![otherwise]As feeling comes back to you, you reach down with a sense of foreboding.[end if] It seems whatever magic made that hair clip appear has decided you'd be better off as a girl...";
					SexChange the player;
	if the evolved of M is 3 and there is an off-stage ghostly tentacle and playerRegion is Mansion:
		let T be a random off-stage ghostly tentacle;
		set up T;
		now T is in the location of the player;
		say "A translucent tentacle floats up from its lifeless body, unfurling to reveal an underside full of giant, octopus-like suckers.";
		anger T.

To loot (M - a tentacle monster):
	let X be a random off-stage plentiful ring;
	if the class of the player is santa's little helper, now X is a random off-stage christmas gift;
	unless X is nothing:
		now X is in the location of the player;
		if X is plentiful accessory:
			now X is pure diamond;
			if M is not massive:
				now X is emerald;
				if a random number between 1 and 2 is 1:
					if M is newborn, now X is sapphire;
					otherwise now X is ruby;
			set shortcut of X;
		say "The defeated [M] [if the loot dropped of M > 0]also [end if]dropped a [printed name of X]!";
		increase the loot dropped of M by 1;
		compute autotaking X.

Part 3 - Variants

To say NewTentacleDesc:
	say "[one of]A mass of [TentacleColour of the item described] tentacles, about the size of a [if extreme proportions fetish is 1]beachball[otherwise]basketball[end if]. Each 'arm' is about as thick around as a normal human penis, and looks just like one too. [or]A [if extreme proportions fetish is 1]beach[otherwise]basket[end if]ball-sized heap of [TentacleColour of the item described] tentacles. Each one is looks almost identical to a normal human penis in shape and thickness, although alien in colour. [in random order]";
	say "[if the evolved of the item described is 3]A single eye is visible inside the main mass, no larger than a plum and as black as the void itself.[otherwise if item described is asleep]A single eye no larger than a plum rests on the ground before it, attached to the main body by a fleshy stalk. It's closed, and the tentacles surrounding it are completely motionless.[otherwise]A single eye no larger than a plum is raised above the main body on a fleshy stalk, scanning the surrounding objects and area with a keen, lucid curiosity.[end if]";
	unless the item described is player-brood:
		do nothing;
	otherwise:
		say "[if the bimbo of the player < 7][line break][first custom style][one of]That thing, it... it really came out of me?[or]It's a fucking demon-spawn. I'm killing it.[or]I have to kill this thing before it decides it wants to breed with me.[or]That thing is disgusting. I can't believe it came out of me.[or]Just as disgusting as its father was. I should kill it.[in random order][otherwise if the bimbo of the player < 12][line break][variable custom style][one of]That really came out of me. Wow.[or]Is it weird that I feel kind of... proud of it?[or]It's kind of cute.[or]It's disgusting, but in an endearing way.[or]If I don't kill it, it might want to breed with me. But it just looks so helpless...[in random order][otherwise][line break][second custom style][one of][big he of the item described]'s adorable![or]How cute![or]I love my baby![or]Mmm, [he of the item described] looks hor~ny![or]I want to take *good care* of my baby boy.[or][big he of the item described] looks like [he of the item described] wants to breed with me. Anything for my baby boy![in random order][end if][roman type][line break]".

To say LargeTentacleDesc:
	say "[one of]A large mass of [TentacleColour of the item described] tentacles, about the size of a person. Each of its arms is as thick around as a soda bottle and ends in a soft, penis-like tip. [or]A person-sized mass of [TentacleColour of the item described] tentacles. Each one is about as thick around as a soda bottle and ends in a shiny, phallic tip.[in random order]";
	say "[if the evolved of the item described is 3]At least a dozen eyes are visible inside the main mass, each about the size of a grapefruit and as black as the void itself.[otherwise if item described is asleep]A single eye the size of a grapefruit is resting on the ground before it, connected to the main body by a fleshy stalk. It is closed, and the surrounding tentacles are completely motionless.[otherwise]A single eye the size of a grapefruit is raised above the main body on a fleshy stalk, scanning the surrounding objects and area with visible intelligence.[end if]";
	unless the item described is player-brood:
		do nothing;
	otherwise:
		say "[if the bimbo of the player < 8][line break][first custom style][one of]It's growing! I have to deal with it now or I may never get another chance![or]I can't believe what this thing did to me. I have to destroy it.[or]It fucked me... even though I'm its mother.[or]I can't be responsible for something this evil.[in random order][otherwise if the bimbo of the player < 13][line break][variable custom style][one of]Seeing him grow like that was actually a little fulfilling.[or][big he of the item described] had sex with me even though I'm [his of the item described] mother.[or][big he of the item described]'s getting big. Could [he of the item described] grow like that again?[or]I probably could have fought more, but... [he of the item described] was just so cute.[or][big his of the item described] tentacles are getting big. Thick. Juicy. Oh no...[in random order][otherwise][line break][second custom style][one of][big he of the item described] had a growth spurt![or]I let him breed me like any good mommy would![or][big he of the item described]'s growing up so fast. Especially where it counts...[or]I want to let him breed me again. [big he of the item described] might grow even bigger![or]Who wants to fuck mommy? You do! You do![or][big he of the item described]'s going to grow up big and horny, just like [his of the item described] daddy.[then at random][end if][roman type][line break]".

To say MassiveTentacleDesc:
	let V be the evolved of the item described;
	say "[one of]A massive heap of [TentacleColour of the item described] tentacles, big enough to dwarf most humans and even some cars. Each arm is as about as thick around as *your* forearm and ends in a soft, penis-like tip. [or]A car-sized mass of [TentacleColour of the item described] tentacles. Each one, about as thick around as your forearm, has a natural sheen and a lightly coloured penis-like head. [in random order]";
	say "[if the evolved of the item described is 3]Dozens of eyes are visible inside the main mass, each about the size of a softball and as black as the void itself.[otherwise if item described is asleep]It is completely motionless, and its single, softball sized eye is closed and resting on the ground before it.[otherwise]A single eye about the size of a softball is raised above the main body on a fleshy stalk, scanning the surrounding objects and area with an unnervingly human measure of intelligence.[end if]";
	unless the item described is player-brood:
		do nothing;
	otherwise:
		say "[if the bimbo of the player < 9][line break][variable custom style][one of]It keeps overpowering me like it's nothing. I have to get out of here.[or]Those tentacles are enormous! I'm not giving it another chance to put them in me![or]How could I let it get so big? Now I'll never get rid of it.[or]It keeps trying to breed with me. I'm your mother you disgusting beast![in random order][otherwise if the bimbo of the player < 14][variable custom style][one of][big he of the item described]'s so strong now. There's no way I can fight him, let alone keep him from fucking me.[or][big he of the item described]'s all grown-up now. What's this wistful feeling?[or]I'm so proud. I wish [he of the item described] were more obedient, but still...[or][big his of the item described] tentacles are so big. I don't think he'll grow much more than that.[in random order][otherwise][line break][second custom style][one of]Look who grew up all big and hung![or]I want to make another.[or]They grow up so fast. And big too, teehee![or][big he of the item described] knows just how to please [his of the item described] mommy.[or]Those [manly-penis]s are so big! [big he of the item described] gets it from [his of the item described] daddy.[or]I wonder when [he of the item described]'ll be ready to breed me again?[in random order][end if][roman type][line break]".

Tentacle Monster ends here.
