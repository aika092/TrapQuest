Orc by Monster begins here.

orc is a monster. orc is male. orc is intelligent. The leftover-type of orc is 148.
orc can be shooed. [has he been made to leave at least once?]

Definition: orc is dungeon dwelling:
	if pack of playing cards is in Dungeon07, decide yes;
	decide no.

[Definition: orc is willing to urinate: decide yes.]

Definition: orc is dark skinned: decide yes.
Definition: orc is condom obsessed: decide yes. [Does he always want to use a condom?]
Definition: orc is able to see stolen goods: decide no. [He doesn't attack, so he can't get back stolen items]

Definition: orc is willing to do licking: decide yes.
Definition: orc is certain to do anilingus:
	if it is not a balls-haver, decide yes;
	decide no.


[Definition: orc is raunchy: decide yes.]

The printed name of orc is "[if item described is in the location of the player][TQlink of item described][end if][input-style][MediumDesc of item described][shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]". The text-shortcut of orc is "orc". Understand "hairy" as orc.

Figure of orc is the file "NPCs/Dungeon/Orc/orc1.jpg".
Figure of orc lapdance female is the file "NPCs/Dungeon/Orc/cutscene-orc1.jpg".
Figure of orc lapdance male is the file "NPCs/Dungeon/Orc/cutscene-orc2.jpg".

To say ShortDesc of (M - orc):
	say "orc".

To say MediumDesc of (M - orc):
	say "hairy orc".

To decide which figure-name is the monster-image of (M - orc):
	if lady fetish is 1, decide on Figure of female orc;
	decide on figure of Orc.

To say MonsterDesc of (M - orc):
	say "This chubby, hairy [man of M] is visibly half-pig, and half human. Presumably in pig-man-culture, it's normal to be completely stark naked, exposing your massive [if lady fetish is 1]breasts and[end if][DickDesc of M] to the world.".

[To say MonsterComment of (M - a orc):
	if lady fetish is 0, say "[if interracial fetish is 1 and the bimbo of the player < 4][line break][first custom style]Ugh, that [manly-penis] is so big, there's nowhere else I can look![otherwise if interracial fetish is 1 and the bimbo of the player < 7][first custom style]Why can't I look away from [his of M] massive black [manly-penis]?[otherwise if interracial fetish is 1 and the bimbo of the player < 11][line break][variable custom style]Wow, it's just so big, and... majestic...[otherwise if interracial fetish is 1 and the bimbo of the player < 14][variable custom style]I'm getting weak at the knees just looking at that majestic monster...[otherwise if interracial fetish is 1][line break][second custom style]I hope [he of M]'ll let me worship [his of M] godly [manly-penis].[otherwise if the player is gendered female and the bimbo of the player < 6][first custom style][one of]Ugh. Put your [manly-penis] away, sicko.[or]Oh I get it, [he of M] doesn't trust women so [he of M] has to watch me all the time. Misogynist.[or]This pig isn't even wearing clothes. Who does [he of M] think [he of M] is?[or][big he of M] looks so sure of [himself of M]. Typical. Men are so obsessed with their penises.[or]A [man of M] in charge of the shop. How original.[in random order][otherwise if the bimbo of the player < 6][first custom style][one of]I probably shouldn't steal anything. [big he of M]'d definitely notice.[or]That dude is cut! Maybe I should get [him of M] to give me some workout tips.[or]This guy looks like [he of M] knows [his of M] stuff. Wish [he of M]'d put some pants on, but still.[in random order][otherwise if the bimbo of the player < 10][variable custom style][one of]I can't stop staring at [his of M] penis.[or][big he of M] looks strong. I wonder what it would feel like if [he of M] let me touch [his of M] muscles.[or]I shouldn't steal anything. Who knows how [he of M] might punish me.[or]I don't think I could outrun [him of M] if I stole something. [big his of M] body is just, wow.[in random order][otherwise][second custom style][one of][big he of M]'s really sexy![or]What a yummy looking penis![or]I want to touch [his of M] muscles.[or]What a HUNK![or]I can't stop staring at [his of M] penis! It looks so yummy![or][big he of M]'s YUMMY[in random order][end if][roman type][line break]";
	otherwise say "[if interracial fetish is 1 and the bimbo of the player < 4][line break][first custom style]Ugh, that [DickDesc of M] is so big, there's nowhere else I can look![otherwise if interracial fetish is 1 and the bimbo of the player < 7][first custom style]Why can't I look away from [his of M] massive black [DickDesc of M]?[otherwise if interracial fetish is 1 and the bimbo of the player < 11][line break][variable custom style]Wow, it's just so big, and... majestic...[otherwise if interracial fetish is 1 and the bimbo of the player < 14][variable custom style]I'm getting weak at the knees just looking at that majestic snake...[otherwise if interracial fetish is 1][line break][second custom style]I hope [he of M]'ll let me worship [his of M] godly [manly-penis].[otherwise if the player is gendered female and the bimbo of the player < 6][first custom style][one of]Ugh. Put your [manly-penis] away, sicko.[or]Oh I get it, [he of M] doesn't trust women so [he of M] has to watch me all the time. Misogynist.[or]This harlot isn't even wearing clothes. Who does [he of M] think [he of M] is?[or][big he of M] looks so sure of [himself of M]. Typical. [big he of M] should know that [DickDesc of M] doesn't mean anything to anyone.[or]A [man of M] in charge of the shop. I like it, but I wish [he of M] wasn't naked.[in random order][otherwise if the bimbo of the player < 6][first custom style][one of]I probably shouldn't steal anything. [big he of M]'d definitely notice.[or]That bitch is cut! Maybe I should get [him of M] to give me some workout tips. After [he of M] puts some underwear on, of course.[or]This [man of M] looks like [he of M] knows [his of M] stuff. Wish [he of M]'d put some pants on, but still.[in random order][otherwise if the bimbo of the player < 10][variable custom style][one of]I can't stop staring at [his of M] [DickDesc of M].[or][big he of M] looks strong. I wonder what it would feel like if [he of M] let me touch [his of M] muscles?[or]I shouldn't steal anything. Who knows how [he of M] might punish me?[or]I don't think I could outrun [him of M] if I stole something. [big his of M] body is just, wow.[in random order][otherwise][second custom style][one of][big he of M]'s really sexy![or]What a yummy looking [DickDesc of M]![or]I want to touch [his of M] muscles.[or]What a HUNK![or]I can't stop staring at [his of M] [DickDesc of M]! It looks so yummy![in random order][end if][roman type][line break]".]

To say LongDickDesc of (M - orc):
	if full-lady fetish is 1:
		say "huge strap-on";
	otherwise:
		say "huge [manly-penis]".

To set up (M - orc):
	reset M;
	now the monstersetup of M is 1;
	now the raw difficulty of M is the starting difficulty of M;
	now M is guarding;
	now M is in Dungeon07;
	now the health of M is the maxhealth of M;
	let N be 3 - the number of accessories held by M;
	if N > 0:
		repeat with X running from 1 to N:
			let A be a random off-stage plentiful accessory;
			if A is accessory:
				if X is 1, now A is emerald;
				if X is 2, now A is ruby;
				if X is 3, now A is pink diamond;
				set shortcut of A;
				now M is carrying A.

To decide which number is the starting difficulty of (M - orc):
	decide on 13.

This is the spawn initial orc rule:
	if debugmode > 1, say "Now summoning orc.";
	if orc is alive, now orc is in Dungeon07.
The spawn initial orc rule is listed in the setting up dungeon monsters rules.

To decide which number is the girth of (M - orc):
	decide on 6.
To decide which number is the grossness of (M - orc): [grossness addiction needed to tolerate it]
	decide on 5.

Definition: orc (called M) is willing to shag:
	[if M is awake and the scared of M is 0 and the boredom of M < 120 and M is threatening, decide yes;]
	decide no.

To say PregGrowth of (M - orc):
	say "the orc's [child]".

To compute labour to (M - orc):
	if M is regional and M is alive:
		compute pregnancy clothing displacement;
		say "[PregFlav][if the father is in the location of the player]The [father][otherwise]The [father] appears almost as if on cue! [big he of M][end if] gets down on one knee and [if mythical creatures fetish is 1]you both watch as a disgusting pig's head begins to emerge from your vagina. This baby is just as much an orc as its father! You groan in despair as the half-pig baby slowly emerges from your womb[otherwise]delivers the beautiful mixed race human baby[end if]. [big he of M] takes it into [his of M] arms and begins to cradle it.[line break][first custom style]'A baby boy! [if the father is mating]A wonderful addition to our growing horde[otherwise]My first child. I will train him in the ways of orc culture, and when he comes of age, perhaps he will be a great enough leader to command an assault on a human settlement and kidnap all their women. We can dream[end if].'[roman type][line break]";
		say "Without giving you a chance to react, never mind reply, [he of M] leaves you to recover from your ordeal.";
		if the father is in the location of the player:
			say "For some reason, you are filled with a sense of deep fulfilment. You feel great!";
			StrengthUp 1;
			DexUp 1;
			IntUp 1;
	otherwise if M is alive:
		Delay Labour.

Definition: orc is human: decide yes.

Definition: orc is dismissable: decide no.

To say LeftoverDesc (N - 148):
	say "The dead crumpled corpse of the orc is on the ground here.[line break]".

Part 1 - Misc Flavour

To say PerceptionFail of (M - orc):
	say "[BigNameDesc of M] fails to notice you sneaking around the other side of the poker table.";
	now the boredom of M is 30;
	now M is not interested. [This won't happen if we don't put it here because the orc has a unique boredom function that keeps him interested.]

To say BecomesBoredFlav of (M - orc):
	say "[BigNameDesc of M] seems to lose interest in you for now.".

To say DiaperReaction of (M - orc):
	if (there is a currently visible diaper or the player is shameless) and M is interested, say "[BigNameDesc of M] frowns as [he of M] watches you. It's very clear that [he of M] can tell what is happening.[line break][variable custom style][if the player is modest]Why did this have to happen to me?![otherwise if the player is not shameless]If [he of M] was kind, [he of M] would look away![otherwise]Yep that's right Sir, I'm going right now in my nappy![end if][roman type][line break]";
	otherwise say "[BigNameDesc of M] doesn't look at you directly or say anything, so it's hard to gauge [if the player is not shameless]if [he of M] saw[otherwise][his of M] reaction[end if].";
	if diaper quest is 1 and M is not playing-poker-badly:
		say "[speech style of M]'Your humiliation... It's just scrumptious. Yes... This moment will be imprinted on my mind for a decent while, I should think!";
		now M is playing-poker-badly.

To compute diaper mess reaction of (M - orc):
	say "[BigNameDesc of M] snorts.[line break][speech style of M]'To think... You would really do that in front of me! You must have absolutely no self-respect!'[roman type][line break][big he of M] can't stop [himself of M] from laughing. [strongHumiliateReflect]";
	if diaper quest is 1 and M is not playing-poker-badly:
		say "[speech style of M]'Your humiliation... It's just scrumptious. Yes... This moment will be imprinted on my mind for a decent while, I should think!";
		now M is playing-poker-badly.

To compute (M - a monster) stomping (N - orc):
	say "[speech style of M]'Oh shit, I'm getting out of here!'[roman type][line break]";
	if M is in the location of the player, say "[BigNameDesc of N] dodges a swipe from [NameDesc of M] and runs for the exit.";
	otherwise say "You hear an orc's shout from [if N is nearby]nearby![otherwise]somewhere in the dungeon![end if]";
	finally destroy N.

To compute proposal leaving of (M - orc):
	do nothing.

To compute seduction choice of (M - orc):
	compute default seduction choice of M;
	if M is seduced:
		if the player is possessing a penis and the largeness of breasts < 3, cutshow figure of orc lapdance male for M;
		otherwise cutshow figure of orc lapdance female for M.

To compute stealing fail reaction of (T - a thing) from (M - orc):
	say "[BigNameDesc of M] grabs a random set of 5 cards and throws them down on the table![line break][speech style of M]'My table and I are going to make you regret that.'[roman type][line break]The cards start to glow!";
	let deckOfCards be a list of numbers;
	repeat with PC running from 0 to 31:
		unless PC is listed in stolenCards, add PC to deckOfCards;
	sort deckOfCards in random order;
	truncate deckOfCards to 5 entries;
	truncate numbersExplained to 0 entries;
	repeat with PC running through deckOfCards:
		compute poker punishment of PC.

To compute partial stealing reaction of (T - a thing) from (M - orc):
	compute stealing fail reaction of T from M.

Part 2 - Perception

[The orc doesn't fight. He just trolls and leaves.]
To anger (M - orc):
	now current-monster is M;
	follow the orc trolls then leaves rule.

To decide which number is the bimbo tolerance of (M - orc):
	decide on 21. [never becomes aggressive]
To decide which number is the bab tolerance of (M - orc):
	decide on 21. [never becomes aggressive]

To say DisapprovalFlav of (M - orc):
	say "You can literally see [NameDesc of M][']s [manly-penis] getting [one of]hard[or]harder and harder[stopping].[line break][speech style of M]'[one of]Ooh baby, you are looking fi-i-ine!'[or]Oh yeah, you're just begging for it, aren't you?'[in random order][roman type][line break]".

To say BecomesAggressive of (M - orc):
	say "[big he of M] strokes [his of M] [manly-penis] as [he of M] watches you. [moderateHumiliateReflect]";
	calm M.

Report going:
	if orc is guarding and orc is on-stage and orc is not in Dungeon07 and orc is not in the location of the player, bore orc.

To compute perception of (M - orc):
	say "[BigNameDesc of M] notices you[if the player is sluttily dressed], grinning at how you're dressed[one of] even though [he of M] is completely naked [himself of M][or][stopping].[otherwise]![end if]";
	if the current-errand of M is completed:
		compute errand completion of M;
	otherwise:
		say "[speech style of M]'Pleasant tidings, [if M is bride-consort]my blushing bride[otherwise]young one[end if][one of].'[roman type][line break][big he of M] speaks with a very posh and dignified accent, completely at odds with [his of M] rough exterior.[line break][speech style of M]'[or]. [stopping][if the poker-timer of M > 0]The table is still recharging, so come back some time soon[otherwise]Perhaps you'd like to play a game of poker with me? Just let me know[end if].'[roman type][line break]";
		if the player is not able to speak, say "[speech style of M]'Oh, you can't speak right now, can you. Well in that case, perhaps just poke me to let me know you want to play.'[roman type][line break]";
		calm M;
		if newbie tips is 1, say "[one of][newbie style]Newbie tip: The orc won't fight you. [big he of M] just wants you to play at the magic poker table.[roman type][line break][or][stopping]".

To compute friendly boredom of (M - orc):
	if the player is not in the location of M, now M is uninterested.

Part 3 - Motion and Seeking

To compute monstermotion of (M - orc):
	if M is not in Dungeon07: [If the orc is not interested in chasing the player, he just returns to the Poker Room.]
		if M is in the location of the player, say "[BigNameDesc of M] walks off to the [the best route from location to Dungeon07], presumably to get back to the poker table.";
		now M is in Dungeon07.

To compute (M - orc) seeking (D - a direction):
	if M is unleashed, blockable move M to D;
	compute monstermotion reactions of M. [The orc moves with excellent purpose when seeking, but also doesn't follow the player around if friendly.]


Part 4 - Combat

The latex punishment rule of orc is usually the no latex punishment rule. [Orc can fuck sex dolls]

To orgasm satisfy (M - orc):
	orgasm M;
	if M is in the location of the player:
		if M is friendly-fucking or M is seduced:
			now M is playing-poker-badly;
			say "[speech style of M]'Oh yes, oh yes, that was exactly what [one of]I had in mind[or]I needed[stopping]. Indeed... I can feel that my poker playing expertise is somewhat suppressed, at least for a little while...'[roman type][line break]";
	satisfy M.

To satisfy (M - orc) for (N - a number) seconds:
	if M is interested:
		if the player is not in Dungeon07, bore M for N seconds;
		FavourUp M;
		if M is in the location of the player and M is awake:
			say SatisfiedFlav of M;
			progress quest of nice-quest;
	otherwise:
		bore M for N seconds;
	compute unlock satisfaction of M;
	send M home. [This makes sure the orc always makes it back to the poker room.]

To say SatisfiedFlav of (M - orc):
	if M is in the location of the player and M is not dying and (M is not in Dungeon07 or the player is not in Dungeon07):
		let U be 0;
		if M is intelligent and M is unfriendly, now U is 1; [This check can cause a line break]
		say "[BigNameDesc of M] seems [if U is 1]satisfied, and loses[otherwise]to lose[end if] interest.".

To bore (M - orc) for (N - a number) seconds:
	compute common boredom of M for N seconds;
	if the player is in Dungeon07 and M is in Dungeon07:
		calm M; [The orc never gets distracted from you while you're there]
	otherwise:
		now M is uninterested;
		send M home.

To say SwallowDemand of (M - orc):
	say "[speech style of M]'It would be unbecoming of a young lass such as yourself to do anything except swallow obediently.'[roman type][line break]".

To say angry punishment insult of (M - orc):
	if M is friendly-fucking, say "[speech style of M]Heh, you'll get over yourself soon enough.'[roman type][line break]";
	otherwise say "[speech style of M][one of]'Are you serious? Come here so I can punish you!'[or]'You little bitch! Get ready for punishment!'[or]'If you're gonna be so ungrateful, I'm gonna have to teach you a lesson!'[or]'You little slut! How dare you!?'[or]'Oh you're gonna regret that you little whore!'[or]'I guess I'm gonna have to punish you after all!'[in random order][roman type][line break]".


To say LandingTaunt of (M - orc):
	say "[BigNameDesc of M] raises [his of M] eyebrows.[line break][speech style of M]'Nice of you to finally join me down here.'[roman type][line break]";
	moderateHumiliate.

To send (M - orc) home:
	if M is alive:
		if M is not in Dungeon07:
			if M is in the location of the player:
				let D be the best route from the location of M to Dungeon07 through labyrinth rooms;
				if D is a direction, say "[BigNameDesc of M] leaves to the [D].";
			now M is in Dungeon07;
		now M is guarding;
		if M is interested, bore M.

To compute default waiting of (M - orc):
	say "[one of][BigNameDesc of M] waits patiently.[or][stopping]".

The monster fucked taunting rule of orc is usually the orc cockslaps the player rule.
This is the orc cockslaps the player rule:
	let M be current-monster;
	if bukkake fetish is 1 and the semen coating of face < 8:
		say "[BigNameDesc of M] takes this opportunity to [one of]smack[or]tap[or]hit[at random] your face with [his of M] [LongDickDesc of M]. After slapping each cheek a couple of times, [he of M] groans, pumping [his of M] shaft as [he of M] fires a rope of [semen] across your face.";
		CumFaceUp 1;
	otherwise:
		say "[BigNameDesc of M] [one of]slaps[or]toys[or]smacks[or]taps[at random] with your face with [his of M] [DickDesc of M], waiting for [his of M] turn.";
	rule succeeds.

The orc priority attack rules is a rulebook. The priority attack rules of orc is the orc priority attack rules.
This is the orc trolls then leaves rule:
	say "[speech style of orc]'How uncouth.'[roman type][line break][BigNameDesc of orc] calmly tosses a smoke bomb onto the ground. The entire place fills with smoke - pink smoke!";
	now the location of orc is pink-smoky;
	update backdrop positions;
	compute pink smoke;
	say "[line break]By the time the smoke has settled at the bottom half of the room, [NameDesc of orc] has gone!";
	finally destroy orc;
	now orc is shooed; [won't come back if you take the playing cards]
	rule succeeds.
The orc trolls then leaves rule is listed in the orc priority attack rules.

To say LickResisting of (M - orc):
	say BallResisting of M.

To say LickResistingResponse of (M - orc):
	say BallResistingresponse of M.

To say LickSubmissionResponse of (M - orc):
	say BallSubmissionResponse of M.

To say LickInitiationFlav of (M - orc):
	say BallInitiationFlav of M.

To say LickEndFlav of (M - orc):
	say BallEndFlav of M.

Section 3 - Damage

To compute damage reaction of (M - orc):
	if M is awake:
		if M is friendly:
			say "[speech style of M]'WHY?!'[roman type][line break]";
		otherwise:
			say DamageReaction (the health of M) of M;
	otherwise:
		now the sleep of M is 0;
		say "[big he of M] wakes up with a groan of frustration.".

To say DamageReactHealthy of (M - orc):
	say "[BigNameDesc of M] endures the attack with a soft grunt.";

To say DamageReactDamaged of (M - orc):
	say DamageReactHealthy of M.

To say DamageReactTired of (M - orc):
	say DamageReactHealthy of M.

To say DamageReactWeak of (M - orc):
	say "[BigNameDesc of M] recoils in pain!".

To loot (M - orc):
	while the loot dropped of M < 4:
		standard loot M.

To compute (M - orc) slinking away:
	if M is in Dungeon07:
		bore M;
	otherwise:
		say SlinkAwayFlav of M;
		bore M;
		now M is in Dungeon07.

To MonsterHeal (M - orc) by (N - a number):
	if the health of M < the maxhealth of M:
		increase the health of M by N;
		if the health of M >= the maxhealth of M:
			now the health of M is the maxhealth of M;
			send M home.



Part 4 - Conversation

Section 1 - Greetings

[The orc uses the default greeting and response functions]

To say FirstResponse of (M - orc):
	if M is not in Dungeon07:
		say "[speech style of M]'I wish to retire to my faithful old chair.'[roman type][line break]";
	otherwise:
		say "[speech style of M]'Good day to you [if the player is presenting as female]ma[']am[otherwise]sir[end if].'[roman type][line break]".

To say InvitationRejected of (M - orc) with (N - a monster):
	say "[speech style of M]'Regrettably, I do not feel the drive to engage in such debauchery right now.'[roman type][line break]".

To say AssistanceRejected of (M - orc) with (N - a monster):
	say "[speech style of M]'It would not do to intervene in such matters.'[roman type][line break]".


Section 2 - Questions


Definition: orc is fluff-question-appropriate: decide no.

talk-orc-question is a talk-object. orc has a number called question-sequence.

To consider (T - talk-orc-question) for (M - a monster):
	if M is orc and pack of playing cards is in Dungeon07 and pack of playing cards is not held:
		now the printed name of T is the substituted form of "[variable custom style]'[if the question-sequence of M is 0]What's your deal, then[otherwise if the question-sequence of M is 1]I'm guessing there's some kind of catch[otherwise if the question-sequence of M is 2]A magic poker table[otherwise if the question-sequence of M is 3]Wait, so if I lose, this magic table will transform me in some way[otherwise]What do you get out of these poker games[end if]?'[roman type][line break]";
		set next numerical response to the substituted form of "[printed name of T]";

To execute (T - talk-orc-question) for (M - a monster):
	say "[speech style of M]'[if the question-sequence of M is 0]My [']deal['], young grasshopper, is that I desire to engage in a simple game of cards! Care to join me? If you win, you shall be rewarded with a prize of rarity and magnificence that correlates to the strength of your hand!'[otherwise if the question-sequence of M is 1]Ohohoh, your feelings serve you well! For you see, this is a magic poker table!'[otherwise if the question-sequence of M is 2]Verily! Whenever a player on the far side of the table loses a game, the table administers a set of... punishments. For each card in their hand, a unique forfeit is applied. How riveting!'[otherwise if the question-sequence of M is 3]Indeed, in five ways! One for each card in your hand! Indeed, great reward does not come without great risks!'[otherwise]The thrill! The excitement! The intrigue! The high stakes! Is there anything more worth living for?!'[end if][roman type][line break]".

talk-orc-poker is a talk-object. orc has a number called poker-timer.
To compute unique unsimulated periodic effect of (M - orc):
	decrease the poker-timer of M by 1.

To consider (T - talk-orc-poker) for (M - a monster):
	if M is orc and M is in Dungeon07:
		now the printed name of T is the substituted form of "[variable custom style]'Let's play a hand of poker then.'[roman type][line break]";
		set next numerical response to the substituted form of "[printed name of T]".

To execute (T - talk-orc-poker) for (M - a monster):
	if pack of playing cards is not in Dungeon07 or pack of playing cards is held:
		say "[speech style of M]'Regrettably, some scoundrel has made off with my playing cards!'[roman type][line break]";
	otherwise if the poker-timer of M > 0:
		say "[speech style of M]'Ah, we still need to wait until the magic of the table recharges. No point playing if there's nothing at stake!'[roman type][line break]";
	otherwise:
		[say "[speech style of M]'Certainly! But first, Aika wants me to tell you that there's currently some rare but devastating bug with the poker minigame which infrequently causes the game to completely lock up after the poker game ends, and prevents you from continuing[if save game limit > 0]. Even worse, with roguelike saving enabled, the game manages to save itself in this state, completely ending your run[end if].'[roman type][line break]";
		reset multiple choice questions;
		set numerical response 1 to "[if save game limit > 0]I'm willing to[otherwise]I've saved the game and so am ready to[end if] risk that.";
		set numerical response 2 to "Oh dear - let me cancel that and [if save game limit > 0]avoid the poker game until this bug is fixed[otherwise]save the game before playing poker with you[end if].";
		if save game limit > 0 and the player is the donator, set numerical response 3 to "Oh dear - let me cheat and [bold type]disable roguelike saving mode[roman type] so I can save the game before playing poker with you.";
		compute multiple choice question;
		if player-numerical-response is 1:
			say "[speech style of M]'I shall deal.'[roman type][line break]";
			compute poker minigame;
		otherwise if player-numerical-response is 3:
			now choice in row 36 of Table of Settings is 0;
			say "Roguelike saving disabled for the rest of this run. Please note that this has not changed your saved preferences; when you start your next fresh game it will still be enabled by default.[line break][bold type]Please now save your game.[roman type][line break]".]
		say "[speech style of M]'Certainly, let's play a hand! I shall deal.'[roman type][line break]";
		compute poker minigame;


talk-orc-easy is a talk-object.

To consider (T - talk-orc-easy) for (M - a monster):
	if M is orc and pack of playing cards is in Dungeon07 and pack of playing cards is not held:
		now the printed name of T is the substituted form of "[variable custom style]'Can I convince you to go easy on me?'[roman type][line break]";
		set next numerical response to the substituted form of "[printed name of T]".

To execute (T - talk-orc-easy) for (M - a monster):
	if diaper quest is 0, say "[speech style of M]'[if orc is playing-poker-badly]Yes, I think our recent, *ahem*, [']enjoyment['] might convince my fingers to slip once or twice...[otherwise]Hmm, perhaps if we were to play a different sort of game! One which included a rather... [']joyous['] ending for yours truly... That might cause my concentration in our next round of poker to wander...'[roman type][line break]";
	otherwise say "[speech style of M]'[if orc is playing-poker-badly]Yes, I think my fond memories of your recent, *ahem*, [']episode['] might convince my fingers to slip once or twice...[otherwise]Hmm, perhaps if I were to get a little show... if I were to witness, say, a diapered lass such as yourself, soiling [his of the player] breaches in my presence... That might cause my concentration in our next round of poker to wander...'[roman type][line break]".

To compute teaching of (M - orc):
	say "[speech style of M]'You know, sometimes you can tell if an item is cursed from some tell-tale hints. Let me tell you a few...'[roman type][line break]";
	teach identifying.

Section 3 - Drink Requesting

To compute friendly drink of (M - orc):
	compute unfriendly drink of M.

To compute unfriendly drink of (M - orc):
	say "The orc ignores your question.".


Part 5 - Seduction

To compute FirstTurnSeduceGrindAnnounce of (M - orc):
	if the player is able to speak:
		say "[variable custom style]'Since you're sat down, I imagine you'll enjoy something like this?'[roman type][line break]".

To compute SeductionGrind of (M - orc):
	increase the seductions-performed of hips by 1;
	increase the gropability of hips by 9999999;
	if the sex-length of M > 1:
		say "You [one of]straddle [his of M] lap, facing away from [him of M], and [or][stopping]grind your [AssDesc] into [his of M] [DickDesc of M]. [slightHumiliateReflect]";
		compute StrongSexProgress of M;
	otherwise:
		say "You allow [NameDesc of M] to grip you tightly and thrust [his of M] [DickDesc of M] up and down in between your [AssDesc][if the player is a pervert or the player is feeling submissive], and even push back to provide [him of M] with extra sensation[end if].[line break][speech style of M]'Yes, yes, YES!'[roman type][line break][BigNameDesc of M] cums hard, while [his of M] [LongDickDesc of M] is still pumping in and out of your crack!";
		if stripper-ears is off-stage and stripper-ears is actually summonable:
			say "Suddenly, two glittery feline ears appear in your hair!";
			summon stripper-ears cursed;
			say "[variable custom style]The game is telling me I'm a professional stripper now?![roman type][line break]";
		otherwise if rugged-headband is worn and police-hat is off-stage:
			transform rugged-headband into police-hat;
			compute class outfit of police-hat;
			let C be a random worn overdress;
			if C is leopard themed clothing, unclash class summon police uniform;
			say "[variable custom style]The game is telling me I'm a professional stripper now?![roman type][line break]";
		otherwise if there is a worn maid headdress:
			let H be a random worn headgear;
			unless H is stripper maid headdress:
				transform H into stripper maid headdress;
				set up stripper maid headdress;
				compute class outfit of stripper maid headdress;
				say "[variable custom style]The game is telling me I'm a professional stripper now?![roman type][line break]";
		otherwise if cumdump-headband is worn and stripper-ears is off-stage:
			if bitch top is worn, WardrobeVanish bitch top;
			class summon porn star tube top;
			if bitch bottoms is worn, WardrobeVanish bitch top;
			class summon black-microshorts;
			class summon porn-star-suspenders;
			transform cumdump-headband into stripper-ears;
			say "[variable custom style]The game is telling me I'm a professional sex worker now?![roman type][line break]";
		if M is wrapped:
			say "The [semen] fills [his of M] condom to the brim.";
			compute default condom filling of M;
		otherwise:
			let C be the concealer of hips;
			if C is nothing, now C is the at least partial concealer of hips;
			if C is nothing, now C is a random top level ass protection clothing;
			if C is nothing, now C is a random worn skirted clothing;
			if C is semen-soak-appropriate clothing:
				UnannouncedSquirt semen on C by (the semen load of M);
			otherwise if bukkake fetish is 1:
				UnannouncedSquirt semen on hips by (the semen load of M);
			otherwise:
				UnannouncedSquirt semen on thighs by (the semen load of M);
		orgasm satisfy M;
		now the sex-length of M is 0; [otherwise it would be left at 1, meaning it wouldn't get reset the next time seduction happened, and he would cum immediately]
		progress quest of greet-quest.

To say AssGropeFlav of (M - orc):
	say "[speech style of M]'[one of]Oh my goodness, with such a glorious sight right in front of me, I can't help but indulge myself!'[or]Oh yes, now THIS is what the doctor ordered!'[or]Outstanding form.'[or]Yes, just like that!'[or]What a heavenly posterior!'[or]Absolutely divine!'[then at random][roman type][line break]".
To compute ass grope of (M - orc):
	if the sex-length of M >= 3:
		say "[BigNameDesc of orc] [one of]spanks[or]slap[or]swats[in random order] you on the [AssDesc]!";
		PainUp 10;
	if the sex-length of M is 2:
		say "[BigNameDesc of orc] grabs your [AssDesc] with both hands, and kneads it like it's made out of silly putty! [moderateHumiliateReflect]";
	otherwise if the seductions-performed of hips > 0: [should be the turn before climax]
		say "[BigNameDesc of orc] grabs you by the hips and pulls you in tighter, really shoving [his of M] [DickDesc of M] into your crack! This is now less of a dance and more full-on frottage!";
		passively stimulate asshole from M.


Part 6 - Poker

[
deckOfCards holds the 32 cards used to play, represented by the numbers 0 to 31. (2 to 6 of all suits are removed).
You calculate which number refers to which value card as follows:
Divide the ID by 4.
Whole number portion + 7 represents the card value (with Ace being 14)
Remainder portion represents the suit:
0 - Spades
1 - Clubs
2 - Diamonds
3 - Hearts
]

poker hand name is a text that varies. [name of hand]

orc can be playing-poker-badly.

pokerCardList is a list of figure names that varies.

[Create an easily accessible list of the 32 card images]
When play begins:
	add Figure of PokerCard00 to pokerCardList;
	let X be the Glulx Resource ID of Figure of PokerCard00;
	repeat with FX running from 1 to 31:
		increase X by 1;
		add the resource-image of X to pokerCardList.

playerHand is a list of numbers that varies.
orcHand is a list of numbers that varies.
cardsToBurn is a list of numbers that varies.
stolenCards is a list of numbers that varies.
numbersExplained is a list of numbers that varies.

To compute poker minigame:
	now temporaryYesNoBackground is Figure of PokerCardDraw;
	now temporaryYesNoResetNeeded is false;
	now the poker-timer of orc is 100;
	say "[one of][speech style of orc]'We're playing 32 card poker - that means the cards from 2 to 6 have been removed. Full houses are easier to get than normal, so they're now worth less than flushes, and your Ace can count as a 6 for the purposes of making a straight.'[roman type][line break][or][stopping]";
	let deckOfCards be a list of numbers;
	truncate playerHand to 0 entries;
	truncate orcHand to 0 entries;
	repeat with PC running from 0 to 31:
		unless PC is listed in stolenCards, add PC to deckOfCards;
	sort deckOfCards in random order;
	let suitedJoy be false;
	if the player is getting lucky, now suitedJoy is true;
	let firstSuit be -1;
	repeat with N running from 1 to 10:
		let PC be entry 1 in deckOfCards;
		if the number of entries in playerHand < 5:
			if the number of entries in playerHand is 0:
				now firstSuit is the remainder after dividing PC by 4;
				add PC to playerHand;
			otherwise if suitedJoy is true and the remainder after dividing PC by 4 is not firstSuit and the number of entries in orcHand < 5: [if the player is getting a lucky suited hand, we redirect all cards we can that aren't of the desired suit to the opponent's hand]
				add PC to orcHand;
			otherwise if suitedJoy is true and the remainder after dividing PC by 4 is not firstSuit: [if the player is getting a lucky suited hand, if the card the player is about to draw is of the wrong suit, we shuffle the deck (once only per card draw)]
				sort deckOfCards in random order;
				now PC is entry 1 in deckOfCards;
				add PC to playerHand;
			otherwise:
				add PC to playerHand;
		otherwise:
			add PC to orcHand;
		remove PC from deckOfCards;
	say line break;
	sort orcHand;
	orc hand print orcHand;
	say line break;
	repeat with pokerTurn running from 1 to 2:
		sort playerHand;
		sort orcHand;
		say line break;
		player hand print playerHand;
		say line break;
		if pokerTurn is 1 and suitedJoy is true:
			let suitedCards be 0;
			repeat with PC running through playerHand:
				if firstSuit is the remainder after dividing PC by 4, increase suitedCards by 1;
			if suitedCards > 2:
				say "Wow, you have [suitedCards] [poker card suit of raw firstSuit] cards! Something tell you that you're having a [bold type]lucky hand![line break]You sense that this might mean that you will have WORSE chances of matching cards in your hand, but BETTER chances of making a flush (all cards of the same suit).[roman type][paragraph break]";
		let cardsToKeep be a list of numbers;
		truncate cardsToBurn to 0 entries;
		let E be 1;
		if pokerTurn is 2 and orc is playing-poker-badly:
			let orcStrength be the poker hand strength of orcHand;
			if orcStrength >= 900000:
				say "[BigNameDesc of orc] visibly considers dropping [his of orc] entire hand into the discard pile, but can't bring [himself of orc] to do it.[line break][speech style of orc]'My apologies, I wanted to help you, I did, but I simply can't bring myself to discard such a wonderful hand!'[roman type][line break][big he of orc] holds onto the entire hand.";
				now orc is not playing-poker-badly;
			otherwise:
				say "[speech style of orc]'Oops!'[roman type][line break][BigNameDesc of orc] drops [his of orc] entire hand into the discard pile.[line break][speech style of orc]'My hand slipped!'[roman type][line break][big he of orc] winks at you.";
				truncate orcHand to 0 entries;
		otherwise:
			repeat with PC running through orcHand:
				let PV be PC / 4;
				let HE be 1;
				repeat with HC running through orcHand:
					let HV be HC / 4;
					if E is not HE and HV is PV: [we've found another card in the hand of the same value]
						add E to cardsToKeep, if absent;
					increase HE by 1;
				increase E by 1;
			if the number of entries in cardsToKeep is 0:
				add 5 to cardsToKeep; [highest value card]
				let orcStrength be the poker hand strength of orcHand;
				if orcStrength >= 900000: [keep straights and flushes]
					repeat with PCE running from 1 to 4:
						add PCE to cardsToKeep;
			repeat with PCE running from 1 to 5:
				if PCE is not listed in cardsToKeep, add PCE to cardsToBurn;
			if the number of entries in cardsToBurn is 0:
				say "[BigNameDesc of orc] keeps [his of orc] entire hand.";
			otherwise:
				sort cardsToBurn;
				say "[BigNameDesc of orc] discards ";
				now E is the number of entries in cardsToBurn;
				sort cardsToBurn in reverse order; [if we removed them in any other order, the number of entries would change and so the pointers would go out of whack]
				repeat with PCE running through cardsToBurn:
					let PC be entry PCE in orcHand;
					say "the [poker card of PC][if E > 2], [otherwise if E is 2] and [otherwise].[end if]";
					decrease E by 1;
					remove PC from orcHand;
		if the number of entries in orcHand < 5:
			say "[big he of orc] draws ";
			now E is 5 - the number of entries in orcHand;
			while E > 0:
				let PC be entry 1 in deckOfCards;
				add PC to orcHand;
				remove PC from deckOfCards;
				say "the [poker card of PC][if E > 2], [otherwise if E is 2] and [otherwise].[end if]";
				decrease E by 1;
			sort orcHand;
			say line break;
			orc hand print orcHand;
			say line break;
		trigger poker-wisp-quest;
		truncate cardsToBurn to 0 entries;
		let pickingCards be true;
		reset multiple choice questions;
		set numerical response 0 to "keep the rest";
		say "PICK WHICH CARDS TO [bold type]DISCARD[roman type]:[line break]";
		repeat with N running from 1 to 5:
			let PC be entry N in playerHand;
			set numerical response N to the substituted form of "[poker card of PC]";
		while pickingCards is true:
			clear the map zone;
			compute multiple choice question;
			if player-numerical-response is 0:
				now pickingCards is false;
			otherwise if player-numerical-response is listed in cardsToBurn:
				remove player-numerical-response from cardsToBurn;
				let PC be entry player-numerical-response in playerHand;
				set numerical response player-numerical-response to the substituted form of "[poker card of PC]";
			otherwise:
				add player-numerical-response to cardsToBurn;
				set numerical response player-numerical-response to " ";
		if the number of entries in cardsToBurn is 0:
			say "You keep your entire hand.";
		otherwise:
			sort cardsToBurn;
			say "You discard ";
			now E is the number of entries in cardsToBurn;
			sort cardsToBurn in reverse order; [if we removed them in any other order, the number of entries would change and so the pointers would go out of whack]
			repeat with PCE running through cardsToBurn:
				let PC be entry PCE in playerHand;
				say "the [poker card of PC][if E > 2], [otherwise if E is 2] and [otherwise].[end if]";
				decrease E by 1;
				remove PC from playerHand;
		if the number of entries in playerHand < 5:
			say "You draw ";
			now E is 5 - the number of entries in playerHand;
			while E > 0:
				let PC be entry 1 in deckOfCards;
				if suitedJoy is true and the remainder after dividing PC by 4 is not firstSuit: [if the player is getting a lucky suited hand, if the card the player is about to draw is of the wrong suit, we shuffle the deck (once only per card draw)]
					sort deckOfCards in random order;
					now PC is entry 1 in deckOfCards;
				add PC to playerHand;
				remove PC from deckOfCards;
				say "the [poker card of PC][if E > 2], [otherwise if E is 2] and [otherwise].[end if]";
				decrease E by 1;
	sort playerHand;
	sort orcHand;
	say line break;
	player hand print playerHand;
	say line break;
	let orcStrength be the poker hand strength of orcHand;
	say "[BigNameDesc of orc] has [bold type][poker hand name]![roman type][line break]";
	let playerStrength be the poker hand strength of playerHand;
	say "You have [bold type][poker hand name]![roman type][line break]";
	if orcStrength is playerStrength:
		say "It's a complete tie! Wow, this is a close one! In this situation, the tie is broken by the suit of the highest card in each player's hand, with the following priority ranking: Hearts, Diamonds, Clubs, Spades.";
		let playerFlushSuit be the remainder after dividing entry 5 in playerHand by 4;
		let orcFlushSuit be the remainder after dividing entry 5 in orcHand by 4;
		say "The highest suit of the highest card in your hand is [poker card suit of raw playerFlushSuit], and [NameDesc of orc]'s is [poker card suit of raw orcFlushSuit]. That means... ";
		if playerFlushSuit > orcFlushSuit, increase playerStrength by 1;
	truncate cardsToBurn to 0 entries;
	display poker interface;
	zero the link-table;
	if playerStrength > orcStrength:
		say "You win! Woohoo![line break]Press any key to continue.";
		wait for a key before continuing;
		temporaryYesNoBackgroundReset; [multiple choice questions won't work properly until we do this]
		let C be a random off-stage mass collectible;
		if playerStrength >= 30000000: [straight flush - happens in addition to normal reward]
			repeat with N running from 1 to 2:
				let J be a random off-stage plentiful bracelet;
				if J is bracelet:
					now J is solid gold;
					set shortcut of J;
					now J is in the location of the player;
			repeat with N running from 1 to 8:
				let J be a random off-stage plentiful ring;
				if J is ring:
					now J is solid gold;
					set shortcut of J;
					now J is in the location of the player;
			say "A whole hoard of solid gold jewellery appears on the table! WOW!!!";
		if playerStrength >= 20000000 and there is an off-stage plentiful necklace: [four of a kind]
			now C is a random off-stage plentiful necklace;
			now C is solid gold;
			set shortcut of C;
		otherwise if playerStrength >= 10000000 and midnight tanto is off-stage: [flush]
			now C is midnight tanto;
			blandify and reveal midnight tanto;
			now midnight tanto is blessed;
		otherwise if playerStrength >= 1000000 and queen-of-hearts heels is off-stage: [full house]
			now C is queen-of-hearts heels;
			blandify and reveal queen-of-hearts heels;
			now the raw-magic-modifier of queen-of-hearts heels is 2;
			now queen-of-hearts heels is kicking;
			now queen-of-hearts heels is luck-influencing;
		otherwise if playerStrength >= 900000 and commando outfit is off-stage: [straight]
			now C is commando outfit;
			blandify and reveal commando outfit;
			now the raw-magic-modifier of commando outfit is 3;
			now commando outfit is speed;
			now commando outfit is strength-influencing;
		otherwise if playerStrength >= 100000 and there is an off-stage combat-bomb bomb: [three of a kind]
			now C is a random off-stage combat-bomb bomb;
			now C is blessed;
			now C is sure;
		otherwise if playerStrength >= 10000 and there is an off-stage pure totem: [two pairs]
			now C is a random off-stage pure totem;
			blandify and reveal C;
		otherwise if playerStrength >= 1000 and there is an off-stage plentiful ring: [pair]
			now C is a random off-stage plentiful ring;
			if playerStrength >= 7000: [King or Ace]
				now C is pure diamond;
			otherwise if playerStrength >= 5000: [Jack or Queen]
				now C is pink diamond;
			otherwise if playerStrength >= 4000: [10]
				now C is ruby;
			otherwise if playerStrength >= 3000: [9]
				now C is emerald;
			otherwise: [7 or 8]
				now C is sapphire;
			set shortcut of C;
		now C is in the location of the player;
		say "A [MediumDesc of C] appears on the table![line break][speech style of orc]'Wow, lucky [boy of the player]!'[roman type][line break]";
		compute autotaking C;
	otherwise:
		say "[BigNameDesc of orc] wins! Uh-oh...[line break]Press any key to continue.";
		wait for a key before continuing;
		temporaryYesNoBackgroundReset; [multiple choice questions won't work properly until we do this]
		say "[line break][speech style of orc]'[one of]My condolences[or]Commiserations[in random order], [if the player is presenting as female]my lady[otherwise]young sir[end if]. [if orc is playing-poker-badly]I hope you understand, this was a complete accident. I discarded my entire hand, as you saw! [end if]Unfortunately, there is no stopping what happens next.'[roman type][paragraph break]";
		truncate numbersExplained to 0 entries;
		repeat with PC running through orcHand:
			compute poker punishment of PC;
	if orc is in the location of the player, say "[line break][speech style of orc]'Well, that's that, for now. [one of]The poker table will need time to replenish its energy, so come back later for another round[or]I eagerly await our next match[stopping].'[roman type][line break]";
	if 23 is listed in playerHand and the player is in Dungeon07 and the recipe of (alchemy key of queen-of-hearts heels) is memorised:
		say "If you wanted to, you could probably steal the queen of hearts card right now. You know from the recipe you learned that it can be used to craft a [UniqueShortDesc of queen-of-hearts heels].";
		reset multiple choice questions;
		set numerical response 1 to "Steal the queen of hearts";
		set numerical response 2 to "Don't steal - return all 5 of your cards";
		compute multiple choice question;
		if player-numerical-response is 1:
			say "You throw your hand of cards back face-down on top of the messy pile of cards in front of you... But secretly keep the queen of hearts for yourself.";
			now queen-of-hearts is carried by the player;
			now the owner of queen-of-hearts is orc;
			progress quest of stealing-quest;
			compute poker theft of queen-of-hearts;
	now orc is not playing-poker-badly.

To compute poker punishment of (PC - a number):
	say "[line break][one of][BigNameDesc of orc] points at [poker card of PC], which is softly glowing. Its punishment effect is about to happen. [BigNameDesc of orc] explains what this particular card will do.[or][stopping]";
	let PV be PC / 4;
	let PS be the remainder after dividing PC by 4;
	if PV is 0: [7]
		unless PV is listed in numbersExplained, say "[speech style of orc]'Sevens are to do with spirits and the fates.'[roman type][paragraph break]";
		if PS is 0:
			say "[speech style of orc]'The [poker card of PC] makes the fates hate you. The next several times you need a lucky break, there will be a lower chance of you getting the outcome you want.'[roman type][line break]";
			decrease the raw luck of the player by 20;
		otherwise if PS is 1 and there is a nonstalking wisp:
			say "[speech style of orc]'The [poker card of PC] summons a spirit to curse you... Unless you meet its demands.'[roman type][line break]";
			deploy a wisp;
		otherwise if PS is 3 and there is a worn tattoo and spade leaves tattoo is not worn:
			say "[speech style of orc]'The [poker card of PC] gives you a special tattoo.'[roman type][line break]";
			summon spade leaves tattoo;
			say "A new tattoo appears, inked onto your skin!";
			try examining spade leaves tattoo;
		otherwise if diaper quest is 0 and PS is 4 and queen-of-spades monokini is off-stage and queen-of-spades monokini is class summonable:
			say "[speech style of orc]'The [poker card of PC] gives you a special item of clothing.'[roman type][line break]";
			class summon queen-of-spades monokini;
			now queen-of-spades monokini is cursed;
			now queen-of-spades monokini is luck-influencing;
			now the raw-magic-modifier of queen-of-spades monokini is -4;
			compute summoned quest of queen-of-spades monokini;
		otherwise if PS is 4 and spoiled crop top is off-stage and spoiled crop top is class summonable:
			say "[speech style of orc]'The [poker card of PC] gives you a special item of clothing.'[roman type][line break]";
			class summon spoiled crop top;
			now spoiled crop top is cursed;
			now spoiled crop top is luck-influencing;
			now the raw-magic-modifier of spoiled crop top is -4;
			compute summoned quest of spoiled crop top;
		otherwise:
			say "[speech style of orc]'The [poker card of PC] makes you permanently slightly more unlucky. Sorry.'[roman type][line break]";
			decrease permanent-luck-modifier by 2;
			say "Everything suddenly seems just a tiny bit... darker, like there's a permanent but gentle shadow covering you at all times.";
	otherwise if PV is 1: [8]
		if diaper quest is 0:
			unless PV is listed in numbersExplained, say "[speech style of orc]'Eights are to do with body shape.'[roman type][paragraph break]";
			if PS is 0 and the player is not a flatchested trap:
				say "[speech style of orc]'The [poker card of PC] tries to grow your breasts.'[roman type][line break]";
				say "You feel a swelling sensation in your chest!";
				BustUp 1;
			otherwise if PS is 1:
				say "[speech style of orc]'The [poker card of PC] tries to widen your hips.'[roman type][line break]";
				say "You feel a stretching sensation in your hips!";
				HipUp 1;
			otherwise if PS is 2:
				if the player is possessing a penis:
					if the player is ready for common event TG:
						say "[speech style of orc]'The [poker card of PC] replaces your genitals.'[roman type][line break]";
						say DefaultSexChangeFlav;
						SexChange the player;
						say "[BigNameDesc of orc] smiles.[line break][speech style of orc]'I know what you're thinking, and yes, that change is permanent. What did you expect from a poker table decorated with female genitalia?'[roman type][line break]";
					otherwise:
						say "[speech style of orc]'The [poker card of PC] tries to shrink your genitals.'[roman type][line break]";
						PenisDown 1;
				otherwise:
					say "[speech style of orc]'The [poker card of PC] tries to puff up your genitals.'[roman type][line break]";
					LabiaUp 1 with comment;
			otherwise:
				say "[speech style of orc]'The [poker card of PC] tries to fatten your buttocks.'[roman type][line break]";
				say "You feel a swelling sensation in your ass cheeks!";
				AssSwell 2;
		otherwise: [cursed diaper, transformed, locked diaper cover, glue (and diaper if there is no underwear)]
			unless PV is listed in numbersExplained, say "[speech style of orc]'Eights affect your underwear.'[roman type][paragraph break]";
			let RDC be a random [off-stage] diaper cover;
			if PS is 1 and there is worn upgradable knickers:
				say "[speech style of orc]'The [poker card of PC] tries to transform your underwear into something more humiliating.'[roman type][line break]";
				let K be a random worn upgradable knickers;
				potentially transform K;
			otherwise if PS is 2 and RDC is actually summonable diaper cover:
				say "[speech style of orc]'The [poker card of PC] gives you a locked diaper cover.'[roman type][line break]";
				summon RDC locked;
				now RDC is waddle-walking;
				say "A [RDC] appears around your loins!";
			otherwise if there is a worn diaper or the number of worn unremovable knickers is 0:
				say "[speech style of orc]'The [poker card of PC] [if PS is not 3 or the number of worn knickers is 0]gives you a cursed diaper[end if][if PS is 3 and the number of worn knickers is 0] and [end if][if PS is 3]glues your underwear to your body[end if].'[roman type][line break]";
				if PS is not 3 or the number of worn knickers is 0:
					let K be a random worn knickers;
					if K is diaper:
						let D be K;
						repeat with E running through eligible diapers:
							if D is K and the DQBulk of E >= the DQBulk of K, now D is E;
						if D is K:
							say "[speech style of orc]'Hmm, that's very unusual, it can't find a single eligible diaper to add to what you're wearing.'[roman type][line break]";
						otherwise:
							say "A split second later, a [MediumDesc of D] appears over the top of your [MediumDesc of K]!";
							blandify and reveal D;
							now D is cursed;
							compute summoned quest of D;
							diaperAdd D;
					otherwise:
						let D be a random eligible diaper;
						if D is diaper:
							if K is knickers:
								transform K into D;
								if D is not cursed, now D is cursed;
								if the quest of D is no-clothing-quest, compute summoned quest of D;
							otherwise:
								say "A split second later, a [MediumDesc of D] appears on you!";
								summon D cursed with quest;
						otherwise:
							say "[speech style of orc]'Hmm, that's very unusual, it can't find a single eligible diaper to make you wear.'[roman type][line break]";
				if PS is 3:
					let K be a random worn knickers;
					if K is knickers:
						say "You feel [if the glue timer of K > 0]the glue inside your [ShortDesc of K] become a lot stronger[otherwise]your [ShortDesc of K] suddenly becoming filled with a strong, sticky glue[end if]![line break][variable custom style]Uh-oh... Now I won't be able to take these off until the glue dries out...[roman type][line break]";
						gluify K;
	otherwise if PV is 2: [9]
		if diaper quest is 0:
			unless PV is listed in numbersExplained, say "[speech style of orc]'Nines are to do with your preferences for various sexual acts.'[roman type][paragraph break]";
			if PS is 0 and (interracial fetish is 1 or max breast size >= 5):
				if interracial fetish is 1:
					say "[speech style of orc]'The [poker card of PC] influences your attraction to black cock.'[roman type][line break]";
					BBCAddictUp 1;
				otherwise:
					say "[speech style of orc]'The [poker card of PC] influences your desire to have your tits fucked.'[roman type][line break]";
					TitfuckAddictUp 1;
			otherwise if PS is 1:
				say "[speech style of orc]'The [poker card of PC] influences your desire to be sodomized.'[roman type][line break]";
				AnalSexAddictUp 1;
			otherwise if PS is 2 and (the player is possessing a vagina or TG fetish is 1):
				say "[speech style of orc]'The [poker card of PC] influences your desire for vaginal sex.'[roman type][line break]";
				VaginalSexAddictUp 1;
			otherwise if PS is 3:
				say "[speech style of orc]'The [poker card of PC] influences your desire to suck dick.'[roman type][line break]";
				OralSexAddictUp 1;
			otherwise:
				say "[speech style of orc]'The [poker card of PC] influences your general desire to have kinky sex.'[roman type][line break]";
				SexAddictUp 1;
		otherwise:
			unless PV is listed in numbersExplained, say "[speech style of orc]'Nines are to do with your preferences and attitudes.'[roman type][paragraph break]";
			if PS is 0 and watersports fetish is 1:
				say "[speech style of orc]'The [poker card of PC] increases your desire to drink urine.'[roman type][line break]";
				UrineTasteAddictUp 1;
			otherwise if PS is 1:
				say "[speech style of orc]'The [poker card of PC] increases your desire to drink human breast milk.'[roman type][line break]";
				MilkTasteAddictUp 1;
			otherwise if PS is 2:
				say "[speech style of orc]'The [poker card of PC] influences your desire to be diapered.'[roman type][line break]";
				DiaperAddictUp 1;
			otherwise:
				say "[speech style of orc]'The [poker card of PC] increase your desire to experience lots of orgasms.'[roman type][line break]";
				SexAddictUp 1;
	otherwise if PV is 3: [10]
		if diaper quest is 0:
			unless PV is listed in numbersExplained, say "[speech style of orc]'Tens affect your head in various ways.'[roman type][paragraph break]";
			if PS is 0:
				say "[speech style of orc]'The [poker card of PC] elongates your hair.'[roman type][line break]";
				HairUp 1;
			otherwise if PS is 1:
				say "[speech style of orc]'The [poker card of PC] enhances your make up.'[roman type][line break]";
				FaceUp 1;
				say "Your face [MakeUpDesc]!";
			otherwise if PS is 2:
				say "[speech style of orc]'The [poker card of PC] colourizes your hair.'[roman type][line break]";
				ColourUp 1;
				say "Your hair is now [HairColour]!";
			otherwise:
				say "[speech style of orc]'The [poker card of PC] plumps up your lips.'[roman type][line break]";
				LipsUp 1;
				say "You feel a swelling sensation in your lips!";
		otherwise:
			unless PV is listed in numbersExplained, say "[speech style of orc]'Tens affect how soon you'll need to use the toilet.'[roman type][paragraph break]";
			if PS is 0 and the bladder of the player < 12:
				say "[speech style of orc]'The [poker card of PC] fills up your bladder to the brim.'[roman type][line break]";
				now the bladder of the player is 12;
				say "[if the player is bursting]You feel DESPERATE to pee!!![otherwise]Nothing feels any different? Strange...[end if]";
			otherwise if PS is 1 and diaper messing >= 3 and rectum < 12:
				say "[speech style of orc]'The [poker card of PC] fills up your bowels to the brim.'[roman type][line break]";
				now rectum is 12;
				say "[if the player is feeling full]You feel DESPERATE to do a poo!!![otherwise]Nothing feels any different? Strange...[end if]";
			otherwise if PS is 2 and diaper messing >= 3:
				say "[speech style of orc]'The [poker card of PC] teleports a suppository up inside your rectum.'[roman type][line break]";
				increase suppository by 3;
				say "Your bowels make a loud growing sound... and you can feel an urgent pressure, begging you to let it rip!";
			otherwise:
				say "[speech style of orc]'The [poker card of PC] doubles the rate of your digestion for a while.'[roman type][line break]";
				say "You can feel that your stomach is [if digestion-timer > 0]going to keep churning away for a long time[otherwise]suddenly churning away[end if]!";
				increase digestion-timer by 900;
	otherwise if PV is 4: [Jack]
		unless PV is listed in numbersExplained, say "[speech style of orc]'Jacks affect your current state in various inconvenient ways, the cheeky buggers.'[roman type][paragraph break]";
		if PS is 0 and there is worn actually cursable clothing:
			say "[speech style of orc]'The [poker card of PC] curses your clothing.'[roman type][line break]";
			let C be a random worn actually cursable clothing;
			say "Your [C] glows dark as it [if C is blessed]loses its blessing[otherwise]becomes cursed[end if]!";
			curse C;
			compute summoned quest of C;
		otherwise if PS is 1:
			say "[speech style of orc]'The [poker card of PC] batters your body.'[roman type][line break]As soon as [he of orc] finishes speaking, an invisible hand swats you on the ass!";
			PainUp 10;
			say "It doesn't stop until you feel sore, and somewhat injured.";
			BodyRuin 3;
		otherwise if PS is 2 and there is worn upgradable clothing:
			say "[speech style of orc]'The [poker card of PC] transforms your clothing.'[roman type][line break]";
			let C be a random worn upgradable clothing;
			potentially transform C;
		otherwise:
			say "[speech style of orc]'The [poker card of PC] will make you feel uncontrollably horny.'[roman type][line break]";
			Arouse 10000;
			check for arousal change;
	otherwise if PV is 5: [Queen]
		unless PV is listed in numbersExplained, say "[speech style of orc]'Queens give you new clothes.'[roman type][paragraph break]";
		if diaper quest is 0:
			let C1 be a random off-stage clubbing dress;
			let C2 be sequins bodysuit;
			if sequins bodysuit is not off-stage or sequins bodysuit is not class summonable:
				now C2 is pink sequins dress;
				if pink sequins dress is not off-stage or pink sequins dress is not class summonable:
					now C2 is a random off-stage sequins belt;
					if C2 is not class summonable, now C2 is slave collar;
			let C3 be a random off-stage my ass vest top;
			if C3 is not class summonable:
				now C3 is heart-strap-thong;
				if heart-strap-thong is not off-stage or heart-strap-thong is not class summonable, now C3 is a random off-stage heart stockings;
			if PS is 0 and spades-strap-bra is off-stage and spades-strap-thong is off-stage and spades-strap-bra is class summonable and spades-strap-thong is class summonable:
				say "[speech style of orc]'The [poker card of PC] gives you spade-themed clothing.'[roman type][line break]";
				class summon spades-strap-bra;
				now spades-strap-bra is cursed;
				now the raw-magic-modifier of spades-strap-bra is -1;
				if interracial fetish is 1, now spades-strap-bra is bbc-addiction-influencing;
				otherwise set up submissiveness-based influence of spades-strap-bra;
				compute summoned quest of spades-strap-bra;
				class summon spades-strap-thong;
				now spades-strap-thong is cursed;
				now the raw-magic-modifier of spades-strap-thong is -1;
				if interracial fetish is 1, now spades-strap-thong is bbc-addiction-influencing;
				otherwise set up submissiveness-based influence of spades-strap-thong;
				compute summoned quest of spades-strap-thong;
			otherwise if PS is 1 and C1 is class summonable:
				say "[speech style of orc]'The [poker card of PC] gives you partywear.'[roman type][line break]";
				class summon C1;
				now C1 is cursed;
				now the raw-magic-modifier of C1 is -1;
				set up sex-based influence of C1;
				compute summoned quest of C1;
			otherwise if PS is 2 and C2 is class summonable:
				say "[speech style of orc]'The [poker card of PC] gives you sparkly clothing.'[roman type][line break]";
				class summon C2;
				now C2 is cursed;
				now the raw-magic-modifier of C2 is -1;
				set up sex-based influence of C2;
				compute summoned quest of C2;
			otherwise if PS is 3 and C3 is class summonable:
				say "[speech style of orc]'The [poker card of PC] gives you clothing with hearts on it.'[roman type][line break]";
				class summon C3;
				now C3 is cursed;
				now the raw-magic-modifier of C3 is -1;
				set up taste-based influence of C3;
				compute summoned quest of C3;
			otherwise:
				say "[BigNameDesc of orc] waits a moment, and then hums with intrigue.[line break][speech style of orc]'It would seem that the [poker card of PC] can't find anything appropriate to make you wear right now.'[roman type][line break]";
		otherwise:
			let C1 be ballet heels;
			if leather-thigh-high-boots is off-stage and the heel skill of the player < 4, now C1 is leather-thigh-high-boots;
			let C2 be a random off-stage jacket;
			let C3 be naughty-skirt;
			let C4 be pinafore-dress;
			if C4 is not off-stage or C2 is worn or C3 is worn or C4 is not class summonable:
				now C4 is a random off-stage heart stockings;
				if C4 is not off-stage or C4 is not actually summonable, now C4 is heart-collar;
			if PS is 0 and C1 is class summonable:
				say "[speech style of orc]'The [poker card of PC] gives you slutty black heels.'[roman type][line break]";
				class summon C1;
				now C1 is cursed;
				compute summoned quest of C1;
			otherwise if PS is 1 and C2 is class summonable:
				say "[speech style of orc]'The [poker card of PC] gives you a slutty jacket.'[roman type][line break]";
				class summon C2;
				now C2 is cursed;
				now the raw-magic-modifier of C2 is -1;
				set up sex-based influence of C2;
				compute summoned quest of C2;
			otherwise if PS is 2 and C3 is off-stage and C3 is class summonable:
				say "[speech style of orc]'The [poker card of PC] gives you a naughty skirt.'[roman type][line break]";
				class summon C3;
				now C3 is cursed;
				now the raw-magic-modifier of C3 is -1;
				set up submissiveness-based influence of C3;
				compute summoned quest of C3;
			otherwise if PS is 3 and (C4 is pinafore-dress or C4 is actually summonable):
				say "[speech style of orc]'The [poker card of PC] gives you clothing with hearts on it.'[roman type][line break]";
				class summon C4;
				now C4 is cursed;
				now the raw-magic-modifier of C4 is -1;
				set up taste-based influence of C4;
				compute summoned quest of C4;
			otherwise:
				say "[BigNameDesc of orc] waits a moment, and then hums with intrigue.[line break][speech style of orc]'It would seem that the [poker card of PC] can't find anything appropriate to make you wear right now.'[roman type][line break]";
	otherwise if PV is 6: [King]
		if game difficulty > 3:
			unless PV is listed in numbersExplained, say "[speech style of orc]'Kings drain your stats.'[roman type][paragraph break]";
			if PS is 0:
				say "[speech style of orc]'The [poker card of PC] slightly drains your strength, dexterity and intelligence.'[roman type][line break]";
				StrengthDown 1;
				DexDown 1;
				IntDown 1;
			otherwise if PS is 1:
				say "[speech style of orc]'The [poker card of PC] heavily drains your strength.'[roman type][line break]";
				StrengthDown 3;
			otherwise if PS is 2:
				say "[speech style of orc]'The [poker card of PC] heavily drains your dexterity.'[roman type][line break]";
				DexDown 3;
			otherwise:
				say "[speech style of orc]'The [poker card of PC] heavily drains your intelligence.'[roman type][line break]";
				IntDown 3;
		otherwise if diaper quest is 0 and (bondage protection is 1 or piercing-fetish is 1):
			unless PV is listed in numbersExplained, say "[speech style of orc]'Kings give you new piercings.'[roman type][paragraph break]";
			let P be a random eligible actually summonable piercing;
			if P is piercing:
				now P is cursed; [some piercings (i.e. earrings) have different appearances depending on BUC status, so we do this to ensure that that the line below says the right thing]
				say "A [P] appears on you!";
				summon P cursed with quest;
			otherwise:
				say "[BigNameDesc of orc] waits a moment, and then hums with intrigue.[line break][speech style of orc]'It would seem that the [poker card of PC] can't find any appropriate piercings.'[roman type][line break]";
		otherwise:
			unless PV is listed in numbersExplained, say "[speech style of orc]'Kings give you locked-on bondage.'[roman type][line break]";
			let C be chastity-belt;
			if the player is not possessing a vagina, now C is a random off-stage fetish appropriate chastity cage;
			let A be a random pair of anklecuffs;
			let DG be a random rubber duck gag;
			if PS is 0 and pair of handcuffs is actually summonable:
				say "[speech style of orc]'The [poker card of PC] locks your wrists together.'[roman type][line break]";
				summon pair of handcuffs locked;
				say "A [pair of handcuffs] appears on your wrists!";
			otherwise if PS is 1 and A is actually summonable:
				say "[speech style of orc]'The [poker card of PC] locks your ankles together.'[roman type][line break]";
				summon A locked;
				say "A [A] appears on your ankles!";
			otherwise if diaper quest is 1 and PS is 2 and DG is actually summonable:
				say "[speech style of orc]'The [poker card of PC] gives you a locked gag.'[roman type][line break]";
				summon DG locked;
				say "A [DG] appears in your mouth!";
			otherwise if PS is 2 and heart-collar is actually summonable:
				say "[speech style of orc]'The [poker card of PC] gives you a locked collar.'[roman type][line break]";
				summon heart-collar locked;
				say "A [heart-collar] appears on your neck!";
			otherwise if PS is 3 and C is actually summonable:
				say "[speech style of orc]'The [poker card of PC] locks away your own access to your genitals.'[roman type][line break]";
				summon C locked;
				say "A [C] appears over your [genitals]!";
			otherwise:
				say "[BigNameDesc of orc] waits a moment, and then hums with intrigue.[line break][speech style of orc]'It would seem that the [poker card of PC] can't find anything appropriate to lock you up with right now.'[roman type][line break]";
	otherwise: [Ace]
		unless PV is listed in numbersExplained, say "[speech style of orc]'Aces have unique, rather troublesome effects, I'm afraid.'[roman type][paragraph break]";
		if PS is 0:
			if diaper quest is 0 and pimp is alive and the times-met of pimp > 0 and portal-hotpants is unclash summonable:
				say "[speech style of orc]'The [poker card of PC] makes [NameDesc of pimp] [speech style of orc]think that you owe [him of pimp] lots of money. And... it gives [him of pimp] a unique way to sell your body for sex, without even asking your permission.'[roman type][line break]";
				unclash class summon portal-hotpants;
				now the favour of pimp is the aggro limit of pimp - 4;
				if max breast size >= 5 and portal-bra is actually summonable:
					summon portal-bra;
					say "The hotpants are immediately followed by a matching chestplate!";
				say "[variable custom style]No way... surely this doesn't mean that... my body parts are... mounted on the statuettes in [NameDesc of pimp][speech style of orc][']s room?![roman type][line break]";
			otherwise if diaper quest is 1 and pimp is alive and the times-met of pimp > 0 and diaper swapping >= 2 and the player is a december 2023 diaper donator and portal-diaper is unclash summonable:
				say "[speech style of orc]'The [poker card of PC] makes [NameDesc of pimp] [speech style of orc]think that you owe [him of pimp] lots of money. And... it gives [him of pimp] a unique way to sell your diaper for use, without even asking your permission.'[roman type][line break]";
				unclash class summon portal-diaper;
				say "[variable custom style]No way... surely this doesn't mean that... my diaper padding is... connected to some other place via a portal?![roman type][line break]";
			otherwise:
				say "[speech style of orc]'The [poker card of PC] summons a new, err, [']friend['] for you.'[roman type][line break]";
				let M be a random horny-wench;
				if interracial fetish is 1, now M is QoS wench;
				if diaper quest is 1, now M is a random dungeon dwelling adult baby slave;
				if M is off-stage, set up M;
				now M is in the location of the player;
				say "[BigNameDesc of orc] points to behind you, and as you turn around, you see that [NameDesc of M] is standing there, also turning around, and noticing you!";
				anger M;
				check guaranteed perception of M;
		otherwise if PS is 1:
			say "[speech style of orc]'The [poker card of PC] unleashes a powerful foe into this Dungeon.'[roman type][line break]";
			let M be a random alive caged dungeon boss;
			if M is nothing:
				if diaper quest is 0, now M is a random off-stage tentacle monster;
				otherwise now M is bowsette;
			if M is dungeon boss:
				now M is unleashed;
				say "You hear the sound of a cage crashing and swinging open from elsewhere in the Dungeon![line break][variable custom style][if Dungeon36 is visited]Oh... Oh no... The [ShortDesc of M]...[otherwise]What was that?![end if][roman type][line break]";
			otherwise if M is monster:
				if M is tentacle monster, now M is massive;
				summon M in the dungeon;
				say "You hear the sound of [if diaper quest is 0]slithering tentacles[otherwise]evil laughter and videogame music[end if] from elsewhere in the Dungeon![line break][variable custom style]Uh-oh...[roman type][line break]";
			otherwise:
				say "[BigNameDesc of orc] waits a moment, and then hums with intrigue.[line break][speech style of orc]'It would seem that the [poker card of PC] can't find anything appropriate to summon right now.'[roman type][line break]";
		otherwise if PS is 2:
			say "[speech style of orc]'The [poker card of PC] curses you with a troublesome burden.'[roman type][line break]";
			let ULO be the list of eligible pledge-lesson-objects;
			sort ULO in random order;
			repeat with LO running through ULO:
				if LO is implanted, remove LO from ULO;
			let E be entry 1 in ULO;
			if E is a pledge-lesson-object:
				pledge-execute E;
				pledge-implant E;
				say "And you can feel that for the rest of your time here, if you disobey the pledge that was just made for you, there will be serious consequences.";
			otherwise:
				StrengthDown 4;
				say "You feel significant amounts of physical strength leaving you![line break][variable custom style]No!!![roman type][line break]";
		otherwise if PS is 3:
			say "[speech style of orc]'The [poker card of PC] teleports you to a troublesome location. Err, I guess this is goodbye for now.'[roman type][line break]";
			compute bad teleport;
	add PV to numbersExplained.

To player hand print (PH - a list of numbers):
	say "Your hand currently contains:[line break]";
	repeat with PC running through PH:
		say "[poker card of PC][if debugmode > 0]([PC])[end if]  ";
	say line break;

To orc hand print (OH - a list of numbers):
	say "[BigNameDesc of orc]'s hand currently contains:[line break]";
	repeat with PC running through OH:
		say "[poker card of PC][if debugmode > 0]([PC])[end if]  ";
	say line break.

To say poker card of (PC - a number):
	say "[poker card value of PC] of [poker card suit of PC]".

To decide which text is the poker card value of (PC - a number):
	decide on the poker card value of raw PC / 4;

To decide which text is the poker card value of raw (PV - a number):
	if PV <= 3, decide on "[PV + 7]";
	if PV is 4, decide on "Jack";
	if PV is 5, decide on "Queen";
	if PV is 6, decide on "King";
	decide on "Ace".

To decide which text is the poker card suit of (PC - a number):
	decide on the poker card suit of raw the remainder after dividing PC by 4.

To decide which text is the poker card suit of raw (PS - a number):
	if PS is 0, decide on "Spades";
	if PS is 1, decide on "Clubs";
	if PS is 2, decide on "Diamonds";
	decide on "Hearts".

To decide which number is the poker hand strength of (CH - a list of numbers):
	sort CH; [just in case. Very important they're in ascending order]
	let VH be a list of numbers; [values from 0-7]
	repeat with PC running through CH:
		add PC / 4 to VH;
	[Check for flush]
	let flush be true;
	let flushSuit be the remainder after dividing entry 5 in CH by 4;
	repeat with PC running through CH:
		if flush is true and the remainder after dividing PC by 4 is not flushSuit, now flush is false;
	[Check for straight]
	let straight be true;
	let straightTracker be entry 1 in VH;
	let E be 1;
	repeat with PV running through VH:
		if E is not 1: [skip first card]
			unless E is 5 and straightTracker is 3 and PV is 7: [Ace can go below 7 in a straight]
				if straight is true and PV is not straightTracker + 1:
					now straight is false; [the next card in the hand is not the next card up in a run]
				otherwise:
					now straightTracker is PV;
		increase E by 1;
	if flush is true and straight is true:
		now poker hand name is "a straight flush to the [poker card value of raw straightTracker]";
		decide on 30000000 + straightTracker;
	[check for other hands]
	let singles be a list of numbers;
	let pairs be a list of numbers;
	let threekind be -1;
	let fourkind be -1;
	repeat with N running from 0 to 7:
		let NCount be 0;
		repeat with PV running through VH:
			if PV is N, increase NCount by 1;
		if NCount is 4, now fourkind is N;
		if NCount is 3, now threekind is N;
		if NCount is 2, add N to pairs;
		if NCount is 1, add N to singles;
	if fourkind >= 0:
		now poker hand name is "four [poker card value of raw fourkind]s";
		decide on 20000000 + (fourkind * 100) + (10 * entry 1 in singles);
	if flush is true:
		now poker hand name is "a flush with [poker card value of entry 5 in CH] high";
		decide on 10000000 + (100000 * entry 5 in VH) + (10000 * entry 4 in VH) + (1000 * entry 3 in VH) + (100 * entry 2 in VH) + (10 * entry 1 in VH);
	if threekind >= 0 and the number of entries in pairs > 0:
		now poker hand name is "[poker card value of raw threekind]s full of [poker card value of raw entry 1 in pairs]s";
		decide on 1000000 + (threekind * 100) + (10 * entry 1 in pairs);
	if straight is true:
		now poker hand name is "a straight to the [poker card value of raw straightTracker]";
		decide on 900000 + (10 * straightTracker);
	if threekind >= 0:
		now poker hand name is "three [poker card value of raw threekind]s";
		decide on 100000 + (threekind * 1000) + (100 * entry 2 in singles) + (10 * entry 1 in singles);
	if the number of entries in pairs is 2:
		now poker hand name is "a pair of [poker card value of raw entry 2 in pairs]s and a pair of [poker card value of raw entry 1 in pairs]s";
		decide on 10000 + (1000 * entry 2 in pairs) + (100 * entry 1 in pairs) + (10 * entry 1 in singles);
	if the number of entries in pairs is 1:
		now poker hand name is "a pair of [poker card value of raw entry 1 in pairs]s";
		decide on 1000 + (1000 * entry 1 in pairs) + (100 * entry 3 in singles) + (10 * entry 2 in singles) + entry 1 in singles;
	let highCardWins be ((10 - entry 5 in VH) * 10000) + ((10 - entry 4 in VH) * 1000) + ((10 - entry 3 in VH) * 100) + ((10 - entry 2 in VH) * 10) + 10 - entry 1 in VH;
	now poker hand name is "a [poker card value of entry 5 in CH]";
	decide on highCardWins * -1.

[To decide which text is the poker hand name of (PHS - a number):
	if PHS >= 30000000, decide on "a straight flush";
	if PHS >= 20000000, decide on "four of a kind";
	if PHS >= 10000000, decide on "a flush";
	if PHS >= 1000000, decide on "a full house";
	if PHS >= 900000, decide on "a straight";
	if PHS >= 100000, decide on "three of a kind";
	if PHS >= 10000, decide on "two pairs";
	if PHS >= 1000, decide on "a pair";
	decide on "nothing special".]

To display poker interface:
	if the graphics-window is g-present:
		let H be map-window-height;
		let W be map-window-width;
		let cardHeight be H / 3;
		let cardWidth be (cardHeight * 100) / 144;
		if cardWidth * 6 > W - 2: [cards at this size too wide for map window width]
			now cardWidth is (W - 2) / 6;
			now cardHeight is (cardWidth * 144) / 100;
		let orcHandRenderY be H - (cardHeight * 3);
		repeat with N running from 0 to 4:
			let orcCardRenderX be map-window-x-root + (N * cardWidth);
			let FP be entry (N + 1) in orcHand;
			let F be entry (FP + 1) in pokerCardList;
			display the image F in the graphics-window at orcCardRenderX by orcHandRenderY with dimensions cardWidth by cardHeight;
		let playerHandRenderY be H - cardHeight;
		repeat with N running from 0 to 4:
			let playerCardRenderX be map-window-x-root + (N * cardWidth);
			let FP be entry (N + 1) in playerHand;
			let F be entry (FP + 1) in pokerCardList;
			if N + 1 is listed in cardsToBurn, now F is Figure of PokerCardDiscard;
			display the image F in the graphics-window at playerCardRenderX by playerHandRenderY with dimensions cardWidth by cardHeight;
			set a graphlink in the graphics-window identified as hypermapyes from playerCardRenderX by playerHandRenderY to (playerCardRenderX + cardWidth) by (playerHandRenderY + cardHeight) as "[N + 1]", ignoring redundant links;
		let playerCardRenderX be map-window-x-root + (5 * cardWidth);
		let deckRenderY be playerHandRenderY - cardHeight;
		display the image Figure of PokerCardDraw in the graphics-window at playerCardRenderX by deckRenderY with dimensions cardWidth by cardHeight;
		set a graphlink in the graphics-window identified as hypermapyes from playerCardRenderX by deckRenderY to (playerCardRenderX + cardWidth) by (deckRenderY + cardHeight) as "0", ignoring redundant links.




Figure of PokerCard00 is the file "Special/Cards/card00.png".
Figure of PokerCard01 is the file "Special/Cards/card01.png".
Figure of PokerCard02 is the file "Special/Cards/card02.png".
Figure of PokerCard03 is the file "Special/Cards/card03.png".
Figure of PokerCard04 is the file "Special/Cards/card04.png".
Figure of PokerCard05 is the file "Special/Cards/card05.png".
Figure of PokerCard06 is the file "Special/Cards/card06.png".
Figure of PokerCard07 is the file "Special/Cards/card07.png".
Figure of PokerCard08 is the file "Special/Cards/card08.png".
Figure of PokerCard09 is the file "Special/Cards/card09.png".
Figure of PokerCard10 is the file "Special/Cards/card10.png".
Figure of PokerCard11 is the file "Special/Cards/card11.png".
Figure of PokerCard12 is the file "Special/Cards/card12.png".
Figure of PokerCard13 is the file "Special/Cards/card13.png".
Figure of PokerCard14 is the file "Special/Cards/card14.png".
Figure of PokerCard15 is the file "Special/Cards/card15.png".
Figure of PokerCard16 is the file "Special/Cards/card16.png".
Figure of PokerCard17 is the file "Special/Cards/card17.png".
Figure of PokerCard18 is the file "Special/Cards/card18.png".
Figure of PokerCard19 is the file "Special/Cards/card19.png".
Figure of PokerCard20 is the file "Special/Cards/card20.png".
Figure of PokerCard21 is the file "Special/Cards/card21.png".
Figure of PokerCard22 is the file "Special/Cards/card22.png".
Figure of PokerCard23 is the file "Special/Cards/card23.png".
Figure of PokerCard24 is the file "Special/Cards/card24.png".
Figure of PokerCard25 is the file "Special/Cards/card25.png".
Figure of PokerCard26 is the file "Special/Cards/card26.png".
Figure of PokerCard27 is the file "Special/Cards/card27.png".
Figure of PokerCard28 is the file "Special/Cards/card28.png".
Figure of PokerCard29 is the file "Special/Cards/card29.png".
Figure of PokerCard30 is the file "Special/Cards/card30.png".
Figure of PokerCard31 is the file "Special/Cards/card31.png".

Figure of PokerCardDraw is the file "Special/Cards/carddraw.png".
Figure of PokerCardDiscard is the file "Special/Cards/carddiscard.png".


Orc ends here.
