Headmistress by Monster begins here.

headmistress is a diamond-teacher. headmistress is guarding. headmistress is female. headmistress can be puddle-investigating. The leftover-type of headmistress is 130.

headmistress is in School08. The text-shortcut of headmistress is "hdm".

Definition: headmistress is a generic-unlocker: decide yes.

Figure of headmistress is the file "NPCs/School/headmistress1.png".
Figure of enslaved diapered headmistress is the file "NPCs/School/headmistress3.png".

To decide which figure-name is the pacified-image of (M - headmistress):
	if M is sex-enslaved, decide on figure of enslaved headmistress;
	if M is diaper-enslaved, decide on figure of enslaved diapered headmistress;
	decide on figure of Missing NPC.

To decide which figure-name is the monster-image of (M - headmistress):
	decide on figure of headmistress.

To say ShortDesc of (M - headmistress):
	say "[if lady fetish is 2]mister [end if]headmistress".

To say MediumDesc of (M - headmistress):
	say "[if lady fetish is 2]mister [end if]headmistress".

To say NameDesc of (C - headmistress):
	say "the [input-style][MediumDesc of C][roman type]".
To say BigNameDesc of (C - headmistress):
	say "The [input-style][MediumDesc of C][roman type]".
To say FuckerDesc of (C - headmistress):
	say "the [MediumDesc of C]".
To say BigFuckerDesc of (C - headmistress):
	say "The [MediumDesc of C]".

To say MonsterDesc of (M - headmistress):
	if M is sex-enslaved:
		if lady fetish is 2:
			say "[BigNameDesc of M][']s dress has been opened further at the front, including a big slit in the skirt section revealing [his of M] bright red chastity cage. A presumably new tattoo sits above [his of M] [DickDesc of M], with the words 'Enter in Rear' and an arrow pointing toward [his of M] butt. [big his of M] ass has been made more easily accessible with the addition of a very long ankle spreader bar, which keeps [his of M] feet over a metre apart from each other at all times. A tally has been started on [his of M] right inner thigh. Looks like [he of M]'s been fucked at least five times already. One ear has been pierced and a large 'SLUT' tag added. [big his of M] wrists are cuffed behind [him of M], leaving [him of M] absolutely powerless to stop anyone using [him of M] as they please. [big his of M] tongue is stuck out of [his of M] mouth with a lewd expression. You watch as [he of M] instinctively tries to draw it back into [his of M] mouth, but then [his of M] 'SLUT' tag flutters wildly as [his of M] earring gives [him of M] a painful electric shock. [big he of M] whimpers and returns to sticking [his of M] tongue as far out of [his of M] mouth as he can.";
		otherwise:
			say "[BigNameDesc of M][']s dress has been opened further at the front, including a big slit in the skirt section right in front of [his of M] bare pussy. A presumably new tattoo sits above [his of M] slit, with the words 'Fuck Here' and an arrow pointing at [his of M] undefended fuckhole. [big his of M] entrance has been made even more easily accessible with the addition of a very long ankle spreader bar, which keeps [his of M] feet over a metre apart from each other at all times. A tally has been started on [his of M] right inner thigh. Looks like [he of M][']s been fucked at least five times already. One ear has been pierced and a large 'SLUT' tag added. [big his of M] wrists are cuffed behind [him of M], leaving [him of M] absolutely powerless to stop anyone using [him of M] as they please. [big his of M] tongue is stuck out of [his of M] mouth with a lewd expression. You watch as [he of M] instinctively tries to draw it back into [his of M] mouth, but then [his of M] 'SLUT' tag flutters wildly as [his of M] earring gives [him of M] a painful electric shock. [big he of M] whimpers and returns to sticking [his of M] tongue as far out of [his of M] mouth as [he of M] can.";
	otherwise if M is diaper-enslaved:
		say "[BigNameDesc of M][']s red dress has been replaced by a heart-themed red onesie with cute pale frills at the shoulders. Matching pale red ribbons keep [his of M] jet black hair in pigtails and make [him of M] look a lot less fierce than [he of M] used to. Long red latex thigh-highs complete the adorable look. It's clear from the bulkiness between [his of M] legs that [he of M]'s well padded under [his of M] new onesie. [one of]More curious, however, is the fact that [he of M]'s keeping [his of M] feet over a metre apart for no clear reason. But then you[or]You[stopping] watch as [he of M] instinctively moves [his of M] feet a little closer together, but then [his of M] fists clench and [his of M] legs shudder wildly as [his of M] high heels give [him of M] painful electric shocks. [big he of M] whimpers and returns to holding [his of M] feet as far apart as [he of M] can.";
	otherwise:
		say "This [man of M] looks almost ageless - [he of M]'s clearly an adult but you can't tell if [he of M]'s 20 or 40. [big his of M] very slim figure is complemented by [if lady fetish is 2]a perfectly sculpted chin and a thick, grabbable butt[otherwise]perfect D cup breasts and not insignificant hips[end if]. A tight red latex dress with a very deep V-neck clings tightly to [him of M], allowing you to appreciate all of [his of M] curves. The dress, although very provocative and not reaching below mid-thigh, somehow gives [him of M] an aura of extreme dominance. This sensation is further enhanced by [his of M] straight neat jet black hair and harsh make up which gives [his of M] face a rather... fierce appearance.".

To say MonsterComment of (M - headmistress):
	say "[variable custom style]It's like I can actually feel raw power coming from [him of M]. [if the bimbo of the player <= 10]I need to be careful[otherwise]I'd better do whatever [he of M] says[end if].[roman type][line break]".

To compute (M - a monster) stomping (N - headmistress):
	if M is in the location of the player, say "Before [he of N] can react, [NameDesc of N] has been pinned to the wall by [NameDesc of M][if diaper quest is 0], and [his of M] [manly-penis] slid into [his of N] asshole[end if]. [big he of N] shrieks[line break][speech style of N]'That fucking [ShortDesc of ex-princess] had one of these roaming [his of ex-princess] lands?!'[roman type][line break]but this turns into an undecipherable wail as [he of M] starts [if diaper quest is 0]cumming. [big his of N] belly stretches to contain all the [semen]. After [he of N] is full, [he of M] tosses [him of N] to the ground, where [he of N] promptly faints[otherwise]transforming[end if].";
	otherwise say "You hear a [man of N] screech[line break][speech style of N]'That fucking [ShortDesc of ex-princess] had one of these roaming [his of ex-princess] lands?!'[roman type][line break][if N is nearby]nearby, [otherwise]somewhere in the region, [end if] followed by a wail of shame.";
	if diaper quest is 1:
		now N is diaper-enslaved;
	otherwise:
		destroy N;
		let L be a random off-stage leftover;
		now L is in the location of M;
		now the leftover-type of L is the leftover-type of N.

To say LeftoverDesc (N - 130):
	say "[BigNameDesc of headmistress]is on the ground here, unconscious. [big he of headmistress] has a [if pregnancy fetish is 1]pregnant looking[otherwise]fully stretched[end if] belly and [semen] slowly flowing out of [his of headmistress] [asshole].".

To set up (M - headmistress):
	reset M;
	now the monstersetup of M is 1;
	now M is in School08;
	now M is guarding;
	now the raw difficulty of M is the starting difficulty of M;
	now the health of M is the maxhealth of M.

To decide which number is the starting difficulty of (M - headmistress):
	decide on 12.

To say speech style of (M - headmistress):
	say "[first custom style]".

Definition: headmistress is dismissable: decide no.

Part - Motion

To compute unique boredom of (M - headmistress):
	if playerRegion is not school and M is undefeated, now M is in School08;
	now M is not puddle-investigating.
Definition: headmistress is closest-toilet-preferring: decide yes.

To compute monstermotion of (M - headmistress):
	if M is puddle-investigating or the bladder of M >= 1000:
		compute toilet seeking of M;
	otherwise if M is not in School08 or M is unfriendly:
		compute room leaving of M.

To compute guarding action of (M - headmistress):
	if M is puddle-investigating:
		check forced seeking 1 of M;
		if M is not interested and M is in the location of the player, check perception of M;
	otherwise if the bladder of M >= 1000:
		compute toilet seeking of M;
	otherwise if M is in an academic room and M is not in School08 and M is not interested and M is friendly: [Sometimes the headmistress gets moved around and it would be good to send her slowly back to her office]
		if M is in School04, try M going south;
		otherwise compute room leaving of M.

An all later time based rule (this is the school recruiters rule):
	if armband is worn:
		if class-time < (lessonFrequency * -5) and the rank of the player < 6: [staff members stalk the player to get them to come back to class]
			if headmistress is alive and headmistress is undefeated and headmistress is not regional, regionally place headmistress;
			if receptionist is alive and receptionist is undefeated and receptionist is not regional:
				now receptionist is guarding;
				deinterest receptionist;
				if playerRegion is Dungeon:
					now receptionist is in Dungeon10;
				otherwise if playerRegion is Hotel:
					now receptionist is in Hotel40;
				otherwise:
					now receptionist is in School01;
		otherwise if playerRegion is school: [staff members don't get stuck out in the field while the player is in the academy!]
			repeat with M running through alive undefeated staff members:
				if M is not in an academic room:
					if M is receptionist:
						now M is in School01;
					otherwise if M is headmistress:
						now M is in School08;
					otherwise:
						regionally place M.

To regionally place (M - headmistress):
	deinterest M; [generally when we regionally place some we want them to be able to notice the player immediately the next time they walk past each other]
	if playerRegion is school:
		now M is guarding;
		now M is in School08;
	otherwise:
		now M is unleashed;
		now the health of M is the maxhealth of M;
		while M is not regional or M is in the location of the player or M is nearby:
			now M is in a random placed room.

To say EnslavedDominationFlav of (M - headmistress):
	say "You grab [NameDesc of M][']s ponytail and force [him of M] to [his of M] knees. ";
	if the number of worn pussy covering actually unavoidable clothing is 0:
		if penis is penis-erect or penis is erect-at-will:
			say "Holding your [if penis is penis-erect]erect[otherwise]rapidly hardening[end if] [ShortDesc of penis] in one hand, you guide it into [his of M] well-lubricated snatch, and thrust away until you bring yourself to a well-earned climax.";
			orgasm quietly;
			check virginity with M;
		otherwise if the player is possessing a penis:
			say "Unable to get an erection right now, you settle for using the fact that [NameDesc of M] isn't allowed to keep [his of M] tongue in [his of M] mouth to your advantage. You wipe your flaccid [player-penis] up and down [his of M] lolling tongue, cleaning it up with [his of M] saliva, and making [him of M] whine and retch. It feels good but you soon grow bored and let go of [his of M] hair.";
		otherwise:
			say "You use the fact that [NameDesc of M] isn't allowed to keep [his of M] tongue in [his of M] mouth to your advantage. You rub your wet pussy up and down [his of M] lolling tongue, cleaning it up with [his of M] saliva, and making [him of M] whine and retch. ";
			if the player is horny:
				say "You're able to use [his of M] tongue to bring yourself to a well-earned climax.";
				orgasm quietly;
			otherwise:
				say "It feels good but you soon grow bored and let go of [his of M] hair.";
	otherwise:
		say "You begin exploring [his of M] holes with your fingers, trying to extract as many squeaks, squeals and grunts as you can from the poor [man of M] before you get bored. Which you eventually, do, of course.";
	say "[speech style of M]'H...haaah~'[roman type][line break]That seems to be all [he of M] is capable of saying with [his of M] tongue stuck out like that.".

To decide which number is the EnslavedDominationThreshold of (M - headmistress):
	decide on a random number between 1000 and 6000.

Part - Perception

To compute nonstudent perception of (M - a headmistress):
	say "[speech style of M]'You! [if M is in the school]Where is your armband[otherwise]You think you can enrol in my school and then just decide to drop out halfway through[end if]?!'[roman type][line break][big he of M] takes an aggressive stance.";
	anger M.

To compute student perception of (M - a headmistress):
	if the armband-print of armband is "new recruit":
		say "[speech style of M]'Hmm, another newbie, is it? Allow me to introduce you to our proud institution. In this academy we will help you understand the elegance of submission. You will rise to the top by learning how to be the perfect bottom. Our curriculum is staged to make sure you aren't exposed to anything too... disturbing... until you have proven yourself capable of accepting it.[paragraph break]'Now listen closely, the way this academy works is as follows. You [bold type]attend a lesson[speech style of M] by entering the classroom that matches your student rank, which is indicated by the font of the writing on your armband. There is usually a [bold type]minor positive benefit[speech style of M] to attending any class, because, well, experience is the best teacher. Of course, there may be other side-effects depending on your choices. Students who perform well in a lesson can be [bold type]promoted to the next rank.[speech style of M] You will have to prove yourself if you want to raise your ranking and earn access to more of the academy. Now, after attending at least one lesson, you will be free to [bold type]go back through the portal in reception[speech style of M] and spend some more time outside of the academy grounds, giving you more time to think on what you've learned, and grow accustomed to the world around you. This is recommended, because [bold type]entering advanced lessons before you're ready for them can be rather overwhelming[speech style of M]. When you're out in the big wide world, your armband notifies you when the other students have arrived and your next lesson is waiting to begin. You get four warnings. If you hear a fifth alarm tone, when you return to the academy [bold type]you will be put in detention for being late[speech style of M], and my staff will begin to hunt you down and enforce your return.[paragraph break][if the player is the donator]'Another reason to return to the real world in between lessons is that you can only access the Extra Credit Zone once in a while. You can't just wait in the academy until you're allowed in again, because time works a bit differently here. [bold type]To access the Extra Credit Zone you must first spend time outside the academy until your armband beeps at least once, and then attend at least one normal lesson, [speech style of M]and then when you enter the Warp Portal to leave the academy, you will be offered the opportunity to enter the Extra Credit Zone. When you successfully complete your Extra Credit Mission, you will be rewarded with (amongst other things) a bracelet. The value of the bracelet will match your rank as a student. Since you can only wear two bracelets at once, if you already have two, and you plan to enter the Extra Credit Zone, [bold type]it is best to spend or trade at least one of your bracelets before returning to the academy.[speech style of M][paragraph break][end if]'Now let me take a closer look at you... ";
		if the player is broken:
			say "Hmmm... your mind appears already too far gone for us to really have much fun with you, but if your owner still wants you to go through the advanced lessons, that's fine with me.'";
		otherwise if there is a worn tiara:
			say "We had one of your kind before and [he of ex-princess] didn't manage to fit in. I wonder if you'll prove similarly challenging.'";
		otherwise if mystical amulet is worn:
			say "I sense you have a large part to play in the destiny of this world. But whether that aligns with my own interests remains to be seen. I will be watching you closely.'";
		otherwise if the appearance of the player > 16 and diaper quest is 0:
			say "If your appearance is anything to go by, you're going to fly through the ranks.'";
		otherwise if the class of the player is schoolgirl or the class of the player is cheerleader:
			say "Heh, I appreciate your eagerness but [if diaper quest is 0]it's not that kind of school. Your teachers will be choosing your uniforms[otherwise]depending on your teachers, you may not get to stay like that for long[end if].'";
		otherwise if the cringe appearance of the player > 10:
			say "If your appearance is anything to go by, you're on the right track already.'";
		otherwise if the appearance of the player > 10:
			say "If your appearance is anything to go by, [if diaper quest is 0]you're on the right track already[otherwise]you have a lot to learn about what TRUE submission entails[end if].'";
		otherwise:
			say "Hmm. Your future is still shrouded in uncertainty. You still have a lot of power to decide your fate. I will be watching your progress with great interest.'";
		now the armband-title of armband is "";
		if diaper quest is 0:
			now the armband-print of armband is "slut in training";
			if earnings + 300 < starting-earnings: [Player can't just race through the game to the hotel warp portal to get the promotion]
				if the appearance of the player > 10 or the player is broken:
					now armband is emerald;
					update students;
					now the armband-print of armband is "promising provocateur";
				if the appearance of the player > 16 or the player is broken:
					now armband is ruby;
					update students;
					now the armband-print of armband is "natural born slut";
		otherwise:
			now the armband-print of armband is "submissive in training";
			if earnings + 300 < starting-earnings: [Player can't just race through the game to the hotel warp portal to get the promotion]
				if the cringe appearance of the player > 10 or the player is broken:
					now armband is emerald;
					update students;
					now the armband-print of armband is "promising peepot";
				if the cringe appearance of the player > 16 or the player is broken:
					now armband is ruby;
					update students;
					now the armband-print of armband is "natural born submissive";
		say "[roman type][line break][big he of M] waves a hand in your direction and your armband slightly transforms! It now reads '[NameBimbo] the [armband-print of armband]' in letters that look like they're made out of [accessory-colour of armband].";
	otherwise if the diaper-duration of M > 0 and the number of worn diaper is 0:
		say "[big he of M] swiftly walks up to you and holds [if diaper messing < 3]a hand to the front[otherwise]hands to the front and back[end if] of your crotch, pushing firmly to get a good feel.[line break][speech style of M]'HEY! Where is your diaper?! You're in big trouble, you naughty baby!'[roman type][line break]";
		anger M;
	otherwise if the bladder of M >= 1000 and M is able to use a free use urinal:
		compute free use urinal perception of M;
	otherwise if M is puddle-investigating and playerRegion is school and M is friendly:
		say "[speech style of M]'[NameBimbo]! I've heard a report of URINATING on the FLOOR! ";
		if the player is not in School10:
			say "Come with me!'[roman type][line break]";
			drag to School10 by M;
			say "[speech style of M]'Well well well. ";
		if the urine-puddle of School10 > 0:
			say "Caught red-handed. I will not stand for this level of indignity! EVERYONE! TIME FOR ASSEMBLY!'[roman type][line break]";
			let A be locked-toilets-assembly;
			if the assemblyCount of A > 0 or the player is getting unlucky, now A is locked-toilets-shame-assembly;
			drag to School16 by M;
			set up A;
			compute start of A;
			follow the assembly computation rule;
		otherwise:
			say "Looks like the reports were false. That is VERY fortunate for you.'[roman type][line break]";
	otherwise if the diaper-duration of M > 0:
		compute diaper check of M.

To say WhoQuestion of (M - a headmistress):
	say "[variable custom style]'Who are you?'[roman type][line break]".

To say WhoAnswer of (M - a headmistress):
	say "[speech style of M]'[one of]It is rather obvious that I am the headmistress of this school. [or][stopping]While it is not the focus of this institution, it would behoove you to learn not to ask stupid questions.'[roman type][line break]".

To say WhereAnswer of (M - a headmistress):
	say "[speech style of M]'[if the rank of the player <= 1]You are in the headmistress['] office, young miss. You can find your classes at the end of the hall on the right.'[otherwise]Ranks are earned, and not without effort. Do not forget that.'[end if][roman type][line break]".

To say EscapeAnswer of (M - a headmistress):
	say "[speech style of M]'Your instructors will let you know when you are ready for higher level curriculum.'[roman type][line break]".

To say StoryAnswer of (M - a headmistress):
	say "[speech style of M]'If you have time to ask questions, you have time to focus on your classwork.'[roman type][line break]".

To say AdviceAnswer of (M - a headmistress):
	say "[speech style of M]'Skipping classes will earn you a detention... or worse.'[roman type][line break]".

To compute teaching offer of (M - a headmistress):
	say "[speech style of M]'I have my [ShortDesc of M] duties to take care of. Ask a teacher if you would like a lesson.'[roman type][line break]".

talk-headmistress-toilet is a talk-object.
headmistress-toilet-ban is initially false.


Definition: headmistress is fluff-question-appropriate:
	if locked-toilets is true and academy-toilet-key is not held, decide no;
	decide yes.

To consider (T - talk-headmistress-toilet) for (M - a monster):
	if M is headmistress and locked-toilets is true and academy-toilet-key is not held and M is friendly:
		now the printed name of T is the substituted form of "[variable custom style]'[if the delicateness of the player < 5]I need to use the toilet.'[otherwise if the delicateness of the player < 10]Please could I have the key to the toilets?'[otherwise if the delicateness of the player < 15]Please may I have permission to use the toilets?'[otherwise if diaper lover > 0]Miss, please can I have permission to use the big potty? Pretty please?'[otherwise]This lowly student begs permission to use the toilets...'[end if][roman type][line break]";
		set next numerical response to the substituted form of "[printed name of T]".

To execute (T - talk-headmistress-toilet) for (M - a monster):
	if headmistress-toilet-ban is true:
		say "[BigNameDesc of M] narrows [his of M] eyes.[line break][speech style of M]'As I said before, your toilet privileges have been revoked. End of discussion.'[roman type][line break]";
	otherwise:
		let no-toilet-rank be 5;
		if diaper quest is 1 or watersports fetish is 0, now no-toilet-rank is 4;
		let able-to-pee be 1;
		repeat with C running through pee covering actually unavoidable clothing:
			if able-to-pee is 1 and C is not usually autoremovable, now able-to-pee is 0;
		if the rank of the player >= no-toilet-rank:
			say "[BigNameDesc of M] looks at your armband and then shakes [his of M] head.[line break][speech style of M]'[one of]As of today, I have decided that [or][stopping][if no-toilet-rank is 4]Pink [end if]Diamond and higher ranked students are not allowed to use the toilets. [if no-toilet-rank is 4]If you want to pee, use a diaper like a good little baby slave[otherwise]At your high rank, you and your peers should be learning to be human toilets yourselves. You can pee in each others['] mouths, or wait until you get home[end if].'[roman type][line break]";
		otherwise if academy-toilet-key is not carried by M:
			if there is a student carrying academy-toilet-key:
				let P be a random student carrying academy-toilet-key;
				say "[BigNameDesc of M] shrugs.[line break][speech style of M]'The key has not yet been returned to me. [BigNameDesc of P] must still have it. You'll have to wait, or go find them.'[roman type][line break]";
			otherwise:
				say "[BigNameDesc of M] shrugs.[line break][speech style of M]'The key has not yet been returned to me. One of your fellow students must have it. You'll have to wait, or go find them.'[roman type][line break]";
		otherwise if able-to-pee is 0:
			say "[BigNameDesc of M] raises an eyebrow.[line break][speech style of M]'How would you manage that, considering what you're wearing?'[roman type][line break]";
		otherwise:
			let R be a random number between 1 and the rank of the player;
			if R > 2:
				say "[BigNameDesc of M] gives you an evil smirk.[line break][speech style of M]'If you want to pee so badly, you'll do it right here, in front of me.'[roman type][line break][big he of M] retrieves a large glass fishbowl, and places it on the ground in front of you.[line break][speech style of M]'Go on. Whenever you're ready.'[roman type][line break]Do you pee in the bowl in front of [NameDesc of M]?";
				if the player is consenting:
					let fishbowl-fill be the bladder of the player;
					if fishbowl-fill >= 6 - (the humiliation of the player / 10000):
						say "You [if there is pee covering clothing and the player is possessing a penis]get out your [player-penis] and [otherwise if there is pee covering clothing]expose your [vagina] and [end if]squat. With an excruciatingly loud tinkling sound, your [urine] flows down into the bowl. It's deeply humiliating. [strongHumiliateReflect][line break][speech style of M]'Good [boy of the player].'[roman type][line break][big he of M] coos, with an air of dominance.";
						now the bladder of the player is 0;
						if a random number between 1 and 2 is watersports fetish:
							say "[BigNameDesc of M] picks up the fishbowl and offers it to you. It looks like it's filled with about [fishbowl-fill] sips of [urine].[line break][speech style of M]'Now drink it.'[roman type][line break][big he of M] doesn't sound like [he of M][']s messing around.";
							let FF be fishbowl-fill;
							let FFR be 0;
							while FF > 0 and FFR is 0:
								say "There [if FF is 1]is 1 sip[otherwise]are [FF] sips[end if] of [urine] remaining. Take a sip?";
								if the player is consenting:
									say "You slurp up some more of your [urine].";
									FaceFill urine by 2;
									decrease FF by 1;
									if the total volume of face > 2, suggest swallowing;
								otherwise:
									say "You shake your head in refusal.";
									now FFR is 1;
							if FF >= a random number between 1 and fishbowl-fill:
								say "[line break][speech style of M]'That was not a request!'[roman type][line break][BigNameDesc of M] throws the fishbowl at your face!";
								AnnouncedSquirt urine on hair by fishbowl-fill;
								UrineTasteAddictUp 1;
								say severeHumiliateReflect;
							otherwise:
								if FF > 0, say "[BigNameDesc of M] wrinkles [his of M] nose.[line break][speech style of M]'I suppose that will do. You may go!'[roman type][line break]";
								otherwise say "[BigNameDesc of M] smirks.[line break][speech style of M]'Very good [boy of the player].'[roman type][line break]";
						otherwise:
							say "[BigNameDesc of M] [if M is in School08]puts the fishbowl inside a large drawer in [his of M] desk[otherwise]clicks [his of M] fingers, and the bowl disappears[end if]. Then [he of M] looks at you, and raises [his of M] eyebrows with impatience.[line break][speech style of M]'You can go now!'[roman type][line break]";
					otherwise:
						say "You try your best but you can't make yourself pee in front of [him of M].[line break][variable custom style]'I, um, I can't go...'[roman type][line break]You say weakly. [BigNameDesc of M] narrows [his of M] eyes.[line break][speech style of M]'Oh that's convenient, because I've just decided to revoke your toilet privileges... indefinitely.'[roman type][line break]";
						now headmistress-toilet-ban is true;
				otherwise:
					say "[variable custom style]'I've, err, changed my mind. I don't need to go after all!'[roman type][line break]You say weakly. [BigNameDesc of M] narrows [his of M] eyes.[line break][speech style of M]'Oh that's convenient, because I've just decided to revoke your toilet privileges... indefinitely.'[roman type][line break]";
					now headmistress-toilet-ban is true;
			otherwise if R is 2:
				say "[BigNameDesc of M] tilts [his of M] head.[line break][speech style of M]'Show me you can hold it first.'[roman type][line break][big he of M] points a finger at you, and shoots some kind of magic ray at you. Suddenly, you're completely paralysed. [big he of M] keeps you like that for several minutes, sitting there and watching you[if the player is bursting] as you fight to hold onto your bladder[end if].";
				compute bladder growth;
				if delayed urination is 1:
					say "It's no good, you can't hold it!";
					let K be a random worn knickers;
					if diaper lover > 0 and K is not diaper and K is not unremovable knickers:
						say "[speech style of M]'Don't you dare soil my office!'[roman type][line break]Just before it starts, [NameDesc of M] summons a thick diaper on you[if K is knickers], destroying your [ShortDesc of K] in the process[end if]!";
						if K is knickers, destroy K;
						let D be a random off-stage giant diaper;
						if D is nothing, now D is a random giant diaper;
						summon D cursed with quest;
					try urinating;
					say "[BigNameDesc of M] shakes [his of M] head.[line break][speech style of M]'Pathetic.'[roman type][line break]";
					if watersports fetish is 1 and the urine-puddle of the location of M > 0:
						say "[BigNameDesc of M] shoves your face into the puddle of [urine] on the floor.[line break][speech style of M]'Look what you did, you filthy slut!'[roman type][line break]The strong smell of your [urine] rushes into your nostrils, and when [NameDesc of M] pulls you back up, your cheek and nose now have a little pee on them.";
						UrineTasteAddictUp 1;
					say "[speech style of M]'Get out of my sight.'[roman type][line break][BigNameDesc of M] spits.";
				otherwise:
					say "[BigNameDesc of M] nods, and releases you from the magic paralysis.[line break][speech style of M]'Good. Okay, here you go.'[roman type][line break]";
					compute key drop of M;
			otherwise:
				say "[BigNameDesc of M] smiles.[line break][speech style of M]'Sure. Remember to get a teacher to supervise you.'[roman type][line break]";
				compute key drop of M.

To compute key drop of (M - headmistress):
	say "[big he of M] [if the player is in School08]placed the key on [his of M] desk[otherwise]tosses the key over to you.";
	now academy-toilet-key is in the location of the player;
	compute autotaking academy-toilet-key.

Headmistress ends here.
