Minotaur by Monster begins here.

A dungeon boss is a kind of monster.  A minotaur is a kind of dungeon boss.  A minotaur is usually father material.  A minotaur is usually male.  A minotaur is usually willing to do anal.  A minotaur is usually willing to do vaginal. The leftover-type of a minotaur is usually 107.

1 captive minotaur is in Dungeon36.  Figure of minotaur is the file "minotaur1.png".  Figure of minotaur caged is the file "minotaur2.png".  Figure of hulk is the file "hulk1.png".  The printed name of minotaur is usually "[if item described is in the location of the player][TQlink of item described][end if][input-style][monster-adjective of item described] [if mythical creature fetish is 0]hulk[otherwise]minotaur[end if][if the item described is captive] (caged)[otherwise if the sleep of the item described > 0] (fast asleep)[end if][shortcut-desc][roman type][if item described is in the location of the player][TQxlink of item described][verb-desc of item described][end if]".  The description of minotaur is usually "[MinotaurDesc]".  The text-shortcut of minotaur is "mi".  Understand "hulk" as minotaur.

To say MinotaurDesc:
	if images visible is 1:
		if mythical creature fetish is 1:
			if item described is captive, display the figure of minotaur caged;
			otherwise display the Figure of Minotaur;
		otherwise:
			display the Figure of Hulk;
	say "[one of][MinoDesc 1][or][MinoDesc 2][sticky random]";
	say "[one of][if pregnancy fetish is 1 and lactation fetish is 1 and inhuman pregnancy > 0 and mythical creature fetish is 1]Flared at the head and permanently erect is a [italic type]massive[roman type] 15 inch bull [manly-penis]. The arm-like tower of mottled black and pink meat is twitching and throbbing, as if the minotaur could cum at any time.[otherwise]Permanently erect and slowly drooling precum is a [italic type]massive[roman type] 15 inch [manly-penis]. It's almost as veiny and muscular as the rest of [his of the item described] body, twitching and pulsing as if ready to cum at any time.[end if][or][if pregnancy fetish is 1 and lactation fetish is 1 and inhuman pregnancy > 0 and mythical creature fetish is 1]Above a large, hairy nutsack is an arm-like tower of mottled black and pink cockmeat. At over a foot long and two inches thick, it's definitely the most [italic type]massive[roman type] penis you've ever seen. It is always erect, twitching, throbbing, and drooling precum, as if on the verge of orgasm at all times.[otherwise]Permanently erect and with thick, bulging veins, is a [italic type]massive[roman type] 15 inch [manly-penis]. It is twitching and steadily leaking precum onto the ground below, as if [he of the item described] could cum at any time.[end if][sticky random]";
	if item described is captive:
		if the bimbo of the player < 8:
			say "[first custom style][one of]A fight with him wouldn't be easy. Good thing he's in that cage.[or]With a body like that [he of the item described] could pin me down and pound me whenever [he of the item described] wanted to. I have to make sure [he of the item described] stays in that cage at all costs![or][big he of the item described]'s definitely a threat. I should make sure [he of the item described] stays in that cage at all costs.[or]Good thing [he of the item described]'s caged. [big he of the item described]'d probably beat me to a pulp just for looking at [him of the item described] funny.[or]Ugh. Anything with a [manly-penis] that big deserves to be caged.[or]Enjoy your cage time, fucker! That [manly-penis] is never coming anywhere near me![or]Man, [he of the item described] looks PISSED. I would be too if I had a [manly-penis] that big and couldn't fuck anything.[or]You know what, if I had a [manly-penis] that big I'd totally take some time in a cage. Better than being down here.[or][big he of the item described]'s pretty much the most manly thing in this game. Maybe the cage is some sort of metaphor?[or]Ugh, god [he of the item described] stinks so bad I can smell him from here.[in random order][roman type][line break]";
		otherwise if the bimbo of the player < 14:
			say "[variable custom style][one of]With a body like that [he of the item described] could pin me down and pound me whenever [he of the item described] wanted to. Good thing [he of the item described]'s caged...[or]Seeing that boner is making me feel weird. I should get out of here.[or][big he of the item described] probably hasn't had sex in a long time. I think it's best for both of us if [he of the item described] stays in that cage.[or]If I were dying of thirst, I might give [him of the item described] a blowjob. But only then![or][big he of the item described]'ll probably try to have that [manly-penis] in someone as soon as [he of the item described] gets out of that cage. Hmm...[or][big he of the item described] has a really strong smell. It's making me feel woozy.[or]If [he of the item described] ever gets out of that cage...it will be interesting.[or]You know, if I have to have sex, I'd prefer someone smaller than that. Good thing [he of the item described]'s up there and not down here.[or][big he of the item described] seems really strong. If [he of the item described] gets out of that cage I should keep an eye on [him of the item described] so he doesn't hurt anyone.[in random order][roman type][line break]";
		otherwise:
			say "[second custom style][one of]Wow, [his of the item described] [manly-penis] is really big.[or]With a body like that [he of the item described] could pin me down and take me whenever [he of the item described] wanted to. Sign me up![or]I could look at that [manly-penis] of [his of the item described] all day. And since [he of the item described]'s caged up, I can![or][big he of the item described] probably hasn't had sex in a long time. Poor thing, I should let [him of the item described] out![or][big his of the item described] [manly-penis] looks really suckable![or]I want that [manly-penis] in me. I need to find a way to let [him of the item described] out.[or][big he of the item described] looks so horny. I need to find a way to fix that.[or]I can smell [him of the item described] from here. It's making me feel so HORNY![or][big he of the item described] smells really good. So manly![or]I'd like to have [his of the item described] big [manly-penis] in me.[or]If [he of the item described] ever gets out of that cage, [he of the item described]'ll probably have a LOT of fun with me![or][big he of the item described] looks kind of dumb. If [he of the item described] escapes that cage I'll stay on my knees so [he of the item described]'ll know what to do![in random order][roman type][line break]";
	otherwise:
		if the bimbo of the player < 10:
			say "[first custom style][one of]I'm not sure I'm prepared to fight [him of the item described], but it's going to happen one way or the other.[or][big he of the item described] still seems so angry. Letting [him of the item described] out of that cage was a bad idea.[or]Holy shit, look at [him of the item described] go! [big he of the item described]'s a killing machine![or][big he of the item described] looks tough. I'm not really sure I can take [him of the item described].[or]Oh god, I think [his of the item described] thing is pointing at me![or]I hope [he of the item described] doesn't want to 'thank' me for letting [him of the item described] out.[or][big he of the item described] doesn't seem to care what [he of the item described] attacks.[or]We're going to fight eventually, but it doesn't have to happen right this minute.[or]As long as I can run away from [him of the item described], I'll be in control.[or]I should stay out of [his of the item described] way.[or]If [he of the item described] decides to fight me, it's a good idea to run.[or]I don't even want to know what [he of the item described] might do with those horns.[in random order][roman type][line break]";
		otherwise if the bimbo of the player < 14:
			say "[variable custom style][one of]I'm not sure what's going to happen now that [he of the item described]'s out of his cage. But I'm sure I'll be sore afterwards.[or][big he of the item described] seems so angry. Maybe if I get [him of the item described] off before I fight [him of the item described] it will be easier.[or][big he of the item described] looks pretty tough. Maybe if I let [him of the item described] have [his of the item described] way with me [he of the item described]'ll leave me alone for a while.[or][big his of the item described] [manly-penis] looks bigger now that [he of the item described]'s out of his cage.[or]I think [he of the item described] wants to thank me for letting [him of the item described] out. Maybe I should let [him of the item described]. Just once.[or]He's fucking everything in sight! I might as well get on my knees, it will happen anyway.[or][big he of the item described] doesn't look that fast. If I end up losing the fight, and [he of the item described] fucks me, it's still kind of on my terms.[or]I should try to keep away from [him of the item described]. At least until I know that boner of [his of the item described] is only pointing at me.[in random order][roman type][line break]";
		otherwise:
			say "[second custom style][one of][big he of the item described]'s out of [his of the item described] cage, now [he of the item described] can fuck me![or]Don't worry baby, I know how to fix that boner![or]Look at that BIG BONER![or][big he of the item described] looks horny. We have that much in common, teehee![or]I only want to fight [him of the item described] after [he of the item described]'s put a couple loads in me. No point attacking someone who's got enough energy to fuck you.[or]Wow! [big his of the item described] [manly-penis] is SO big! I wonder how it would feel inside me?[or]I hope [he of the item described] wants to thank me for letting [him of the item described] out![or]I should let [him of the item described] have fun with everyone else before I get on my knees. That way, I'll have [him of the item described] all to myself.[or]I really want to get on my knees and stuff that [manly-penis] in my mouth![or][big he of the item described] looks like [he of the item described] needs a good blowjob.[or]I want to stay nearby in case [he of the item described] wants to fuck me.[or]I want to stay nearby. It's so fun watching [him of the item described] obliterate everyone in [his of the item described] path![or][big he of the item described] looks so angry! I know just the way to get [him of the item described] nice and calmed down![in random order][roman type][line break]".
	
To say MinoDesc (N - a number):
	if mythical creature fetish is 1:
		if N is 1, say "A nude, and extremely powerful looking minotaur. He has a bestial, cow-like face, with small beady black eyes and large flat nostrils, which are connected by a [one of]brass[or]iron[or]onyx[or]gold[sticky random] ring. Just like a bull, his head is covered in thick [one of]auburn[or]gold[or]black[or]brown[or]ashen[sticky random] fur, and two long [one of]black[or]cream-coloured[or]white[sticky random] horns extend from just above his floppy bovine ears. He has wide, masculine shoulders, and a thick barrel chest, covered with a thick Austin Powers style rug. In fact, his entire body is coated with sheets of rippling muscle, which looks as though it could burst free of his [one of]golden brown skin[or]dark brown skin[or]skin[sticky random] at any time. His legs are furry, and end in gray hooves. Hanging between them is a long tail, but you'd be lying if you said that's what really drew your attention.";
		if N is 2, say "A naked bull-man with rippling muscles covering his entire body. His face is bestial, covered in thick [one of]auburn[or]gold[or]black[or]brown[or]ashen[sticky random] fur, with big flat nostrils and beady black eyes. Two long [one of]black[or]bony[or]white[sticky random] horns extend from just above his floppy bovine ears, one of which is pierced with a small [one of]brass[or]iron[or]gold[or]onyx[sticky random] ring. He is seething with rage, his wide shoulders covered in a fine sheen of sweat, and his thick barrel chest heaves with every breath. His skin is [one of]dark brown[or]golden brown[or]a fair brown[sticky random] and from the waist down he is covered in fur, with gray hooves in place of feet. Hanging between his legs is a long tail, but that's not what draws your attention.";
	otherwise:
		say "A naked hulk of a man with rippling muscles covering his entire body. He looks like the result of some mad science experiment gone wrong.  He is seething with rage, his wide shoulders covered in a fine sheen of sweat and his thick barrel chest heaving with every breath.".


To set up (M - a minotaur):
	if M is alive: [We don't want to reset the minotaur if he's dead]
		reset M;
		now the monstersetup of M is 1;
		now M is large;
		now the difficulty of M is 12;
		now the variety of M is 0;
		now the sex-length of M is 3;
		now the health of M is the maxhealth of M;
		if mythical creature fetish is 0, now the text-shortcut of M is "hu".

To decide which number is the girth of (M - a minotaur):
	decide on 8.

This is the spawn initial minotaur rule:
	if debugmode > 1, say "Now summoning minotaur.";
	if diaper quest is 1:
		repeat with M running through minotaurs:
			now M is not dungeon dwelling;
			remove M from play;
	otherwise:
		let M be a random minotaur;
		if M is released and M is alive, summon M in the dungeon.
The spawn initial minotaur rule is listed in the setting up dungeon monsters rules.

Definition: a minotaur (called M) is concealment immune: [Can the monster ignore salves of concealment, butt slut, etc.]
	decide yes.

Definition: a minotaur (called M) is able to remove cursed plugs: [Can the monster remove all butt plugs?]
	decide yes.

Definition: A minotaur (called M) is willing to shag:
	if the sleep of M is 0 and the scared of M is 0 and the boredom of M < 120 and M is released, decide yes;
	decide no.

To say PregGrowth of (M - a minotaur):
	say "the [if mythical creature fetish is 0][one of]minotaur[or]calf[or]half-bull[purely at random][otherwise]hulk[end if]".
	
To compute labour to (M - a minotaur):
	if M is regional and M is alive:
		compute pregnancy clothing displacement;
		say "[PregFlav][if the father is in the location of the player]The [father][otherwise]The [father] appears almost as if on cue!  [big he of M][end if] gets on all fours and grunts gently as [he of M] watches the [if mythical creature fetish is 0]half-giant baby emerge[otherwise]minotaur calf[end if].  After the painful but relatively fast birthing, [he of M] roars and picks up the wailing pup in [his of M] arms.  You can do nothing but watch as [he of M] takes your new son away.[line break][variable custom style][if the father is mating]I guess it's only right that it should join its siblings...[otherwise if mythical creature fetish is 1]I can't believe I just gave birth to a half-bull...[otherwise]I can't believe I just gave birth to a half-giant...[end if][roman type][line break]";
		if the father is in the location of the player:
			say "For some reason, you are filled with a sense of deep fulfilment.  You feel great!";
			StrengthUp 1;
			DexUp 1;
			IntUp 1;
	otherwise if M is alive:
		Delay Labour.
	
To say LeftoverDesc (N - 107):
	say "The corpse of a minotaur lies on the ground. The back of its skull is missing, and a huge bloody hole is all that remains of its brain.[line break]".

Part 1 - Perception

To compute perception of (M - a minotaur):
	say "The [M] notices you[if the player is sluttily dressed][one of], even though you're not completely sure if [he of M] has more of the mind of a beast or the mind of a man.[or].[stopping][otherwise]![end if]";
	anger M;
	if M is released:
		if M is mating:
			if the pregnancy of the player > 0: 
				say "He snorts gently and nuzzles your belly.";
				now the boredom of M is 50;
				calm M;
			otherwise:
				say "It looks like [he of M] wants more offspring...";				
		otherwise:
			say "He does not look friendly.  In fact, you can see some fierce hunger in his eyes.";
	otherwise:
		say "He growls angrily at his captivity.".

To say BecomesBoredFlav of (M - a minotaur):
	say "The [M] seems to lose interest in you for now.".
	
	
Part 2 - Motion, Seeking and Sleeping

To compute motion of (M - a minotaur):
	if M is released:
		now neighbour finder is the location of M;
		let A be a random N-viable direction;
		if A is a random N-viable direction and the room A from the location of M is not Dungeon12 and the room A from the location of M is not Dungeon41:
			try M going A;
		otherwise if A is a random N-viable direction and the room A from the location of M is not Dungeon12 and the room A from the location of M is not Dungeon41:
			try M going A;
		MonsterStomp M.

To compute (M - a minotaur) seeking (D - a direction):
	if M is released, try M going D.
	
To compute sleeping of (M - a minotaur): [This will need to change if sleep / boredom / scared mechanics change]
	if the scared of M > 0:
		decrease the scared of M by seconds;
		if the scared of M < 0, now the scared of M is 0;
	if the boredom of M > 0:
		decrease the boredom of M by seconds;
		if the boredom of M <= 0:
			now the boredom of M is 0;
	if the sleep of M > 0:
		decrease the sleep of M by seconds;
		if the sleep of M <= 0:
			now the sleep of M is 0;
			if M is in the location of the player, say "The [M] wakes up!". [Very slight deviation from the default.]
			

Part 3 - Minotaur Stomp

To MonsterStomp (M - a monster):
	let stomped be 0;
	repeat with N running through monsters in the location of M:
		if stomped is 0 and N is not penetrating a body part and N is not grabbing the player and N is not M:
			compute M stomping N;
			now stomped is 1.

[To compute (M - a monster) stomping (N - a monster): Consider this a template!
	say "The [M] kills the [N]!";
	destroy N;
	let L be a random off-stage leftover;
	now L is in the location of M;
	now the leftover-type of L is the leftover-type of N.]

To compute (M - a monster) stomping (N - a monster):[We keep this completely generic to avoid potential conflicts with the specific instances of each monster.]
	say "The [M] is glitching, it doesn't know how to deal with the [N]. Report this bug please!";[This will not happen with default game but with added monsters it allows the ability for them to be dungeon dwelling and have a scene where the minotaur kills them.]	

Part 4 - Combat

Section 1 - Attack

Definition: a minotaur (called M) is automatically tempted:
	if the bimbo of the player >= 20, decide yes;
	if the appearance of the player > 7, decide yes;
	if class of the player is cowgirl, decide yes;
	if thickness of hips > 5 and pregnancy fetish is 1, decide yes;
	if wanking > 1, decide yes;
	if the player is monster fucked, decide yes;
	decide no.

Definition: a minotaur (called M) is eager to slap:
	if a random number between 1 and 4 is 1, decide yes;
 	decide no.

The minotaur priority attack rules is a rulebook.  The priority attack rules of a minotaur is usually the minotaur priority attack rules.

This is the minotaur still caged rule:
	if current-monster is captive:
		if current-monster is automatically tempted:
			if (a random number between 1 and 4 is 1 or wanking > 1) and the blue-balls of current-monster < 5:
				say "[one of]The [current-monster] turns toward you, grunting primally as [he of current-monster] vigorously thrusts his giant [manly-penis] through the sturdy bars of his cage.[or]The [current-monster] stares holes in your skull, drooling slightly as [he of current-monster] roughly humps the space between the heavy bars of his cage..[or]The [current-monster] never once looks away from you, hungrily thrusting [his of current-monster] massive [manly-penis] through the bars of his cage, panting roughly as [he of current-monster] smears them with thick, slimy pre-cum.[in random order]";
				increase the blue-balls of current-monster by 1;
			otherwise:
				if the blue-balls of current-monster > 4:
					say CagedMinotaurMessFlav;
					now the blue-balls of current-monster is 0;
				otherwise:
					say ArousedCagedMinotaurFlav;
		otherwise:
			say "[one of][CagedMinotaurFlav][or][or][or][in random order]";
		rule succeeds.
The minotaur still caged rule is listed first in the minotaur priority attack rules.

To say CagedMinotaurFlav:
	say "[one of]The [current-monster] angrily shakes the metal bars of [his of current-monster] cage.[or]The [current-monster] paws at the floor of [his of current-monster] cage, snarling with anger.[or]The [current-monster] snarls angrily at [his of current-monster] captive state.[or]The [current-monster] bangs at the bars of [his of current-monster] cage. The sound of clanging metal rings through the room.[or]The [current-monster] throws [him of current-monster]self at the bars of [his of current-monster] cage. You try to ignore [his of current-monster] angry snarling.[in random order]".

To say ArousedCagedMinotaurFlav:
	say "[one of]The [current-monster] thrusts his arm through a gap in his cage, grasping at the empty air between you.[or]The [current-monster] fixes you with an intimidating stare, furiously masturbating with obvious hunger in his eyes.[or]The [current-monster] stares at you with obvious and unrestrained arousal.[or]The [current-monster] stares at you with unrestrained lust in his eyes. [if the bimbo of the player < 8]You feel uncomfortable.[otherwise if the bimbo of the player < 12][line break][otherwise]You feel sexy![end if][or]The [current-monster] seems incredibly taken with your crotch.[in random order]".

To say CagedMinotaurMessFlav:
	let L be a random number between 2 and 4;
	say "The [current-monster] roars as his cock explodes all over the floor and walls around him. [run paragraph on]";
	if a random number between 1 and the dexterity of the player > 5 and bukkake fetish is 1:
		say "It comes out so violently that you can't get out of the way, and you take several ropes all over your face.";
		if image cutscenes is 1 and mythical creature fetish is 1, display figure of minotaur cutscene 6;
		CumFaceUp 2;
		increase the semen-puddle of Dungeon36 by L - 2;
	otherwise:
		say "It comes out so violently that you have to dodge out of the way to avoid getting slammed into the wall.";
		increase the semen-puddle of Dungeon36 by L.

This is the minotaur prioritises defeating others rule:
	if the number of monsters in the location of current-monster > 1: [1 is going to be the minotaur itself]
		MonsterStomp current-monster;
		rule succeeds.
The minotaur prioritises defeating others rule is listed last in the minotaur priority attack rules.

To compute the flying player taunting of (M - a minotaur):
	say "[one of]The [M] clearly refuses to move from your location until you stop floating.[or][stopping]".

The latex punishment rule of a minotaur is usually the no latex punishment rule.

To say LandingTaunt of (M - a minotaur):
	say "The [M] growls with impatience.";
	humiliate 50.

To decide which number is the rounds of sex left of (M - a minotaur):
	decide on 1. [We don't do climax the standard way]

To say AssholePenetrationFlav of (M - a minotaur):[TODO: EXPAND DONG]
	say "The [M] forces [himself of M] into your [asshole]!".;
	if image cutscenes is 1 and mythical creature fetish is 1, display figure of minotaur cutscene 3.

To compute anal sex of (M - a minotaur):
	if the sex-length of M > 0:
		compute fuckhole sex of M;
		if the soreness of asshole < 9 or the reaction of the player is 0, ruin asshole;
		if the reaction of the player > 0:
			if the soreness of asshole > 8, say "[one of]Your lack of resistance against the [M] is preventing you from going over the edge and fainting, but it is extremely humiliating to let yourself get fucked by a half bull.[or][or][or][or][cycling]";
	otherwise if the health of M > 5:
		compute fuckhole sex of M;
		AssFill 2;
		if the soreness of asshole < 10 or the reaction of the player is 0, ruin asshole;
	otherwise:
		if the dexterity of the player + the strength of the player > 20 + a random number between 1 and 10:
			say "[one of]The [M] is completely spent, and collapses on top of you, half-crushing your body.[or]The [M], completely spent, collapses on top of you, half crushing your body.[or]The [M] collapses on top of you, spent.[at random] You barely manage to crawl out from underneath him, [if the soreness of asshole > 4 or the openness of asshole < 8]wincing as his[one of] shrinking[or] cum-slicked[or][or][at random] [manly-penis] [one of]slides out[or]pops out[at random] of your [asshole][otherwise]sighing as his[one of] shrinking[or] cum-slicked[or][or][at random] [manly-penis] slides out of your [asshole].";
			now M is not penetrating asshole;
		otherwise:
			say "[one of]The [M] is completely spent, and collapses on top of you, crushing your body.  You can't manage to crawl out from underneath his weight![or]The [M], spent, collapses on top of you, crushing you under his full weight. You try, but you don't manage to crawl out from underneath him![or]The [M] lets go of you as the torrent finally stops, collapsing on top of you just before you manage to crawl out from underneath him.[or]The [M], completely spent, collapses on top of you, pinning you down with his full weight. No matter what you do, you can't manage to crawl out from underneath him![at random] You're going to have to wait [one of]like this with his [manly-penis] in your [asshole] until he wakes up...[or]with his [manly-penis] buried in your asshole until he wakes up...[at random]";
			if image cutscenes is 1 and mythical creature fetish is 1, display figure of minotaur cutscene 2;
		AssFill 2;	
		orgasm M;
		now the sleep of M is 150.

To compute vaginal sex of (M - a minotaur):
	if the sex-length of M > 0:
		compute fuckhole sex of M;
		if the soreness of vagina < 9 or the reaction of the player is 0, ruin vagina;
		if the reaction of the player > 0:
			if the soreness of vagina > 8, say "[one of]Your lack of resistance against the [M] is preventing you from going over the edge and fainting, but it is extremely humiliating to let yourself get fucked by a half bull.[or][or][or][or][cycling]";
	otherwise if the health of M > 5:
		compute fuckhole sex of M;
		WombFill 2;
		if the soreness of vagina < 10 or the reaction of the player is 0, ruin vagina;
	otherwise:
		WombFill 2;
		say "[one of]The [M] seems completely spent - he pulls your bloated body off his shaft and leaves you to recover.[or]The [M], spent, pulls your bloated body off his deflating shaft and leaves you to recover.[or]The [M] pulls your bloated body off his shaft as his ejaculation finally ends, leaving you on the floor to recover.[or]The [M] removes his deflating shaft from your [vagina], completely spent.[at random]  He stalks off, probably looking for another woman to breed.";
		let H be a random off-stage cow ears;
		let T be a random bred by a bull tattoo;
		if there is a worn tattoo and T is not worn:
			summon T;
			say "The words 'BRED BY A BULL' materialise on your left side!  They are now permanently inked onto your skin.";
		otherwise if H is actually summonable and lactation fetish is 1: 
			say "[bold type]A pair of fake cow ears materialise on your head.[roman type][line break]";
			now H is worn by the player;
		orgasm M;
		bore M for 500 seconds.

To orgasm (M - a minotaur):
	if the class of the player is priestess and (the virgin of the player is 0 or the player is male):
		say "You feel a surge in your holy aura and a voice appears in your head: 'Good work, Sister!'";
		decrease the charge of the dungeon altar by 300;
	if the class of the player is princess, follow the betrothal rule.

To compute fuckhole sex of (M - a minotaur):
	let F be a random fuckhole penetrated by M;
	if the sex-length of M > 0:
		if the sex-length of M is 1, say "The [M] roars as you feel [one of]him start to ejaculate[or]cum spurting into your [variable F] in waves[or]his [manly-penis] erupt inside of you[or]his balls begin emptying themselves inside of you[in random order]!  But he[one of] doesn't stop fucking you[or]'s still going![or] doesn't stop plowing you at all.[or] doesn't relent with his brutal pounding![at random]";
		otherwise say "[one of]The [M] is brutally plowing away at your [variable F]![or]The [M] grunts primally as he pounds your poor [variable F]![or]The [M] growls hungrily as his [if mythical creature fetish is 1]massive horsecock[otherwise]massive cock[end if] jack-hammers your [variable F].[or]The [M] holds you with a rigid death-grip as he slams in and out of your [variable F].[or]The [M] holds you down with an ironclad grip as he brutally pounds your [variable F]![or]The [M]'s massive [if mythical creature fetish is 1]furry [otherwise]nutsack[end if] slaps audibly against your taint as he brutally jack-hammers your [variable F].[in random order]";
		decrease the sex-length of M by 1;
		if the health of M > 10, decrease the health of M by 5;
	otherwise:
		say "[one of]The [M] is still fucking your [variable F][or]The [M] continues to pound your [variable F][or]The [M] relentlessly plows your [variable F][or]The [M] plows your [variable F] without stopping[or]The [M]'s massive [if mythical creature fetish is 1]horsecock[otherwise]cock[end if] continues to slam in and out of your [variable F][at random]! [run paragraph on]";
		if the reaction of the player is 0, say "[one of]No matter much you clench your muscles, you can't stop the torrent of [semen] into your [BellyDesc][or]Your resistance does nothing to stop the virtually endless torrent of [semen] into your belly.[or]Even with your struggling, he somehow finds more [semen] to cram into your [BellyDesc][or]No matter what you do, his [semen] continues to torrent into your [BellyDesc] at full force.[at random]";
		otherwise say "[one of]Relaxing your [variable F], you accept the virtually endless torrent of [semen] into your [BellyDesc].[or]You do your best to stay relaxed as his [semen] spurts into your belly.[or]You can feel his [manly-penis] twitching as it continues to spurt [semen] into your [BellyDesc].[or]Relaxing your inner muscles, you accept the unending torrent of [semen] into your [BellyDesc].[at random]";
		decrease the health of M by 5;
		if image cutscenes is 1 and mythical creature fetish is 1 and the health of M <= 5, display figure of minotaur cutscene 5.

To say StrikingSuccessFlav of (M - a minotaur) on (B - a body part):
	say "The [M] pummels you [TargetName of B] with his fists!";
	BodyRuin 1. [The minotaur automatically does 1 damage, and 2 if he hits.]
		
To say StrikingFailureFlav of (M - a minotaur) on (B - a body part):
	say "You try to dodge the [M]'s fist, but he still manages to land a glancing blow!  Ouch!";
	BodyRuin 1.

To compute striking attack of (M - a minotaur):
	let B be a random body part;
	if B is a fuckhole, now B is hips;
	if B is hair, now B is breasts;
	if B is not hips and B is not breasts, now B is belly;
	if the accuracy roll of M > the dexterity of the player:
		say "[StrikingSuccessFlav of M on B]";
		compute M striking B;
	otherwise:	
		say "[StrikingFailureFlav of M on B]".

The monster fucked taunting rule of a minotaur is usually the minotaur replaces the monster rule.
This is the minotaur replaces the monster rule:
	let M be current-monster;
	let N be a random monster penetrating a fuckhole;
	if there is a monster penetrating asshole, now N is a random monster penetrating asshole; [This is the one who's important to get rid of since this is where we're going.]
	say "The [M] kicks the [N] out of the way, and takes over!  He [one of]sticks his giant [manly-penis] in your [asshole],[or]shoves his massive [manly-penis] up your ass[or]jams his giant dick into your [asshole][or]clumsily rams his huge dick in your [asshole][at random] and [one of]starts cumming[or]starts to cum[at random] almost immediately!  [variable custom style][if the anal sex addiction of the player < 4]Oh Fuck![otherwise if the anal sex addiction of the player < 6]I thought I was safe with the [N] inside me![otherwise]Finally, a real dick![end if][roman type]";
	if image cutscenes is 1 and mythical creature fetish is 1, display figure of minotaur cutscene 3;
	now the sex-length of M is 0;
	now the chosen-orifice of M is asshole;
	destroy N;
	now M is penetrating asshole;
	ruin asshole;
	rule succeeds.

To compute (M - a minotaur) attacking (C - a clothing):
	if C is displacable:
		say "The [M] pulls the crotch of your [printed name of C] to the side!";
		displace C;
	otherwise:
		say "The [M] rips off your [printed name of C]!";
		destroy C.
	
To compute (M - a minotaur) attacking (C - a clothing):
	say "The [M] tears off your [printed name of C]!";
	destroy C.

To compute (M - a minotaur) removing (X - a thing):
	say "The [M] forcefully and roughly pulls the [printed name of X] out of your [asshole] and drops it on the ground!";		
	now X is in the location of the player;
	if X is anal beads, ruin asshole times the notch-taken of X / 2;
	dislodge X.

To decide if (M - a minotaur) is willing to creampie (F - a vagina):
	if the player is male, decide no;
	if pregnancy fetish is 0 or the pregnancy of the player is not 0, decide no;
	if the class of the player is cowgirl, decide yes;
	if the thickness of hips > 6 - a random number between 1 and the pregnancy rate of the player, decide yes;
	decide no.

To compute the orifice choosing of (M - a minotaur):
	if M is willing to creampie vagina:
		now the chosen-orifice of M is vagina;
	otherwise:
		now the chosen-orifice of M is asshole.

To compute (M - a minotaur) entering vagina:
	now the sex-length of M is the health of M / 10;
	say "The [M] stares at your body and sniffs your [vagina], assessing you.  He seems to make up his mind that you are a suitable breeding partner and poises his huge penis with the tip at the entrance to your [vagina].  [one of]With very little effort, he forces his[or]With no effort at all, he shoves his[or]Without the slightest hint of difficulty he rams his[or]Without pausing, he forces his[at random] giant [manly-penis] in, and starts fucking you vigorously.  [if the sex-length of M is 0]Within seconds, he has started cumming inside you![otherwise if the class of the player is cowgirl]You find yourself mooing submissively as your [vagina] is instantly stretched beyond belief by his huge bull [manly-penis].[otherwise]Your [vagina] is instantly - and painfully - stretched beyond belief.[one of][line break][variable custom style]Could I get pregnant from this bull?![roman type][line break][or][stopping][end if]";
	if image cutscenes is 1 and mythical creature fetish is 1, display figure of minotaur cutscene 4;
	now M is penetrating vagina;
	ruin vagina.

To compute (M - a minotaur) entering asshole:
	now the sex-length of M is the health of M / 10;
	say "The [M] [one of]forcefully pushes his[or]clumsily rams his[or]jams his[or]forces his[at random] giant [manly-penis] into your [asshole], and starts fucking you furiously!  [if the sex-length of M is 0]Within seconds, he has started cumming inside you![otherwise]Your [asshole] is instantly stretched beyond belief.[end if]";
	now M is penetrating asshole;
	ruin asshole.

Section 2 - Damage

To compute damage of (M - a minotaur):
	if the health of M > 0:
		if the sleep of noun is 0:
			if M is uninterested:
				say "He growls viciously!  Uh-oh...";
				now M is interested;
				anger M;
			otherwise:
				say DamageReaction (the health of M) of M;
		otherwise:
			anger M;
			now M is interested;
			now the sleep of M is 0;
			say "He wakes up, and immediately stands up to full height with a powerful roar!";
	otherwise:
		compute death of M;

To say DamageReaction (N - a number) of (M - a minotaur):
	if N > (the maxhealth of M / 4) * 3:
		say "The [M] effortlessly shrugs off your hit!";
	otherwise if N > (the maxhealth of M / 4) * 3:
		say "[big he of M] snarls through clenched teeth!";
	otherwise if N > (the maxhealth of M / 4):
		say "[big he of M] roars savagely, taking the hit!";
	otherwise:
		say "[big he of M] seems to be breathing more heavily, as if [he of M]'s getting tired!".

To compute unique death of (M - a minotaur):
	let H be a random off-stage minotaur horn;
	if mythical creature fetish is 1 and H is a thing:
		say "The minotaur falls to the ground, dead.  One of his horns smacks into the ground and snaps clean off.  The [H] appears to be hollow - you could almost use this as a drinking vessel!";
		now H is in the location of the player;
		compute autotaking H;
	otherwise:
		let B be a random off-stage potion of bull strength;
		say "The [M] falls to the ground, defeated.  [if B is potion]He dropped a [printed name of B]![end if]";
		if B is potion:
			now B is in the location of the player;
			compute autotaking B.

To uniquely destroy (M - a minotaur):
	now M is captive.


To loot (M - a minotaur):
	let X be a random off-stage plentiful ring;
	unless X is nothing:
		now X is in the location of the player;
		now X is solid gold;
		set shortcut of X;
		say "The defeated [M] [if the loot dropped of M > 0]also [end if]dropped a [printed name of X]!";
		increase the loot dropped of M by 1;
		compute autotaking X.



Minotaur ends here.
