Minotaur by Monster begins here.

minotaur is a dungeon boss. minotaur is male. The leftover-type of minotaur is 107. minotaur has a number called nutting-turns. The nutting-turns of minotaur is usually 0.

Definition: A minotaur is dungeon dwelling: decide yes.

minotaur is in Dungeon36. minotaur is caged.
Figure of minotaur is the file "NPCs/Bosses/Minotaur/minotaur1.png". Figure of minotaur caged is the file "NPCs/Bosses/Minotaur/minotaur2.png". Figure of hulk is the file "NPCs/Bosses/Minotaur/hulk1.png".

Figure of Minotaur Cutscene 1 is the file "Special/Cutscene/cutscene-minotaur-stomp1.png".
Figure of Minotaur Cutscene 2 is the file "Special/Cutscene/cutscene-minotaur-sleep1.png".
Figure of Minotaur Cutscene 3 is the file "Special/Cutscene/cutscene-minotaur-fuck1.png".
Figure of Minotaur Cutscene 4 is the file "Special/Cutscene/cutscene-minotaur-fuck2.png".
Figure of Minotaur Cutscene 5 is the file "Special/Cutscene/cutscene-minotaur-climax1.png".
Figure of Minotaur Cutscene 6 is the file "Special/Cutscene/cutscene-minotaur-splash1.png".
Figure of Minotaur Cutscene 7 is the file "Special/Cutscene/cutscene-minotaur-ready1.png".

To decide which figure-name is the monster-image of (M - minotaur):
	if lady fetish is 1, decide on figure of missing NPC;
	if mythical creature fetish is 1:
		if M is penetrating a body part and M is asleep, decide on figure of minotaur cutscene 2;
		if M is caged, decide on figure of minotaur caged;
		otherwise decide on figure of minotaur;
	otherwise:
		decide on figure of hulk;
To decide which figure-name is the anal-sex-monster-image of (M - minotaur):
	if lady fetish is 0 and mythical creature fetish is 1, decide on figure of minotaur cutscene 3;
	decide on figure of missing NPC.
To decide which figure-name is the vaginal-sex-monster-image of (M - minotaur):
	if lady fetish is 0 and mythical creature fetish is 1, decide on figure of minotaur cutscene 4;
	decide on figure of missing NPC.



The text-shortcut of minotaur is "mi". Understand "large", "hulk" as minotaur.

To say ShortDesc of (M - minotaur):
	say "[if mythical creature fetish is 0]hulk[otherwise]minotaur[end if]".

To say MediumDesc of (M - minotaur):
	say "large [ShortDesc of minotaur][if M is caged] (caged)[end if]".

Definition: minotaur is willing to do oral: decide no.

Definition: minotaur is willing to do titfucks: decide no.


To say LongDickDesc of (M - a minotaur):
	if pregnancy fetish is 1 and lactation fetish is 1 and inhuman pregnancy > 0 and mythical creature fetish is 1:
		if full-lady fetish is 1, say "equine strap-on";
		otherwise say "15 inch bull-penis";
	otherwise:
		if full-lady fetish is 1, say "15 inch strap-on";
		otherwise say "15 inch penis".


To say MonsterDesc of (M - minotaur):
	if mythical creature fetish is 1, say "A naked bull-[man of M] with rippling muscles covering [his of M] entire body. [big his of M] face is bestial, covered in thick [one of]auburn[or]gold[or]black[or]brown[or]ashen[sticky random] fur, with big flat nostrils and beady black eyes. Two long gray horns extend from just above [his of M] floppy bovine ears, one of which is pierced with a small [one of]brass[or]iron[or]gold[or]onyx[sticky random] ring. [big he of M] is seething with rage, [his of M] wide shoulders covered in a fine sheen of sweat and [if lady fetish is 1]her heavy bosom[otherwise]his thick barrel chest[end if] heaves with every breath. Between [his of M] legs is a fully erect, [LongDickDesc of M], which twitches and throbs as if it could blow at any time.";
	otherwise say "A naked hulk of a [man of M] with rippling muscles covering [his of M] entire body. [big he of M] looks like the result of some mad science experiment gone wrong. [big he of M] is seething with rage, [his of M] wide shoulders covered in a fine sheen of sweat and [if lady fetish is 1]her heavy bosom[otherwise]his thick barrel chest[end if] heaving with every breath. Between [his of M] legs is a fully erect [LongDickDesc of M], which twitches and throbs as if it could blow at any time.".

To say MonsterComment of (M - minotaur):
	if item described is caged:
		if the bimbo of the player < 8:
			say "[first custom style][one of]A fight with him wouldn't be easy. Good thing he's in that cage.[or]With a body like that [he of the item described] could pin me down and pound me whenever [he of the item described] wanted to. I have to make sure [he of the item described] stays in that cage at all costs![or][big he of the item described]'s definitely a threat. I should make sure [he of the item described] stays in that cage at all costs.[or]Good thing [he of the item described]'s caged. [big he of the item described]'d probably beat me to a pulp just for looking at [him of the item described] funny.[or]Ugh. Anything with a [manly-penis] that big deserves to be caged.[or]Enjoy your cage time, fucker! That [manly-penis] is never coming anywhere near me![or]Man, [he of the item described] looks PISSED. I would be too if I had a [manly-penis] that big and couldn't fuck anything.[or]You know what, if I had a [manly-penis] that big I'd totally take some time in a cage. Better than being down here.[or][big he of the item described]'s pretty much the most manly thing in this game. Maybe the cage is some sort of metaphor?[or]Ugh, god [he of the item described] stinks so bad I can smell him from here.[in random order][roman type][line break]";
		otherwise if the bimbo of the player < 14:
			say "[variable custom style][one of]With a body like that [he of the item described] could pin me down and pound me whenever [he of the item described] wanted to. Good thing [he of the item described]'s caged...[or]Seeing that boner is making me feel weird. I should get out of here.[or][big he of the item described] probably hasn't had sex in a long time. I think it's best for both of us if [he of the item described] stays in that cage.[or]If I were dying of thirst, I might give [him of the item described] a blowjob. But only then![or][big he of the item described]'ll probably try to have that [manly-penis] in someone as soon as [he of the item described] gets out of that cage. Hmm...[or][big he of the item described] has a really strong smell. It's making me feel woozy.[or]If [he of the item described] ever gets out of that cage... it will be interesting.[or]You know, if I have to have sex, I'd prefer someone smaller than that. Good thing [he of the item described]'s up there and not down here.[or][big he of the item described] seems really strong. If [he of the item described] gets out of that cage I should keep an eye on [him of the item described] so he doesn't hurt anyone.[in random order][roman type][line break]";
		otherwise:
			say "[second custom style][one of]Wow, [his of the item described] [manly-penis] is really big.[or]With a body like that [he of the item described] could pin me down and take me whenever [he of the item described] wanted to. Sign me up![or]I could look at that [manly-penis] of [his of the item described] all day. And since [he of the item described]'s caged up, I can![or][big he of the item described] probably hasn't had sex in a long time. Poor thing, I should let [him of the item described] out![or][big his of the item described] [manly-penis] looks really suckable![or]I want that [manly-penis] in me. I need to find a way to let [him of the item described] out.[or][big he of the item described] looks so horny. I need to find a way to fix that.[or]I can smell [him of the item described] from here. It's making me feel so HORNY![or][big he of the item described] smells really good. So manly![or]I'd like to have [his of the item described] big [manly-penis] in me.[or]If [he of the item described] ever gets out of that cage, [he of the item described]'ll probably have a LOT of fun with me![or][big he of the item described] looks kind of dumb. If [he of the item described] escapes that cage I'll stay on my knees so [he of the item described]'ll know what to do![in random order][roman type][line break]";
	otherwise:
		if the bimbo of the player < 10:
			say "[first custom style][one of]I'm not sure I'm prepared to fight [him of the item described], but it's going to happen one way or the other.[or][big he of the item described] still seems so angry. Letting [him of the item described] out of that cage was a bad idea.[or]Holy shit, look at [him of the item described] go! [big he of the item described]'s a killing machine![or][big he of the item described] looks tough. I'm not really sure I can take [him of the item described].[or]Oh god, I think [his of the item described] thing is pointing at me![or]I hope [he of the item described] doesn't want to 'thank' me for letting [him of the item described] out.[or][big he of the item described] doesn't seem to care what [he of the item described] attacks.[or]We're going to fight eventually, but it doesn't have to happen right this minute.[or]As long as I can run away from [him of the item described], I'll be in control.[or]I should stay out of [his of the item described] way.[or]If [he of the item described] decides to fight me, it's a good idea to run.[or]I don't even want to know what [he of the item described] might do with those horns.[in random order][roman type][line break]";
		otherwise if the bimbo of the player < 14:
			say "[variable custom style][one of]I'm not sure what's going to happen now that [he of the item described]'s out of his cage. But I'm sure I'll be sore afterwards.[or][big he of the item described] seems so angry. Maybe if I get [him of the item described] off before I fight [him of the item described] it will be easier.[or][big he of the item described] looks pretty tough. Maybe if I let [him of the item described] have [his of the item described] way with me [he of the item described]'ll leave me alone for a while.[or][big his of the item described] [manly-penis] looks bigger now that [he of the item described]'s out of his cage.[or]I think [he of the item described] wants to thank me for letting [him of the item described] out. Maybe I should let [him of the item described]. Just once.[or]He's fucking everything in sight! I might as well get on my knees, it will happen anyway.[or][big he of the item described] doesn't look that fast. If I end up losing the fight, and [he of the item described] fucks me, it's still kind of on my terms.[or]I should try to keep away from [him of the item described]. At least until I know that boner of [his of the item described] is only pointing at me.[in random order][roman type][line break]";
		otherwise:
			say "[second custom style][one of][big he of the item described]'s out of [his of the item described] cage, now [he of the item described] can fuck me![or]Don't worry baby, I know how to fix that boner![or]Look at that BIG BONER![or][big he of the item described] looks horny. We have that much in common, teehee![or]I only want to fight [him of the item described] after [he of the item described]'s put a couple loads in me. No point attacking someone who's got enough energy to fuck you.[or]Wow! [big his of the item described] [manly-penis] is SO big! I wonder how it would feel inside me?[or]I hope [he of the item described] wants to thank me for letting [him of the item described] out![or]I should let [him of the item described] have fun with everyone else before I get on my knees. That way, I'll have [him of the item described] all to myself.[or]I really want to get on my knees and stuff that [manly-penis] in my mouth![or][big he of the item described] looks like [he of the item described] needs a good blowjob.[or]I want to stay nearby in case [he of the item described] wants to fuck me.[or]I want to stay nearby. It's so fun watching [him of the item described] obliterate everyone in [his of the item described] path![or][big he of the item described] looks so angry! I know just the way to get [him of the item described] nice and calmed down![in random order][roman type][line break]".

To set up (M - minotaur):
	if M is alive: [We don't want to reset the minotaur if he's dead]
		reset M;
		now the monstersetup of M is 1;
		now the difficulty of M is 12;
		now the sex-length of M is 3;
		now the health of M is the maxhealth of M;
		if mythical creature fetish is 0, now the text-shortcut of M is "hu".

To decide which number is the girth of (M - minotaur):
	decide on 8.

To decide which number is the semen load of (M - minotaur):
	decide on 18.

This is the spawn initial minotaur rule:
	if debugmode > 1, say "Now summoning minotaur.";
	if minotaur is unleashed and minotaur is alive, summon minotaur in the dungeon.
The spawn initial minotaur rule is listed in the setting up dungeon monsters rules.

Definition: minotaur (called M) is concealment immune: [Can the monster ignore salves of concealment, butt slut, etc.]
	decide yes.

Definition: minotaur (called M) is able to remove cursed plugs: [Can the monster remove all butt plugs?]
	decide yes.

Definition: minotaur (called M) is willing to shag:
	if M is awake and the scared of M is 0 and the boredom of M < 120 and M is threatening, decide yes;
	decide no.

To say PregGrowth of (M - minotaur):
	say "the [if mythical creature fetish is 1][one of]minotaur[or]calf[or]half-bull[purely at random][otherwise]hulk[end if]".

To compute labour to (M - minotaur):
	if M is regional and M is alive:
		compute pregnancy clothing displacement;
		say "[PregFlav][if the father is in the location of the player]The [father][otherwise]The [father] appears almost as if on cue! [big he of M][end if] gets on all fours and grunts gently as [he of M] watches the [if mythical creature fetish is 0]half-giant baby emerge[otherwise]minotaur calf[end if]. After the painful but relatively fast birthing, [he of M] roars and picks up the wailing pup in [his of M] arms. You can do nothing but watch as [he of M] takes your new son away.[line break][variable custom style][if the father is mating]I guess it's only right that it should join its siblings...[otherwise if mythical creature fetish is 1]I can't believe I just gave birth to a half-bull...[otherwise]I can't believe I just gave birth to a half... whatever [he of M] is[end if][roman type][line break]";
		if the father is in the location of the player:
			say "For some reason, you are filled with a sense of deep fulfilment. You feel great!";
			StrengthUp 1;
			DexUp 1;
			IntUp 1;
	otherwise if M is alive:
		Delay Labour.

To say LeftoverDesc (N - 107):
	say "The corpse of a [if mythical creature fetish is 1]minotaur[otherwise]hulk[end if] lies on the ground. The back of [his of minotaur] skull is missing, and a huge bloody hole is all that remains of [his of minotaur] brain.[line break]".

Part 1 - Perception

To compute perception of (M - minotaur):
	say "[BigNameDesc of M] notices you[if the player is sluttily dressed]![otherwise]![end if]";
	anger M;
	if M is unleashed:
		if M is mating:
			if the pregnancy of the player > 0:
				say "[big he of M] snorts gently and nuzzles your belly.";
				now the boredom of M is 50;
				calm M;
			otherwise:
				say "It looks like [he of M] wants more offspring...";
		otherwise:
			say "[big he of M] does not look friendly. In fact, you can see some fierce hunger in [his of M] eyes.";
	otherwise:
		say "[big he of M] growls angrily at [his of M] captivity.".

To say BecomesBoredFlav of (M - minotaur):
	say "[BigNameDesc of M] seems to lose interest in you for now.".

To compute delay of (M - minotaur):
	say "[BigNameDesc of M] stands over you, [his of M] [LongDickDesc of M] quickly raising to full mast in front of your face.";
	if lady fetish is 0 and mythical creature fetish is 1, alwayscutshow figure of minotaur cutscene 7 for M.


Part 2 - Motion, Seeking and Sleeping

To compute monstermotion of (M - minotaur):
	if M is unleashed:
		now neighbour finder is the location of M;
		let A be a random N-viable direction;
		if A is a random N-viable direction and the room A from the location of M is not Dungeon12 and the room A from the location of M is not Dungeon41:
			try M going A;
		otherwise if A is a random N-viable direction and the room A from the location of M is not Dungeon12 and the room A from the location of M is not Dungeon41:
			try M going A;
		MonsterStomp M.

To compute (M - minotaur) seeking (D - a direction):
	if M is unleashed, try M going D.

To compute sleep reduction of (M - minotaur):
	if M is asleep:
		decrease the sleep of M by seconds;
		if the M is awake:
			now the sleep of M is 0;
			if M is in the location of the player, say "[BigNameDesc of M] wakes up!".[Very slight deviation from the default.]



Part 4 - Combat

Section 1 - Attack

Definition: minotaur (called M) is automatically tempted:
	if the bimbo of the player >= 20, decide yes;
	if the appearance of the player > 7, decide yes;
	if class of the player is cowgirl, decide yes;
	if thickness of hips > 5 and pregnancy fetish is 1, decide yes;
	if wanking > 1, decide yes;
	if the player is monster fucked, decide yes;
	decide no.

Definition: minotaur (called M) is eager to slap:
	if a random number between 1 and 4 is 1, decide yes;
 	decide no.

The minotaur priority attack rules is a rulebook. The priority attack rules of minotaur is the minotaur priority attack rules.
To compute interaction of (M - a minotaur):
	if M is caged:
		if M is automatically tempted:
			if (a random number between 1 and 4 is 1 or wanking > 1) and the blue-balls of M < 5:
				say "[one of][BigNameDesc of M] turns toward you, grunting primally as [he of M] vigorously thrusts [his of M] giant [DickDesc of M] through the sturdy bars of [his of M] cage.[or][BigNameDesc of M] stares holes in your skull, drooling slightly as [he of M] roughly humps the space between the heavy bars of [his of M] cage.[or][BigNameDesc of M] never once looks away from you, hungrily thrusting [his of M] massive [DickDesc of M] through the bars of [his of M] cage, panting roughly as [he of M] smears them with thick, slimy pre-cum.[in random order]";
				increase the blue-balls of M by 1;
			otherwise:
				if the blue-balls of M > 4:
					say CagedMinotaurMessFlav;
					now the blue-balls of M is 0;
				otherwise:
					say ArousedCagedMinotaurFlav;
		otherwise:
			say "[one of][CagedMinotaurFlav][or][or][or][in random order]".

To say CagedMinotaurFlav:
	say "[one of][BigNameDesc of current-monster] angrily shakes the metal bars of [his of current-monster] cage.[or][BigNameDesc of current-monster] paws at the floor of [his of current-monster] cage, snarling with anger.[or][BigNameDesc of current-monster] snarls angrily at [his of current-monster] captive state.[or][BigNameDesc of current-monster] bangs at the bars of [his of current-monster] cage. The sound of clanging metal rings through the room.[or][BigNameDesc of current-monster] throws [him of current-monster]self at the bars of [his of current-monster] cage. You try to ignore [his of current-monster] angry snarling.[in random order]".

To say ArousedCagedMinotaurFlav:
	let M be current-monster;
	say "[one of][BigNameDesc of M] thrusts [his of M] arm through a gap in [his of M] cage, grasping at the empty air between you.[or][BigNameDesc of current-monster] fixes you with an intimidating stare, furiously masturbating with obvious hunger in [his of M] eyes.[or][BigNameDesc of M] stares at you with obvious and unrestrained arousal.[or][BigNameDesc of M] stares at you with unrestrained lust in his eyes. [if the bimbo of the player < 8]You feel uncomfortable.[otherwise if the bimbo of the player < 12][line break][otherwise]You feel sexy![end if][or][BigNameDesc of M] seems incredibly taken with your crotch.[in random order]".

To say CagedMinotaurMessFlav:
	let L be a random number between 2 and 4;
	let M be current-monster;
	say "[BigNameDesc of M] roars as [his of M] [DickDesc of M] explodes all over the floor and walls around [him of M]. [run paragraph on]";
	if a random number between 1 and the dexterity of the player > 5 and bukkake fetish is 1:
		say "It comes out so violently that you can't get out of the way, and you take several ropes all over your face.";
		if lady fetish is 0 and mythical creature fetish is 1, cutshow figure of minotaur cutscene 6 for M;
		CumFaceUp 2;
		SemenPuddleUp L - 2;
	otherwise:
		say "It comes out so violently that you have to dodge out of the way to avoid getting slammed into the wall.";
		SemenPuddleUp L.

This is the minotaur prioritises defeating others rule:
	if the number of undefeated awake monsters in the location of current-monster > 1: [1 is going to be the minotaur itself]
		MonsterStomp current-monster;
		rule succeeds.
The minotaur prioritises defeating others rule is listed last in the minotaur priority attack rules.

To compute the flying player taunting of (M - minotaur):
	say "[one of][BigNameDesc of M] clearly refuses to move from your location until you stop floating.[or][stopping]".

The latex punishment rule of minotaur is the no latex punishment rule.

To say LandingTaunt of (M - minotaur):
	say "[BigNameDesc of M] growls with impatience.";
	humiliate 50.

To decide which number is the rounds of sex left of (M - minotaur):
	decide on 1. [We don't do climax the standard way]

To compute anal sex of (M - minotaur):
	if the sex-length of M > 0:
		compute fuckhole sex of M;
		if the soreness of asshole < 9 or the reaction of the player is 0, ruin asshole;
		if the reaction of the player > 0:
			if the soreness of asshole > 8, say "[one of]Your lack of resistance against [NameDesc of M] is preventing you from going over the edge and fainting[if mythical creature fetish is 1], but it is extremely humiliating to let yourself get fucked by a half bull[end if].[or][or][or][or][cycling]";
	otherwise if the health of M > 5:
		compute fuckhole sex of M;
		unless M is wrapped, AssFill 2;
		if the soreness of asshole < 10 or the reaction of the player is 0, ruin asshole;
	otherwise:
		now the nutting-turns of M is 0;
		let R be 20 + a random number between 1 and 10;
		if debuginfo > 0, say "[input-style][ShortDesc of M] sleep pin check: d10+20.5 ([R].5) | [strength of the player + dexterity of the player] = ([strength of the player]) strength + ([dexterity of the player]) dexterity[roman type][line break]";
		if the dexterity of the player + the strength of the player > 20 + R:
			say "[one of][BigNameDesc of M] is completely spent, and collapses on top of you, half-crushing your body.[or][BigNameDesc of M], completely spent, collapses on top of you, half crushing your body.[or][BigNameDesc of M] collapses on top of you, spent.[at random] You barely manage to crawl out from underneath [him of M], [if the soreness of asshole > 4 or the openness of asshole < 8]wincing as [his of M][one of] shrinking[or] cummy[or][or][at random] [manly-penis] [one of]slides out[or]pops out[at random] of your [asshole][otherwise]sighing as his[one of] shrinking[or] cummy[or][or][at random] [manly-penis] slides out of your [asshole][end if].";
			now M is not penetrating asshole;
		otherwise:
			say "[one of][BigNameDesc of M] is completely spent, and collapses on top of you, crushing your body. You can't manage to crawl out from underneath [his of M] weight![or][BigNameDesc of M], spent, collapses on top of you, crushing you under [his of M] full weight. You try, but you don't manage to crawl out from underneath [him of M]![or][BigNameDesc of M] lets go of you as the torrent finally stops, collapsing on top of you just before you manage to crawl out from underneath [him of M].[or][BigNameDesc of M], completely spent, collapses on top of you, pinning you down with his full weight. No matter what you do, you can't manage to crawl out from underneath [him of M]![at random] You're going to have to wait [one of]like this with [his of M] [DickDesc of M] in your [asshole] until [he of M] wakes up...[or]with [his of M] [DickDesc of M] buried in your asshole until [he of M] wakes up...[at random]";
			if lady fetish is 0 and mythical creature fetish is 1, alwayscutshow figure of minotaur cutscene 2 for M;
		AssFill 2;
		orgasm M;
		now the sleep of M is 150;
		display entire map.

To compute vaginal sex of (M - minotaur):
	if the sex-length of M > 0:
		compute fuckhole sex of M;
		if the soreness of vagina < 9 or the reaction of the player is 0, ruin vagina;
		if the reaction of the player > 0:
			if the soreness of vagina > 8, say "[one of]Your lack of resistance against [NameDesc of M] is preventing you from going over the edge and fainting[if mythical creature fetish is 1], but it is extremely humiliating to let yourself get fucked by a half bull[end if].[or][or][or][or][cycling]";
	otherwise if the health of M > 5:
		compute fuckhole sex of M;
		unless M is wrapped, PussyFill 2;
		if the soreness of vagina < 10 or the reaction of the player is 0, ruin vagina;
	otherwise:
		now the nutting-turns of M is 0;
		PussyFill 2;
		say "[one of][BigNameDesc of M] seems completely spent - [he of M] pulls your bloated body off [his of M] shaft and leaves you to recover[or][BigNameDesc of M], spent, pulls your bloated body off [his of M] deflating shaft and leaves you to recover[or][BigNameDesc of M] pulls your bloated body off [his of M] shaft as [his of M] ejaculation finally ends, leaving you on the floor to recover[or][BigNameDesc of M] removes [his of M] deflating shaft from your [vagina], completely spent[at random]. [big he of M] stalks off, probably looking for another woman to breed.";
		if there is a worn tattoo and bred by a bull tattoo is not worn:
			summon bred by a bull tattoo;
			say "The words 'BRED BY A BULL' materialise on your left side! They are now permanently inked onto your skin.";
		otherwise if cow-ears is off-stage and cow-ears is actually summonable and lactation fetish is 1:
			say "[bold type]A pair of fake cow ears materialise on your head.[roman type][line break]";
			summon cow-ears cursed;
		orgasm M;
		bore M for 500 seconds.

To compute priestessBlessing of (M - minotaur):
	say "You feel a surge in your holy aura and a voice appears in your head: 'Good work, Sister!'";
	decrease the charge of the dungeon altar by 300.

To compute fuckhole sex of (M - minotaur):
	let F be a random fuckhole penetrated by M;
	if the sex-length of M > 0:
		if the sex-length of M is 1, say "[BigNameDesc of M] roars as you feel [if M is wrapped][one of]the condom starting to fill[or]cum spurting into the condom[or][his of M] [DickDesc of M] erupting into the condom[in random order][otherwise][one of][him of M] start to ejaculate[or]cum spurting into your [variable F] in waves[or][his of M] [DickDesc of M] erupt inside of you[or][his of M] balls begin emptying themselves inside of you[in random order][end if]! But [he of M][one of] doesn't stop fucking you.[or]'s still going![or] doesn't stop plowing you at all.[or] doesn't relent with [his of M] brutal pounding![at random]";
		otherwise say "[one of][BigNameDesc of M] is brutally plowing away at your [variable F]![or][BigNameDesc of M] grunts primally as [he of M] pounds your poor [variable F]![or][BigNameDesc of M] growls hungrily as [his of M] [LongDickDesc of M] jack-hammers your [variable F].[or][BigNameDesc of M] holds you with a rigid death-grip as [he of M] slams in and out of your [variable F].[or][BigNameDesc of M] holds you down with an ironclad grip as [he of M] brutally pounds your [variable F]![or][BigNameDesc of M]'s massive [if mythical creature fetish is 1]furry [end if]nutsack slaps audibly against your taint as [he of M] brutally jack-hammers your [variable F].[in random order]";
		decrease the sex-length of M by 1;
		if the health of M > 10, decrease the health of M by 5;
	otherwise:
		increase the nutting-turns of minotaur by 1;
		say "[one of][BigNameDesc of M] is still fucking your [variable F][or][BigNameDesc of M] continues to pound your [variable F][or][BigNameDesc of M] relentlessly plows your [variable F][or][BigNameDesc of M] plows your [variable F] without stopping[or][BigNameDesc of M]'s massive [if mythical creature fetish is 1]horsecock[otherwise]cock[end if] continues to slam in and out of your [variable F][at random]! [run paragraph on]";
		if M is wrapped:
			if the reaction of the player is 0:
				if a random number between the nutting-turns of minotaur and 1 > 3:[condom breaks]
					say "Your squirming puts extra pressure on the overly-stretched condom inside you, and you feel a torrent of warmth as it suddenly bursts open, flooding your belly with [semen]!";
					now M is unwrapped;
					if M is penetrating asshole, AssFill the nutting-turns of minotaur * 2;
					otherwise PussyFill the nutting-turns of minotaur * 2;
			otherwise:
				if a random number between 1 and the nutting-turns of minotaur > 6:
					say "The condom reaches its breaking point, twitching with effort before suddenly bursting open and flooding your belly with a torrent of warm [semen]!";
					now M is unwrapped;
					if M is penetrating asshole, AssFill the nutting-turns of minotaur * 2;
					otherwise PussyFill the nutting-turns of minotaur * 2;
			if M is wrapped:[if the condom didn't break, we still need to display flavour]
				say "[one of]You can feel [his of M] [DickDesc of M] twitching as it continues to spurt [semen] into the rapidly stretching condom.[or]The condom continues to stretch.[or][big he of M] somehow finds more [semen] to cram into the rapidly stretching condom.[or][big his of M] [semen] keeps coming, torrenting into the condom at full force.[at random]";
		otherwise:
			if the reaction of the player is 0, say "[one of]No matter much you clench your muscles, you can't stop the torrent of [semen] into your [BellyDesc][or]Your resistance does nothing to stop the virtually endless torrent of [semen] into your belly.[or]Even with your struggling, [he of M] somehow finds more [semen] to cram into your [BellyDesc][or]No matter what you do, [his of M] [semen] continues to torrent into your [BellyDesc] at full force.[at random]";
			otherwise say "[one of]Relaxing your [variable F], you accept the virtually endless torrent of [semen] into your [BellyDesc].[or]You do your best to stay relaxed as [his of M] [semen] spurts into your belly.[or]You can feel [his of M] [DickDesc of M] twitching as it continues to spurt [semen] into your [BellyDesc].[or]Relaxing your inner muscles, you accept the unending torrent of [semen] into your [BellyDesc].[at random]";
		decrease the health of M by 5;
		if lady fetish is 0 and mythical creature fetish is 1, cutshow figure of minotaur cutscene 5 for M.

To say StrikingSuccessFlav of (M - minotaur) on (B - a body part):
	say "[BigNameDesc of M] pummels you [TargetName of B] with [his of M] fists!";
	BodyRuin 1. [The minotaur automatically does 1 damage, and 2 if he hits.]

To say StrikingFailureFlav of (M - minotaur) on (B - a body part):
	say "You try to dodge [NameDesc of M]'s fist, but [he of M] still manages to land a glancing blow! Ouch!";
	BodyRuin 1.

To compute striking attack of (M - minotaur):
	let B be a random body part;
	if B is a fuckhole, now B is hips;
	if B is hair, now B is breasts;
	if B is not hips and B is not breasts, now B is belly;
	if the accuracy roll of M > the dexterity of the player:
		say "[StrikingSuccessFlav of M on B]";
		compute M striking B;
	otherwise:
		say "[StrikingFailureFlav of M on B]".

The monster fucked taunting rule of minotaur is the minotaur replaces the monster rule.
This is the minotaur replaces the monster rule:
	let M be current-monster;
	let N be a random monster penetrating a fuckhole;
	if there is a monster penetrating asshole, now N is a random monster penetrating asshole; [This is the one who's important to get rid of since this is where we're going.]
	say "[BigNameDesc of M] kicks the [N] out of the way, and takes over! [big he of M] [one of]sticks [his of M] giant [DickDesc of M] in your [asshole],[or]shoves [his of M] massive [DickDesc of M] up your ass[or]jams [his of M] giant dick into your [asshole][or]clumsily rams [his of M] huge dick in your [asshole][at random] and [one of]starts cumming[or]starts to cum[at random] almost immediately! [line break][variable custom style][if the anal sex addiction of the player < 4]Oh Fuck![otherwise if the anal sex addiction of the player < 6]I thought I was safe with the [N] inside me![otherwise]Finally, a real dick![end if][roman type][line break]";
	if lady fetish is 0 and mythical creature fetish is 1, cutshow figure of minotaur cutscene 3 for M;
	now the sex-length of M is 0;
	now the chosen-orifice of M is asshole;
	destroy N;
	now M is penetrating asshole;
	ruin asshole;
	rule succeeds.

To compute (M - minotaur) attacking (C - a clothing):
	if C is displacable:
		say "[BigNameDesc of M] pulls the crotch of your [printed name of C] to the side!";
		displace C;
	otherwise:
		say "[BigNameDesc of M] rips off your [printed name of C]!";
		destroy C.

To compute (M - minotaur) attacking (C - a clothing):
	say "[BigNameDesc of M] tears off your [printed name of C]!";
	destroy C.

To compute (M - minotaur) removing (X - a thing):
	say "[BigNameDesc of M] forcefully and roughly pulls the [printed name of X] out of your [asshole] and drops it on the ground!";
	now X is in the location of the player;
	if X is anal beads, ruin asshole times the notch-taken of X / 2;
	dislodge X.

To decide if (M - minotaur) is willing to creampie (F - a vagina):
	if the player is male, decide no;
	if pregnancy fetish is 0 or the pregnancy of the player is not 0, decide no;
	if the class of the player is cowgirl, decide yes;
	if the thickness of hips > 6 - a random number between 1 and the pregnancy rate of the player, decide yes;
	decide no.

To compute the orifice choosing of (M - minotaur):
	if M is willing to creampie vagina:
		now the chosen-orifice of M is vagina;
	otherwise:
		now the chosen-orifice of M is asshole.

To decide which number is the condom resistance of (M - minotaur):
	if the class of the player is cowgirl, decide on 99;
	decide on 3.

To say CondomForceFailFlav of (M - minotaur):
	say "[one of]You reach between your legs, grasping for [NameDesc of M]'s [DickDesc of M] as you quickly take out a condom. [big his of M] copious precum makes [his of M] shaft too slippery, and you can't get a good grip before [he of M] roughly smacks your hand away.[or]You take out a condom and reach between your legs, awkwardly trying to apply it to [NameDesc of M]'s [DickDesc of M]. [big he of M] snorts angrily and smacks your hand out of the way.[at random]";

To set up sex length of (M - minotaur) in (B - a fuckhole):
	now the sex-length of M is 0;[preventative measure to prevent bugginess]
	set up sex length (the health of M / 10) of M in B.


To say AssholePenetrationFlav of (M - minotaur):
	say "[BigNameDesc of M] [one of]forcefully pushes [his of M][or]clumsily rams [his of M][or]jams [his of M][or]forces [his of M][at random] [LongDickDesc of M] into your [asshole], and starts fucking you furiously! [if the sex-length of M is 0]Within seconds, [he of M] has started cumming inside you![otherwise]Your [asshole] is instantly stretched beyond belief.[end if]".


To say VaginaPenetrationFlav of (M - minotaur):
	say "[BigNameDesc of M] stares at your body and sniffs your [vagina], assessing you. [big he of M] seems to make up [his of M] mind that you are a suitable breeding partner and poises [his of M] huge penis with the tip at the entrance to your [vagina]. [one of]With very little effort, [he of M] forces [his of M][or]With no effort at all, [he of M] shoves [his of M][or]Without the slightest hint of difficulty [he of M] rams [his of M][or]Without pausing, [he of M] forces [his of M][at random] giant [DickDesc of M] in, and starts fucking you vigorously. [if the sex-length of M is 0]Within seconds, [he of M] has started cumming inside you![otherwise if the class of the player is cowgirl and mythical creature fetish is 1]You find yourself mooing submissively as your [vagina] is instantly stretched beyond belief by [his of M] huge bull [DickDesc of M].[otherwise if pregnancy fetish is 1 and mythical creature fetish is 1]Your [vagina] is instantly - and painfully - stretched beyond belief.[one of][line break][variable custom style]Could I get pregnant from this bull?![roman type][line break][or][stopping][otherwise]Your [vagina] is instantly - and painfully - stretched beyond belief.[end if]".

Section 2 - Damage

To compute damage of (M - minotaur):
	if the health of M > 0:
		if M is awake:
			if M is uninterested:
				say "[big he of M] growls viciously! Uh-oh...";
				now M is interested;
				anger M;
			otherwise:
				say DamageReaction (the health of M) of M;
		otherwise:
			anger M;
			now M is interested;
			now the sleep of M is 0;
			say "[big he of M] wakes up, and immediately stands up to full height with a powerful roar!";
	otherwise:
		compute death of M;

To say DamageReactHealthy of (M - minotaur):
	say "[BigNameDesc of M] effortlessly shrugs off your hit!".

To say DamageReactDamaged of (M - minotaur):
	say "[big he of M] snarls through clenched teeth!".

To say DamageReactTired of (M - minotaur):
	say "[big he of M] roars savagely, taking the hit!".

To say DamageReactWeak of (M - minotaur):
	say "[big he of M] seems to be breathing more heavily, as if [he of M]'s getting tired!".

To compute unique death of (M - minotaur):
	let H be a random off-stage horn;
	let X be a random off-stage equine-strapon;
	let B be a random off-stage potion of bull strength;
	now M is bossdefeated;
	if mythical creature fetish is 1:
		if full-lady fetish is 1 and X is clothing and a random number between 1 and 3 is 1 and the player is the donator:
			now the strap-length of X is 11;[15 inches]
			say "[BigNameDesc of M] falls to the ground, dead. His body disappears, but the huge strapon dildo he was wearing stays behind. It seems like you could actually wear it!";
			now X is in the location of the player;
			compute autotaking X;
		otherwise if H is a thing:
			say "[BigNameDesc of M] falls to the ground, dead. One of [his of M] horns smacks into the ground and snaps clean off. The [ShortDesc of H] appears to be hollow - you could almost use this as a drinking vessel!";
			now H is in the location of the player;
			compute autotaking H;
		otherwise:
			say "[BigNameDesc of M] falls to the ground, defeated.";
	otherwise:
		now X is a random off-stage strapon-dildo;
		if full-lady fetish is 1 and X is clothing and a random number between 1 and 3 is 1 and the player is the donator:
			now the strap-length of X is 11;[15 inches]
			say "[BigNameDesc of M] falls to the ground, dead. [big his of M] body disappears, but the huge strapon dildo [he of M] was wearing stays behind. It seems like you could actually wear it!";
			now X is in the location of the player;
			compute autotaking X;
		otherwise if B is potion:
			say "[BigNameDesc of M] falls to the ground, defeated. [if B is potion][big he of M] dropped a [ShortDesc of B]![end if]";
			now B is in the location of the player;
			compute autotaking B;
		otherwise:
			say "[BigNameDesc of M] falls to the ground, defeated.".

[To uniquely destroy (M - minotaur):
	now M is caged.]

To loot (M - minotaur):
	let X be a random off-stage plentiful ring;
	unless X is nothing:
		now X is in the location of the player;
		now X is solid gold;
		set shortcut of X;
		say "The defeated [M] [if the loot dropped of M > 0]also [end if]dropped a [printed name of X]!";
		increase the loot dropped of M by 1;
		compute autotaking X.



Minotaur ends here.

