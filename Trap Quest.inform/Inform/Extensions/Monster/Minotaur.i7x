Minotaur by Monster begins here.

minotaur is a dungeon boss. minotaur is male. The leftover-type of minotaur is 107. minotaur has a number called nutting-turns. The nutting-turns of minotaur is usually 0. minotaur has a number called swing-intensity.

minotaur is in Dungeon36. minotaur is caged.
Figure of minotaur is the file "NPCs/Bosses/Minotaur/minotaur1.png". Figure of minotaur caged is the file "NPCs/Bosses/Minotaur/minotaur2.png". Figure of hulk is the file "NPCs/Bosses/Minotaur/hulk1.jpg".

Figure of Minotaur Cutscene 1 is the file "NPCs/Bosses/Minotaur/cutscene-minotaur-stomp1.png".
Figure of Minotaur Cutscene 2 is the file "NPCs/Bosses/Minotaur/cutscene-minotaur-sleep1.png".
Figure of Minotaur Cutscene 3 is the file "NPCs/Bosses/Minotaur/cutscene-minotaur-fuck1.png".
Figure of Minotaur Cutscene 4 is the file "NPCs/Bosses/Minotaur/cutscene-minotaur-fuck2.png".
Figure of Minotaur Cutscene 5 is the file "NPCs/Bosses/Minotaur/cutscene-minotaur-climax1.png".
Figure of Minotaur Cutscene 6 is the file "NPCs/Bosses/Minotaur/cutscene-minotaur-splash1.png".
Figure of Minotaur Cutscene 7 is the file "NPCs/Bosses/Minotaur/cutscene-minotaur-ready1.png".

To decide which figure-name is the monster-image of (M - minotaur):
	if lady fetish is 1, decide on figure of missing NPC;
	if mythical creature fetish is 1:
		if M is penetrating a body part and M is asleep, decide on figure of minotaur cutscene 2;
		if M is caged, decide on figure of minotaur caged;
		otherwise decide on figure of minotaur;
	otherwise:
		decide on figure of hulk;
To decide which figure-name is the anal-sex-monster-image of (M - minotaur):
	if lady fetish is 0 and mythical creature fetish is 1, decide on figure of minotaur cutscene 3;
	decide on figure of missing NPC.
To decide which figure-name is the vaginal-sex-monster-image of (M - minotaur):
	if lady fetish is 0 and mythical creature fetish is 1, decide on figure of minotaur cutscene 4;
	decide on figure of missing NPC.

The text-shortcut of minotaur is "mi". Understand "large", "hulk" as minotaur.

To say ShortDesc of (M - minotaur):
	say "[if mythical creature fetish is 0]hulk[otherwise]minotaur[end if]".

To say MediumDesc of (M - minotaur):
	say "large [ShortDesc of minotaur][if M is caged] (caged)[end if]".

Definition: minotaur is willing to do oral: decide no.

Definition: minotaur is willing to do titfucks: decide no.

Definition: minotaur is musky: decide yes.

To say LongDickDesc of (M - a minotaur):
	if pregnancy fetish is 1 and lactation fetish is 1 and inhuman pregnancy > 0 and mythical creature fetish is 1:
		if full-lady fetish is 1, say "equine strap-on";
		otherwise say "15 inch bull-penis";
	otherwise:
		if full-lady fetish is 1, say "15 inch strap-on";
		otherwise say "15 inch penis".

To say MonsterDesc of (M - minotaur):
	if mythical creature fetish is 1, say "A naked bull-[man of M] with rippling muscles covering [his of M] entire body. [big his of M] face is bestial, covered in thick [one of]auburn[or]gold[or]black[or]brown[or]ashen[sticky random] fur, with big flat nostrils and beady black eyes. Two long gray horns extend from just above [his of M] floppy bovine ears, one of which is pierced with a small [one of]brass[or]iron[or]gold[or]onyx[sticky random] ring. [big he of M] is seething with rage, [his of M] wide shoulders covered in a fine sheen of sweat and [if lady fetish is 1][his of M] heavy bosom[otherwise][his of M] thick barrel chest[end if] heaves with every breath. Between [his of M] legs is a fully erect, [LongDickDesc of M], which twitches and throbs as if it could blow at any time.";
	otherwise say "A naked hulk of a [man of M] with rippling muscles covering [his of M] entire body. [big he of M] looks like the result of some mad science experiment gone wrong. [big he of M] is seething with rage, [his of M] wide shoulders covered in a fine sheen of sweat and [if lady fetish is 1][his of M] heavy bosom[otherwise][his of M] thick barrel chest[end if] heaving with every breath. Between [his of M] legs is a fully erect [LongDickDesc of M], which twitches and throbs as if it could blow at any time.";
	if M is caged, say DungeonBossCageDesc of M.

To say DungeonBossCageDesc of (M - minotaur):
	if M is caged, say "[big he of M] is currently trapped within a giant steel birdcage, held suspended by a tough-looking chain. The bars seem sturdy enough to hold [him of M], but they are far apart, allowing [him of M] to see or even reach through them with ease[if the intelligence of the player > 7]. If provoked, [he of M] might even be capable of swinging several feet in any direction[end if]. Looking up at the chain that keeps the cage suspended, you notice it doesn't appear connected to any device or [bold type]lever[roman type] in the room. To lower [him of M] and open the cage, you'd need to find something elsewhere. [if mystical amulet is in the location of M][line break]Underneath [his of M] feet, you can see a valuable-looking amulet.[end if]";
	otherwise say "The cage is open and empty[if mystical amulet is in Dungeon36][line break], save for a valuable-looking amulet.[end if]".

To say MonsterComment of (M - minotaur):
	if M is penetrating a body part:
		if the bimbo of the player < 8:
			say "[first custom style][big he of M]'s destroying my [variable random orifice penetrated by M]!";
		otherwise if the bimbo of the player < 14:
			say "[variable custom style]Come on [variable random orifice penetrated by M], you can take it. T-this couldn't go on for ever.";
		otherwise:
			say "[second custom style]That's right, [stud of M]! Let it all out!";
	otherwise if the player is pheromonal:
		if the bimbo of the player < 8:
			if M is caged, say "[first custom style][big his of M] cage smells.... no, [caps he of M] smells. Wait, why do I even care? I should go...[roman type][line break]";
			otherwise say "[first custom style]I can smell [his of M]... stench. I have to get out of here! I can't take this!";
		otherwise if the bimbo of the player < 14:
			if M is caged, say "[variable custom style][big his of M] cage smells really... musky? Its like [he of M]'s the most *virile* thing in the universe. Wait, what?[roman type][line break]";
			otherwise say "[variable custom style][big his of M] musk is turning me on so much! I don't know if I can resist any longer![roman type][line break]";
		otherwise:
			if M is caged, say "[second custom style]I can smell [him of M] from here. It's making me so HORNY![roman type][line break]";
			otherwise say "[second custom style][big his of M] musk is so strong! I bet [he of M] can't WAIT to stuff that huge thing in me! Heeheehee![roman type][line break]";
	otherwise if item described is caged:
		if the bimbo of the player < 8:
			say "[first custom style][one of]A fight with [him of M] wouldn't be easy. Good thing [he of M]'s in that cage.[or]With a body like that [he of M] could pin me down and pound me whenever [he of M] wanted to. I have to make sure [he of M] stays in that cage at all costs![or][big he of M]'s definitely a threat. I should make sure [he of M] stays in that cage at all costs.[or]Good thing [he of M]'s caged. [big he of M]'d probably beat me to a pulp just for looking at [him of M] funny.[or]Ugh. Anything with a [manly-penis] that big deserves to be caged.[or]Enjoy your cage time, fucker! That [manly-penis] is never coming anywhere near me![or]Man, [he of M] looks PISSED. I would be too if I had a [DickDesc of M] that big and couldn't fuck anything.[or]You know what, if I had a [manly-penis] that big I'd totally take some time in a cage. Better than being down here.[or][big he of M]'s pretty much the most manly thing in this game. Maybe the cage is some sort of metaphor?[or]Ugh, god [he of M] stinks so bad I can smell [him of M] from here.[in random order][roman type][line break]";
		otherwise if the bimbo of the player < 14:
			say "[variable custom style][one of]With a body like that [he of M] could pin me down and pound me whenever [he of M] wanted to. Good thing [he of M]'s caged...[or]Seeing that boner is making me feel weird. I should get out of here.[or][big he of M] probably hasn't had sex in a long time. I think it's best for both of us if [he of M] stays in that cage.[or]If I were dying of thirst, I might give [him of M] a blowjob. But only then![or][big he of M]'ll probably try to have that [manly-penis] in someone as soon as [he of M] gets out of that cage. Hmm...[or][big he of M] really stinks, doesn't [he of M]?[or]If [he of M] ever gets out of that cage... it will be interesting.[or]You know, if I have to have sex, I'd prefer someone smaller than that. Good thing [he of M]'s up there and not down here.[or][big he of M] seems really strong. If [he of M] gets out of that cage I should keep an eye on [him of M] so [he of M] doesn't hurt anyone.[in random order][roman type][line break]";
		otherwise:
			say "[second custom style][one of]Wow, [his of M] [manly-penis] is really big.[or]With a body like that [he of M] could pin me down and take me whenever [he of M] wanted to. Sign me up![or]I could look at that [manly-penis] of [his of M] all day. And since [he of M]'s caged up, I can![or][big he of M] probably hasn't had sex in a long time. Poor thing, I should let [him of M] out![or][big his of M] [manly-penis] looks really suckable![or]I want that [manly-penis] in me. I need to find a way to let [him of M] out.[or][big he of M] looks so horny. I need to find a way to fix that.[or][big he of M] stinks. So manly![or]I'd like to have [his of M] big [manly-penis] in me.[or]If [he of M] ever gets out of that cage, [he of M]'ll probably have a LOT of fun with me![or][big he of M] looks kind of dumb. If [he of M] escapes that cage I'll stay on my knees so [he of M]'ll know what to do![in random order][roman type][line break]";
	otherwise:
		if the bimbo of the player < 10:
			say "[first custom style][one of]I'm not sure I'm prepared to fight [him of M], but it's going to happen one way or the other.[or][big he of M] still seems so angry. Letting [him of M] out of that cage was a bad idea.[or]Holy shit, look at [him of M] go! [big he of M]'s a killing machine![or][big he of M] looks tough. I'm not really sure I can take [him of M].[or]Oh god, I think [his of M] thing is pointing at me![or]I hope [he of M] doesn't want to 'thank' me for letting [him of M] out.[or][big he of M] doesn't seem to care what [he of M] attacks.[or]We're going to fight eventually, but it doesn't have to happen right this minute.[or]As long as I can run away from [him of M], I'll be in control.[or]I should stay out of [his of M] way.[or]If [he of M] decides to fight me, it's a good idea to run.[or]I don't even want to know what [he of M] might do with those horns.[in random order][roman type][line break]";
		otherwise if the bimbo of the player < 14:
			say "[variable custom style][one of]I'm not sure what's going to happen now that [he of M]'s out of [his of M] cage. But I'm sure I'll be sore afterwards.[or][big he of M] seems so angry. Maybe if I get [him of M] off before I fight [him of M] it will be easier.[or][big he of M] looks pretty tough. Maybe if I let [him of M] have [his of M] way with me [he of M]'ll leave me alone for a while.[or][big his of M] [manly-penis] looks bigger now that [he of M]'s out of [his of M] cage.[or]I think [he of M] wants to thank me for letting [him of M] out. Maybe I should let [him of M]. Just once.[or][big he of M]'s fucking everything in sight! I might as well get on my knees, it will happen anyway.[or][big he of M] doesn't look that fast. If I end up losing the fight, and [he of M] fucks me, it's still kind of on my terms.[or]I should try to keep away from [him of M]. At least until I know that boner of [his of M] is only pointing at me.[in random order][roman type][line break]";
		otherwise:
			say "[second custom style][one of][big he of M]'s out of [his of M] cage, now [he of M] can fuck me![or]Don't worry baby, I know how to fix that boner![or]Look at that BIG BONER![or][big he of M] looks horny. We have that much in common, teehee![or]I only want to fight [him of M] after [he of M]'s put a couple loads in me. No point attacking someone who's got enough energy to fuck you.[or]Wow! [big his of M] [manly-penis] is SO big! I wonder how it would feel inside me?[or]I hope [he of M] wants to thank me for letting [him of M] out![or]I should let [him of M] have fun with everyone else before I get on my knees. That way, I'll have [him of M] all to myself.[or]I really want to get on my knees and stuff that [manly-penis] in my mouth![or][big he of M] looks like [he of M] needs a good blowjob.[or]I want to stay nearby in case [he of M] wants to fuck me.[or]I want to stay nearby. It's so fun watching [him of M] obliterate everyone in [his of M] path![or][big he of M] looks so angry! I know just the way to get [him of M] nice and calmed down![in random order][roman type][line break]".

To set up (M - minotaur):
	if M is alive: [We don't want to reset the minotaur if he's dead]
		reset M;
		now the monstersetup of M is 1;
		now the raw difficulty of M is the starting difficulty of M;
		now the sex-length of M is 3;
		now the health of M is the maxhealth of M;
		if mythical creature fetish is 0, now the text-shortcut of M is "hu".

To decide which number is the starting difficulty of (M - minotaur):
	decide on 12.

To decide which number is the girth of (M - minotaur):
	decide on 8.

To decide which number is the base semen load of (M - minotaur):
	decide on 18.

This is the spawn initial minotaur rule:
	if debugmode > 1, say "Now summoning minotaur.";
	if minotaur is unleashed and minotaur is alive, summon minotaur in the dungeon.
The spawn initial minotaur rule is listed in the setting up dungeon monsters rules.

To compute unique early action of (M - minotaur):
	if M is caged and M is not in Dungeon36, now M is in Dungeon36. [failsafe against rare bugs]

Definition: minotaur (called M) is concealment immune: [Can the monster ignore salves of concealment, butt slut, etc.]
	decide yes.

Definition: minotaur (called M) is able to remove cursed plugs: [Can the monster remove all butt plugs?]
	decide yes.

Definition: minotaur (called M) is willing to shag:
	if M is awake and the scared of M is 0 and the boredom of M < 120 and M is threatening, decide yes;
	decide no.

To say PregGrowth of (M - minotaur):
	say "the [if mythical creature fetish is 1][one of]minotaur[or]calf[or]half-bull[purely at random][otherwise]hulk[end if]".

To compute labour to (M - minotaur):
	if M is regional and M is alive:
		compute pregnancy clothing displacement;
		say "[PregFlav][if the father is in the location of the player]The [father][otherwise]The [father] appears almost as if on cue! [big he of M][end if] gets on all fours and grunts gently as [he of M] watches the [if mythical creature fetish is 0]half-giant baby emerge[otherwise]minotaur calf[end if]. After the painful but relatively fast birthing, [he of M] roars and picks up the wailing pup in [his of M] arms. You can do nothing but watch as [he of M] takes your new son away.[line break][variable custom style][if the father is mating]I guess it's only right that it should join its siblings...[otherwise if mythical creature fetish is 1]I can't believe I just gave birth to a half-bull...[otherwise]I can't believe I just gave birth to a half... whatever [he of M] is[end if][roman type][line break]";
		if the father is in the location of the player:
			say "For some reason, you are filled with a sense of deep fulfilment. You feel great!";
			StrengthUp 1;
			DexUp 1;
			IntUp 1;
	otherwise if M is alive:
		Delay Labour.

To say LeftoverDesc (N - 107):
	say "The corpse of a [if mythical creature fetish is 1]minotaur[otherwise]hulk[end if] lies on the ground. The back of [his of minotaur] skull is missing, and a huge bloody hole is all that remains of [his of minotaur] brain.[line break]".

[
0 - normal
1 - one leg
2 - two legs
3 - in the air
]
minotaur has a number called critical-windup.

Part 1 - Perception

To compute perception of (M - minotaur):
	say "[BigNameDesc of M] notices you[if the player is sluttily dressed]![otherwise]![end if]";
	anger M;
	if M is unleashed:
		if M is mating:
			if the pregnancy of the player > 0:
				say "[big he of M] snorts gently and nuzzles your belly.";
				now the boredom of M is 50;
				calm M;
			otherwise:
				say "It looks like [he of M] wants more offspring...";
		otherwise:
			say "[big he of M] does not look friendly. In fact, you can see some fierce hunger in [his of M] eyes.";
	otherwise:
		say "[big he of M] growls angrily at [his of M] captivity.".

To say BecomesBoredFlav of (M - minotaur):
	say "[BigNameDesc of M] seems to lose interest in you for now.".

To compute delay of (M - minotaur):
	if the player is prone, say "[BigNameDesc of M] stands over you, [his of M] [LongDickDesc of M] quickly raising to full mast in front of your face.";
	otherwise say "[BigNameDesc of M] closes in on you, [his of M] [LongDickDesc of M] quickly raising to full mast.";
	if lady fetish is 0 and mythical creature fetish is 1, alwayscutshow figure of minotaur cutscene 7 for M.

Part 2 - Motion, Seeking and Sleeping

To compute monstermotion of (M - minotaur):
	compute room leaving of M;
	MonsterStomp M.

To compute sleep reduction of (M - minotaur):
	if M is asleep:
		decrease the sleep of M by seconds;
		if the M is awake:
			now the sleep of M is 0;
			if M is in the location of the player, say "[BigNameDesc of M] wakes up!".[Very slight deviation from the default.]

Part 4 - Combat

Section 1 - Attack

Definition: minotaur (called M) is automatically tempted:
	if M is wrangling a body part, decide yes;
	if the bimbo of the player >= 20, decide yes;
	if the appearance of the player > 7, decide yes;
	if the player is pheromonal, decide yes;
	if thickness of hips > 5 and pregnancy fetish is 1, decide yes;
	if wanking > 1, decide yes;
	if the player is monster fucked, decide yes;
	decide no.

Definition: minotaur (called M) is eager to slap:
	if a random number between 1 and 4 is 1, decide yes;
	decide no.

The minotaur priority attack rules is a rulebook. The priority attack rules of minotaur is the minotaur priority attack rules.
To compute interaction of (M - minotaur):
	if M is caged:
		if M is automatically tempted:
			compute CagedMinotaurLusting of M;
		otherwise:
			say "[one of][CagedMinotaurFlav][or][or][or][in random order]".

To say CagedMinotaurFlav:
	say "[one of][BigNameDesc of current-monster] angrily shakes the metal bars of [his of current-monster] cage.[or][BigNameDesc of current-monster] paws at the floor of [his of current-monster] cage, snarling with anger.[or][BigNameDesc of current-monster] snarls angrily at [his of current-monster] captive state.[or][BigNameDesc of current-monster] bangs at the bars of [his of current-monster] cage. The sound of clanging metal rings through the room.[or][BigNameDesc of current-monster] throws [himself of current-monster] at the bars of [his of current-monster] cage. You try to ignore [his of current-monster] angry snarling.[in random order]".

To compute CagedMinotaurLusting of (M - minotaur):
	let R be a random number between 1 and 4;
	if M is wrangling a body part:
		compute CagedMinotaurMolesting of M;
	otherwise if R is 1 or (R < 3 and the blue-balls of M < 0) and the swing-intensity of M < 5:[He doesn't do anything.]
		say "[one of][BigNameDesc of M] stares at you with obvious and unrestrained arousal.[or][BigNameDesc of M] stares at you with unrestrained lust in [his of M] eyes.[or][BigNameDesc of M]'s [DickDesc of M] twitches as [he of M] stares at your crotch.[or]You can hear [NameDesc of M] breathing heavily as [he of M] stares at your crotch.[in random order]";
		if the blue-balls of M < 3, increase the blue-balls of M by 1;
		decrease the swing-intensity of M by 1;
	otherwise if R is 2 and the swing-intensity of M < 5:[He jerks off]
		if the blue-balls of M > a random number between 4 and 9:[minimum number is 5, max is 10]
			compute CagedMinotaurExplosion of M;
		otherwise:
			say CagedMinotaurThrustFlav of M;
			increase the blue-balls of M by 1;
		decrease the swing-intensity of M by 1;
	otherwise if R is 3 and the swing-intensity of M < 5:[He swings]
		say CagedMinotaurSwingFlav of M;
		increase the swing-intensity of M by 2;
		if the swing-intensity of M > 6, now the swing-intensity of M is 5;
	otherwise:[He grabs]
		compute CagedMinotaurGrabbing of M;
	if the swing-intensity of M < 0, now the swing-intensity of M is 0.

To compute CagedMinotaurExplosion of (M - minotaur):
	say "[BigNameDesc of M] roars as [he of M] slams [his of M] [LongDickDesc of M] between the bars of [his of M] cage, aiming the tip right at you as [if M is a balls-haver][his of M] giant balls, followed by [end if]every inch of [his of M] thick, veiny shaft flex with primal power.";
	let L be a random number between 2 and 4;
	let D be a random number between 1 and the dexterity of the player;
	if the player is pheromonal and (a random number between 1 and the intelligence of the player) < the blue-balls of M, now D is 0;[pheromonal? you're less likely to dodge]
	if D < the blue-balls of M and bukkake fetish is 1:
		say "You're too late to realize that's your cue to dodge, and half a second later, an explosion of hot [semen] hits you directly in the [if the player is prone]face, [otherwise]chest, knocking you over and [end if]sending you flying into the opposite wall.";
		if lady fetish is 0 and mythical creature fetish is 1, cutshow figure of minotaur cutscene 6 for M;
		if the player is prone:
			CumFaceUp 2;
			SemenPuddleUp L - 2;
		otherwise:
			now the stance of the player is 1;
			AnnouncedSquirt semen on breasts by 2;
			SemenPuddleUp L - 2;
		now another-turn is 1;
		now another-turn-flavour is "The wind is knocked out of you!";
	otherwise:
		say "You somehow realize that's your cue to dodge, and half a second later, you narrowly avoid an explosion of hot [semen] hitting you directly in the [if the player is prone]face[otherwise]chest[end if]. [BigFuckerDesc of M] steps away from the edge of the cage, apparently satisfied for now.";
		SemenPuddleUp L;
	now the blue-balls of M is -5.

To say CagedMinotaurThrustFlav of (M - minotaur):
	say "[one of][BigNameDesc of M] turns toward you, grunting primally as [he of M] vigorously thrusts [his of M] giant [DickDesc of M] through the sturdy bars of [his of M] cage.[or][BigNameDesc of M] stares holes into you, drooling slightly as [he of M] roughly humps the space between the heavy bars of [his of M] cage.[or][BigNameDesc of M] never once looks away from you, hungrily thrusting [his of M] massive [DickDesc of M] through the bars of [his of M] cage.[or][BigNameDesc of M] pants roughly as [he of M] thrusts [his of M] [LongDickDesc of M] through the bars of the cage, smearing them with thick, slimy pre-cum.[in random order]".

To say CagedMinotaurSwingFlav of (M - minotaur):
	let N be the swing-intensity of M;
	say "[BigNameDesc of M] [one of]bang [his of M] fists against the side of the cage[or]throws [his of M] weight against the side of the cage[or]slams against the bars of the cage[or]throws [himself of M] at the side of the cage[in random order], [if N <= 0]causing it to swing slightly back and forth[otherwise if N is 1]causing it to slowly swing a few centimeters back and forth[otherwise if N is 2]causing it to steadily swing a few inches back and forth[otherwise if N is 3]causing it to swing a few feet in either direction[otherwise if N is 4]causing it to steadily swing several feet back and forth[otherwise]swinging the cage in a huge arc covering most of the room[end if]".

To compute CagedMinotaurGrabbing of (M - minotaur):
	say "[BigNameDesc of M] [one of]thrusts [his of M] arm through a gap in[or]reaches through the bars in[or]tries to grab you through the bars of[or]pushes [his of M] arms through the gaps in[in random order] [his of M] cage!";
	let D be the dexterity of the player;
	let B be the blue-balls of M;
	let S be the swing-intensity of M;
	if S < 2, now B is 0;
	if a random number between B * -2 and D < S * 2:[You're caught!]
		say "[big his of M] muscular hand barely closes around one of your wrists, and in the next moment, [he of M]'s yanked it into [his of M] cage, pulling your torso flat against the bars!";
		now the swing-intensity of M is 0;
		now M is wrangling arms;
	otherwise:
		say "[one of][big he of M] catches nothing but air.[or][big his of M] efforts are all in vain.[or][big he of M] only manages to capture the air.[or][big he of M] only catches the air between you.[in random order]".

To compute CagedMinotaurMolesting of (M - minotaur):
	if the blue-balls of M > a random number between 4 and 9:[NUT]
		if the largeness of breasts < 6 and the thickness of hips < 5 and asshole is undefended:[If you fit into the cage, you faint, since the cum bloating would mean you couldn't get out.]
			say "[if the reaction of the player is 0][BigNameDesc of M] shrugs off your attempts to struggle[otherwise][BigNameDesc of M] breathes heavily[end if] as [he of M] pulls your arm even further through the gap between the bars, which is just wide enough to let [him of M] drag in your shoulder, followed by your head, neck, chest, hips, knees, and finally your feet. You [if the sex addiction of the player < 5]curse[otherwise]thank nintendolls for[end if] your petite frame as [FuckerDesc of M] pulls you all the way into the cage and effortlessly pins you down with [his of M] huge limbs. [big his of M] 'smallest' limb is rock-hard, throbbing, and drooling precum as [he of M] lifts your legs and aligns [his of M] [cockhead of M] with your exposed asshole. Knowing there's nothing more you can do at this point, you [if the reaction of the player is 0]stop struggling and [end if]grit your teeth as [he of M] slams right in. You faint.";
			now delayed fainting is 1;
			now the fainting reason of the player is 24;
		otherwise:
			say "[if the reaction of the player is 0][BigNameDesc of M] shrugs off your attempts to struggle[otherwise][BigNameDesc of M] breathes heavily[end if] as [he of M] pulls your head through another gap between the bars and pushes you down so your face is level with [his of M] huge, precum-drooling [LongDickDesc of M]. [big his of M] [cockhead of M] immediately connects with your lips, and [if the player is pheromonal]your head swims from being exposed to such a huge dosage of virile musk. Your instincts take over, and immediately open wide so [his of M] shaft can slide[otherwise]your head swims from the insane saltiness of the stuff. You open your mouth to wretch, and immediately feel your jaw being pried open as [his of M] shaft slides[end if] right in. You feel a single, tell-tale throb, and half a second later you feel hot [semen] exploding into your mouth. Even [he of M] isn't prepared for the force, and you're flung back from the edge of the cage as a huge amount of cum shoots down your throat.";
			StomachSemenUp 5;
			now M is penetrating face; [This allows orgasm function to work properly]
			orgasm M;
			now the blue-balls of M is -5;
			now the swing-intensity of M is 0;
			dislodge M;
	otherwise:[TODO: migrate to seduction grope, probably.]
		increase the blue-balls of M by 2;
		compute seduction grope of M.

To compute breasts grope of (M - minotaur):
	say "[BigNameDesc of M]'s [DickDesc of M] twitches as [he of M] reaches [his of M] other arm through the bars, [one of]roughly[or]clumsily[or]eagerly[in random order] [one of]fondling[or]molesting[or]groping[or]squeezing[in random order] your [BreastDesc].".

To compute asshole grope of (M - minotaur):
	let C be a random top level ass protection clothing;
	let S be a random worn skirted crotch-in-place clothing;
	if S is clothing, now C is S;
	let A be a random uncursed anal beads penetrating asshole;
	if A is nothing and M is able to remove cursed plugs monster, now A is a random anal beads penetrating asshole;
	if A is clothing:
		let N be the notch-taken of A;
		say "[BigNameDesc of M] forces [his of M] other arm through the bars and grabs the hook at the end of your anal beads. All [N] beads are pulled out of your [asshole] at once!";
		ruin asshole times (N + 1) / 2;
		now A is in the location of the player;
		dislodge A;
		say "The [A] falls to the ground.";
		force immediate inventory-focus redraw;
	otherwise:
		say "[BigNameDesc of M] forces [his of M] other arm through the bars and [one of]roughly[or]clumsily[or]eagerly[in random order] uses [his of M] thick fingers to [one of]prods[or]poke[in random order] your [asshole][if C is clothing] through your [ShortDesc of C][end if].".

To compute vagina grope of (M - minotaur):
	let C be a random top level protection clothing;
	let S be a random worn skirted crotch-in-place clothing;
	say "[if S is clothing and S is not C][BigNameDesc of M] forces [his of M] [one of]throbbing[or]twitching[or]rock-hard[or]rigid[in random order] [DickDesc of M] up under your [ShortDesc of S], grinding [his of M] [cockhead of M][otherwise][BigNameDesc of M] grinds [his of M] [one of]throbbing[or]twitching[or]rock-hard[or]rigid[in random order] [DickDesc of M][end if] [if C is clothing]against your [vagina] through your [ShortDesc of C][otherwise][one of]between your labia[or]between your nether lips[or]against your clit[or]along your labia lips[in random order]![end if]";
	if the player is pheromonal:
		say "The sensation of [his of M] virile [DickDesc of M] [if C is clothing]so nearly touching your[otherwise]touching your bare[end if] [vagina] sends your hormones into overdrive!";
		Arouse 300.

To compute penis grope of (M - minotaur):
	let C be a random top level protection clothing;
	let S be a random worn skirted crotch-in-place clothing;
	say "[if S is clothing and S is not C][BigNameDesc of M] forces [his of M] [one of]throbbing[or]twitching[or]rock-hard[or]rigid[in random order] [DickDesc of M] up under your [ShortDesc of S], grinding [his of M] [otherwise][BigNameDesc of M] grinds [his of M] [one of]throbbing[or]twitching[or]rock-hard[or]rigid[in random order] [end if][DickDesc of M] [if C is clothing]against your [player-penis] through your [ShortDesc of C][otherwise]against your [player-penis]![end if]".

To compute belly grope of (M - minotaur):
	let B be a random worn belly covering clothing;
	say "You feel [NameDesc of M]'s breath hot on your face as [he of M] holds [his of M] [DickDesc of M] against your [ShortDesc of belly], [if B is clothing]darkening your [ShortDesc of B] with precum[otherwise]smearing you with precum[end if] as [he of M] [one of]furiously[or]eagerly[or]hungrily[in random order] jerks off with [his of M] free hand.".

To compute face grope of (M - minotaur):
	if there is worn ringagged clothing:
		say "[BigNameDesc of M] grinds [his of M] [cockhead of M] against your face, smearing your lips with sticky precum as [he of M] clumsily tries to shove [his of M] [DickDesc of M] into your open mouth.";
	otherwise if the player is pheromonal or (the player is a nympho and the player is not feeling dominant):
		say "[BigNameDesc of M] grinds [his of M] [LongDickDesc of M] against your face, breathing heavily as [if the player is pheromonal]your hormone-addled senses force you to[otherwise]You can't help but[end if] worship [his of M][if M is a balls-haver] balls and [end if][one of]throbbing[or]pulsing[or]twitching[in random order] shaft with your tongue.";
		increase the blue-balls of M by 1;
	otherwise:
		say "[BigNameDesc of M] grinds [his of M] [cockhead of M] against your face, smearing your lips with thick, sticky precum.";
	if the player is pheromonal:
		say "Your head swims as you breathe in [his of M] potent virile musk.";
		Arouse 300.

To compute thighs grope of (M - minotaur):
	let C be a random worn crotch-in-place leg covering clothing;
	let S be a random worn skirted crotch-in-place clothing;
	say "[if S is clothing and S is not C][BigNameDesc of M] forces [his of M] [one of]throbbing[or]twitching[or]rock-hard[or]rigid[in random order] [DickDesc of M] up under your [ShortDesc of S], thrusting [his of M] [otherwise][BigNameDesc of M] thrusts [his of M] [one of]throbbing[or]twitching[or]rock-hard[or]rigid[in random order] [end if][DickDesc of M] between your legs!".

To compute ass grope of (M - minotaur):
	let S be a random worn skirted crotch-in-place clothing;
	say "[BigNameDesc of M] forces [his of M] other arm through the bars, [one of]roughly[or]clumsily[or]eagerly[in random order] [one of]fondling[or]molesting[or]groping[or]squeezing[in random order] your [AssDesc][if S is clothing] over your [ShortDesc of S][end if].".

This is the minotaur prioritises defeating others rule:
	if the critical-windup of current-monster is 0 and the number of undefeated awake monsters in the location of current-monster > 1: [1 is going to be the minotaur itself]
		MonsterStomp current-monster;
		rule succeeds.
The minotaur prioritises defeating others rule is listed last in the minotaur priority attack rules.

To compute the flying player taunting of (M - minotaur):
	say "[one of][BigNameDesc of M] clearly refuses to move from your location until you stop floating.[or][stopping]".

The latex punishment rule of minotaur is the no latex punishment rule.

To say LandingTaunt of (M - minotaur):
	say "[BigNameDesc of M] growls with impatience.";
	humiliate 50.

To decide which number is the rounds of sex left of (M - minotaur):
	decide on 1. [We don't do climax the standard way]

To compute anal sex of (M - minotaur):
	if the sex-length of M > 0:
		compute fuckhole sex of M;
		if the soreness of asshole < 9 or the reaction of the player is 0, ruin asshole;
		if the reaction of the player > 0:
			if the soreness of asshole > 8, say "[one of]Your lack of resistance against [NameDesc of M] is preventing you from going over the edge and fainting[if mythical creature fetish is 1], but it is extremely humiliating to let yourself get fucked by a half bull[end if].[or][or][or][or][cycling]";
	otherwise if the health of M > 5:
		compute fuckhole sex of M;
		unless M is wrapped, AssFill 2;
		if the soreness of asshole < 10 or the reaction of the player is 0, ruin asshole;
	otherwise:
		TimesSubmittedUp M by 1;
		now the nutting-turns of M is 0;
		let R be 20 + a random number between 1 and 10;
		if debuginfo > 0, say "[input-style][ShortDesc of M] sleep pin check: d10+20.5 ([R].5) | [strength of the player + dexterity of the player] = ([strength of the player]) strength + ([dexterity of the player]) dexterity[roman type][line break]";
		if the dexterity of the player + the strength of the player > 20 + R:
			say "[one of][BigNameDesc of M] is completely spent, and collapses on top of you, half-crushing your body.[or][BigNameDesc of M], completely spent, collapses on top of you, half crushing your body.[or][BigNameDesc of M] collapses on top of you, spent.[at random] You barely manage to crawl out from underneath [him of M], [if the soreness of asshole > 4 or the openness of asshole < 8]wincing as [his of M][one of] shrinking[or] cummy[or][or][at random] [DickDesc of M] [one of]slides out[or]pops out[at random] of your [asshole][otherwise]sighing as [his of M][one of] shrinking[or] cummy[or][or][at random] [DickDesc of M] slides out of your [asshole][end if].";
			now M is not penetrating asshole;
		otherwise:
			say "[one of][BigNameDesc of M] is completely spent, and collapses on top of you, crushing your body. You can't manage to crawl out from underneath [his of M] weight![or][BigNameDesc of M], spent, collapses on top of you, crushing you under [his of M] full weight. You try, but you don't manage to crawl out from underneath [him of M]![or][BigNameDesc of M] lets go of you as the torrent finally stops, collapsing on top of you just before you manage to crawl out from underneath [him of M].[or][BigNameDesc of M], completely spent, collapses on top of you, pinning you down with [his of M] full weight. No matter what you do, you can't manage to crawl out from underneath [him of M]![at random] You're going to have to wait [one of]like this with [his of M] [DickDesc of M] in your [asshole] until [he of M] wakes up...[or]with [his of M] [DickDesc of M] buried in your [asshole] until [he of M] wakes up...[at random]";
			if lady fetish is 0 and mythical creature fetish is 1, alwayscutshow figure of minotaur cutscene 2 for M;
		say CreampieReactionFlav to M in asshole;
		if mythical creature fetish is 1 and cow-horns is off-stage and cow-ears is worn and (M is the father or M is mating):
			say "[bold type]Your fake cow ears transform into a pair of very permanent horns[roman type][line break]";
			only destroy cow-ears;
			summon cow-horns;
		AssFill 2;
		orgasm M;
		now the sleep of M is 150;
		display entire map.

To compute vaginal sex of (M - minotaur):
	if the sex-length of M > 0:
		compute fuckhole sex of M;
		if the soreness of vagina < 9 or the reaction of the player is 0, ruin vagina;
		if the reaction of the player > 0:
			if the soreness of vagina > 8, say "[one of]Your lack of resistance against [NameDesc of M] is preventing you from going over the edge and fainting[if mythical creature fetish is 1], but it is extremely humiliating to let yourself get fucked by a half bull[end if].[or][or][or][or][cycling]";
	otherwise if the health of M > 5:
		compute fuckhole sex of M;
		unless M is wrapped, PussyFill 2;
		if the soreness of vagina < 10 or the reaction of the player is 0, ruin vagina;
	otherwise:
		TimesSubmittedUp M by 1;
		now the nutting-turns of M is 0;
		PussyFill 2;
		say "[one of][BigNameDesc of M] seems completely spent - [he of M] pulls your bloated body off [his of M] shaft and leaves you to recover[or][BigNameDesc of M], spent, pulls your bloated body off [his of M] deflating shaft and leaves you to recover[or][BigNameDesc of M] pulls your bloated body off [his of M] shaft as [his of M] ejaculation finally ends, leaving you on the floor to recover[or][BigNameDesc of M] removes [his of M] deflating shaft from your [vagina], completely spent[at random]. [big he of M] stalks off, probably looking for another woman to breed.";
		say CreampieReactionFlav to M in vagina;
		if there is a worn tattoo and bred by a bull tattoo is not worn:
			summon bred by a bull tattoo;
			say "The words 'BRED BY A BULL' materialise on your left side! They are now permanently inked onto your skin.";
		otherwise if cow-ears is off-stage and cow-ears is actually summonable and lactation fetish is 1:
			say "[bold type]A pair of fake cow ears materialise on your head.[roman type][line break]";
			summon cow-ears cursed;
		orgasm M;
		bore M.

To say CreampieReactionFlav to (M - minotaur) in (F - vagina):
	if the class of the player is cowgirl:
		say "[second custom style]'[one of]Mm...Moo. Moooo.'[or]Moo. Moooo.'[or]M-moo. Mooo.'[at random][roman type][line break]";
	otherwise if the player is able to get pregnant:
		say "[second custom style][if the bimbo of the player < 7][one of]That was so much [semen]...Please don't let me be pregnant.[or]So much...I hope I'm not pregnant.[at random][otherwise if the bimbo of the player < 12][one of]T-That was so much [semen]. [big he of M] really wanted me pregnant![or]So that's what it feels like to be bred. Did it take?[at random][otherwise][one of]So much [semen]...that means I'm probably preggers![or]Fuck, that was so much [semen]. I hope I'm knocked up.[at random][end if][roman type][line break]";
	otherwise:
		say "[second custom style][if the bimbo of the player < 7]I thought [he of M] would never stop...[otherwise if the bimbo of the player < 12]So much [semen]...where does it all come from...[otherwise if M is a balls-haver]Those big balls must have been full. There's so much~[otherwise]There's just so much~[end if][roman type][line break]".

To say CreampieReactionFlav to (M - minotaur) in (F - asshole):
	if M is not penetrating asshole:
		say "[second custom style][if the bimbo of the player < 7]I thought [he of M] would never stop...[otherwise if the bimbo of the player < 12]So much [semen]...where does it all come from...[otherwise if M is a balls-haver]Those big balls must have been full. There's so much~[otherwise]There's just so much~[end if][roman type][line break]";
	otherwise:
		say "[second custom style][if the bimbo of the player < 7][one of]It isn't over? Come on....[or]I thought [he of M] was finished...[at random][otherwise if the bimbo of the player < 12][one of]OK, so it actually isn't over. Great...[or]After filling me with all that [semen], I thought [he of M]'d be finished...[at random][otherwise][one of]I kind of want to stay like this.[or]Aww, [he of M]'s tired. I shouldn't wake [him of M].[at random][end if][roman type][line break]".

To compute priestessBlessing of (M - minotaur):
	say "You feel a surge in your holy aura and a voice appears in your head: [line break][second custom style]'Good work, Sister!'[roman type][line break]";
	decrease the charge of the dungeon altar by 300;
	RitualUp 2.

To compute fuckhole sex of (M - minotaur):
	let F be a random fuckhole penetrated by M;
	if the sex-length of M > 0:
		if the sex-length of M is 1, say "[BigNameDesc of M] roars as you feel [if M is wrapped][one of]the condom starting to fill[or]cum spurting into the condom[or][his of M] [DickDesc of M] erupting into the condom[in random order][otherwise][one of][him of M] start to ejaculate[or]cum spurting into your [variable F] in waves[or][his of M] [DickDesc of M] erupts inside of you[in random order][end if]! But [he of M][one of] doesn't stop fucking you.[or]'s still going![or] doesn't stop plowing you at all.[or] doesn't relent with [his of M] brutal pounding![at random]";
		otherwise say "[one of][BigNameDesc of M] is brutally plowing away at your [variable F]![or][BigNameDesc of M] grunts primally as [he of M] pounds your poor [variable F]![or][BigNameDesc of M] growls hungrily as [his of M] [LongDickDesc of M] jack-hammers your [variable F].[or][BigNameDesc of M] holds you with a rigid death-grip as [he of M] slams in and out of your [variable F].[or][BigNameDesc of M] holds you down with an ironclad grip as [he of M] brutally pounds your [variable F]![or][BigNameDesc of M][']s massive [if mythical creature fetish is 1]furry [end if]nutsack slaps audibly against your taint as [he of M] brutally jack-hammers your [variable F].[in random order]";
		decrease the sex-length of M by 1;
		if the health of M > 10, decrease the health of M by 5 * combatSpeed;
	otherwise:
		increase the nutting-turns of minotaur by 1;
		say "[one of][BigNameDesc of M] is still fucking your [variable F][or][BigNameDesc of M] continues to pound your [variable F][or][BigNameDesc of M] relentlessly plows your [variable F][or][BigNameDesc of M] plows your [variable F] without stopping[or][BigNameDesc of M][']s massive [if mythical creature fetish is 1]horsecock[otherwise]cock[end if] continues to slam in and out of your [variable F][at random]! [run paragraph on]";
		if M is wrapped:
			if the reaction of the player is 0:
				if a random number between the nutting-turns of minotaur and 1 > 3:[condom breaks]
					say "Your squirming puts extra pressure on the overly-stretched condom inside you, and you feel a torrent of warmth as it suddenly bursts open, flooding your belly with [semen]!";
					now M is unwrapped;
					if M is penetrating asshole, AssFill the nutting-turns of minotaur * 2;
					otherwise PussyFill the nutting-turns of minotaur * 2;
			otherwise:
				if a random number between 1 and the nutting-turns of minotaur > 6:
					say "The condom reaches its breaking point, twitching with effort before suddenly bursting open and flooding your belly with a torrent of warm [semen]!";
					now M is unwrapped;
					if M is penetrating asshole, AssFill the nutting-turns of minotaur * 2;
					otherwise PussyFill the nutting-turns of minotaur * 2;
			if M is wrapped:[if the condom didn't break, we still need to display flavour]
				say "[one of]You can feel [his of M] [DickDesc of M] twitching as it continues to spurt [semen] into the rapidly stretching condom.[or]The condom continues to stretch.[or][big he of M] somehow finds more [semen] to cram into the rapidly stretching condom.[or][big his of M] [semen] keeps coming, torrenting into the condom at full force.[at random]";
		otherwise:
			if the reaction of the player is 0, say "[one of]No matter much you clench your muscles, you can't stop the torrent of [semen] into your [BellyDesc][or]Your resistance does nothing to stop the virtually endless torrent of [semen] into your belly.[or]Even with your struggling, [he of M] somehow finds more [semen] to cram into your [BellyDesc][or]No matter what you do, [his of M] [semen] continues to torrent into your [BellyDesc] at full force.[at random]";
			otherwise say "[one of]Relaxing your [variable F], you accept the virtually endless torrent of [semen] into your [BellyDesc].[or]You do your best to stay relaxed as [his of M] [semen] spurts into your belly.[or]You can feel [his of M] [DickDesc of M] twitching as it continues to spurt [semen] into your [BellyDesc].[or]Relaxing your inner muscles, you accept the unending torrent of [semen] into your [BellyDesc].[at random]";
		decrease the health of M by 5 * combatSpeed;
		if lady fetish is 0 and mythical creature fetish is 1, cutshow figure of minotaur cutscene 5 for M.

To compute damaging attack of (M - a minotaur):
	if a random number between 1 and 7 is 1 and asshole is undefended:[He lifts you into the air.]
		compute grab attack of M;
	otherwise:
		compute striking attack of M;
		BodyRuin 1.[The minotaur always does at least one damage]

To compute grab attack of (M - a minotaur):
	if the accuracy roll of M >= the dexterity of the player:
		say "[BigNameDesc of M] grabs your leg and slowly drags you toward [him of M].";
		now M is wrangling thighs;
		increase the critical-windup of M by 1;
	otherwise:
		say "[BigNameDesc of M] tries to grab your leg, but you avoid it!";

This is the minotaur grabs both legs rule:
	let M be current-monster;
	if the critical-windup of M is 1 and M is wrangling a body part:
		if the accuracy roll of M >= the dexterity of the player:
			say "[BigNameDesc of M] grabs your [if M is wrangling thighs]other leg[otherwise]legs[end if], [his of M] [DickDesc of M] growing harder and harder as [he of M] pulls you in close.";
			increase the critical-windup of M by 1;
		otherwise:
			say "[BigNameDesc of M] tries to grab your [if M is wrangling thighs]other leg[otherwise]legs[end if], but you manage to avoid it!";
		rule succeeds;
	otherwise if the critical-windup of M is 1:
		now the critical-windup of M is 0.
The minotaur grabs both legs rule is listed last in the minotaur priority attack rules.

To say WrangleResistFailFlav of (T - minotaur):
		say "You struggle, but [one of]you're no match for [FuckerDesc of T][']s powerful muscles![or]you can't break [FuckerDesc of T][']s powerful grip.[or][FuckerDesc of T] is just too strong![in random order]".

To compute WrangleResistSuccess of (T - minotaur):
	say WrangleResistSuccessFlav of T;
	now the critical-windup of T is 0;
	dislodge T.

This is the minotaur lifts before crits rule:
	let M be current-monster;
	if M is wrangling a body part and the critical-windup of M is 2:
		say "[BigNameDesc of M] slowly lifts you up and off the ground, positioning your [asshole] right above [his of M] [LongDickDesc of M].";
		increase the critical-windup of M by 1;
		now M is not wrangling thighs;
		now M is grabbing the player;[Your last struggle will actually be pointless, but it's more fun if we pretend it's not. Use your safe-word, sweetie!]
		if lady fetish is 0 and mythical creature fetish is 1, alwayscutshow figure of minotaur cutscene 7 for M;
		rule succeeds;
	otherwise if the critical-windup of M is 2:[failsafe]
		now the critical-windup of M is 0.
The minotaur lifts before crits rule is listed last in the minotaur priority attack rules.

This is the minotaur crits the player rule:
	let M be current-monster;
	if M is grabbing the player and the critical-windup of M > 2:
		say AssholePenetrationFlav of M;
		now the stance of the player is 1;
		now M is not grabbing the player;
		now M is penetrating asshole;
		ruin asshole;
		set up sex length of M in asshole;
		now the critical-windup of M is 0;
		rule succeeds;
	otherwise if the critical-windup of M > 2:
		now the critical-windup of M is 0.
The minotaur crits the player rule is listed last in the minotaur priority attack rules.

To compute unique periodic effect of (M - a minotaur):
	if M is not in the location of the player or the player is prone:
		now the critical-windup of M is 0;
	if the critical-windup of M is 0:
		now M is not grabbing the player;
		now M is not wrangling thighs.

To decide which number is the strength roll of (M - minotaur):
	let D be a random number between 1 and the health of M / 2;
	if debugmode is 1, say "Player [strength of the player] | [D].5 [ShortDesc of M][line break]";
	decide on D.

To say StrikingSuccessFlav of (M - minotaur) on (B - a body part):
	say "[BigNameDesc of M] pummels you [TargetName of B] with [his of M] fists!".

To say StrikingFailureFlav of (M - minotaur) on (B - a body part):
	say "You try to dodge [NameDesc of M][']s fist, but [he of M] still manages to land a glancing blow! Ouch!".

To decide which body part is the painful-part of (M - minotaur):
	let B be a random body part;
	if B is a fuckhole, now B is hips;
	if B is hair, now B is breasts;
	if B is not hips and B is not breasts, now B is belly;
	decide on B.

The monster fucked taunting rule of minotaur is the minotaur replaces the monster rule.
This is the minotaur replaces the monster rule:
	let M be current-monster;
	let N be a random monster penetrating a fuckhole;
	if there is a monster penetrating asshole, now N is a random monster penetrating asshole; [This is the one who's important to get rid of since this is where we're going.]
	say "[BigNameDesc of M] kicks the [N] out of the way, and takes over! [big he of M] [one of]sticks [his of M] giant [DickDesc of M] in your [asshole],[or]shoves [his of M] massive [DickDesc of M] up your ass[or]jams [his of M] giant dick into your [asshole][or]clumsily rams [his of M] huge dick in your [asshole][at random] and [one of]starts cumming[or]starts to cum[at random] almost immediately![line break][variable custom style][if the anal sex addiction of the player < 4]Oh Fuck![otherwise if the anal sex addiction of the player < 6]I thought I was safe with the [N] inside me![otherwise]Finally, a real dick![end if][roman type][line break]";
	if lady fetish is 0 and mythical creature fetish is 1, cutshow figure of minotaur cutscene 3 for M;
	now the sex-length of M is 0;
	now the chosen-orifice of M is asshole;
	destroy N;
	now M is penetrating asshole;
	ruin asshole;
	rule succeeds.

To compute (M - minotaur) attacking (C - a clothing):
	if C is displacable:
		say "[BigNameDesc of M] pulls the crotch of your [printed name of C] to the side!";
		displace C;
	otherwise:
		say "[BigNameDesc of M] rips off your [printed name of C]!";
		destroy C.

To compute (M - minotaur) attacking (C - a clothing):
	say "[BigNameDesc of M] tears off your [printed name of C]!";
	destroy C.

To compute (M - minotaur) removing (X - a thing):
	say "[BigNameDesc of M] forcefully and roughly pulls the [printed name of X] out of your [asshole] and drops it on the ground!";
	now X is in the location of the player;
	if X is anal beads, ruin asshole times the notch-taken of X / 2;
	dislodge X.

To decide if (M - minotaur) is willing to creampie (F - a vagina):
	if the player is not possessing a vagina, decide no;
	if pregnancy fetish is 0 or the pregnancy of the player is not 0, decide no;
	if the player is pheromonal, decide yes;
	if the thickness of hips > 6 - a random number between 1 and the pregnancy rate of the player, decide yes;
	decide no.

To compute the orifice choosing of (M - minotaur):
	if M is willing to creampie vagina:
		now the chosen-orifice of M is vagina;
	otherwise:
		now the chosen-orifice of M is asshole.

To decide which number is the condom resistance of (M - minotaur):
	if the class of the player is cowgirl, decide on 99;
	decide on 3.

To say CondomForceFailFlav of (M - minotaur):
	say "[one of]You reach between your legs, grasping for [NameDesc of M][']s [DickDesc of M] as you quickly take out a condom. [big his of M] copious precum makes [his of M] shaft too slippery, and you can't get a good grip before [he of M] roughly smacks your hand away.[or]You take out a condom and reach between your legs, awkwardly trying to apply it to [NameDesc of M][']s [DickDesc of M]. [big he of M] snorts angrily and smacks your hand out of the way.[at random]";

To set up sex length of (M - minotaur) in (B - a fuckhole):
	now the sex-length of M is 0;[preventative measure to prevent bugginess]
	set up sex length (the health of M / 10) of M in B.

To say AssholePenetrationFlav of (M - minotaur):
	say "[BigNameDesc of M] [one of]forcefully pushes [his of M][or]clumsily rams [his of M][or]jams [his of M][or]forces [his of M][at random] [LongDickDesc of M] into your [asshole], and starts fucking you furiously! [if the sex-length of M is 0]Within seconds, [he of M] has started cumming inside you![otherwise]Your [asshole] is instantly stretched beyond belief.[end if]".

To say VaginaPenetrationFlav of (M - minotaur):
	say "[BigNameDesc of M] stares at your body and sniffs your [vagina], assessing you. [big he of M] seems to make up [his of M] mind that you are a suitable breeding partner and poises [his of M] [LongDickDesc of M] with the tip at the entrance to your [vagina]. [one of]With very little effort, [he of M] forces [his of M][or]With no effort at all, [he of M] shoves [his of M][or]Without the slightest hint of difficulty [he of M] rams [his of M][or]Without pausing, [he of M] forces [his of M][at random] giant [DickDesc of M] in, and starts fucking you vigorously. [if the sex-length of M is 0]Within seconds, [he of M] has started cumming inside you![otherwise if the class of the player is cowgirl and mythical creature fetish is 1]You find yourself mooing submissively as your [vagina] is instantly stretched beyond belief by [his of M] huge [LongDickDesc of M].[otherwise if pregnancy fetish is 1 and mythical creature fetish is 1]Your [vagina] is instantly - and painfully - stretched beyond belief.[one of][line break][variable custom style]Could I get pregnant from this bull?![roman type][line break][or][stopping][otherwise]Your [vagina] is instantly - and painfully - stretched beyond belief.[end if]".

To compute default caged domination of (M - minotaur):
	say EnslavedDominationFlav of M;
	if the raw delicateness of the player > the EnslavedDominationThreshold of M:
		say "You feel vaguely better about yourself, but mostly just a bit ambivalent.";
	otherwise:
		DelicateDown 1;
	if the player is getting unlucky:
		say "[BigNameDesc of M] reaches through the bars and manages to grab your arm!";
		now M is wrangling arms.

To compute unique teleportation to (R - Dungeon36):
	if minotaur is in Dungeon36 and minotaur is caged:
		now minotaur is wrangling arms;
		say "You have appeared right next to the minotaur's cage! What rotten luck! [big he of minotaur] manages to grab your arm!".

Section 2 - Damage

To say CombatProvokedReaction of (M - minotaur):
	if the sleep of M > 0, say "[big he of M] wakes up, and immediately stands up to full height with a powerful roar!";
	otherwise say "[big he of M] growls viciously[if M is unleashed]! Uh-oh...[otherwise]![end if]".

To say DamageReactHealthy of (M - minotaur):
	say "[BigNameDesc of M] effortlessly shrugs off your hit!".

To say DamageReactDamaged of (M - minotaur):
	say "[big he of M] snarls through clenched teeth!".

To say DamageReactTired of (M - minotaur):
	say "[big he of M] roars savagely, taking the hit!".

To say DamageReactWeak of (M - minotaur):
	say "[big he of M] seems to be breathing more heavily, as if [he of M][']s getting tired!".

To compute unique automatic banishment of (M - minotaur):
	let H be a random off-stage horn;
	let X be a random off-stage equine-strapon;
	let B be bull-strength-potion;
	now M is bossdefeated;
	if M is caged, say "The cage swings wildly, and then suddenly the chain snaps and the cage falls to the floor with a thud! The cage door's hinges snap, and the door falls away. ";
	say "The injured [ShortDesc of M] turns and flees towards the exit of the dungeon. ";
	if mythical creature fetish is 1:
		if X is clothing and the player is getting lucky:
			now the strap-length of X is 11;[15 inches]
			if full-lady fetish is 1, say "The huge strapon dildo [he of M] was wearing loosens and slips from [his of M] loins. It seems like you could actually wear it!";
			otherwise say "A huge strapon dildo materialises on the floor in front of you.";
			say GotLuckyFlav;
			now X is in the location of the player;
			compute autotaking X;
			increase the loot dropped of M by 1;
		if H is a thing:
			say "One of [his of M] injured horns catches smacks into a low doorway and snaps clean off, clattering to the ground. [BigNameDesc of M] whines loudly with pain but doesn't stop running away. The [ShortDesc of H] appears to be hollow - you could almost use this as a drinking vessel!";
			now H is in the location of the player;
			compute autotaking H;
			increase the loot dropped of M by 1;
	otherwise:
		now X is a random off-stage strapon-dildo;
		if X is clothing and a random number between 1 and 3 is 1:
			now the strap-length of X is 11;[15 inches]
			say "The huge strapon dildo [he of M] was wearing loosens and slips from [his of M] loins. It seems like you could actually wear it!";
			if full-lady fetish is 1, say "The huge strapon dildo [he of M] was wearing loosens and slips from [his of M] loins. It seems like you could actually wear it!";
			otherwise say "A huge strapon dildo materialises on the floor in front of you.";
			now X is in the location of the player;
			compute autotaking X;
			increase the loot dropped of M by 1;
		otherwise if B is potion:
			say "In [his of M] haste, [he of M] dropped a [ShortDesc of B]!";
			now B is in the location of the player;
			compute autotaking B;
			increase the loot dropped of M by 1.

To loot (M - minotaur):
	let X be a random off-stage plentiful ring;
	unless X is nothing:
		now X is in the location of the player;
		now X is solid gold;
		set shortcut of X;
		say "The defeated [M] [if the loot dropped of M > 0]also [end if]dropped a [printed name of X]!";
		increase the loot dropped of M by 1;
		compute autotaking X.

Minotaur ends here.
