Mittens by Equippables begins here.

A mittens is a kind of gloves. A mittens is usually unique. A mittens is usually cotton. A mittens is usually diaper-addiction-influencing.

To decide which number is the initial outrage of (C - a mittens):
	if diaper quest is 1, decide on 0;
	decide on the initial cringe of C.

To decide which number is the initial cringe of (C - a mittens):
	decide on 7.

To decide which number is the damage improvement of (W - a mittens):
	let X be -2;
	increase X by the magic-modifier of W;
	decide on X.

To compute attack of (W - a mittens) at (M - a monster):
	say "You [if the bimbo of the player < 10][one of]weakly[or]pathetically[or]timidly[at random][otherwise][one of]playfully[or]meekly[or]softly[at random][end if] slap at [NameDesc of M] with your [printed name of W].".

To say ShortDesc of (W - a mittens):
	say "pink mittens".

This is the mittens prevent manual dexterity rule:
	if there are worn mittens:
		if manual hands attempt is 1, say "Your [random worn mittens] mean you don't have the manual dexterity to do that.";
		rule fails.
The mittens prevent manual dexterity rule is listed in the manual dexterity restriction rules.

To say BabywearDonationDeclarationFlav of (M - a monster) on (C - a mittens):
	if M is intelligent, say "[speech style of M]'[if the health of M < the maxhealth of M]It seems like we need to keep you from being too dangerous[otherwise]Babies don't need their hands, since they should be letting the grown-ups take care of them[end if].'[roman type][line break]".

rubber mittens is a mittens. rubber mittens is latex. The printed name of rubber mittens is "[TQlink of item described][item style][unless magic-curse of the item described is bland or curse-ID of the item described is unsure][magic-curse] [end if][raw-magic-modifier-desc]pair of latex mittens[clothing-title-after]". Figure of rubber mittens is the file "Items/Accessories/Equippables/mittens1.png". The text-shortcut of rubber mittens is "lmi".
blue rubber mittens is a mittens. blue rubber mittens is latex. The printed name of blue rubber mittens is "[TQlink of item described][item style][unless magic-curse of the item described is bland or curse-ID of the item described is unsure][magic-curse] [end if][raw-magic-modifier-desc]pair of blue latex mittens[clothing-title-after]". Figure of blue rubber mittens is the file "Items/Accessories/Equippables/mittens4.png". The text-shortcut of blue rubber mittens is "blmi".
inflated rubber mittens is a mittens. inflated rubber mittens is latex. The printed name of inflated rubber mittens is "[TQlink of item described][item style][unless magic-curse of the item described is bland or curse-ID of the item described is unsure][magic-curse] [end if][raw-magic-modifier-desc]pair of inflated latex mittens[clothing-title-after]". Figure of inflated rubber mittens is the file "Items/Accessories/Equippables/mittens3.png". The text-shortcut of inflated rubber mittens is "ilmi".
teddy bear mittens is a mittens. The printed name of teddy bear mittens is "[TQlink of item described][item style][unless magic-curse of the item described is bland or curse-ID of the item described is unsure][magic-curse] [end if][raw-magic-modifier-desc]pair of teddy bear mittens[clothing-title-after]". Figure of teddy bear mittens is the file "Items/Accessories/Equippables/mittens2.png". The text-shortcut of teddy bear mittens is "tmi".

To decide which figure-name is clothing-image of (C - rubber mittens):
	decide on figure of rubber mittens.

To decide which figure-name is clothing-image of (C - blue rubber mittens):
	decide on figure of blue rubber mittens.

To decide which figure-name is clothing-image of (C - inflated rubber mittens):
	decide on figure of inflated rubber mittens.

To decide which figure-name is clothing-image of (C - teddy bear mittens):
	decide on figure of teddy bear mittens.

To say ClothingDesc of (W - rubber mittens):
	say "A pair of large pink latex mittens. You lose some manual dexterity and some slapping power from wearing them.";
	if the player is an adult baby and W is worn, say "You feel much, much better at running away from monsters whilst on your knees.".

To say ClothingDesc of (W - blue rubber mittens):
	say "A pair of large blue latex mittens. You lose some manual dexterity and some slapping power from wearing them.";
	if the player is an adult baby and W is worn, say "You feel much, much better at running away from monsters whilst on your knees.".

To say ShortDesc of (W - inflated rubber mittens):
	say "inflated latex mittens".

To say ClothingDesc of (W - inflated rubber mittens):
	say "A pair of large black latex mittens, somehow kept fully inflated to a spherical shape at all times, probably via magic. You lose some manual dexterity and all your slapping power from wearing them.";
	if the player is an adult baby and W is worn, say "You feel much, much better at running away from monsters whilst on your knees.".

To say ClothingDesc of (W - teddy bear mittens):
	say "A pair of large pink baby mittens with a teddy bear pattern and small bells. You lose some manual dexterity and some slapping power from wearing them.";
	if the player is an adult baby and W is worn, say "You feel much, much better at running away from monsters whilst on your knees.".

Definition: rubber mittens is pink themed: decide yes.
Definition: rubber mittens is blue themed: decide yes.


teddy bear mittens is womanly.
Definition: teddy bear mittens is bear themed: decide yes.
Definition: teddy bear mittens is pink themed: decide yes.

To decide which number is the initial cringe of (C - teddy bear mittens):
	decide on 9.

Definition: a rubber mittens is babywear: decide yes.
Definition: a rubber mittens is transformation-protected if diaper lover <= 0.

To decide which object is the unique-upgrade-target of (C - rubber mittens):
	if the number of worn latex clothing > 1, decide on a random off-stage inflated rubber mittens;
	decide on a random off-stage teddy bear mittens.

To decide which number is the damage improvement of (W - inflated rubber mittens):
	decide on -10.

Definition: inflated rubber mittens is black themed: decide yes.
Definition: inflated rubber mittens is inflation themed: decide yes.

This is the inflated mittens prevent manual dexterity rule:
	if inflated rubber mittens is worn:
		if manual hands attempt is 1, say "Your [inflated rubber mittens] mean you don't have the manual dexterity to do that.";
		rule fails.
The inflated mittens prevent manual dexterity rule is listed in the hands restriction rules.


Mittens ends here.
